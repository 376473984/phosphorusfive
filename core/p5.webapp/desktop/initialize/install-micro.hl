

/*
 * Downloads and installs Micro.
 *
 * Expects the root account to be logged into the system.
 *
 * Notice, when a new release is created, the two next variables needs to be updated,
 * if a new version of Micro has been created, to make sure the SHA value of Micro's zip file, is
 * the expected value, in addition to the URL to Micro's zip file download.
 *
 * To create a new SHA for your Micro zip file, use [load-file] and [p5.crypto.hash.create-sha512]
 * on the results.
 */
_micro-url:"https://github.com/polterguy/micro/releases/download/v4.2/micro.zip"
_micro-sha:4LeHZVRKTHgtfBOjZT66QwgNaABoDI+BN92tc0gXeMqYiucu6Jz2htb9aXwRG2Xb1xndafoKYZfTfzPgL0l3QA==





/*
 * Downloading Micro.
 */
p5.http.get:x:/@_micro-url?value





/*
 * Saving zip file we downloaded above to user's temp folder.
 */
save-file:~/temp/micro.zip
  src:x:/@p5.http.get/*/result/*/content?value





/*
 * Verifying sha-512 value of zip file.
 */
load-file:~/temp/micro.zip
p5.crypto.hash.create-sha512:x:/-/*?value
if:x:/-?value
  !=:x:/@_micro-sha?value
  throw:Wrong sha-512 value of Micro's zip file!! No known ways to recover!! Major security breach is possible, such as a 'Man in the Middle' attack between your server, and the GitHub server where Micro is hosted!!





/*
 * Unzipping Micro into modules folder.
 */
unzip:~/temp/micro.zip
  dest:/modules/





/*
 * Renaming the Micro main folder, to remove any versioning numbers, etc.
 *
 * Notice, we only do this, if filename actually is different.
 */
if:x:/@unzip/0?name
  !=:/modules/micro/

  /*
   * Actual download folder, after unzip, had a different name than what we'd expect it to have.
   */
  move-folder:x:/@unzip/0?name
    dest:/modules/micro/





/*
 * Making sure we evaluate the "startup.hl" file for Micro.
 */
load-file:/modules/micro/startup.hl
eval:x:/-/*
