

/*
 * Creates our "desktop", which basically just allows the user to select which application he wants to start.
 *
 * By default, there's only one application (or module) in P5, which is the Bazar.
 */





/*
 * Including main Micro CSS files, and making sure root "cnt" widget 
 * has the "container-fullscreen" CSS class, in addition to that it's empty.
 *
 * Notice, we also include the "Sea Breeze" skin at this point.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/sea-breeze.css
set-widget-property:cnt
  class:container-fullscreen
clear-widget:cnt





/*
 * Including main desktop CSS file.
 */
p5.web.include-css-file:/desktop/media/main.css





/*
 * Now we can determine all modules the user has installed on his system, and create a "desktop icon" for
 * each of these modules, that have a "launch.hl" file, to allow the user to actually launch his apps.
 *
 * Hint, a module doesn't necessary have to include a "launch.hl" file, but if it doesn't, it won't have its
 * own desktop icon created, and will be treated as an invisible "component", instead of an "app".
 */
list-folders:/modules/





/*
 * Listing all "launch.hl" files inside the results of our above folder listing operation.
 */
list-files:x:/@list-folders/*?name
  filter:launch.hl





/*
 * Looping through all "launch.hl" files from above, and creating a widget wrapping each of them, 
 * that will invoke the file, once clicked.
 */
for-each:x:/@list-files/*?name

  /*
   * Checking if currently iterated "app" has a "desktop.hl" widget file, at which point we use this "desktop.hl"
   * file, instead of our "generic" launch widget.
   */
  replace:x:/@_dp?value
    src:/launch.hl
    dest:/desktop.hl
  if
    fetch:x:/0/0?value
      file-exists:x:/@replace?value

    /*
     * Currently iterated app has a desktop widget declaration file.
     *
     * Using this widget instead of our generic "launch app" widget.
     *
     * Notice, this allows you to create pure "desktop widgets", among other things,
     * which doesn't do anything else, but actually show some piece of widget on your desktop.
     */
    load-file:x:/@replace?value
    add:x:/../*/create-widget/*/widgets/*/div/*/widgets
      src:x:/@load-file/*/*

  else

    /*
     * Currently iterated app does not have a desktop widget, hence we use the generic one.
     */
    split:x:/@_dp?value
      =:/
    eval-x:x:/+/**
    add:x:/../*/create-widget/*/widgets/*/div/*/widgets
      src
        container
          element:a
          href:/{0}
            :x:/@split/0/-2?name
          class:desktop-app shaded rounded air-inner bg
          widgets
            span
              class:desktop-app-name
              href:x:/@split/0/-2?name
              widgets
                span
                  class:desktop-app-name
                  innerValue:@"<span class=""icon-home desktop-app-icon""></span><span style=""position:absolute;top:5px;right:5px;font-size:.8rem;"">Welcome</span>"





/*
 * Creating actual desktop widget.
 *
 * This widget basically becomes your "desktop", with one icon, for each "component" that has a "launch.hl" file.
 */
create-widget
  class:row
  widgets
    div
      class:col desktop-apps
      widgets
        literal
          element:a
          href:#
          role:button
          class:desktop-logout
          title:Log out
          oninit

            /*
             * Dislpaying either "login" button or "logout" button, depending upon user's current status.
             */
            whoami
            if:x:/@whoami/*/default?value

              /*
               * User is not logged in.
               */
              set-widget-property:x:/../*/_event?value
                innerValue:@"<span class=""icon-key""></span>"
                title:Log in

            else

              /*
               * User is logged in.
               */
              set-widget-property:x:/../*/_event?value
                innerValue:@"<span class=""icon-power-off""></span>"
                title:Log out

          onclick

            /*
             * Either logging user in, or out, depending upon his current status.
             */
            whoami
            if:x:/@whoami/*/default?value
              p5.core.login
            else
              p5.core.logout
