

/*
 * Creates our "desktop", which basically just allows the user to select which application
 * he wants to start.
 *
 * Plus also some apps, and/or components, the user probably wants to download.
 */





/*
 * Verifying Micro is installed, and if not, automatically installing it.
 */
vocabulary:p5.io.unroll-path.@MICRO
if:x:/@vocabulary/*
  not

  /*
   * Micro is not installed, making sure we download it, and install it automatically.
   */
  load-file:/desktop/install-micro.hl
  eval:x:/-/*

  /*
   * Now our setup process is finished, and we can logout user from system.
   *
   * Notice, we choose to do this explicitly, since the root user might not necessary
   * enjoyt it particularly much, if we keep him logged in at the localhost, or whatever client
   * he were using to setup the system.
   */
  load-file:/desktop/logout.hl
  eval:x:/-/*





/*
 * Including main Micro CSS files, and making sure root "cnt" widget 
 * has the "container-fullscreen" CSS class.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
set-widget-property:cnt
  class:container-fullscreen





/*
 * Including main desktop CSS file.
 */
p5.web.include-css-file:/desktop/main.css





/*
 * Making sure user is logged in, and if not, forcing him to login, before we display the desktop.
 */
whoami
if:x:/-/*/default?value

  /*
   * User is not logged in, forcing him to login, before we allow him to continue.
   */
  load-file:/desktop/login.hl
  eval:x:/-/*

  /*
   * Returning early to avoid evaluating the rest of our file.
   */
  return





/*
 * Now we can determine all "apps" the user has installed on his system, and create a "desktop icon" for
 * each of these apps, to allow the user to actually launch his apps.
 *
 * First we start by listing all folders at root.
 */
list-folders:/





/*
 * Then we remove folders that we know for sure does not contain any apps.
 *
 * Notice, the below "/db/" removal, will fissle, if you override your default p5.data database folder.
 * Hence, if you do, you must also explicitly change the "/db/" value below.
 */
set:x:@"/@list-folders/*(/""/db/""|/""/obj/""|/""/bin/""|/""/users/""|/""/common/""|/""/code/""|/""/desktop/""|/""/Properties/"")"





/*
 * Listing all "launch.hl" files inside of the results of our above folder listing operation.
 */
list-files:x:/@list-folders/*?name
  filter:launch.hl





/*
 * Looping through all "launch.hl" files from above, and creating a widget wrapping each of them, 
 * that will invoke the file, once clicked.
 */
for-each:x:/@list-files/*?name

  /*
   * Checking if currently iterated "app" has a "desktop.hl" widget file.
   */
  replace:x:/@_dp?value
    what:launch.hl
  if
    fetch:x:/0/0?value
      file-exists:{0}/desktop.hl
        :x:/@replace?value

    /*
     * Currently iterated app has a desktop widget declaration file.
     *
     * Using this widget instead of our generic "app" widget.
     */

  else

    /*
     * Currently iterated app does not have a desktop widget, hence we use the generic one.
     */
    split:x:/@_dp?value
      =:/
    eval-x:x:/+/**
    add:x:/../*/create-widget/*/widgets/*/div/*/widgets
      src
        container
          element:a
          href:/{0}
            :x:/@split/0?name
          class:desktop-app shaded rounded air-inner bg
          widgets
            span
              class:desktop-app-name
              innerValue:x:/@split/0?name





/*
 * Creating actual desktop widget.
 */
create-widget
  class:row
  widgets
    div
      class:col
      widgets
        a
          href:#
          role:button
          class:desktop-logout
          innerValue:@"<span class=""icon-power-off""></span>"
          title:Log out
          onclick

            /*
             * Loggin user out of system, by evaluating file responsible for doing just that.
             */
            load-file:/desktop/logout.hl
            eval:x:/-/*
