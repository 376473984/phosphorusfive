

/*
 * Initialises our "components" and "apps", in addition to the core micro web operating system.
 *
 * This will basically create our [p5.web.load-ui] event, in addition to evaluating all "startup.hl" files,
 * for each module installed in the core web operating system.
 */





/*
 * Retrieving all "startup.hl" files, inside all of our installed modules.
 */
list-folders:/modules/
list-files:x:/@list-folders/*?name
  filter:startup.hl





/*
 * Evaluating all files from above result, to make sure we initialise all apps.
 *
 * Notice, we explicitly check for Micro and System42, and if found, we evaluate these first, before
 * we evaluate any of the other modules, since other modules might be dependent upon these modules during startup.
 */
load-file:x:@"/@list-files/*(/""~/system42/""|/""~/micro/"")?name"
eval:x:/-/*
set:x:@"/@list-files/*(/""~/system42/""|/""~/micro/"")"
load-file:x:/@list-files/*?name
eval:x:/-/*





/*
 * Evaluating our desktop/micro web os "startup.hl" file.
 *
 * But only if file actually exists, which allows developers and distributors to simply delete the "desktop" folder,
 * to start out with a clean slate.
 */
if
  fetch:x:/0/0?value
    file-exists:/desktop/startup.hl

  /*
   * "Desktop" app exists.
   */
  load-file:/desktop/startup.hl
  eval:x:/-/*
