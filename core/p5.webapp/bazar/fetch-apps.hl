
/*
 * File responsible for retrieving available Bazar apps.
 */





/*
 * Checking cache.
 */
p5.web.cache.get:p5.bazar.apps
if:x:/-/*/*/*

  /*
   * Apps are already cached.
   */
  add:x:/./*/return
    src:x:/@p5.web.cache.get/*/*/*
  return





/*
 * Fetching available apps from GitHub.
 *
 * Creating one fetcher thread for each configured Bazar.
 */
load-file:/bazar/configuration/bazars.hl
apply:x:/../*/wait
  src:x:/@load-file/*/*/bazar
  template
    fork
      {p5.http.get}:x:?value
wait






/*
 * Transforming result to lambda.
 */
hyper2lambda:x:/@wait/**/p5.http.get/*/result/*/content?value





/*
 * Making sure we cache results, in case user does another roundtrip later.
 *
 * Notice, we only cache it for 10 minutes.
 */
add:x:/../*/p5.web.cache.set/*/*
  src:x:/@hyper2lambda/*
p5.web.cache.set:p5.bazar.apps
  minutes:10
  src
    apps





/*
 * Returning results to caller.
 */
add:x:/../*/return
  src:x:/@hyper2lambda/*
return
