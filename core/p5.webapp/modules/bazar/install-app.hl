
/*
 * File responsible for downloading an installing an app.
 *
 * Expects [url] to app, and [local-url], in addition to optionally [paypal-id].
 *
 * If a [paypal-id] is supplied, app will not be downloaded before user has paid in accordance
 * to the specified PayPal ID, and a redirect to PayPal will occur, and after a successful
 * purchase has been placed, a redirect back to the server will occur, after which the app
 * will be automatically downloaded and installed.
 *
 * [local-url] becomes the URL the app will be given on the server after having been downloaded.
 */





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  url:string
  local-url:string
micro.lambda.contract.optional:x:/..
  paypal-id:string





/*
 * Storing our [local-url] in user's session.
 */
p5.web.session.set:bazar.local-url
  src:x:/../*/local-url?value





/*
 * Checking if the link somehow points to PayPal, at which point we have to create
 * a little bit of "form magic".
 */
if:x:/../*/paypal-id?value
  and:x:/../*/paypal-id?value
    !=:

  /*
   * This is a PayPal link, creating a form, which allows PayPal to redirect
   * the client back to ourselves, after having purchased, passing in the URL
   * of the app to be downloaded, indirectly as an HTTP GET argument when transaction 
   * is finished.
   *
   * Notice, we do this by storing the actual URL in the user's session, with a randomly
   * generated GUID being the "app-id".
   *
   * First creating a random GUID.
   */
  p5.types.guid.new

  /*
   * Then storing our app's download URL in user's session, with the GUID being its key.
   */
  p5.web.session.set:x:/@p5.types.guid.new?value
    src:x:/../*/url?value

  /*
   * Redirecting client to PayPal to perform purchase.
   */
  p5.web.get-root-location
  eval-x:x:/+/*
  micro.paypal.purchase
    paypal-id:x:/../*/paypal-id?value
    url:@"{0}bazar?app-url={1}"
      :x:/@p5.web.get-root-location?value
      :x:/@p5.types.guid.new?value

else

  /*
   * This is a free app, hence we can download it immediately.
   */
  eval-x:x:/+/*
  micro.evaluate.file:/modules/bazar/download-app.hl
    url:x:/../*/url?value
