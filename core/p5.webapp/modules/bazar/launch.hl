
/*
 * File responsible for launching the "server manager", which among other things,
 * allows you to install new apps, by downloading the from the "bazar".
 */





/*
 * Clearing out anything previously added to page, to make sure we start
 * out with a blank slate, in addition to making sure we've got our 
 * "container-fullscreen" CSS class on main container.
 */
clear-widget:cnt
set-widget-property:cnt
  class:container-fullscreen





/*
 * Including Micro, and adding Awesome Fonts.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:/desktop/media/main.css





/*
 * Verifying user is logged in with a root account, and if not, 
 * forcing user to login before proceeding.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in with a root account, therefor we force him to do just that, and return early,
   * to avoid evaluation of the rest of the file.
   */
  p5.core.login
    message:You must login with a root account to access the Bazar
  return





/*
 * Checking if this is a "return to download" invocation, after having purchased
 * an app at PayPal.
 */
p5.web.query.get:app-url
if:x:/-/*?value

  /*
   * This is a redirect from PayPal, invoking file responsible for downloading 
   * and installing app, after first having retrieved the app's download URL.
   */
  p5.web.session.get:x:/@p5.web.query.get/*?value
  eval-x:x:/+/*
  micro.evaluate.file:/modules/bazar/download-app.hl
    url:x:/@p5.web.session.get/*?value





/*
 * Retrieving available apps.
 */
micro.evaluate.file:/modules/bazar/fetch-apps.hl





/*
 * Looping through each available app, checking if its condition is not met,
 * at which point the app is not installed, and we can add it to our [create-widget]
 * invocation below, allowing user to install the app.
 */
for-each:x:/@micro.evaluate.file/*

  /*
   * Checking "condition" of app, which basically is assumed to be an Active Event,
   * which if existing, implies the app is already installed.
   */
  vocabulary:x:/@_dp/#/*/condition?value
  if:x:/-/*

    /*
     * App is already installed.
     *
     * It's [condition] event was found on server from before.
     */
    continue

  /*
   * HTML encoding values, to prevent man in the middle attack.
   */
  p5.html.html-encode:x:/@_dp/#/*/name?value
  p5.html.html-encode:x:/@_dp/#/*/url?value
  p5.html.html-encode:x:/@_dp/#/*/description?value
  p5.html.html-encode:x:/@_dp/#/*/author-readme?value
  markdown2html:x:/-?value
  p5.html.html-encode:x:/@_dp/#/*/price?value
  p5.html.html-encode:x:/@_dp/#/*/paypal-id?value
  p5.html.html-encode:x:/@_dp/#/*/local-url?value
  p5.html.html-encode:x:/@_dp/#/*/image?value

  /*
   * Making sure we apply icon-image to markdown results.
   */
  html2lambda:x:/@markdown2html?value
  eval-x:x:/+/**
  insert-before:x:/@html2lambda/0
    src
      img
        @src:x:/..for-each/*/p5.html.html-encode/[7,8]?value
        @style:"max-width:30%;float:right;margin-left:1rem;margin-bottom:1rem;"
        @class:shaded rounded
  lambda2html:x:/@html2lambda/*
  set:x:/@markdown2html?value
    src:x:/@lambda2html?value

  /*
   * Adding app to [create-widget] invocation below, to allow user to install it.
   */
  eval-x:x:/+/*/*/container/**(/literal/*/class(/=bazar-app-name|/=bazar-app-description|/=bazar-app-price)/./*/innerValue|/onclick/*(/_app-name|/_app-url|/_app-author-description|/_paypal-id|/_local-url))
  add:x:/../*/create-widget/*/widgets/*/div/*/widgets/*/div/*/widgets/=apps
    src
      container
        element:a
        href:#
        role:button
        class:bazar-app shaded rounded air-inner bg
        onclick

          /*
           * Forward evaluate above.
           */
          _app-name:x:/..for-each/*/p5.html.html-encode/[0,1]?value
          _app-url:x:/..for-each/*/p5.html.html-encode/[1,2]?value
          _app-author-description:x:/@markdown2html?value
          _paypal-id:x:/..for-each/*/p5.html.html-encode/[5,6]?value
          _local-url:x:/..for-each/*/p5.html.html-encode/[6,7]?value

          /*
           * Asking user to confirm installation, and initiating download and 
           * installation process.
           */
          eval-x:x:/+/**(/_url|/_paypal-id|/_local-url)
          create-widgets
            micro.widgets.modal:bazar-confirm-installation
              widgets
                h3
                  innerValue:Confirm installation of {0}
                    :x:/@_app-name?value
                p
                  innerValue:@"Are you sure you want to install {0}? Its developer has the following to say about it."
                    :x:/@_app-name?value
                div
                  class:success rounded air-inner
                  innerValue:x:/@_app-author-description?value
                p
                  innerValue:@"Notice, this process might take some time. Please allow Phosphorus Five to finish it without interruptions."
                div
                  class:right
                  widgets
                    div
                      class:strip
                      style:"display:inline-block;"
                      widgets
                        button
                          innerValue:Yes
                          style:"margin-bottom:0;"
                          onclick:@"return p5.bazar_download_app(event);"
                          oninit

                            /*
                             * Setting initial focus to button.
                             */
                            micro.page.set-focus:x:/../*/_event?value

                            /*
                             * Including JavaScript necessary to create a "please wait"
                             * screen, as user clicks the "Yes" button.
                             */
                            p5.web.include-javascript:@"
p5.bazar_download_app = function(e) {
  var obsc = document.createElement('div');
  obsc.id = 'bazar-obscurer';
  obsc.className = 'bazar-ajax-wait';
  var header = document.createElement('h1');
  var cnt = document.createTextNode('Please wait ...');
  header.appendChild(cnt);
  obsc.appendChild(header);
  document.body.appendChild(obsc);
  p5.$(e.currentTarget.id).raise('.onclick', {
    onsuccess:function(){
      var obsc = p5.$('bazar-obscurer').el;
      obsc.parentElement.removeChild(obsc);
    }});
  e.stopPropagation();
  return false;
}"

                          .onclick

                            /*
                             * Passing in URL to file responsible for downloading and
                             * installing the app.
                             */
                            _url:x:/@_app-url?value
                            _paypal-id:x:/@_paypal-id?value
                            _local-url:x:/@_local-url?value
                            eval-x:x:/+/*
                            micro.evaluate.file:/modules/bazar/install-app.hl
                              url:x:/@_url?value
                              paypal-id:x:/@_paypal-id?value
                              local-url:x:/@_local-url?value

                            /*
                             * Redirecting user to desktop, after installation process if
                             * finished.
                             */
                            p5.web.get-root-location
                            p5.web.set-location:x:/-?value

                        button
                          innerValue:No
                          style:"margin-bottom:0;"
                          onclick

                            /*
                             * Simply deleting modal widget.
                             */
                            delete-widget:bazar-confirm-installation

        widgets
          literal
            class:bazar-app-name
            innerValue:x:/..for-each/*/p5.html.html-encode/[0,1]?value
          literal
            class:bazar-app-description
            innerValue:x:/..for-each/*/p5.html.html-encode/[2,3]?value
          literal
            class:bazar-app-price
            innerValue:x:/..for-each/*/p5.html.html-encode/[4,5]?value





/*
 * Checking if user have all apps installed, and if so, giving him a simple
 * notification about that.
 */
if:x:/../*/create-widget/*/widgets/*/div/*/widgets/*/div/*/widgets/=apps/*
  not

  /*
   * User have all apps already installed, informing him about that fact in a modal widget,
   * and returning early, while also encouraging developers to start creating apps.
   */
  create-widget
    class:row
    widgets
      div
        class:col
        widgets
          div
            class:rounded shaded bg air-inner
            style:"margin:5% 15%;"
            widgets
              h3
                innerValue:It's awfully empty around here ...
              p
                innerValue:@"It seems you've already got all available apps installed. There might exist other repositories out there, which you could add to your Bazar though.
If you know the URL to another Bazar you trust, feel free to click the '+' button at the top right corner, to add another Bazar."

              p
                innerValue:@"Now of course, if you're a geek, you could also create your own Bazar, or create your own app, and have me host it for you. If the latter, 
feel free to create your app, and toss me an email at <a href=""mailto:thomas@gaiasoul.com"">thomas@gaiasoul.com</a>, to inform me about your app, 
requesting me to host it for you. But please, before you do, <a target=""_blank"" href=""https://gaiasoul.com/2017/08/16/bazar-rules-of-engagement/"">read the guidelines for an app submission</a>
to my Bazar. If you want me to check out your Bazar, and maybe blog about it, feel free to send me a link to it, such that I can have a look at it. I love bragging about other people's work,
created on top of my stuff."
              p
                innerValue:@"Alternatively, create your own app, and your own Bazar declaration, and host your Bazar e.g. at WordPress, GitHub or Google Disc. It is also easy to link your Bazar apps towards PayPal, 
accepting payments for your app, before it's installed on anybody's systems."
              p
                innerValue:@"If you choose to <a href=""https://gaiasoul.com"" target=""_blank"">subscribe to my blog</a>, you might be given tips about new apps, and Bazars out there every now and then."
              p
                innerValue:@"<strong>Notice</strong>, just because you've installed everything there is to install today, doesn't mean there won't be new apps in the Bazar a week from now. You should make it a habit to visit your Bazar, 
at least once a week, to see if there are any new apps, and/or updates to your existing apps. Updates to your apps, will also be possible to download from your Bazar."
              p
                innerValue:@"Feeling grateful about what you were just given for free? Feel free to <a target=""_blank"" href=""https://www.paypal.com/cgi-bin/webscr?cmd=_s-xclick&hosted_button_id=L6FEM7JPPQZXW"">give a small donation</a>."





/*
 * Creating actual Bazar widget, wrapping all available apps from Bazar.
 */
create-widget
  position:0
  class:row
  widgets
    div
      class:col
      widgets

        /*
         * Add Bazar wrapper.
         */
        div
          class:bazar-add
          widgets
            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      style:"margin-bottom:0;"
                      oninit

                        /*
                         * Checking main configuration file, to verify users are allowed to add new Bazars,
                         * and if not, disable the button.
                         */
                        p5.config.get:bazar.allow-users-adding-bazar
                        if:x:/-/*?value
                          !=:true

                          /*
                           * Current distro does not allow users to add new Bazars, simply disabling the "add" button.
                           */
                          set-widget-property:x:/../*/_event?value
                            disabled

                      onclick

                        /*
                         * Allowing user to add a Bazar manifest URL.
                         */
                        create-widgets
                          micro.widgets.modal:bazar-add-bazar
                            widgets
                              h3
                                innerValue:Add new Bazar
                              p
                                innerValue:@"If you have a link to an existing Bazar, you can add it to your own list of available Bazars here."

                              /*
                               * Using a wizard window for simplicity.
                               */
                              micro.widgets.wizard-form:bazar-add-bazar-wizard
                                text:bazar-bazar-url
                                  .data-field:url
                                  info:Bazar URL
                                  onkeydown:@"if (event.keyCode == 13) {p5.$('bazar-add-bazar-btn').raise('onclick');return false;}"
                                  oninit
                                    micro.page.set-focus:x:/../*/_event?value

                              /*
                               * Wrapper for "Add" and "Close" buttons.
                               */
                              div
                                class:right
                                widgets
                                  div
                                    class:strip
                                    style:"display:inline-block;"
                                    widgets

                                      /*
                                       * Add Bazar button.
                                       */
                                      button:bazar-add-bazar-btn
                                        innerValue:Add Bazar
                                        style:"margin-bottom:0;"
                                        onclick

                                          /*
                                           * Retrieving Bazar URL user just typed in.
                                           */
                                          micro.widgets.wizard-form.value:bazar-add-bazar-wizard

                                          /*
                                           * Loading Bazars file, to verify this Bazar doesn't already exist in it.
                                           */
                                          load-file:/modules/bazar/configuration/bazars.hl
                                          if:x:@"/@load-file/*/*/bazar/""={0}"""
                                            :x:/@micro.widgets.wizard-form.value/*/url?value

                                            /*
                                             * Bazar already exists in user's list of trusted Bazars.
                                             */
                                            p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                              class:micro-modal-content
                                            eval-x:x:/+/*
                                            micro.windows.info:You're already using this Bazar
                                              class:micro-windows-info warning
                                              parent:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                                            micro.page.set-focus:bazar-bazar-url
                                            return

                                          /*
                                           * Loading Bazar file, to see if it has additional suggested trusted developers.
                                           *
                                           * Making sure we do this in a try/catch block
                                           */
                                          try

                                            /*
                                             * Creating an HTTP GET request to retrieve Bazar file's content.
                                             */
                                            p5.http.get:x:/@micro.widgets.wizard-form.value/*/url?value

                                            /*
                                             * Transforming above result to lambda, to make it possible to parse manifest file,
                                             * and look for additional suggested trusted distributors (PGP keys).
                                             */
                                            hyper2lambda:x:/@p5.http.get/*/*/content?value

                                            /*
                                             * Loading existing "trusted distributors", to be able to "diff" it with any potential new
                                             * keys from the newly added Bazar.
                                             */
                                            load-file:/modules/bazar/configuration/trusted-app-distributors.hl

                                            /*
                                             * Used to hold all new keys, from newly added Bazar.
                                             */
                                            _new-keys
                                            for-each:x:/@hyper2lambda/*/app/*/developer-key/=$?value
                                              if:x:/@load-file/*/*/fingerprint/={0}
                                                :x:/@_dp?value
                                                not

                                                /*
                                                 * Key didn't exist in list of trustees from before.
                                                 */
                                                add:x:/@_new-keys
                                                  src:x:/@_dp?value

                                            /*
                                             * Checking if there was any new keys, and if so, asking user to confirm that
                                             * he trusts them.
                                             */
                                            if:x:/@_new-keys/*

                                              /*
                                               * There are new keys in this Bazar, making sure user explicitly accepts
                                               * to trust these keys.
                                               *
                                               * First applying all new keys to grid below.
                                               */
                                              apply:x:/./**/rows
                                                src:x:/@_new-keys/*
                                                template
                                                  item
                                                    {fingerprint}:x:?name

                                              /*
                                               * Passing in keys to [_keys] collection in OK button of outer most modal 
                                               * widget, and forward evaluating [_bazar-url] inside of Yes button.
                                               */
                                              add:x:/./*/create-widgets/**/_keys
                                                src:x:/@_new-keys/*
                                              eval-x:x:/./*/create-widgets/**/_bazar-url

                                              /*
                                               * Then displaying a new inner modal widget, with a warning, and a grid,
                                               * displaying fingerprints of all keys user must add to his list of trustees.
                                               */
                                              create-widgets
                                                micro.widgets.modal:bazar-confirm-trust
                                                  widgets
                                                    h3
                                                      innerValue:Please confirm
                                                    p
                                                      class:warning rounded air-inner shaded
                                                      innerValue:@"You are about to add the following keys to your trustee list, are you sure you wish to do this?
Adding a key to an app distributor that you don't know for sure you can trust, might have <strong>major security implications</strong>!"
                                                    micro.widgets.grid
                                                      columns
                                                        Fingerprint
                                                      rows
                                                    div
                                                      class:right
                                                      widgets
                                                        div
                                                          class:strip
                                                          style:"display:inline-block;"
                                                          widgets
                                                            button
                                                              innerValue:Yes
                                                              style:"margin-bottom:0;"
                                                              onclick

                                                                /*
                                                                 * Adding all keys to list of trustees.
                                                                 *
                                                                 * These first two, have been forward evaluated above.
                                                                 */
                                                                _keys
                                                                _bazar-url:x:/@micro.widgets.wizard-form.value/*/url?value

                                                                /*
                                                                 * Loading both Bazars file, and trustee file, such that we
                                                                 * can add our own additional items to them now.
                                                                 */
                                                                load-file:/modules/bazar/configuration/bazars.hl
                                                                load-file:/modules/bazar/configuration/trusted-app-distributors.hl

                                                                /*
                                                                 * Adding new items to files.
                                                                 */
                                                                eval-x:x:/+/*/*
                                                                add:x:/../*/load-file/[0,1]/*
                                                                  src
                                                                    bazar:x:/@_bazar-url?value
                                                                apply:x:/+/*
                                                                  src:x:/@_keys/*
                                                                  template
                                                                    {fingerprint}:x:?name
                                                                add:x:/../*/load-file/[1,2]/*
                                                                  src

                                                                /*
                                                                 * Converting lambda results, and saving it to files.
                                                                 */
                                                                save-file:x:/../*/load-file/[0,1]/*?name
                                                                  lambda2hyper:x:/../*/load-file/[0,1]/*/*
                                                                save-file:x:/../*/load-file/[1,2]/*?name
                                                                  lambda2hyper:x:/../*/load-file/[1,2]/*/*

                                                                /*
                                                                 * Deleting both of our modal widgets.
                                                                 */
                                                                delete-widget:bazar-confirm-trust
                                                                delete-widget:bazar-add-bazar

                                                                /*
                                                                 * Deleting cache, and reloading location for simplicity.
                                                                 */
                                                                p5.web.cache.set:p5.bazar.apps
                                                                p5.web.reload-location

                                                            button
                                                              innerValue:No
                                                              style:"margin-bottom:0;"
                                                              onclick

                                                                /*
                                                                 * Simply deleting outer most modal widget.
                                                                 */
                                                                delete-widget:bazar-confirm-trust

                                              /*
                                               * Returning early, to avoid deletion of outer most modal widget.
                                               */
                                              return

                                            else

                                              /*
                                               * Bazar contained no additional keys, simply adding it as is, for then to save
                                               * the Bazars declaration file.
                                               */
                                              eval-x:x:/+/*/*
                                              add:x:/../*/load-file/*
                                                src
                                                  bazar:x:/@micro.widgets.wizard-form.value/*/url?value
                                              save-file:x:/../*/load-file/*?name
                                                lambda2hyper:x:/../*/load-file/*/*

                                              /*
                                               * Deleting cache, and reloading location for simplicity.
                                               */
                                              p5.web.cache.set:p5.bazar.apps
                                              p5.web.reload-location

                                          catch

                                            /*
                                             * Oops, exception ocurred ...!!
                                             *
                                             * Displaying error bubble to user, and returning early.
                                             */
                                            p5.web.widgets.find-first:bazar-add-bazar
                                              class:micro-modal-content
                                            eval-x:x:/+/*
                                            micro.windows.info:Oops, not a valid Bazar URL
                                              class:micro-windows-info warning
                                              parent:x:/@p5.web.widgets.find-first/*/*?value

                                            /*
                                             * Setting focus to textbox, and returning early.
                                             */
                                            micro.page.set-focus:bazar-bazar-url
                                            return

                                          /*
                                           * Deleting modal widget.
                                           */
                                          delete-widget:bazar-add-bazar

                                      /*
                                       * Close add new Bazar modal window.
                                       */
                                      button
                                        innerValue:Close
                                        style:"margin-bottom:0;"
                                        onclick

                                          /*
                                           * Deleting modal widget.
                                           */
                                          delete-widget:bazar-add-bazar

                    button
                      innerValue:@"<span class=""icon-close""></span>"
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Redirecting client back to root URL of P5.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value

        /*
         * Bazar(s) apps wrapper.
         */
        div
          style:"clear:both;"
          class:bazar-wrapper
          widgets:apps

