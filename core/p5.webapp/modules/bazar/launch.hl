
/*
 * File responsible for launching the "server manager", which among other things,
 * allows you to install new apps, by downloading the from the "bazar".
 */





/*
 * Clearing out anything previously added to page, to make sure we start
 * out with a blank slate, in addition to making sure we've got our 
 * "container-fullscreen" CSS class on main container.
 */
clear-widget:cnt
set-widget-property:cnt
  class:container-fullscreen





/*
 * Including Micro, and adding Awesome Fonts.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:/desktop/media/main.css





/*
 * Verifying user is logged in with a root account, and if not, 
 * forcing user to login before proceeding.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in with a root account, therefor we force him to do just that.
   */
  load-file:/desktop/login.hl
  eval:x:/-/*
    message:You must login with a root account to access the Bazar
  return





/*
 * Checking if this is a "return to download" invocation, after having purchased
 * an app at PayPal.
 */
p5.web.query.get:app-url
if:x:/-/*?value

  /*
   * This is a redirect from PayPal, invoking file responsible for downloading 
   * and installing app, after first having retrieved the app's download URL.
   */
  p5.web.session.get:x:/@p5.web.query.get/*?value
  eval-x:x:/+/*
  micro.evaluate.file:/modules/bazar/download-app.hl
    url:x:/@p5.web.session.get/*?value





/*
 * Retrieving available apps.
 */
micro.evaluate.file:/modules/bazar/fetch-apps.hl





/*
 * Looping through each available app, checking if its condition is not met,
 * at which point the app is not installed, and we can add it to our [create-widget]
 * invocation below, allowing user to install the app.
 */
for-each:x:/@micro.evaluate.file/*

  /*
   * Checking "condition" of app, which basically is assumed to be an Active Event,
   * which if existing, implies the app is already installed.
   */
  vocabulary:x:/@_dp/#/*/condition?value
  if:x:/-/*

    /*
     * App is already installed.
     *
     * It's [condition] event was found on server from before.
     */
    continue

  /*
   * HTML encoding values, to prevent man in the middle attack.
   */
  p5.html.html-encode:x:/@_dp/#/*/name?value
  p5.html.html-encode:x:/@_dp/#/*/url?value
  p5.html.html-encode:x:/@_dp/#/*/description?value
  p5.html.html-encode:x:/@_dp/#/*/author-readme?value
  markdown2html:x:/-?value
  p5.html.html-encode:x:/@_dp/#/*/price?value
  p5.html.html-encode:x:/@_dp/#/*/paypal-id?value
  p5.html.html-encode:x:/@_dp/#/*/local-url?value
  p5.html.html-encode:x:/@_dp/#/*/image?value

  /*
   * Making sure we apply icon-image to markdown results.
   */
  html2lambda:x:/@markdown2html?value
  eval-x:x:/+/**
  insert-before:x:/@html2lambda/0
    src
      img
        @src:x:/..for-each/*/p5.html.html-encode/[7,8]?value
        @style:"max-width:30%;float:right;margin-left:1rem;margin-bottom:1rem;"
        @class:shaded rounded
  lambda2html:x:/@html2lambda/*
  set:x:/@markdown2html?value
    src:x:/@lambda2html?value

  /*
   * Adding app to [create-widget] invocation below, to allow user to install it.
   */
  eval-x:x:/+/*/*/container/**(/literal/*/class(/=bazar-app-name|/=bazar-app-description|/=bazar-app-price)/./*/innerValue|/onclick/*(/_app-name|/_app-url|/_app-author-description|/_paypal-id|/_local-url))
  add:x:/../*/create-widget/*/widgets/*/div/*/widgets/*/div/*/widgets
    src
      container
        element:a
        href:#
        role:button
        class:bazar-app shaded rounded air-inner bg
        onclick

          /*
           * Forward evaluate above.
           */
          _app-name:x:/..for-each/*/p5.html.html-encode/[0,1]?value
          _app-url:x:/..for-each/*/p5.html.html-encode/[1,2]?value
          _app-author-description:x:/@markdown2html?value
          _paypal-id:x:/..for-each/*/p5.html.html-encode/[5,6]?value
          _local-url:x:/..for-each/*/p5.html.html-encode/[6,7]?value

          /*
           * Asking user to confirm installation, and initiating download and 
           * installation process.
           */
          eval-x:x:/+/**(/_url|/_paypal-id|/_local-url)
          create-widgets
            micro.widgets.modal:bazar-confirm-installation
              widgets
                h3
                  innerValue:Confirm installation of {0}
                    :x:/@_app-name?value
                p
                  innerValue:@"Are you sure you want to install {0}? Its developer has the following to say about it."
                    :x:/@_app-name?value
                div
                  class:success rounded air-inner
                  innerValue:x:/@_app-author-description?value
                p
                  innerValue:@"Notice, this process might take some time. Please allow Phosphorus Five to finish it without interruptions."
                div
                  class:right
                  widgets
                    div
                      class:strip
                      style:"display:inline-block;"
                      widgets
                        button
                          innerValue:Yes
                          style:"margin-bottom:0;"
                          onclick:@"return p5.bazar_download_app(event);"
                          oninit

                            /*
                             * Setting initial focus to button.
                             */
                            micro.page.set-focus:x:/../*/_event?value

                            /*
                             * Including JavaScript necessary to create a "please wait"
                             * screen, as user clicks the "Yes" button.
                             */
                            p5.web.include-javascript:@"
p5.bazar_download_app = function(e) {
  var obsc = document.createElement('div');
  obsc.id = 'bazar-obscurer';
  obsc.className = 'bazar-ajax-wait';
  var header = document.createElement('h1');
  var cnt = document.createTextNode('Please wait ...');
  header.appendChild(cnt);
  obsc.appendChild(header);
  document.body.appendChild(obsc);
  p5.$(e.currentTarget.id).raise('.onclick', {
    onsuccess:function(){
      var obsc = p5.$('bazar-obscurer').el;
      obsc.parentElement.removeChild(obsc);
    }});
  e.stopPropagation();
  return false;
}"

                          .onclick

                            /*
                             * Passing in URL to file responsible for downloading and
                             * installing the app.
                             */
                            _url:x:/@_app-url?value
                            _paypal-id:x:/@_paypal-id?value
                            _local-url:x:/@_local-url?value
                            eval-x:x:/+/*
                            micro.evaluate.file:/modules/bazar/install-app.hl
                              url:x:/@_url?value
                              paypal-id:x:/@_paypal-id?value
                              local-url:x:/@_local-url?value

                            /*
                             * Redirecting user to desktop, after installation process if
                             * finished.
                             */
                            p5.web.get-root-location
                            p5.web.set-location:x:/-?value

                        button
                          innerValue:No
                          style:"margin-bottom:0;"
                          onclick

                            /*
                             * Simply deleting modal widget.
                             */
                            delete-widget:bazar-confirm-installation

        widgets
          literal
            class:bazar-app-name
            innerValue:x:/..for-each/*/p5.html.html-encode/[0,1]?value
          literal
            class:bazar-app-description
            innerValue:x:/..for-each/*/p5.html.html-encode/[2,3]?value
          literal
            class:bazar-app-price
            innerValue:x:/..for-each/*/p5.html.html-encode/[4,5]?value





/*
 * Checking if user have all apps installed, and if so, giving him a simple
 * notification about that.
 */
if:x:/../*/create-widget/*/widgets/*/div/*/widgets/*/div/*/widgets/*
  not

  /*
   * User have all apps already installed, informing him about that fact in a modal widget,
   * and returning early, while also encouraging developers to start creating apps.
   */
  create-widgets
    micro.widgets.modal
      widgets
        h3
          innerValue:It's awfully empty around here ...
        p
          innerValue:@"It seems you've already got all available apps installed. Now of course, if you're a geek, you could probably
create some of your own, and have me host them for you. If so, feel free to create it, and toss me an email at <a href=""mailto:thomas@gaiasoul.com"">thomas@gaiasoul.com</a>, to
inform me about your app, requesting me to host it for you. But please, before you do, <a target=""_blank"" href=""https://gaiasoul.com/2017/08/16/bazar-rules-of-engagement/"">read the guidelines for an app submission</a>!"
        p
          innerValue:Notice, this implies allowing me to scrutinize your app's source code carefully among other things, to make sure it is in no ways harmful.
        div
          class:right
          widgets
            button
              innerValue:Close
              style:"margin-bottom:0;"
              oninit

                /*
                 * Setting initial focus to button, to allow easy closing and return to root URL of server.
                 */
                micro.page.set-focus:x:/../*/_event?value

              onclick

                /*
                 * Redirecting user to root URL.
                 */
                p5.web.get-root-location
                p5.web.set-location:x:/-?value

  /*
   * Returning early, to avoid evaluation of the rest of our file.
   */
  return





/*
 * Creating actual Bazar widget, wrapping all available apps from Bazar.
 */
create-widget
  class:row
  widgets
    div
      class:col
      widgets
        a
          class:bazar-leave
          innerValue:@"<span class=""icon-close""></span>"
          title:Leave Bazar
          oninit

            /*
             * Setting URL of hyperlink to main root URL of P5.
             */
            p5.web.get-root-location
            set-widget-property:x:/../*/_event?value
              href:x:/@p5.web.get-root-location?value

        div
          class:bazar-wrapper
          widgets
