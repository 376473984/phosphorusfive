/*
 * Allows user to uninstall an app.
 */





/*
 * Creating a modal widget to allow user to type in a new Bazar URL.
 */
create-widgets
  micro.widgets.modal:bazar-uninstall-app
    widgets
      h3
        innerValue:Delete app
      micro.widgets.grid:bazar-uninstall-app-grid
        oninit

          /*
           * Checking which apps are installed, and creating one grid row for each app.
           */
          list-folders:/modules/
          for-each:x:/@list-folders/*?name

            /*
             * Figuring out app's name.
             */
            split:x:/@_dp?value
              =:/

            /*
             * Adding item to grid
             */
            eval-x:x:/+/*/*/*|/+/**/_app-name
            add:x:/../*/micro.widgets.grid.databind
              src
                item
                  .row
                    .app-row
                  Name:x:/@split/0/-?name
                  Uninstall
                    style:"width:5px;"
                    widgets
                      button
                        innerValue:@"<span class=""icon-bin""></span>"
                        title:Delete this app. Warning, this action is permanent!
                        onclick

                          /*
                           * Forward evaluated above.
                           */
                          _app-name:x:/@split/0/-?name

                          /*
                           * Uninstalling app.
                           */
                          desktop.uninstall-app:/modules/{0}/
                            :x:/@_app-name?value

                          /*
                           * Giving feedback to user.
                           */
                          micro.windows.info:App was successfully uninstalled
                            class:micro-windows-info success

                          /*
                           * Removing row from datagrid.
                           */
                          p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                            .app-row
                          delete-widget:x:/-/*/*?value

          /*
           * Databinding grid.
           */
          micro.widgets.grid.databind:bazar-uninstall-app-grid

      div
        class:right
        widgets
          div
            class:strip
            style:"display:inline-block;"
            widgets
              button
                innerValue:Close
                oninit

                  /*
                   * Setting focus to close button initially.
                   */
                  micro.page.set-focus:x:/../*/_event?value

                onclick

                  /*
                   * Deleting modal widget.
                   */
                  delete-widget:bazar-uninstall-app

