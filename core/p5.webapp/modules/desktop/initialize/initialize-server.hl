

/*
 * Initialises server, by allowing user to set server salt, and/or set root password.
 *
 * If the server needs to be setup before allowing the user to proceed to your app, or the micro web os desktop,
 * this event will return "true".
 */





/*
 * Creating main content container.
 */
create-container-widget:desktop-main-container
  class:container





/*
 * Checking if we should create server salt, or ask for root password.
 */
if
  p5.auth._has-salt
  not

  /*
   * Checking if Micro exists, at which point we include it.
   */
  vocabulary:p5.io.unroll-path.@MICRO
  if:x:/-/*
    p5.web.include-css-file:@MICRO/media/main.css
    p5.web.include-css-file:@MICRO/media/skins/serious.css
    micro.page.set-focus:server-salt

  /*
   * No server salt, asking user to provide one.
   */
  create-widget
    parent:desktop-main-container
    widgets
      div
        class:row air-top
        widgets
          div
            class:col-50 offset-25
            widgets

              /*
               * First some description.
               */
              h3
                innerValue:Please provide a random server salt
              p:salt-description
                innerValue:You don't need to remember your salt, but to maximise the security of your system, it should to be as random as possible.
              p:salt-description-2
                innerValue:The server salt is used to hash your passwords, in addition to seed the random number generator, when cryptographically secure random numbers are needed.

              /*
               * Then our textarea, which actually allows the user to type in a salt.
               */
              literal:server-salt
                element:textarea
                placeholder:Provide a random server salt ...
                rows:10
                class:fill

              /*
               * Wrapper for our "save" server salt button.
               */
              div
                class:right
                widgets
                  button
                    innerValue:Save
                    onclick

                      /*
                       * Retrieving server salt, and setting it.
                       */
                      get-widget-property:server-salt
                        value
                      if:x:/@get-widget-property/*/*?value
                        =:

                        /*
                         * No salt, cannot proceed ...
                         */
                        set-widget-property:salt-description
                          innerValue:@"<strong>Please provide a server salt!!</strong>"
                        return

                      /*
                       * Setting salt.
                       */
                      p5.auth._set-server-salt:x:/@get-widget-property/*/*?value

                      /*
                       * Reloading URL for simplicity, which will ask the user for a root password.
                       */
                      p5.web.reload-location

  /*
   * Returning true to caller, to signal that server is in setup/initialize phase.
   */
  return:bool:true

else-if
  p5.auth._root-password-is-null

  /*
   * Checking if Micro exists, at which point we include it.
   */
  vocabulary:p5.io.unroll-path.@MICRO
  if:x:/-/*
    p5.web.include-css-file:@MICRO/media/main.css
    p5.web.include-css-file:@MICRO/media/skins/serious.css
    micro.page.set-focus:root-password

  /*
   * Root password is null, asking user to provide one.
   */
  create-widget
    parent:desktop-main-container
    widgets
      div
        class:row air-top
        widgets
          div
            class:col-50 offset-25
            widgets


              /*
               * First some description.
               */
              h3
                innerValue:Please provide a root password
              p:root-description
                innerValue:Please make sure you remember your password, otherwise you will not be able to access your system!
              p:root-description-2
                innerValue:@"When you click 'Save', your server will be automatically setup. This might take some few minutes. Please let it finish its job without interruptions!"

              /*
               * Then our textbox widgets, asking user to provide a root password for his system.
               */
              input:root-password
                type:password
                placeholder:Root password ...
                class:fill
                onkeydown:@"if (event.keyCode == 13) {p5.$('setup-save-btn').raise('onclick');return false;}"
              input:root-password-repeat
                type:password
                placeholder:Repeat password ...
                class:fill
                onkeydown:@"if (event.keyCode == 13) {p5.$('setup-save-btn').raise('onclick');return false;}"

              /*
               * Then the "save root password" button wrapper.
               */
              div
                class:right
                widgets
                  button:setup-save-btn
                    innerValue:Save
                    onclick

                      /*
                       * Retrieving password(s), making sure they're not empty, and that they match.
                       */
                      get-widget-property:root-password
                        value
                      get-widget-property:root-password-repeat
                        value
                      if:x:/../*/get-widget-property/*/root-password/*?value
                        =:
                        or:x:/../*/get-widget-property/*/root-password/*?value
                          !=:x:/../*/get-widget-property/*/root-password-repeat/*?value

                        /*
                         * Password were not given, or textbox values did not match.
                         */
                        set-widget-property:root-description
                          innerValue:@"<strong>Provide a password, and make sure you repeat it exactly, character for character!!</strong>"
                        return

                      /*
                       * Setting password.
                       */
                      eval-x:x:/+/*
                      p5.auth._set-root-password
                        password:x:/@get-widget-property/*/*?value

                      /*
                       * Reloading URL for simplicity, since we're now done with setting up the server.
                       */
                      p5.web.reload-location

  /*
   * Returning true to caller, to signal that server is in setup/initialize phase.
   */
  return:bool:true





/*
 * Returning false to caller, to signal that the server salt and root password has already been setup.
 */
return:bool:false
