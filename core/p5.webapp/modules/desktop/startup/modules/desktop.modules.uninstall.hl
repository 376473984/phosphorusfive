
/*
 * Creates the __[desktop.modules.uninstall]__ Active Event.
 */

/*
 * This event will uninstall the specified __[\_arg]__ module.
 *
 * Expects __[\_arg]__ being the local folder's name, from within your _"/modules/"_ folder. If
 * uninstallation is successful, the event will return boolean _"true"_ - Otherwise it will return
 * _"false"_.
 */
create-event:desktop.modules.uninstall

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * Checking if [_arg] is a fully qualified path, and if not, making sure it becomes one.
   */
  if
    starts-with:x:/../*/_arg?value
      src:/
    not
    set:x:/../*/_arg?value
      src:/modules/{0}/
        :x:/../*/_arg?value

  /*
   * Checking to see if app is even installed.
   */
  if
    fetch:x:/0/0?value
      folder-exists:x:/../*/_arg?value

    /*
     * App exists, now checking if there is an "uninstall.hl" file for it.
     */
    if
      fetch:x:/0/0?value
        file-exists:{0}uninstall.hl
          :x:/../*/_arg?value

      /*
       * Evaluating "uninstall.hl" file for app.
       */
      micro.evaluate.file:{0}uninstall.hl
        :x:/../*/_arg?value

    /*
     * Then deleting the folder that contains the old version of our app.
     */
    delete-folder:x:/../*/_arg?value

    /*
     * Returning success to caller.
     */
    return:bool:true

  /*
   * App didn't exist
   */
  return:bool:false
