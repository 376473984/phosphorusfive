/*
 * Creates the __[desktop.help.widgets.file-meta]__ extension widget that displays meta information for all files
 * recursively beneath some specified **[folder]** argument. This is the widget
 * that renders the information you're currently reading. Made public to make it easy to
 * create meta information traversel for your own modules.
 *
 * Among other things, it will retrieve the first comment from your files, and display
 * it as Markdown.
 */
create-event:desktop.help.widgets.file-meta

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    folder:string

  /*
   * Returns widget to caller.
   */
  eval-x:x:/+/**/.folder|/+/*/*/*/button/*/innerValue
  return
    container
      style:"clear:both;"
      widgets
        button
          innerValue:Meta information for {0}
            :x:/../*/folder?value
          onclick

            /*
             * Creating a cover widget, returning to client, and invoking actual implementation.
             */
            create-widgets
              micro.widgets.cover:desktop-waiting-for-meta
                message:Please wait while I retrieve meta information from your module ...
            eval-x:x:/+/**/p5.web.widgets.ajax-events.raise
            micro.lambda.create-timeout
              milliseconds:1
              onfinish
                p5.web.widgets.ajax-events.raise:x:/../*/_event?value
                  .onclick

          .onclick

            /*
             * Retrieving parent widget's first 'ol' child, and setting it to visible.
             */
            p5.web.widgets.get-parent:x:/../*/_event?value
            p5.web.widgets.find:x:/-/*/*?value
              element:ul
            set-widget-property:x:/-/*/*?value
              visible:true

            /*
             * Figuring out meta information for module.
             */
            .folder:x:/../*/folder?value
            desktop._list-files:x:/-?value
            load-file:x:/-/*?name
              convert:false
            hyper2lambda:x:/@load-file/*?value
              keep-comments:true
            p5.io.unroll-path:x:/@.folder?value
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:1
              element:h2
              innerValue:Module meta information
            p5.io.folder.get-length:x:/@.folder?value
            set:x:/-/*?value
              /:x:/./-/*?value.decimal
                _:decimal:1024
            p5.io.folder.get-creation-time:x:/@.folder?value
            p5.types.date.format:x:/-/*?value
              format:"ddd dd. MMM yyyy - HH:mm"
            if
              fetch:x:/0/0?value
                file-exists:{0}startup/version.hl
                  :x:/@p5.io.unroll-path?value
              load-file:{0}startup/version.hl
                :x:/@p5.io.unroll-path?value
              set:x:/../*/create-widgets/**/Name/=Version/./*/Value?value
                src:x:/@load-file/*/*/*/return?value
            if
              hyper-ide.is-open
              eval-x:x:/+/**/.folder-x
              add:x:/../*/create-widgets/*/micro.widgets.grid/**/rows/*/item/[0,1]/*/Value/*/*
                src
                  href:#
                  onclick
                    .folder-x:x:/@p5.io.unroll-path?value
                    hyper-ide.folder-explorer.select-path:x:/-?value
              set:x:/../*/create-widgets/*/micro.widgets.grid/**/rows/*/item/[0,1]/*/Value/*/*?name
                value:a
            eval-x:x:/+/**/Value
            create-widgets
              micro.widgets.grid
                class:striped hover
                parent:x:/@p5.web.widgets.get-parent/*/*?value
                position:2
                rows
                  item
                    Name:Path
                    Value
                      widgets
                        span
                          innerValue:x:/@p5.io.unroll-path?value
                  item
                    Name:Hyperlambda files
                    Value:x:/@desktop._list-files/*?count
                  item
                    Name:Node count
                    Value:x:/@hyper2lambda/**(!/\..comment)?count
                  item
                    Name:Comment count
                    Value:x:/@hyper2lambda/**/\..comment?count
                  item
                    Name:Size of folder
                    Value:"{0:#.#} KB"
                      :x:/@p5.io.folder.get-length/*?value
                  item
                    Name:Installation date
                    Value:x:/@p5.types.date.format?value
                  item
                    Name:Version
                    Value:?

            /*
             * Adding a code to comment ration pie chart.
             */
            eval-x:x:/+/*/*
            add:x:/+2/**/micro.widgets.chart.pie/*/data
              src
                Code:x:/@hyper2lambda/**(!/\..comment)?count
                Comments:x:/@hyper2lambda/**/\..comment?count
            eval-x:x:/+/**/.module-name
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:3
              class:row
              widgets
                div
                  class:col
                  widgets
                    h3
                      innerValue:Code to comments ratio
                    micro.widgets.chart.pie
                      data
                div
                  class:col
                  widgets
                    h3
                      innerValue:Module's relative size in KB
                    container
                      oninit

                        /*
                         * Forward evaluated above.
                         */
                        .module-name:x:/../*/p5.io.unroll-path?value
                        split:x:/-?value
                          =:/

                        /*
                         * Listing folders and retrieving each folder's size in KB.
                         */
                        list-folders:/modules/
                        for-each:x:/@list-folders/*?name
                          p5.io.folder.get-length:x:/@_dp?value
                          /:x:/-/*?value
                            _:1024
                          eval-x:x:/+/*/*
                          split:x:/@_dp?value
                            =:/
                          add:x:/../*/create-widgets/*/*/data
                            src:"{0}:{1}"
                              :x:/@split/0/-?name
                              :x:/..for-each/*/\/?value

                        /*
                         * Creating a bar chart displaying each module folder's size.
                         */
                        add:x:/+/*/*/data/*/{0}
                          :x:/@split/0/-?name
                          src
                            color:LightGreen
                        create-widgets
                          micro.widgets.chart.column
                            parent:x:/../*/_event?value
                            data
            /*
             * Looping through each [create-event] invocation in files in folder, and
             * creating a widget for each.
             */
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:4
              element:h3
              innerValue:Active Events created by module
            create-container-widget:desktop-help-events-for-folder
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:5
              element:ol
            for-each:x:/@hyper2lambda/**(/create-event|/p5.events.create)?value
              create-widget
                parent:desktop-help-events-for-folder
                element:li
                widgets
                  text:<strong>[
                  a
                    href:#
                    innerValue:x:/@_dp?value
                    .event:x:/@_dp?value
                    onclick
                      get-widget-property:x:/../*/_event?value
                        .event
                      micro.lambda.contract.get:x:/-/*/*?value
                      create-widgets
                        micro.widgets.modal
                          widgets
                            h3
                              innerValue:Lambda contract
                            pre
                              innerValue:x:/@micro.lambda.contract.get/0
                  text:]</strong>
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:6
              element:h3
              innerValue:Hyperlambda file information

            /*
             * Deleting button and cover widget.
             */
            delete-widget:x:/../*/_event?value
            delete-widget:desktop-waiting-for-meta

        ul
          visible:false
          class:desktop-help-meta-file-list
          events

            /*
             * Returns all Hyperlambda files recursively from the given folder.
             */
            desktop._list-files
              add:x:/../*/return
                list-files:x:/../*/_arg?value
                  filter:.hl
              list-folders:x:/../*/_arg?value
              for-each:x:/-/*?name
                add:x:/../*/return
                  desktop._list-files:x:/@_dp?value
              return

          oninit

            /*
             * Forward evaluated above.
             */
            .folder:x:/../*/folder?value

            /*
             * Retrieving all Hyperlambda files recursively inside of folder.
             */
            p5.io.unroll-path:x:/@.folder?value
            desktop._list-files:x:/@p5.io.unroll-path?value

            /*
             * Creating one "li" element for each file, with its description, if existing.
             */
            for-each:x:/@desktop._list-files/*?name
              load-file:x:/@_dp?value
                convert:false
              hyper2lambda:x:/@load-file/*?value
                keep-comments:true

              /*
               * Checking if file contains a descriptive comment, and if so, creating one "li" element containing its
               * description.
               */
              .filename
              set:x:/@.filename?value
                replace:x:/@_dp?value
                  src:x:/@p5.io.unroll-path?value
                  dest:../
              if
                hyper-ide.is-open

                /*
                 * Hyper IDE is running, making sure allow for clicking file path to edit file.
                 */
                insert-before:x:/../**/create-widget/*/widgets/0
                  src
                    a
                      href:#
                      .filepath:x:/@_dp?value
                      innerValue:x:/@.filename?value
                      style:"display:inline-block;margin-bottom:1rem;"
                      onclick
                        get-widget-property:x:/../*/_event?value
                          .filepath
                        hyper-ide.folder-explorer.select-path:x:/-/*/*?value

              else

                /*
                 * Hyper IDE is not running, simply adding a label widget to "li" widget.
                 */
                insert-before:x:/../**/create-widget/*/widgets/0
                  src
                    label
                      innerValue:x:/@.filename?value

              if:x:/@hyper2lambda/0?name
                =:..comment

                /*
                 * File contains an initial comment.
                 */
                markdown2html:x:/@hyper2lambda/0?value
                eval-x:x:/+/*/*/*/innerValue
                create-widget
                  parent:x:/../*/_event?value
                  element:li
                  widgets
                    div
                      innerValue:x:/@markdown2html?value

              else

                /*
                 * No initial comment in file.
                 */
                create-widget
                  parent:x:/../*/_event?value
                  element:li
                  widgets
                    div
                      class:air-inner rounded warning
                      innerValue:<p>File does not contain information about what it does!</p>

          widgets
