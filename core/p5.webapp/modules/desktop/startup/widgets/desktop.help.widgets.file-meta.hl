/*
 * Creates the __[desktop.help.widgets.file-meta]__ extension widget that displays meta information for all files
 * recursively beneath some specified **[folder]** argument. This is the widget
 * that renders the information you're currently reading. Made public to make it easy to
 * create meta information traversel for your own modules.
 *
 * Among other things, it will retrieve the first comment from your files, and display
 * it as Markdown.
 */
create-event:desktop.help.widgets.file-meta

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    folder:string

  /*
   * Returns widget to caller.
   */
  eval-x:x:/+/**/.folder|/+/*/*/*/button/*/innerValue
  return
    container
      style:"clear:both;"
      widgets
        button
          innerValue:Retrieve meta information for {0}
            :x:/../*/folder?value
          onclick

            /*
             * Creating a cover widget, returning to client, and invoking actual implementation.
             */
            create-widgets
              micro.widgets.cover:desktop-waiting-for-meta
            eval-x:x:/+/**/p5.web.widgets.ajax-events.raise
            micro.lambda.create-timeout
              milliseconds:1
              onfinish
                p5.web.widgets.ajax-events.raise:x:/../*/_event?value
                  .onclick

          .onclick

            /*
             * Retrieving parent widget's first 'ol' child, and setting it to visible.
             */
            p5.web.widgets.get-parent:x:/../*/_event?value
            set-widget-property:x:/-/*/*?value
              class:air-inner rounded success
            p5.web.widgets.find:x:/-2/*/*?value
              element:ol
            set-widget-property:x:/-/*/*?value
              visible:true

            /*
             * Figuring out total node count for entire folder, and displaying to user.
             */
            .folder:x:/../*/folder?value
            desktop._list-files:x:/-?value
            load-file:x:/-/*?name
              convert:false
            hyper2lambda:x:/@load-file/*?value
              keep-comments:true
            p5.io.unroll-path:x:/@.folder?value
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:1
              element:h2
              innerValue:Module meta information
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              position:2
              element:p
              innerValue:Below are all Hyperlambda files inside of the folder.
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:hr
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Path; <em>'{0}'</em>
                :x:/@p5.io.unroll-path?value
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Folder's total Hyperlambda file count; {0}
                :x:/@desktop._list-files/*?count
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Folder's total node count; {0}
                :x:/@hyper2lambda/**?count
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Folder's total comment count; {0}
                :x:/@hyper2lambda/**/\..comment?count
            p5.io.folder.get-length:x:/@.folder?value
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Folder's size in bytes; {0}
                :x:/@p5.io.folder.get-length/*?value
            p5.io.folder.get-creation-time:x:/@.folder?value
            p5.types.date.format:x:/-/*?value
              format:"ddd dd. MMM yyyy - HH:mm"
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h4
              innerValue:Folder was created; {0}
                :x:/@p5.types.date.format?value

            /*
             * Looping through each [create-event] invocation in files in folder, and
             * creating a widget for each.
             */
            create-widget
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:h3
              innerValue:Events created by module
            create-container-widget:desktop-help-events-for-folder
              parent:x:/@p5.web.widgets.get-parent/*/*?value
              element:ol
            for-each:x:/@hyper2lambda/**(/create-event|/p5.events.create)?value
              create-widget
                parent:desktop-help-events-for-folder
                element:li
                innerValue:<strong>[{0}]</strong>
                  :x:/@_dp?value

            /*
             * Deleting button and cover widget.
             */
            delete-widget:x:/../*/_event?value
            delete-widget:desktop-waiting-for-meta

        ol
          visible:false
          events

            /*
             * Returns all Hyperlambda files recursively from the given folder.
             */
            desktop._list-files
              add:x:/../*/return
                list-files:x:/../*/_arg?value
                  filter:.hl
              list-folders:x:/../*/_arg?value
              for-each:x:/-/*?name
                add:x:/../*/return
                  desktop._list-files:x:/@_dp?value
              return

          oninit

            /*
             * Forward evaluated above.
             */
            .folder:x:/../*/folder?value

            /*
             * Retrieving all Hyperlambda files recursively inside of folder.
             */
            p5.io.unroll-path:x:/@.folder?value
            desktop._list-files:x:/@p5.io.unroll-path?value

            /*
             * Creating one "li" element for each file, with its description, if existing.
             */
            for-each:x:/@desktop._list-files/*?name
              load-file:x:/@_dp?value
                convert:false
              hyper2lambda:x:/@load-file/*?value
                keep-comments:true

              /*
               * Checking if file contains a descriptive comment, and if so, creating one "li" element containing its
               * description.
               */
              .filename
              set:x:/@.filename?value
                replace:x:/@_dp?value
                  src:x:/@p5.io.unroll-path?value
                  dest:../
              if
                hyper-ide.is-open

                /*
                 * Hyper IDE is running, making sure allow for clicking file path to edit file.
                 */
                insert-before:x:/../**/create-widget/*/widgets/0
                  src
                    a
                      href:#
                      .filepath:x:/@_dp?value
                      innerValue:x:/@.filename?value
                      style:"display:inline-block;margin-bottom:1rem;"
                      onclick
                        get-widget-property:x:/../*/_event?value
                          .filepath
                        hyper-ide.folder-explorer.select-path:x:/-/*/*?value

              else

                /*
                 * Hyper IDE is not running, simply adding a label widget to "li" widget.
                 */
                insert-before:x:/../**/create-widget/*/widgets/0
                  src
                    label
                      innerValue:x:/@.filename?value

              if:x:/@hyper2lambda/0?name
                =:..comment

                /*
                 * File contains an initial comment.
                 */
                markdown2html:x:/@hyper2lambda/0?value
                eval-x:x:/+/*/*/*/innerValue
                create-widget
                  parent:x:/../*/_event?value
                  element:li
                  widgets
                    div
                      innerValue:x:/@markdown2html?value

              else

                /*
                 * No initial comment in file.
                 */
                create-widget
                  parent:x:/../*/_event?value
                  element:li
                  widgets
                    div
                      class:air-inner rounded warning
                      innerValue:<p>File does not contain information about what it does!</p>

          widgets
