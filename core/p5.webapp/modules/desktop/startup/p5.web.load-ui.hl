
/*
 * File responsible for creating our default [p5.web.load-ui] event, which basically is our default URL resolver,
 * and "micro web operating system", allowing the user to see his desktop, installed apps, and visit the Bazar.
 */





/*
 * Creating the default [p5.web.load-ui] URL resolver and Active Event.
 *
 * If you wish to entirely bypass the default "web operating system", with its desktop, and its Bazar, feel free
 * to create your own module, which overrides the default [p5.web.load-ui] event.
 *
 * Just remember to somehow make sure you're able to setup the server salt, and have the user supply a root password.
 *
 * Hint; You can make sure your users are setting up a server salt and root password, by invoking the 
 * "@DESKTOP/initialize/initialize-server.hl" file, which will do nothing, if the server salt and root password has 
 * already been setup, and return "false" if it is safe to proceed to your own app, and the server salt and root 
 * password has already been setup.
 *
 * Or, you can see the logic this event provides for checking if the server salt and root pasword has already been setup.
 * The latter would probably be slightly more efficient, and require less resources.
 */
create-event:p5.web.load-ui

  /*
   * First we must check if server has been setup, with a root password, 
   * and a server salt - And if not, we do so.
   */
  if
    p5.auth._has-salt
    not
    or
      p5.auth._root-password-is-null

    /*
     * No server salt, or no root password, hence the server needs to be initialized with a server salt, 
     * and a root password, before we can proceed.
     */
    micro.evaluate.file:@DESKTOP/initialize/initialize-server.hl

  else

    /*
     * Setting default title of page.
     */
    p5.web.page.set-title:Phosphorus V - 7.0 - In the beginning there was Hyperlambda

    /*
     * Loading "pre" plugins, making sure we pass in [url] to all plugin events.
     */
    vocabulary:~desktop.plugins.pre.
    for-each:x:/@vocabulary/*?value
      eval-x:x:/+/*
      eval:x:/@_dp?value
        url:x:/../*/url?value

    if:x:/../*/url?value
      =:/

      /*
       * Root URL was requested, loading the main desktop, but first checking if system has been configured with
       * a "default app".
       */
      p5.config.get:p5.core.default-app
      if:x:/@p5.config.get/*?value
        micro.evaluate.file:x:/@p5.config.get/*?value
      else
        throw:No default app configured in your web.config, feel free to add up e.g. '<add key="p5.core.default-app" value="/modules/desktop/launch.hl" />' to make sure your desktop starts initially, in your <appSettings> section.

    else

      /*
       * A specific application was requested, hence evaluating it's "launch.hl" file.
       *
       * Notice, to create some basic URL rewriting support, for all apps, we only use the first part of the URL
       * when determining which app to load. This allows any app to create sub URLs, with its own folders, etc, and
       * then do its own internal mapping, by using the URL itself, which is passed in as [url] into the app's
       * "launch.hl" file.
       *
       * For instance, to load some sub folder in Micro, the URL could be "/micro/foo/bar", etc ...
       * However, determining the URL, and doing the actual resolving, is up to the specific module itself, 
       * and not done by this startup file.
       */
      split:x:/../*/url?value
        =:/
        =:?

      /*
       * Checking if app exists, and if not, returning some simple 404 Not found.
       */
      if
        fetch:x:/0/0?value
          file-exists:/modules/{0}/launch.hl
            :x:/@split/0?name
        not

        /*
         * Non-existing URL.
         */
        p5.web.response.set-status-code:404
        p5.web.echo:Oops 404, the place where URLs go to die!
        return

      /*
       * Checking if user has access to module.
       *
       * Defaulting access to "false".
       */
      if
        p5.auth.has-access-to-path:bool:false
          filter:p5.module
          path:/modules/{0}/launch.hl
            :x:/@split/0?name
        not

        /*
         * User doesn't have access to this module.
         */
        p5.web.response.set-status-code:403
        micro.css.include
        p5.core.login
        return

      /*
       * Loading the file, and evaluating it.
       */
      eval-x:x:/+/*/url
      micro.evaluate.file:/modules/{0}/launch.hl
        :x:/@split/0?name
        url:x:/../*/url?value

    /*
     * Loading "post" plugins, making sure we pass in [url] to all plugin events.
     */
    vocabulary:~desktop.plugins.post.
    for-each:x:/@vocabulary/*?value
      eval-x:x:/+/*
      eval:x:/@_dp?value
        url:x:/../*/url?value
