
/*
 * Creates the __[p5.error.Exception]__ Active Event.
 */

/*
 * This event is invoked every time any exception occurs in the system, unless some other
 * event has explicitly overridden it, for a particular type of exception.
 *
 * To override this logic for specific exception types, create an event having the typename
 * of your exception, instead of the _"Exception"_ name of this event.
 */
create-event:p5.error.Exception

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    message:string
    trace:string
    type:string

  /*
   * Making sure we correctly return exception to client.
   */
  p5.web.response.set-status-code:500
  p5.web.response.set-status-description:500 Interal server error
  p5.html.html-encode:x:/../*/message?value
  p5.html.html-encode:x:/../*/trace?value

  /*
   * We only show stack trace for LambdaExceptions
   */
  if:x:/../*/type?value
    ~:Lambda

    p5.web.echo:@"<h1>{0}</h1><p><strong>Message</strong> - '{1}'</p><pre class=""warning"">{2}</pre>"
      :x:/../*/type?value
      :x:/../*/p5.html.html-encode/[0,1]?value
      :x:/../*/p5.html.html-encode/[1,2]?value

  else

    p5.web.echo:@"<h1>{0}</h1><p><strong>Message</strong> - '{1}'</p>"
      :x:/../*/type?value
      :x:/../*/p5.html.html-encode/[0,1]?value


  /*
   * Returning true to caller, to stop exception from "propagating".
   */
  return:bool:true
