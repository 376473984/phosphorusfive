
/*
 * Allows the user to uninstall a module.
 */





/*
 * Creating a modal widget that displays all modules, and allows the user to uninstall a module.
 */
create-widgets
  micro.widgets.modal:desktop-uninstall-app
    widgets
      h3
        innerValue:Uninstall module(s)
      micro.widgets.grid:desktop-uninstall-app-grid
        oninit

          /*
           * Checking which apps are installed, and creating one grid row for each app.
           */
          list-folders:/modules/
          for-each:x:/@list-folders/*?name

            /*
             * Figuring out app's name.
             */
            split:x:/@_dp?value
              =:/

            /*
             * Adding item to grid
             */
            eval-x:x:/+/*/*/*|/+/**/.app-name
            add:x:/../*/micro.widgets.grid.databind
              src
                item
                  Name:x:/@split/0/-?name
                  Uninstall
                    style:"width:5px;"
                    widgets
                      button
                        innerValue:@"<span class=""icon-bin""></span>"
                        title:Delete this app. Warning, this action is permanent!
                        onclick

                          /*
                           * Forward evaluated above.
                           */
                          .app-name:x:/@split/0/-?name

                          /*
                           * Uninstalling app.
                           */
                          desktop.modules.uninstall:/modules/{0}/
                            :x:/@.app-name?value

                          /*
                           * Reloading location for simplicity reasons, to make sure
                           * we re-create all desktop icons, etc.
                           */
                          p5.web.reload-location

          /*
           * Databinding grid.
           */
          micro.widgets.grid.databind:desktop-uninstall-app-grid

      div
        class:right
        widgets
          div
            class:strip
            widgets
              button
                innerValue:Close
                oninit

                  /*
                   * Setting focus to close button initially.
                   */
                  micro.page.set-focus:x:/../*/_event?value

                onclick

                  /*
                   * Deleting modal widget.
                   */
                  delete-widget:desktop-uninstall-app
