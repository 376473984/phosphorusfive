/*
 * Allows the user to uninstall a module.
 */





/*
 * Creating a modal widget that displays all modules, and allows the user to uninstall a module.
 */
create-widgets
  micro.widgets.modal:desktop-uninstall-app
    widgets
      h3
        innerValue:Uninstall
      micro.widgets.grid:desktop-uninstall-app-grid
        class:hover
        oninit

          /*
           * Checking which apps are installed, and creating one grid row for each app.
           */
          desktop.modules.list
          for-each:x:/@desktop.modules.list/*

            /*
             * Adding item to grid
             */
            eval-x:x:/+/*/*/*|/+/**/.app-name
            add:x:/../*/micro.widgets.grid.databind
              src
                item
                  Name:x:/@_dp/#?name
                  Uninstall
                    style:"width:5px;"
                    widgets
                      button
                        innerValue:@"<span class=""icon-bin""></span>"
                        title:Uninstall module
                        onclick

                          /*
                           * Forward evaluated above.
                           */
                          .app-name:x:/@_dp/#?name

                          /*
                           * Making sure user confirms uninstallation, with another modal widget.
                           */
                          eval-x:x:/+/*/*/*/h3/*/innerValue
                          create-widgets
                            micro.widgets.modal:desktop-confirm-uninstallation
                              widgets
                                div
                                  class:warning air-inner
                                  widgets
                                    h3
                                      innerValue:Confirm uninstallation of '{0}'
                                        :x:/@.app-name?value
                                    p
                                      innerValue:@"<strong>Warning</strong> - This action is permanent, and will entirely delete your module, and uninstall it from your Phosphorus Five installation.
Are you sure you want to continue?"
                                div
                                  class:strip right
                                  widgets
                                    button
                                      innerValue:Yes
                                      onclick

                                        /*
                                         * Forward evaluated above.
                                         */
                                        .app-name:x:/@_dp/#?name

                                        /*
                                         * Uninstalling app, wrapping our attempt in a try/catch block,
                                         * to make sure we show intelligent feedback if uninstallation was
                                         * not successful.
                                         */
                                        try
                                          desktop.modules.uninstall:x:/@.app-name?value
                                        catch
                                          micro.windows.info:x:/@message?value
                                            class:micro-windows-info warning
                                          return

                                        /*
                                         * Reloading location for simplicity reasons, to make sure
                                         * we re-create all desktop icons, etc.
                                         *
                                         * However, before we can reload location, we'll need to make
                                         * sure user is notified on next pageload that uninstallation was a success.
                                         */
                                        desktop.evaluate.on-next-pageload
                                          lambda

                                            /*
                                             * Giving user some feecback, in addition to re-launching uninstall
                                             * module, in case user wants to uninstall multiple modules.
                                             */
                                            micro.windows.info:Your module was successfully uninstalled
                                              class:micro-windows-info success

                                        /*
                                         * Reloading location, now with an "evaluate on next pageload" lambda object.
                                         */
                                        p5.web.reload-location

                                    button
                                      innerValue:No
                                      oninit

                                        /*
                                         * Setting initial focus to "No" button.
                                         */
                                        micro.page.set-focus:x:/../*/_event?value

                                      onclick

                                        /*
                                         * Deleting secondary modal widget.
                                         */
                                        delete-widget:desktop-confirm-uninstallation

          /*
           * Databinding grid.
           */
          micro.widgets.grid.databind:desktop-uninstall-app-grid

      div
        class:right
        widgets
          div
            class:strip
            widgets
              button
                innerValue:Close
                oninit

                  /*
                   * Setting focus to close button initially.
                   */
                  micro.page.set-focus:x:/../*/_event?value

                onclick

                  /*
                   * Deleting modal widget.
                   */
                  delete-widget:desktop-uninstall-app
