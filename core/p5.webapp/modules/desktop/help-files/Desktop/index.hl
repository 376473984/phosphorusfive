/*
 * This is our main "landing page" for our Desktop help files.
 */
h2
  innerValue:Desktop

p
  innerValue:@"The Desktop module is the GUI interface for Phosphorus Five, and responsible for loading up your modules, and
doing basic URL resolving. It is hence arguably the <em>'core'</em> of the graphical parts, and the main entry point of
interaction for Phosphorus Five. It hence is the <em>'host application'</em> for all your modules and applications
in Phosphorus Five."

/*
 * All files at "root" of Desktop help folder.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@DESKTOP/help-files/Desktop/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
