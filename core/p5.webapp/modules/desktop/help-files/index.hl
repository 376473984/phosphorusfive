/*
 * This is our main "landing page" for the help system in Hyper IDE.
 *
 * This file displays the general "index", and such serves as a starting ground
 * for other files in the help system.
 */
h2
  innerValue:Phosphorus Five help files

img
  style:"margin-left:1rem; float:right;max-width: 20%;"
  src:modules/hyper-ide/media/logo.svg

p
  innerValue:@"Welcome to the help files for Phosphorus Five. These help files can be launched from within any module, and will guide you through
both standard usage of Phosphorus Five, and its modules - In addition to how to create your own apps. This allows you to have these
help files open from within the module you want to learn about."

p
  innerValue:@"These documentation files, are consciously created such that they should bring you up from
the point where you can barely interact with a computer, to the point of where you're a <em>'guru software developer'</em>.
If they don't, feel free to toss me an email at thomas@gaiasoul.com. If this is your first visit to Phosphorus Five,
I suggest you start out with either Hyper IDE (if you're a software developer), or Camphora Five, if you have never
created software before. If you want to dive into creating Phosphorus Five web apps yourself, you'd probably want to
start with the <em>'Hyperlambda'</em> section."

literal
  element:h4
  innerValue:Main section


/*
 * All files at "root" of help files folder.
 */
desktop.help.widgets.help-index
  folder:@DESKTOP/help-files/

literal
  element:h3
  innerValue:Sub sections


/*
 * Sub-sections, these are basically folders beneath the root folder of our help system.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other folders in our main help folder.
     */
    list-folders:@DESKTOP/help-files/

    /*
     * Then retrieving all "help plugin folders".
     */
    .exe
      for-each:x:/../*/~desktop.help-files?value
        insert-before:x:/.
          src:x:/@_dp?value
    vocabulary:~desktop.help-files.
    for-each:x:/@vocabulary/*?value
      insert-before:x:/@.exe/0
        src:x:/@_dp?value
    eval:x:/@.exe

    /*
     * Looping through each folders in "/help-files/" folder.
     */
    for-each:x:/@list-folders/*|/@eval/*?name

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp?value
        =:/
      replace:x:/@split/0/-?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            role:button
            innerValue:x:/@replace?value
            .folder:x:/@_dp?value
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .folder
              desktop.help.display-file:{0}index.hl
                :x:/@get-widget-property/*/*?value

h3
  innerValue:Supplying a patch

p
  innerValue:@"Notice, for legal reasons I don't accept patches for Phosphorus Five. This is true regardless of
what an amazingly skilled software developer you are. However, if you send me one or two lines of code, or accurately describe
a bug or a problem, I would be happy to assist you. I prefer accepting such requests at
the <a href=""https://github.com/polterguy/phosphorusfive"">GitHub project website</a>, for obvious reasons. It
is also extremely easy to create your own modules and plugins for Phosphorus Five, extending it, if you find
it lacking some favourite feature of yours."

p
  innerValue:For ethical reasons, I don't wrap software components or libraries that are not Open Source in nature! At least not into the core itself.

h3
  innerValue:Press

p
  innerValue:@"If you want to write about Phosphorus Five for a press article, you can use the logo of
the bird, which you can find at the top of this article. You can also contact me at thomas@gaiasoul.com, if you
want to interview me for some reasons, or need additional information about the system."

h3
  innerValue:Print documentation

p
  innerValue:@"Below is a button that will automatically generate a PDF file for you, containing all Markdown files in the documentation system. Notice, this process might take some time, depending upon how much documentation you have for your installation."

div
  class:right
  widgets
    button
      class:large
      title:Prints documentation files
      innerValue:@"<span class=""icon-printer""></span>"
      onclick

        /*
         * Creating a cover widget.
         */
        create-widgets
          micro.widgets.cover:desktop-help-print-cover
            message:Please wait while I generate your PDF file ...

        /*
         * Creating a timeout to make sure cover widget is displayed on client side.
         */
        micro.lambda.create-timeout
          milliseconds:1
          onfinish

            /*
             * Creates our PDF file.
             */
            micro.evaluate.file:@DESKTOP/helpers/create-documentation-pdf.hl

            /*
             * Deleting cover widget, and downloading PDF.
             */
            delete-widget:desktop-help-print-cover
            micro.download.file:x:/@micro.evaluate.file?value
