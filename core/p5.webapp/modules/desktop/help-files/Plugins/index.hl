/*
 * This is our main "landing page" for our Plugins help files.
 */
h2
  innerValue:Plugins

img
  class:desktop-help-icon-image
  src:"https://phosphorusfive.files.wordpress.com/2018/03/2000px-plastic_brick_red-svg.png"

p
  innerValue:@"Here you can find the reference documentation for all the plugins in Phosphorus Five.
These parts of the documentation explains the different Active Event assemblies, their Active Events,
and how to consume these in your own Hyperlambda applications. Notice, this part of the documentation is fairly
dry and technical in nature. If this is your first encounter with Phosphorus Five, I suggest you rather
start out with the <em>'Hyperlambda'</em> section, which provides a tutorial point of entry to P5."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@DESKTOP/help-files/Plugins/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
