

/*
 * Creates our "desktop", which basically just allows the user to select which application he wants to start.
 *
 * By default, there's only one application (or module) in P5, which is the Bazar.
 */





/*
 * Including main Micro CSS files, and making sure root "cnt" widget 
 * has the "container-fullscreen" CSS class, in addition to that it's empty.
 *
 * Notice, we also include the "Serious" skin at this point.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css





/*
 * Including main desktop CSS file.
 */
p5.web.include-css-file:@DESKTOP/media/main.css





/*
 * Creating main content container.
 */
create-container-widget:desktop-main-container
  class:container





/*
 * Now we can determine all modules the user has installed on his system, and create a "desktop icon" for
 * each of these modules that has a "desktop.hl" file.
 */
list-folders:/modules/





/*
 * Listing all "desktop.hl" files inside the results of our above folder listing operation.
 */
list-files:x:/@list-folders/*?name
  filter:desktop.hl





/*
 * Looping through all "desktop.hl" files from above, and creating a widget wrapping each of them, 
 * that will invoke its associated "launch.hl" file, once clicked.
 */
for-each:x:/@list-files/*?name

  /*
   * Currently iterated app has a desktop widget declaration file.
   */
  load-file:x:/@_dp?value
  add:x:/../*/create-widget/*/widgets/*/div/=desktop-apps/*/widgets
    src:x:/@load-file/*/*





/*
 * Creating actual desktop widget.
 *
 * This widget basically becomes your "desktop", with one icon, for each "component" that has a "desktop.hl" file.
 */
create-widget
  parent:desktop-main-container
  class:row
  widgets
    div
      class:col-100
      widgets
        div
          class:right
          widgets
            div
              class:strip toolbar
              style:"display:inline-block;"
              widgets
                literal
                  element:button
                  oninit

                    /*
                     * Dislpaying either "login" button or "logout" button, depending upon user's current status.
                     */
                    whoami
                    if:x:/@whoami/*/default?value

                      /*
                       * User is not logged in.
                       */
                      set-widget-property:x:/../*/_event?value
                        innerValue:@"<span class=""icon-key""></span>"
                        title:Log in

                    else

                      /*
                       * User is logged in.
                       */
                      set-widget-property:x:/../*/_event?value
                        innerValue:@"<span class=""icon-power-off""></span>"
                        title:Log out

                  onclick

                    /*
                     * Either logging user in, or out, depending upon his current status.
                     */
                    whoami
                    if:x:/@whoami/*/default?value
                      p5.core.login
                    else
                      p5.core.logout
    div:desktop-apps
      class:col-100 desktop-apps
      widgets
