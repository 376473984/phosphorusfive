
/*
 * Executed after server has been initially installed. Guides the 
 * user through setting up a secure root password, etc
 */


/*
 * Creating actual guide form
 */
create-widget:set-root-pwd-tab
  parent:guide
  class:flash-from-left col-xs-12 col-md-4 col-md-push-2 col-sm-6 col-sm-push-1 col-lg-4 col-lg-push-3
  widgets


    /*
     * First paragraph of informational text
     */
    literal
      element:h2
      class:hidden-xs
      innerValue:@"Root password"
    literal
      element:p
      innerValue:Then create a root password


    /*
     * Root password container wrapper
     */
    container
      class:form
      widgets
        container
          class:form-group
          widgets
            container
              class:input-group
              widgets
                literal
                  class:input-group-addon
                  innerValue:Password
                literal:root-password
                  element:input
                  type:password
                  class:form-control
                  placeholder:Root password ...
                  oninit
                    send-javascript:@"$('#root-password').focus().select();"
                  onkeypress:"return p5.keyInstallPressed(event);"
            container
              class:input-group prepend-top
              widgets
                literal
                  class:input-group-addon
                  innerValue:Repeat
                literal:root-password-repeat
                  element:input
                  type:password
                  class:form-control
                  placeholder:Repeat ...
                  onkeypress:"return p5.keyInstallPressed(event);"
            literal:update-root
              element:button
              class:btn btn-primary prepend-top col-sm-4 col-sm-push-8 col-xs-6 col-xs-push-6
              innerValue:Save
              onclick
                _widgets
                  root-password
                  root-password-repeat
                get-widget-property:x:/-/*?name
                  value
                if:x:/-/*/root-password/*?value
                  !=:x:/./-/*/root-password-repeat/*?value
                  sys42.windows.info-tip:Sorry, passwords don't match
                    _class:info-window-error
                  send-javascript:@"$('#root-password').focus().select();"
                else
                  set:x:/+/*?value
                    src:x:/../*/get-widget-property/*/root-password/*?value
                  p5.security.set-root-password
                    password

                  // Logging in root user until we have created our "superuser" account
                  set:x:/+/*/password?value
                    src:x:/../*/get-widget-property/*/root-password/*?value
                  login
                    username:root
                    password
                  delete-widget:set-root-pwd-tab
                  sys42.utilities.execute-lambda-file:@CMS/installation/system-finished.hl
                  sys42.cms.create-navbar-menu

include-javascript:@"
p5.keyInstallPressed = function(e) {
  if(e.keyCode == 13) {
    p5.$('update-root').raise('onclick');
    return false;
  }
}"

