
/*
 * Editor for [html] types of pages, relies on CKEditor
 */


/*
 * Loading page from database
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_page-id?value


/*
 * Setting the value of textarea to [content] from database
 */
set:x:/../*/create-widget/*/widgets/*/=ck-editor/*/_value?value
  src:x:/../*/select-data/*/*/content?value


/*
 * Creates the editor widget
 */
create-widget
  parent:cms-specialized-editor-surface
  class:col-xs-12
  style:"display:inline-block;width:100%;"
  widgets
    sys42.widgets.ck-editor:ck-editor
      _height:451
      _value


      /*
       * Lambda events for Editor Surface
       */
      _events


        /*
         * "Sink" event required by main editor to retrieve data
         * from specialized editor during saving process
         */
        sys42.get-page-editor-data
          insert-before:x:/../0
            src
              type:html
          get-widget-property:ck-editor
            innerValue
          insert-before:x:/../0
            src:content
          set:x:/../0?value
            src:x:/../*/get-widget-property/*/*?value
