
/*
 * Editor for [controls] types of pages
 */


/*
 * Loading page from database
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_id?value


/*
 * Setting the content of editor_surface to the [lambda] from database
 */
set:x:/../*/create-widget/*/widgets/*/=editor_surface/*/innerValue?value
  src:x:/../*/select-data/*/*/lambda?value


/*
 * Making sure editor is loaded with correct parent widget.
 */
sys42.cms.get-specialized-editor-surface
set:x:/../*/create-widget/*/parent?value
  src:x:/./-?value


/*
 * Creating our widget to display our editor surface
 */
create-widget
  parent
  class:col-xs-12
  widgets
    sys42.widgets.codemirror:editor_surface
      _height:555px
      innerValue


      /*
       * Lambda events for Editor Surface
       */
      events


        /*
         * "Sink" event required by main editor to retrieve data
         * from specialized editor during saving process
         */
        sys42.get-page-editor-data
          insert-before:x:/../0
            src
              type:lambda
          get-widget-property:editor_surface
            innerValue
          insert-before:x:/../0
            src:lambda
          set:x:/../0?value
            src:x:/../*/get-widget-property/*/*?value
