
/*
 * Creates the toolbar for the CMS page editor, and starts the specialized editor for page type.
 *
 * Expects [_editor] being the name of the custom editor, and [_page-id] being the id of the page that is edited.
 */


/*
 * Loads all toolbar buttons, as plugins, and appends into toolbar button widget below.
 */
list-files:@CMS/page-editor/toolbar-buttons/
  filter:.hl
load-file:x:/@list-files/*?name
add:x:/../*/create-widget/**/container/=cms-editor-toolbar-buttons/*/widgets/*/container/*/widgets
  src:x:/@load-file/*/*


/*
 * Loads all specialized toolbar buttons, if there are any, as plugins, and appends into toolbar button widget below.
 */
if
  fetch:x:/0/0?value
    folder-exist:@CMS/page-editor/specialized-editors/{0}/toolbar-buttons/
      :x:/../*/_editor?value
  list-files:@CMS/page-editor/specialized-editors/{0}/toolbar-buttons/
    :x:/../*/_editor?value
  load-file:x:/@list-files/*?name
  add:x:/../*/create-widget/**/container/=cms-editor-toolbar-buttons/*/widgets/*/container/*/widgets
    src:x:/@load-file/*/*


/*
 * Creating toolbar wrapper widget.
 */
create-widget
  parent:cms-page-editor-surface
  widgets


    /*
     * Custom editor surface.
     *
     * This is where the "specialized editor" is loaded, which depends upon what type of page this is.
     */
    container:cms-specialized-editor-surface

    /*
     * Bottom toolbar widget.
     *
     * This widget is dynamically populated by loading all files in "/toolbar-buttons/" folder.
     */
    container:cms-editor-toolbar-buttons
      class:col-md-12 text-right
      widgets
        container
          class:btn-group
          widgets

            /*
             * This is where the toolbar buttons actually ends up.
             */


/*
 * Loading "specialized editor", which depends upon which page type we're editing.
 */
eval-x:x:/+/*
sys42.utilities.execute-lambda-file:@CMS/page-editor/specialized-editors/{0}.hl
  :x:/../*/_editor?value
  _page-id:x:/../*/_page-id?value
