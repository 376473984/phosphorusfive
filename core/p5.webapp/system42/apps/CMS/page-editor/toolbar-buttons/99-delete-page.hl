
/*
 * Delete page button.
 */
button
  innerValue:@"<span class=""glyphicon glyphicon-trash""></span> Delete"
  class:btn btn-default diffuse btn-attach-top
  onclick

    /*
     * Making sure user doesn't accidentally delete one of our "special pages".
     */
    sys42.cms.get-edited-page-id
    if:x:/@sys42.cms.get-edited-page-id?value
      =:/app-loader
      or:x:/@sys42.cms.get-edited-page-id?value
        =:/

      /*
       * Oops, user tried to delete "special page", warning user, and setting focus to "select page dropdown",
       * before we return early.
       */
      sys42.windows.info-tip:This page cannot be deleted!
      send-javascript:@"$('#cms-select-page').focus().select();"
      return

    /*
     * Asking user to confirm deletion of page.
     */
    sys42.windows.confirm
      _header:Please confirm deletion
      _body:Are you sure you want to delete this page? This action cannot be undone!
      .oncancel

        /*
         * Making sure we set focus to "select page dropdown" if user closes dialogue, without
         * pressing "OK".
         */
        send-javascript:@"$('#cms-select-page').focus().select();"
      .onok

        /*
         * Deleting page.
         */
        sys42.delete-currently-edited-page

        /*
         * Making sure we set focus to "select page dropdown" if user closes dialogue, without
         * pressing "OK".
         */
        send-javascript:@"$('#cms-select-page').focus().select();"


  /*
   * Lambda events for delete button
   */
  events


    /*
     * Deletes the currently edited page
     */
    sys42.delete-currently-edited-page

      /*
       * Retrieving currently edited page, and deleting it.
       */
      sys42.cms.get-edited-page-id
      delete-data:x:@"/*/*/p5.page/""={0}"""
        :x:/@sys42.cms.get-edited-page-id?value

      /*
       * Settings currently edited page to "nothing", before we repopulate select dropdown, and invoke [edit-page], which
       * will simply clear the editor.
       */
      sys42.cms.set-currently-edited-page
      sys42.cms.populate-select-page
      sys42.cms.edit-page
      sys42.cms.create-navbar-menu
