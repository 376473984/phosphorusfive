
/*
 * Responsible for showing the "No access" page.
 */


/*
 * Loading up our template.
 */
p5.io.file.load:x:/../*/_template?value


/*
 * Checking if there's a user logged in, and if so, showing relevant feedback to user.
 */
whoami
if:x:/../*/whoami/*/default?value
  not

  /*
   * User is logged in, explaining that he does not have access to this URL
   */
  set:x:/../*/p5.io.file.load/*/*/create-widget/*/widgets/**/={content}?value
    src:@"<h2>Sorry!</h2>
<p>I cannot give you access to that page!</p>"

else

  /*
   * User is not logged in, explaining that he may have success accessing this page,
   * if he logs in. Since we're creating a "big" login form here though, we hide the
   * login button from the main menu, if it exist.
   *
   * PS!
   * We do this by adding our own widget hierarchy into the template just loaded.
   */
  add:x:/../*/p5.io.file.load/*/*/create-widget/*/widgets/**/={content}/./.
    src
      container
        class:col-xs-12
        widgets
          literal
            element:h2
            innerValue:Sorry!
          literal
            element:p
            innerValue:@"I cannot give you access to that page!"
          container
            class:form col-md-4 col-md-push-1 prepend-top flash-from-left col-xs-12
            widgets
              container
                class:form-group
                widgets
                  container
                    class:input-group
                    widgets
                      literal
                        class:input-group-addon
                        innerValue:Username
                      literal:login-username
                        element:input
                        type:text
                        class:form-control
                        placeholder:Username ...
                        oninit
                          p5.web.send-javascript:@"setTimeout(function(){$('#login-username').focus().select();},250);"
                          if
                            fetch:x:/0/0?value
                              widget-exist:login-menu-button
                            delete-widget:login-menu-button
                        onkeypress:"return p5.keyLoginPress(event);"
                  container
                    class:input-group
                    widgets
                      literal
                        class:input-group-addon
                        innerValue:Password
                      literal:login-password
                        element:input
                        type:password
                        class:form-control
                        placeholder:Password ...
                        oninit
                          p5.web.send-javascript:@"$('#user-username').focus().select();"
                        onkeypress:"return p5.keyLoginPress(event);"
                  container
                    class:text-right
                    widgets
                      literal:login-user
                        element:button
                        class:btn btn-primary btn-attach-top
                        innerValue:Login
                        onclick
                          _widgets
                            login-username
                            login-password
                          get-widget-property:x:/-/*?name
                            value
                          set:x:/../**/login/*/username?value
                            src:x:/../*/get-widget-property/*/login-username/*?value
                          set:x:/../**/login/*/password?value
                            src:x:/../*/get-widget-property/*/login-password/*?value
                          if
                            login
                              username
                              password
                              persist:bool:true

                            // Reloading page to make sure everything is wired up together correctly
                            p5.web.reload-location
                          else
                            sys42.windows.info-tip:Sorry, wrong credentials
                              _class:info-window-error

  /*
   * Making sure we entirely remove the literal:{content} widget.
   */
  set:x:/../*/p5.io.file.load/*/*/create-widget/*/widgets/**/={content}/.


/*
 * Evaluating template, now with modified content.
 */
eval:x:/../*/p5.io.file.load/*


/*
 * Returning intelligent status codes to client.
 */
p5.web.response.set-status-code:403
p5.web.response.set-status-description:403 Marvin cannot give you access to that page


/*
 * Including JavaScript to help handle carriage return key in login box.
 */
p5.web.include-javascript:@"p5.keyLoginPress = function(e) {
  if(e.keyCode == 13) {
    p5.$('login-user').raise('onclick');
    return false;
  }
}"

set-title:No access
if
  fetch:x:/0/0?value
    widget-exist:header
  set-widget-property:header
    innerValue:No access
