
/*
 * Responsible for loading up page of type [html].
 */

/*
 * Verifying user has access to [p5.page] object first.
 */
if
  sys42.verify-access:x:/../*/_args/*/role?value

  /*
   * User has access, now loading template and exchanging {content} 
   * with HTML from database.
   */
  load-file:x:/../*/_args/*/template?value

  /*
   * Exchanging literal:{content}'s value with HTML from database.
   */
  set:x:/-/*/*/create-widget/*/widgets/**/={content}?value
    src:x:/../*/_args/*/content?value

  /*
   * Evaluating template which now has its {content} replaced with HTML from database.
   */
  eval:x:/./*/load-file/*

else

  /*
   * Loading page that shows user does not have access to object.
   */
  sys42.execute-lambda-file:@CMS/page-loader/special-page/no-access.hl
    _template:@CMS/page-templates/default.hl


/*
 * Then making sure we modify the "header" and title of page
 */
if
  fetch:x:/0/0?value
    widget-exist:header
  set-widget-property:header
    innerValue:x:/../*/_args/*/name?value
set-title:x:/../*/_args/*/name?value

