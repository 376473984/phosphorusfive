
/*
 * Shows the form that allows for editing the GnuPG database
 */


/*
 * Verifying user is authorized to access this application
 */
whoami
if:x:/-/*/role?value
  !=:root
  throw:Sorry, this page is only accessible to logged in users


/*
 * Looping through each public key, adding to table widget below
 */
p5.crypto.list-public-keys
p5.crypto.list-private-keys
for-each:x:/../*/p5.crypto.list-public-keys/*
  _widgets
    container
      _key-id
      element:tr
      style:"cursor:pointer;"
      onclick
        get-widget-property:x:/../*/_event?value
          _key-id
        p5.crypto.get-key-details:x:/-/*/*?value
        format-date:x:/-/*/creation-time?value
          format:d. MMM yyyy
        format-date:x:/-2/*/expires?value
          format:d. MMM yyyy
        join:x:/../*/p5.crypto.get-key-details/*/user-ids/*?value
          sep:"\r\n"
        html-encode:x:/-?value
        eval-x:x:/+
        _tbl:@"<table class=""table table-hover""><thead><tr><th>Field</th><th>Value</th></tr></thead>
<tr>
  <td>Algorithm</td>
  <td>{0}</td>
</tr>
<tr>
  <td>Strength</td>
  <td>{1}</td>
</tr>
<tr>
  <td>Creation date</td>
  <td>{2}</td>
</tr>
<tr>
  <td>Encryption key</td>
  <td>{3}</td>
</tr>
<tr>
  <td>Master key</td>
  <td>{4}</td>
</tr>
<tr>
  <td>Revoked</td>
  <td>{5}</td>
</tr>
<tr>
  <td>Version</td>
  <td>{6}</td>
</tr>
<tr>
  <td>Expires</td>
  <td>{7}</td>
</tr>
<tr>
  <td>Fingerprint</td>
  <td>{9}</td>
</tr>
<tr>
  <td colspan=""2""><pre><small>{8}</small></pre></td>
</tr>
</table>"
          :x:/../*/p5.crypto.get-key-details/*/algorithm?value
          :x:/../*/p5.crypto.get-key-details/*/strength?value
          :x:/../*/format-date/[0,1]?value
          :x:/../*/p5.crypto.get-key-details/*/is-encryption-key?value
          :x:/../*/p5.crypto.get-key-details/*/is-master-key?value
          :x:/../*/p5.crypto.get-key-details/*/is-revoked?value
          :x:/../*/p5.crypto.get-key-details/*/version?value
          :x:/../*/format-date/[1,2]?value
          :x:/../*/html-encode?value
          :x:/../*/p5.crypto.get-key-details/*/fingerprint?value
        eval-x:x:/+/*/_body
        sys42.confirm-window
          _header:Key details
          _body:x:/../*/_tbl?value
          _extra-buttons
            literal
              _key-id
              element:button
              class:btn btn-default
              innerValue:Delete key
              onclick
                send-javascript:@"$('#confirm-window').modal('toggle');"
                get-widget-property:x:/../*/_event?value
                  _key-id
                p5.crypto.delete-private-key:x:/../*/get-widget-property/*/*?value
                p5.crypto.delete-public-key:x:/../*/get-widget-property/*/*?value
                delete-widget:gnu-pg-keys
                sys42.execute-lisp-file:/system42/apps/gnupg.hl
            literal
              _key-id
              element:button
              class:btn btn-default
              innerValue:Submit to keyserver
              onclick:@"return p5.getClicked(event);"
              _onclick
                send-javascript:@"$('#confirm-window').modal('toggle');"
                get-widget-property:x:/../*/_event?value
                  _key-id
                p5.crypto.get-public-key:x:/-/*/*?value
                url-encode:x:/../*/p5.crypto.get-public-key?value
                p5.net.http-post:"https://pgp.mit.edu/pks/add"
                  Content-Type:application/x-www-form-urlencoded
                  content:@"keytext={0}"
                    :x:/../*/url-encode?value
                if:x:/-/*/result/*/status?value
                  =:OK

                  /*
                   * Success
                   */
                  sys42.info-window:Key successfully added to <em>pgp.mit.edu</em> keyserver!
                else

                  /*
                   * Error
                   */
                  sys42.info-window:Oops, something went wrong while submitting your key to keyserver!!
                    _error:true
                    _time:more
            literal
              _key-id
              element:button
              class:btn btn-default
              innerValue:View public key
              onclick
                get-widget-property:x:/../*/_event?value
                  _key-id
                p5.crypto.get-public-key:x:/-/*/*?value
                eval-x:x:/+/*/innerValue
                create-literal-widget
                  element:pre
                  parent:confirm-window-body-wrapper
                  position:1
                  innerValue:@"{0}

<strong>Hint, using your public key as a signature for your emails
is probably a smart thing!
You can also feature it on a publicly visible web page!
If you submit your key to a key server, others can also
look it up, and use it to send you encrypted emails!</strong>"
                    :x:/../*/p5.crypto.get-public-key?value
                set-widget-property:x:/../*/_event?value
                  disabled
      widgets
        literal
          element:td
          innerValue:pub
        literal
          element:td
          innerValue
        literal
          element:td
          innerValue
  html-encode:x:/..for-each/*/_dp/#?name
  set:x:/./*/_widgets/*/container/*/widgets/**/literal/[1,2]/*/innerValue?value
    src:x:/./-?value
  set:x:/./*/_widgets/*/container/*/widgets/**/literal/[2,3]/*/innerValue?value
    src:x:/..for-each/*/_dp/#?value
  set:x:/..for-each/*/_widgets/**/_key-id?value
    src:x:/..for-each/*/_dp/#?value

  // Checking if this public key has a private key associated with it, and if so, changing the UI to reflect this fact
  if:x:/../*/p5.crypto.list-private-keys/*/={0}
    :x:/..for-each/*/_dp/#?value
    set:x:/..for-each/*/_widgets/*/container/*/widgets/**/literal/[0,1]/*/innerValue?value
      src:sec/pub
    add:x:/..for-each/*/_widgets/*/container
      src:"class:bold"

  // Adding key row to table widget
  add:x:/../*/create-widget/=gnu-pg-keys/*/widgets
    src:x:/..for-each/*/_widgets/*



/*
 * Create search text box
 */
if
  fetch:x:/0/0?value
    widget-exist:search-wrp
  delete-widget:search-wrp
create-widget:search-wrp
  parent:content
  class:col-xs-12 col-md-4 col-md-push-8 form-group
  widgets
    void:search-input
      element:input
      type:search
      class:form-control
      placeholder:Lookup key from MIT PGP keyserver ...
      onkeypress:"return p5.searchKeyPressed(event);"
      _onsearch

        /*
         * Retrieving keys matching search from pgp.mit.edu, and displaying in a table widget, 
         * allowing user to inspect and download keys
         */
        if
          fetch:x:/0/0?value
            widget-exist:search-result
          delete-widget:search-result
        get-widget-property:search-input
          value
        p5.net.http-get:"https://pgp.mit.edu/pks/lookup?search={0}&fingerprint=on"
          :x:/../*/get-widget-property/*/*?value
        html2lambda:x:/-/**/content?value
        if:x:/../*/p5.net.http-get/*/result/*/status?value
          !=:OK
          sys42.info-window:@"No results for query"
        else
          for-each:x:/../*/html2lambda/**/pre/[1,]/*/a/*/#text/=~@/.
            _widget
              container
                element:tr
                widgets
                  literal
                    element:td
                    innerValue
                  literal
                    element:td
                    innerValue
                  container
                    element:td
                    widgets
                      literal
                        element:button
                        _get-key-url
                        class:btn btn-default
                        innerValue:Get
                        onclick:@"return p5.getClicked(event);"
                        _onclick
                          get-widget-property:x:/../*/_event?value
                            _get-key-url
                          p5.net.http-get:{0}{1}
                            :"https://pgp.mit.edu"
                            :x:/../*/get-widget-property/*/*?value
                          html2lambda:x:/-/**/content?value
                          p5.crypto.import-public-pgp-key::x:/../**/pre/*/#text?value
                          delete-widget:gnu-pg-keys
                          delete-widget:search-result
                          sys42.execute-lisp-file:/system42/apps/gnupg.hl
                          sys42.info-window:@"Key was added to your GnuPG database"
            html-encode:x:/..for-each/*/_dp/#/*/#text?value
            set:x:/..for-each/*/_widget/*/*/widgets/*/literal/[0,1]/*/innerValue?value
              src:x:/./-?value
            html-encode:x:/..for-each/*/_dp/#/+?value
            set:x:/..for-each/*/_widget/*/*/widgets/*/literal/[1,2]/*/innerValue?value
              src:"<pre>{0}</pre>"
                :x:/././-?value
            set:x:/..for-each/*/_widget/*/*/widgets/*/container/*/widgets/*/literal/*/_get-key-url?value
              src:x:/..for-each/*/_dp/#/./*/a/[0,1]/*/@href?value
            add:x:/..for-each/./*/create-widget/*/widgets
              src:x:/..for-each/*/_widget/*
          create-widget:search-result
            parent:content
            position:1
            element:table
            class:table
            widgets
              container
                element:thead
                widgets
                  container
                    element:tr
                    widgets
                      literal
                        element:th
                        innerValue:Identity
                      literal
                        element:th
                        innerValue:Fingerprint
                      literal
                        element:th
                        innerValue:Get key
          sys42.info-window:@"Search finished, see results below"
    literal
      element:span
      class:glyphicon glyphicon-search form-control-feedback
      innerValue:&nbsp;&nbsp;


include-javascript:@"p5.searchKeyPressed = function(e) {
  if(e.keyCode == 13) {
    p5.$('search-input').raise('_onsearch', {
      onbefore:function () {
          $('#modal-layer').toggleClass('hidden');
          $('#search-input').prop('disabled',true);
      },
      onsuccess:function () {
          $('#modal-layer').toggleClass('hidden');
          $('#search-input').prop('disabled',false);
      },
      onerror:function () {
          $('#modal-layer').toggleClass('hidden');
          $('#search-input').prop('disabled',false);
      }
    });
    return false;
  }
},

p5.getClicked = function (e) {
p5.$(e.target.id).raise('_onclick', {
  onbefore:function () {
      $('#modal-layer').toggleClass('hidden');
  },
  onsuccess:function () {
      $('#modal-layer').toggleClass('hidden');
  },
  onerror:function () {
      $('#modal-layer').toggleClass('hidden');
  }
});
return false;
}"

/*
 * Keypair widget(s)
 */
create-widget:gnu-pg-keys
  parent:content
  element:table
  class:table table-hover
  widgets
    literal
      element:thead
      innerValue:@"<tr><th>Type</th><th>User ID</th><th>Key ID</th></tr>"

/*
 * Create keypair button, but only if it does not exist from before
 */
if
  fetch:x:/0/0?value
    widget-exist:create-keypair
  delete-widget:create-keypair
create-widget:create-keypair
  parent:content
  class:text-right
  widgets
    literal
      element:button
      class:btn btn-default
      innerValue:Create keypair
      onclick
        sys42.wizard-window
          _header:Create keypair
          _body:@"Please provide a seed, an identity, and a password for your keypair"
          _widgets
            textarea:seed
              placeholder:@"Seed for random number generator. The more random characters you provide here, the more secure your private key will be. Type in at least 500-1000 random characters here, such as 'ƒµ˛¸UTgh/&5dg67'. 500 characters will fill the textarea entirely."
              mandatory:true
            text:identity
              label:Identity
              placeholder:Identity; John Doe <john@doe.com> ...
              mandatory:true
              autocomplete:off
            password:password
              label:Password
              mandatory:true
              placeholder:Provide a password for the private key ...
              autocomplete:off
            password:password-repeat
              label:Password
              mandatory:true
              placeholder:Repeat password ...
              autocomplete:off
          _onok
            get-widget-property:strength
              value
            if:x:/../*/password?value
              =:x:/../*/password-repeat?value

              /*
               * Passwords matched each other
               */
              set:x:/..?value
                src:bool:true
              eval-x:x:/+/*/*
              fork
                p5.crypto.create-pgp-keypair
                  identity:x:/../*/identity?value
                  password:x:/../*/password?value
                  seed:x:/../*/seed?value
                  strength:x:/../*/get-widget-property/*/*?value
              sys42.info-window:@"Creating your key might take some time, refresh page to check if it is done"
                _time:more
            else

              /*
               * Failure" Passwords did not match! Signaling that fact to caller
               */
              set:x:/..?value
                src:Passwords don't match
              send-javascript:@"$('#password').focus().select();"
        get-parent-widget:password-repeat
        eval-x:x:/+/*/after
        create-widget
          after:x:/./-2/*?value
          class:prepend-left prepend-top prepend-bottom prepend-right input-group
          widgets
            literal
              class:input-group-addon
              innerValue:Bit strength
            container:strength
              element:select
              class:form-control
              title:Suggested value here is 4096. Anything less is insecure, anything above 4096 will take a lot of time to create.
              widgets
                literal
                  element:option
                  innerValue:1024
                literal
                  element:option
                  innerValue:2048
                literal
                  element:option
                  innerValue:4096
                  selected
                literal
                  element:option
                  innerValue:8192
                literal
                  element:option
                  innerValue:16384

/*
 * Creating modal layer
 */
if
  fetch:x:/0/0?value
    widget-exist:modal-layer
  delete-widget:modal-layer
create-widget:modal-layer
  parent:content
  style:"position:fixed;top:0;left:0;width:100%;height:100%;z-index:2000;background-color:rgba(0,0,0,.2);"
  class:hidden
  widgets
    literal
      element:h1
      style:"margin-top:100px;margin-left:40%;"
      innerValue:Please wait ...
