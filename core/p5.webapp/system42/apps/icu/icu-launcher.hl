
/*
 * Main entry point for starting the ICU keyboard. Expects the parameter [_keyboard-widget],
 * which is a container widget, that can somehow wrap the keyboard. Optionally, supply a
 * [_output-widget], which becomes the "sheet surface", which the keyboard writes to.
 * Optionally, if you wish to load a specific "board definition", supply [_board], pointing
 * to the ID of a specific board.
 * Also supply a [_language-builder] if you wish to allow for user to create new pictures for words
 */


/*
 * Loading ICU stylesheet file
 */
include-stylesheet-file:/media/css/icu.css


/*
 * Defaulting [_board] we're using to the main "root" board, unless explicitly overridden by caller
 */
if:x:/../*/_board?value
  set:x:/../*/_current-board?value
    src:x:/../*/_board?value
_current-board:guid:00000000-0000-0000-0000-000000000000
set-page-data:sys42.icu.current-board
  src:x:/../*/_current-board?value


/*
 * Verifying keyboard widget exists, and is of type [container]
 */
list-widgets:x:/../*/_keyboard-widget?value
if:x:/-/*?name
  !=:container
  throw:You need to supply a [_keyboard-widget], pointing to a [container] type of widget to the ICU keyboard


/*
 * Empties the keyboard widget, and sets the parent of widget, before finally making sure
 * board widget is of type table, and has correct CSS classes
 */
clear-widget:x:/../*/_keyboard-widget?value
set:x:/../**/={_parent-widget}?value
  src:x:/../*/_keyboard-widget?value
set:x:/../**/={_language-builder}?value
  src:x:/../*/_language-builder?value
set-widget-property:x:/../*/_keyboard-widget?value
  element:table
  class:icu-keyboard


/*
 * Associates the "create new icon" lambda event with [_keyboard-widget]
 */
set-widget-lambda-event:x:/../*/_keyboard-widget?value

  /*
   * Launches the "create new, or edit existing picture" wizard
   */
  sys42.icu.create-board-picture
    add:x:/+
      src:x:/../*(/_x|/_y|/_board|/_language-builder)
    sys42.execute-lisp-file:/system42/apps/icu/create-board-picture.hl

  /*
   * Associates an existing square in [_board] with given [_thumb] and [_large] image, at specified [_x]/[_y]
   */
  sys42.icu.set-board-picture-for-square
    delete-widget:icu-create-board-picture
    add:x:/+/*/_onok
      src:x:/../*/~_
    sys42.wizard-window
      _header:Word ...?
      _widgets
        text:icu-key-word
          placeholder:...
          label:Word
          mandatory:false
      _onok
        select-data:x:@"/*/*/icu.board/=:guid:{0}"
          :x:/../*/_board?value
        if:x:/../*/select-data/[0,1]/*?count
          =:int:0

          /*
           * We do not have an existing board, making sure we create one
           */
          set:x:/..if/*/insert-data/*?value
            src:x:/../*/_board?value.guid
          eval-x:x:/+/*/*/*
          add:x:/..if/*/insert-data/*
            src
              item
                x:x:/../*/_x?value
                y:x:/../*/_y?value
                thumb:x:/../*/_thumb?value
                large:x:/../*/_large?value
                word:x:/../*/icu-key-word?value
          insert-data
            icu.board
        else
          select-data:x:@"/*/*/icu.board/=:guid:{0}"
            :x:/../*/_board?value
          set:x:/..else/*/update-data/*/*?value
            src:x:/../*/_board?value.guid
          add:x:/..else/*/update-data/*/*
            src:x:/..else/*/select-data/*/*
          eval-x:x:/+/*/*/*
          add:x:/..else/*/update-data/*/*
            src
              item
                x:x:/../*/_x?value
                y:x:/../*/_y?value
                thumb:x:/../*/_thumb?value
                large:x:/../*/_large?value
                word:x:/../*/icu-key-word?value
          update-data:x:@"/*/*/icu.board/=:guid:{0}"
            :x:/../*/_board?value
            src
              icu.board
          find-widget
            element:a
            _x:x:/../*/_x?value
            _y:x:/../*/_y?value
          set-widget-property:x:/-/*?value
            style:@"background:Transparent url({0}) no-repeat top left;background-size:contain;"
              :x:/../*/_thumb?value
          find-widget-like
            class:icu-active
          set-widget-property:x:/-/*?value
            class:icu-key
        return:bool:true


/*
 * Retrieving height and width of keyboard in number of picture units
 */
select-data:x:/*/*/icu.settings.size
if:x:/-/*
  set:x:/../*/_settings/*/height?value
    src:x:/../*/select-data/[0,1]/0/*/height?value
  set:x:/../*/_settings/*/width?value
    src:x:/../*/select-data/[0,1]/0/*/width?value


/*
 * Contains the width and height in number of picture units for our keyboard.
 * Defaults to 7x6, but can be overridden in settings.
 * PS!
 * 14x3 equals 42, any questions ...? :)
 */
_settings
  width:int:14
  height:int:3


/*
 * Retrieves the currently active board from database
 */
select-data:x:@"/*/*/icu.board/=:guid:{0}"
  :x:/../*/_current-board?value


/*
 * Creates the board
 */
_board-widgets
_cur-y:int:0
while:x:/../*/_cur-y?value
  <:x:/../*/_settings/*/height?value
  _cur-x:int:0

  /*
   * Creating specified table row (tr) widget
   */
  add:x:/../*/_board-widgets
    src
      create-container-widget
        element:tr
        parent:{_parent-widget}
        widgets
  while:x:/./*/_cur-x?value
    <:x:/../*/_settings/*/width?value

    /*
     * Creating specified table cell (td) widget
     */
    set:x:/+3/**/_x?value
      src:x:/..while/..while/*/_cur-x?value
    set:x:/+2/**/_y?value
      src:x:/../*/_cur-y?value
    set:x:/+/**/_board?value
      src:x:/../*/_current-board?value
    add:x:/../*/_board-widgets/0/-/*/widgets
      src
        container
          element:td
          widgets
            literal
              element:a
              href:#
              class:icu-empty-cell icu-key
              style:""
              _x
              _y
              _board
              onclick
                get-widget-property:x:/../*/_event?value
                  _x
                  _y
                  _board
                  _parent-widget:{_parent-widget}
                add:x:/+
                  src:x:/../*/get-widget-property/*/*
                sys42.icu.create-board-picture
                  _language-builder:{_language-builder}
                find-widget-like:{_parent-widget}
                  class:icu-active
                if:x:/-/*?value
                  set-widget-property:x:/./-/*?value
                    class:icu-empty-cell
                set-widget-property:x:/../*/_event?value
                  class:icu-empty-cell icu-active
    if:x:/../*/select-data/[1,2]/*/*/*/x/={0}/./*/y/={1}/.
      :x:/..while/..while/*/_cur-x?value.string
      :x:/../*/_cur-y?value.string
      /*
       * This is not an empty key
       */
      set:x:/../*/_board-widgets/0/-/*/widgets/0/-/*/widgets/*/literal/*/class?value
        src:icu-key icu-cell
      set:x:/../*/_board-widgets/0/-/*/widgets/0/-/*/widgets/*/literal/*/style?value
        src:@"background:Transparent url({0}) no-repeat top left;background-size:contain;"
          :x:/../*/select-data/[1,2]/*/*/*/x/={0}/./*/y/={1}/./*/thumb?value
            :x:/..while/..while/*/_cur-x?value.string
            :x:/../*/_cur-y?value.string
    set:x:/././*/_cur-x?value
      +:x:/./././*/_cur-x?value
        _:int:1
  set:x:/../*/_cur-y?value
    +:x:/../*/_cur-y?value
      _:int:1





/*
 * Creates our widget
 */
eval:x:/../*/_board-widgets


