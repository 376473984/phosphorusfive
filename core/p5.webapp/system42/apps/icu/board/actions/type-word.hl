
/*
 * Action for "type word". Types out word on sheet, by appending widget with image and text to sheet's list of widgets
 */


/*
 * Name of action type. Will be shown when user chooses action in for instance "create-image-action-association.hl"
 */
_action-name:Type word


/*
 * Callback invoked when an action of this type is to be evaluated. Expects;
 * [_x]     ==> x position in board of "key" to associate with action
 * [_y]     ==> y position in board of "key" to associate with action
 * [_image] ==> ID from database of image to display
 * [_board] ==> ID from database of board we're creating action for
 */
_action

  /*
   * Retrieving [_output-widget] supplied when instantiating ICU
   */
  sys42.icu.get-output-widget

  /*
   * Making sure output widget doesn't contain more than 42 "words"
   */
  get-children-widgets:x:/-?value
  if:x:/-/*/*?count
    >=:int:36
    sys42.info-window:Sheet is full
      _error:true
    return

  /*
   * Selecting [icu.image] from database that was clicked, and creating a widget from
   * it, adding this widget to the [_output-widget] that was specified when the ICU app was launched
   */
  select-data:x:@"/*/*/""=:guid:{0}"""
    :x:/../*/_image?value
  eval-x:x:/+/**
  create-widget
    parent:x:/../*/sys42.icu.get-output-widget?value
    class:icu-word
    title:x:/../*/select-data/0/*/description?value
    widgets
      literal
        element:img
        alt:x:/../*/select-data/0/*/description?value
        src:x:/../*/select-data/0/*/thumb?value
      literal
        innerValue:x:/../*/select-data/0/*/word?value
