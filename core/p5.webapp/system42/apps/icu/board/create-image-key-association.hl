
/*
 * Responsible for creating a "key"/image association for the specified [_board], at the given
 * [_x]/[_y] position, using the specified [_language-builder] as widget
 */


/*
 * Making sure we pass in [_language-builder], [_board], [_x] and [_y], to whomever needs it
 */
set:x:/../**/={_board}?value
  src:x:/../*/_board?value

set:x:/../**/={_x}?value
  src:x:/../*/_x?value

set:x:/../**/={_y}?value
  src:x:/../*/_y?value

set:x:/../**/={_language-builder}?value
  src:x:/../*/_language-builder?value


/*
 * Making sure any previously created "create board image" widgets are destroyed
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-create-board-image-key-ass
  delete-widget:icu-create-board-image-key-ass


/*
 * Creating widget which allows for associating new image with "key"
 */
eval-x:x:/+/*/parent
create-widget:icu-create-board-image-key-ass
  parent:{_language-builder}
  class:prepend-top
  events

    /*
     * Creates an image insertion from given [_thumb], [_medium], [_title] and [_description].
     * Passes on [_x], [_y] and [_board], in addition to ID of [icu.image]'s id to "key action" association logic.
     * Used to create image insertions from flickr.com images, to use as "key image" associations
     */
    sys42.icu.create-image

      /*
       * Forward evaluating wizard widgets properties
       */
      eval-x:x:/../*/sys42.wizard-window/*/_widgets/*/*(/value|/innerValue)

      /*
       * Adding all given parameters (except [_event]) to [_onok] callback for wizard widget
       */
      add:x:/../*/sys42.wizard-window/*/_onok
        src:x:/../*(/~_!/_event)
      sys42.wizard-window
        _header:Properties
        _widgets
          text:image-word
            label:Word
            placeholder:Image word ...
            mandatory:true
            value:x:/../*/_title?value
          textarea:image-description
            placeholder:Description ...
            innerValue:x:/../*/_description?value
        _onok

          /*
           * Parametrizing [insert-data] invocation, before inserting a new [icu.image] into database,
           * making sure we HTML encode [word] and [description] to not mess up HTML in case there are 
           * angle brackets, etc in text. Making sure image is inserted with currently logged in username as [user]
           */
          whoami
          eval-x:x:/../*/insert-data/*/*
          set:x:/../*/insert-data/*/*(/thumb|/medium)?value
            rel-src:x:/../*/_{0}?value
              :x:?name
          set:x:/../*/insert-data/*/*/word?value
            html-encode:x:/../*/insert-data/*/*/word?value
          set:x:/../*/insert-data/*/*/description?value
            html-encode:x:/../*/insert-data/*/*/description?value
          insert-data
            icu.image
              thumb
              medium
              word:x:/../*/image-word?value
              description:x:/../*/image-description?value
              user:x:/../*/whoami/*/username?value
              visibility:x:/../*/_visibility?value

          /*
           * Creating "action association" with key/image, making sure we pass in [_image] being ID 
           * from database to image record
           */
          set:x:/+/*/_image?value
            src:x:/../*/insert-data/0?value
          sys42.execute-lisp-file:/system42/apps/icu/board/create-key-action-association.hl
            _board:{_board}
            _x:{_x}
            _y:{_y}
            _image

          /*
           * Making sure we close wizard dialog
           */
          return:bool:true

  widgets
    container
      class:icu-create-board
      widgets
        container
          class:input-group
          widgets
            void:icu-create-board-search-box
              element:input
              type:search
              class:form-control input-lg
              placeholder:Search ...
              onkeypress:"return p5.searchIcuTextBox(event);"
              onkeyup:"return p5.searchIcuTextBoxEsc(event);"
              oninit
                send-javascript:@"$('#{0}').focus();"
                  :x:/../*/_event?value
            span
              class:input-group-btn
              widgets

                /*
                 * Search locally button
                 */
                button:icu-create-board-search-locally
                  innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
                  class:btn-default btn btn-lg
                  title:Searches for images in local database, your images, and shared/public images
                  onclick

                    /*
                     * Making sure search text box gets focus back again
                     */
                    send-javascript:@"$('#icu-create-board-search-box').focus().select();"

                    /*
                     * Retrieves value from search textbox, and searches for images using "search-for-image.hl" file.
                     * Supplying "search callback" to do actual search in local database
                     */
                    get-widget-property:icu-create-board-search-box
                      value
                    eval-x:x:/+/*/_query
                    sys42.execute-lisp-file:/system42/apps/icu/board/search-for-image.hl
                      _widget:icu-flickr-result
                      _query:x:/../*/get-widget-property/*/*?value
                      _start:0
                      _end:10
                      _on-image-selected

                        /*
                         * Simply uses the existing (local) image reference as key, applying arguments first
                         */
                        set:x:/+/*/_image?value
                          src:x:/../*/_id?value
                        sys42.execute-lisp-file:/system42/apps/icu/board/create-key-action-association.hl
                          _board:{_board}
                          _x:{_x}
                          _y:{_y}
                          _image
                      _onsearch

                        /*
                         * Search callback for actually retrieving images. Searches local database
                         */
                        whoami
                        replace:x:/../*/_query?value
                          what:" "
                          with:".*"
                        select-data:x:@"/*/*/icu.image/*((/user/""={0}""/.|/visibility/=public/.)&(/word/""=:regex:/{1}/i""/.|/description/""=:regex:/{1}/i""/.))/[{2},{3}]"
                          :x:/../*/whoami/*/username?value
                          :x:/../*/replace?value
                          :x:/../*/_start?value
                          :x:/../*/_end?value

                        /*
                         * Databinding return value
                         */
                        databind:x:/../*/return
                          src:x:/../*/select-data/*
                          template
                            image
                              {thumb}:x:/*/thumb?value
                              {medium}:x:/*/medium?value
                              {title}:x:/*/word?value
                              {description}:x:/*/description?value
                              {id}:x:?value
                        return


                /*
                 * Search globally button
                 */
                button:icu-create-board-search-globally
                  innerValue:@"<span class=""glyphicon glyphicon-globe""></span>"
                  class:btn-default btn btn-lg
                  title:Searches for images on flickr
                  onclick

                    /*
                     * Making sure search text box gets focus back again
                     */
                    send-javascript:@"$('#icu-create-board-search-box').focus().select();"

                    /*
                     * Retrieves value from search textbox, and searches for images using "search-for-image.hl" file.
                     * Supplying "search callback" to do actual search in flickr
                     */
                    get-widget-property:icu-create-board-search-box
                      value
                    eval-x:x:/+/*/_query
                    sys42.execute-lisp-file:/system42/apps/icu/board/search-for-image.hl
                      _widget:icu-flickr-result
                      _query:x:/../*/get-widget-property/*/*?value
                      _start:0
                      _end:10
                      _on-image-selected

                        /*
                         * Downloading image, both thumb and medium, to local common folder, 
                         * for then to ask user of title and description to use for insertion into local database.
                         * Starting with "thumb"
                         */
                        p5.net.http-get:x:/../*/_thumb?value
                        split:x:/../*/_thumb?value
                          =:/
                        save-file:/common/public/{0}
                          :x:/../*/split/[0,1]/0/-?name
                          src:x:/../*/p5.net.http-get/[0,1]/*/result/*/content?value

                        /*
                         * Then downloading "medium"
                         */
                        p5.net.http-get:x:/../*/_medium?value
                        split:x:/../*/_medium?value
                          =:/
                        save-file:/common/public/{0}
                          :x:/../*/split/[1,2]/0/-?name
                          src:x:/../*/p5.net.http-get/[1,2]/*/result/*/content?value

                        /*
                         * Asking user for description and title of image
                         */
                        set:x:/../*/sys42.icu.create-image/*(/_title|/_description)?value
                          rel-src:x:/../*/{0}?value
                            :x:?name
                        set:x:/../*/sys42.icu.create-image/*/_thumb?value
                          src:/common/public/{0}
                            :x:/../*/split/[0,1]/0/-?name
                        set:x:/../*/sys42.icu.create-image/*/_medium?value
                          src:/common/public/{0}
                            :x:/../*/split/[1,2]/0/-?name
                        sys42.icu.create-image
                          _x:{_x}
                          _y:{_y}
                          _board:{_board}
                          _thumb
                          _medium
                          _title
                          _description
                          _visibility:public
                      _onsearch

                        /*
                         * Search callback for actually retrieving images. Searches flickr
                         */
                        -:x:/../*/_end?value.int
                          _:x:/../*/_start?value.int
                        /:x:/../*/_start?value.int
                          _:x:/./-?value.int
                        +:x:/-?value.int
                          _:int:1
                        p5.flickrnet.search
                          text:x:/../*/_query?value
                          per-page:x:/../*/\-?value
                          page:x:/../*/\+?value

                        /*
                         * Databinding return value
                         */
                        databind:x:/../*/return
                          src:x:/../*/p5.flickrnet.search/*
                          template
                            image
                              {thumb}:x:/*/thumb?value
                              {medium}:x:/*/medium?value
                              {title}:x:/*/title?value
                              {description}:x:/*/description?value
                        return

                /*
                 * Close search button
                 */
                button:icu-close-create-board
                  innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                  class:btn-default btn btn-lg
                  onclick
                    delete-widget:icu-create-board-image-key-ass
                    sys42.icu.icu-create-board-image-key-ass.closed

    container:icu-flickr-result
      class:bg-warning rounded-corners prepend-top air-padding-more flash-from-left
      visible:false


/*
 * Including JavaScript necessary to trap carriage return in search textbox
 */
include-javascript:@"
p5.searchIcuTextBox = function(e) {
  if(e.keyCode == 13) {
    p5.$('icu-create-board-search-locally').raise('onclick');
    return false;
  }
}
p5.searchIcuTextBoxEsc = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-close-create-board').raise('onclick');
    return false;
  }
}
p5.lightboxKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-lightbox-picture-close').raise('onclick');
    return false;
  }
};"


/*
 * Evaluates the file that allows drag and drop of files into area for associating custom files with "key",
 * but first making suer our specifie language builder has the callback necessary for allowing uploading
 * of files
 */
set-widget-ajax-event:{_language-builder}

  /*
   * Callback necessary for drag'n'drop uploading of images
   */
  _onupload

    /*
     * Retrieving filename, file content, and saving file to "public" document folder
     */
    get-http-param:file-data
    get-http-param:file-name
    split:x:/../*/get-http-param/[1,2]/*?value
      =:.
    switch:x:/../*/split/0/-?name
      case:png
      case:jpg
      case:jpeg
        /*
         * This is ok
         */
        _foo:Do nothing ...
      default:
        sys42.info-window:Only image files are allowed to upload here
          _error:true
          _time:more
        return
    whoami
    p5.string.decode-base64:x:/../*/get-http-param/*/file-data?value
    save-file:/users/{0}/documents/public/{1}
      :x:/../*/whoami/*/username?value
      :x:/../*/get-http-param/[1,2]/*?value
      src:x:/../*/p5.string.decode-base64/*?value

    /*
     * Now creating "thumb" of image to speed up "key" loading
     */
    p5.imaging.resize:/users/{0}/documents/public/{1}
      :x:/../*/whoami/*/username?value
      :x:/../*/get-http-param/[1,2]/*?value
      width:75
      height:75
      keep-aspect-ratio:true
      background-color:White
      dest:/users/{0}/documents/public/thumb-{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value

    /*
     * Asking user for description and title of image
     */
    eval-x:x:/+/*(/_thumb|/_medium|/_title|/_description)
    sys42.icu.create-image
      _x:{_x}
      _y:{_y}
      _board:{_board}
      _thumb:/users/{0}/documents/public/thumb-{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
      _medium:/users/{0}/documents/public/{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
      _title:x:/../*/split/0?name
      _description:{0} - description
        :x:/../*/split/0?name
      _visibility:private


/*
 * Evaluates the file that makes "search for image widget" a dropzone for images
 */
sys42.execute-lisp-file:/system42/misc/drag-and-drop-upload.hl
  _widget:{_language-builder}
  _hover-css-class:icu-file-in-dropzone
  _dropped-css-class:icu-file-uploading
