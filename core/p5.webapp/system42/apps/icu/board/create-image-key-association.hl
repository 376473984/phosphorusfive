
/*
 * Responsible for creating a "key"/image association for the specified [_board], at the given
 * [_x]/[_y] position, using the specified [_language-builder] as widget
 */


/*
 * Making sure any previously created "create board image" widgets are destroyed
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-create-board-image-key-ass
  delete-widget:icu-create-board-image-key-ass


/*
 * Making sure we pass in [_language-builder], [_board], [_x] and [_y], to whomever needs it
 */
set:x:/../**/={_board}?value
  src:x:/../*/_board?value

set:x:/../**/={_x}?value
  src:x:/../*/_x?value

set:x:/../**/={_y}?value
  src:x:/../*/_y?value

set:x:/../**/={_language-builder}?value
  src:x:/../*/_language-builder?value


/*
 * Creating widget which allows for associating new image with "key"
 */
eval-x:x:/+/*/parent
create-widget:icu-create-board-image-key-ass
  parent:{_language-builder}
  class:prepend-top
  events

    /*
     * Creates an image insertion from given [_thumb], [_large], [_title] and [_description].
     * Passes on [_x], [_y] and [_board], in addition to ID of [icu.image]'s id to [sys42.icu.set-board-picture-for-square]
     */
    sys42.icu.create-image

      /*
       * Forward evaluating wizard widgets properties
       */
      eval-x:x:/../*/sys42.wizard-window/*/_widgets/*/*(/value|/innerValue)

      /*
       * Adding all given parameters (except [_event]) to [_onok] callback for wizard widget
       */
      add:x:/../*/sys42.wizard-window/*/_onok
        src:x:/../*(/~_!/_event)
      sys42.wizard-window
        _header:Properties
        _widgets
          text:image-word
            label:Word
            placeholder:Image word ...
            mandatory:true
            value:x:/../*/_title?value
          textarea:image-description
            placeholder:Description ...
            innerValue:x:/../*/_description?value
        _onok

          /*
           * Parametrizing [insert-data] invocation, before inserting a new [icu.image] into database,
           * making sure we HTML encode [word] and [description] to not mess up HTML in case there are angle brackets, etc in
           * text
           */
          eval-x:x:/+4/*/*
          set:x:/+3/*/*(/thumb|/medium)?value
            rel-src:x:/../*/_{0}?value
              :x:?name
          set:x:/../*/insert-data/*/*/word?value
            html-encode:x:/../*/insert-data/*/*/word?value
          set:x:/../*/insert-data/*/*/description?value
            html-encode:x:/../*/insert-data/*/*/description?value
          insert-data
            icu.image
              thumb
              medium
              word:x:/../*/image-word?value
              description:x:/../*/image-description?value

          /*
           * Creating "action association" with key/image
           */
          set:x:/+/*/_image?value
            src:x:/../*/insert-data/0?value
          sys42.execute-lisp-file:/system42/apps/icu/board/create-image-action-association.hl
            _board:{_board}
            _x:{_x}
            _y:{_y}
            _image
          return:bool:true

  widgets
    container
      class:icu-create-board
      widgets
        container
          class:input-group
          widgets
            void:icu-create-board-search-box
              element:input
              type:search
              class:form-control input-lg
              placeholder:Search ...
              onkeypress:"return p5.searchIcuTextBox(event);"
              onkeyup:"return p5.searchIcuTextBoxEsc(event);"
              oninit
                send-javascript:@"$('#{0}').focus();"
                  :x:/../*/_event?value
            span
              class:input-group-btn
              widgets
                button:icu-create-board-search
                  class:btn-default btn btn-lg
                  type:button
                  innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
                  onclick
                    clear-widget:icu-flickr-result
                    set-widget-property:icu-flickr-result
                      visible:true
                    get-widget-property:icu-create-board-search-box
                      value
                    send-javascript:@"$('#icu-create-board-search-box').focus().select();"
                    p5.flickrnet.search
                      text:x:/../*/get-widget-property/*/*?value
                      per-page:10
                    for-each:x:/../*/p5.flickrnet.search/*/result
                      html-encode:x:/..for-each/*/_dp/#/*/description?value
                      eval-x:x:/+/**(/img/*(/src|/title)|/h3/*/innerValue|/_medium|/_thumb|/_title|/_description)
                      create-widget
                        parent:icu-flickr-result
                        class:icu-flickr-result-item
                        widgets
                          a
                            href:#
                            onclick
                              if
                                fetch:x:/0/0?value
                                  widget-exist:icu-lightbox-picture
                                delete-widget:icu-lightbox-picture
                              create-widget:icu-lightbox-picture
                                class:icu-lightbox-picture
                                onkeyup:"p5.lightboxKeyUp(event);"
                                widgets
                                  div:icu-lightbox-picture-modal-window
                                    class:icu-lightbox-picture-modal-window
                                    widgets
                                      h3
                                        innerValue:x:/..for-each/*/_dp/#/*/title?value
                                      img
                                        src:x:/..for-each/*/_dp/#/*/medium?value
                                        alt:Choose image, or click outside to close
                                        title:x:/..for-each/*/html-encode?value
                                      div
                                        style:"text-align:right;"
                                        class:prepend-top
                                        widgets
                                          button:icu-lightbox-picture-close
                                            class:btn btn-default
                                            style:"width:80px;"
                                            innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                                            onclick
                                              send-javascript:@"
$('#icu-lightbox-picture-modal-window').toggleClass('icu-lightbox-picture-modal-window').toggleClass('action-modal-window-fadeout').one('animationend',
  function(e) {
    $('#' + e.target.id).hide();
    $('#icu-lightbox-picture').fadeOut({duration:200});
  });"
                                          button
                                            class:btn btn-primary
                                            style:"width:80px; margin-left:10px;"
                                            innerValue:@"<span class=""glyphicon glyphicon-ok""></span>"
                                            oninit
                                              send-javascript:@"$('#{0}').focus();"
                                                :x:/../*/_event?value
                                            onclick
                                              _board:{_board}
                                              _x:{_x}
                                              _y:{_y}
                                              delete-widget:icu-lightbox-picture
                                              _medium:x:/..for-each/*/_dp/#/*/medium?value
                                              _thumb:x:/..for-each/*/_dp/#/*/thumb?value
                                              _description:x:/..for-each/*/_dp/#/*/description?value
                                              get-widget-property:icu-create-board-search-box
                                                value
                                              add:x:/+2
                                                src:x:/../*(/_board|/_x|/_y|/_thumb|/_medium|/_description)
                                              eval-x:x:/+/*/_title
                                              sys42.icu.create-image
                                                _title:x:/../*/get-widget-property/*/*?value
                            widgets
                              img
                                src:x:/..for-each/*/_dp/#/*/thumb?value
                button:icu-close-create-board
                  innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                  class:btn-default btn btn-lg
                  onclick
                    delete-widget:icu-create-board-image-key-ass
                    sys42.icu.icu-create-board-image-key-ass.closed
    container:icu-flickr-result
      class:bg-warning rounded-corners prepend-top air-padding-more flash-from-left
      visible:false


/*
 * Including JavaScript necessary to trap carriage return in search textbox
 */
include-javascript:@"
p5.searchIcuTextBox = function(e) {
  if(e.keyCode == 13) {
    p5.$('icu-create-board-search').raise('onclick');
    return false;
  }
}
p5.searchIcuTextBoxEsc = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-close-create-board').raise('onclick');
    return false;
  }
}
p5.lightboxKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-lightbox-picture-close').raise('onclick');
    return false;
  }
};"


/*
 * Evaluates the file that allows drag and drop of files into area for associating custom files with "key",
 * but first making suer our specifie language builder has the callback necessary for allowing uploading
 * of files
 */
set-widget-ajax-event:{_language-builder}

  /*
   * Callback necessary for drag'n'drop uploading of images
   */
  _onupload

    /*
     * Retrieving filename, file content, and saving file to "public" document folder
     */
    get-http-param:file-data
    get-http-param:file-name
    split:x:/../*/get-http-param/[1,2]/*?value
      =:.
    switch:x:/../*/split/0/-?name
      case:png
      case:jpg
      case:jpeg
      case:gif
        /*
         * This is ok
         */
        _foo
      default:
        sys42.info-window:Only image files are allowed to upload here
          _error:true
          _time:more
        return
    whoami
    p5.string.decode-base64:x:/../*/get-http-param/*/file-data?value
    save-file:/users/{0}/documents/public/{1}
      :x:/../*/whoami/*/username?value
      :x:/../*/get-http-param/[1,2]/*?value
      src:x:/../*/p5.string.decode-base64/*?value

    /*
     * Now creating "thumb" of image to speed up "key" loading
     */
    p5.imaging.resize:/users/{0}/documents/public/{1}
      :x:/../*/whoami/*/username?value
      :x:/../*/get-http-param/[1,2]/*?value
      width:75
      height:75
      keep-aspect-ratio:true
      background-color:White
      dest:/users/{0}/documents/public/thumb-{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value

    /*
     * Asking user for description and title of image
     */
    eval-x:x:/+/*(/_thumb|/_medium|/_title|/_description)
    sys42.icu.create-image
      _x:{_x}
      _y:{_y}
      _board:{_board}
      _thumb:/users/{0}/documents/public/thumb-{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
      _medium:/users/{0}/documents/public/{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
      _title:x:/../*/split/0?name
      _description:{0} - description
        :x:/../*/split/0?name


/*
 * Evaluates the file that makes "search for image widget" a dropzone for images
 */
sys42.execute-lisp-file:/system42/misc/drag-and-drop-upload.hl
  _widget:{_language-builder}
  _hover-css-class:icu-file-in-dropzone
  _dropped-css-class:icu-file-uploading
