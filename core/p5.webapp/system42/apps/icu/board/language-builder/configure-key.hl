
/*
 * Configures currently "active" key in keyboard.
 *
 * If you pass in [_query], then the search box will be initially filled out with that text/word,
 * otherwise it will remember queries across multiple openings of widget
 */
_icu-query
if:x:/../*/_query?value

  /*
   * Explicit query given
   */
  set:x:/../*/_icu-query?value
    src:x:/../*/_query?value
else

  /*
   * Using last used query from session, of there are any
   */
  get-session-value:sys42.icu.last-used-language-builder-query
  if:x:/-/*?value

    /*
     * We have a query in our session object
     */
    set:x:/../*/_icu-query?value
      src:x:/..else/*/get-session-value/*?value




/*
 * Verifying language building is allowed
 */
sys42.icu.get-language-builder-widget
if:x:/-?value
  not
  sys42.info-window:No language building is allowed according to configuration of ICU. Contact sys-admin of app.
    _error:true
    _time:more
  return:bool:false




/*
 * Making sure any previously created "create board image" widgets are destroyed, before we
 * create main "language builder" widget
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-configure-key
  delete-widget:icu-configure-key




/*
 * Now creating language builder widget, first making sure main language builder wrapper is shown
 */
sys42.delete-css-classes:x:/../*/sys42.icu.get-language-builder-widget?value
  _class:icu-hide-language-builder




/*
 * Then loading all search plugins from plugin folder.
 * These are simply just buttons that are appended into input-group for search textbox
 */
list-files:/system42/apps/icu/board/language-builder/search-plugins/
  filter:hl
for-each:x:/-/*?name

  /*
   * Adding file as button to input group
   */
  load-file:x:/..for-each/*/_dp?value
  add:x:/-/*/*/*/onclick
    src

      /*
       * Storing last used query in session
       */
      get-widget-property:icu-configure-key-search-textbox
        value
      set-session-value:sys42.icu.last-used-language-builder-query
        src:x:/./-/*/*?value

  insert-before:x:/../*/create-widget/*/widgets/*/span/*/widgets/0/-
    src:x:/./-2/*/*




/*
 * Now we can finally create our language builder
 */
eval-x:x:/+/*/parent
create-widget:icu-configure-key
  parent:x:/../*/sys42.icu.get-language-builder-widget?value
  class:input-group
  onkeyup:"return p5.searchIcuTextBoxEsc(event);"

  /*
   * Common events for handling images
   */
  events




    /*
     * Associates currently "active key" with given [_image]
     */
    sys42.icu.configure-key-with-image-action

      /*
       * Retrieving active key's x and y position, in addition to ID of widget that's active
       */
      sys42.icu.get-active-key

      /*
       * Retrieving current board
       */
      get-page-value:sys42.icu.active-board

      /*
       * Retrieving current [icu.board] definition from database
       */
      select-data:x:@"/*/*/icu.board/=:guid:{0}"
        :x:/../*/get-page-value/*?value

      /*
       * Deleting any existing image associations for existing [_x]/[_y] position in board
       */
      set:x:/../*/select-data/*/*/items/*/item/*/x/={0}/./*/y/={1}/.
        :x:/../*/sys42.icu.get-active-key/*/x?value
        :x:/../*/sys42.icu.get-active-key/*/y?value

      /*
       * Moving result from [select-data] into [update-data] below, and making sure [update-data]
       * updates the correct database object
       */
      set:x:/../*/update-data/*/*?value
        src:x:/../*/get-page-value/*?value.guid
      add:x:/../*/update-data/*/*
        src:x:/../*/select-data/[0,1]/*/*

      /*
       * Adding a new [item] into [items] collection of board definition into [update-data], forward
       * evaluating expressions inside of [add], making sure [x] and [y] are constants when [updata-data] is evaluated,
       * and that [image] points to ID of image from [icu.word]
       * In addition, making sure [action] becomes specified [_action] lambda object
       */
      add:x:/+2/*/*/*/action
        src:x:/../*/_action/*
      eval-x:x:/+/*/*/*(/x|/y|/image)
      add:x:/../*/update-data/*/*/*/items
        src
          item
            x:x:/../*/sys42.icu.get-active-key/*/x?value
            y:x:/../*/sys42.icu.get-active-key/*/y?value
            image:x:/../*/_image?value
            action

      /*
       * Updating database board definition, now with one additional [item] in its [items] collection
       */
      update-data:x:@"/*/*/icu.board/=:guid:{0}"
        :x:/../*/get-page-value/*?value
        src
          icu.board

      /*
       * Changing image of currently edited image "key" in [_icu-keyboard-widget], to reflect changes just done,
       * before removing "active" CSS class, and setting correct CSS classes for "key"
       */
      select-data:x:@"/*/*/icu.word/""=:guid:{0}"""
        :x:/../*/_image?value

      /*
       * Updating "src" attribute and "alt" attribute of image
       *
       * Notice, the client side screws things up here for us, when concatenating values, hence
       * we need to prepend the URL with the "base" parts for app
       */
      get-base-location
      find-widget:x:/../*/sys42.icu.get-active-key/*/id?value
        element:img
      set-widget-property:x:/-/*?value
        src:{0}{1}
          :x:/././-2?value
          :x:/../*/select-data/[1,2]/*/*/images/*/image/[0,1]?value
        alt:x:/../*/select-data/[1,2]/*/*/word?value

      /*
       * Setting "innerValue" of "span" HTML element to word from [icu.word]
       */
      find-widget:x:/../*/sys42.icu.get-active-key/*/id?value
        element:span
      set-widget-property:x:/-/*?value
        innerValue:x:/../*/select-data/[1,2]/*/*/word?value

      /*
       * Associating [_action] lamba object to [onclick] of "key"
       */
      add:x:/+/*
        src:x:/../*/_action/*
      set-widget-ajax-event:x:/../*/sys42.icu.get-active-key/*/id?value
        onclick
          sys42.icu.on-key-clicked:x:/../*/_event?value




    /*
     * Takes an existing image as [_arg], and starts configuring it as an image key,
     * given a default word of [_word] and description of [_description].
     *
     * Will insert the given [_arg] image as [icu.word] with [word] and [description] supplied by user.
     *
     * Expects image supplied to already have been resized at this point, to fit the key size, with equal
     * height and width
     */
    sys42.icu.configure-image-for-key

      /*
       * Creates a modal window, asking user for "word" and "description" for image insertion into database
       */
      include-javascript:@"p5.configureImageKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-hide-configure-image').raise('onclick');
  }
}
p5.configureImageKeyPress = function(e) {
  if(e.keyCode == 13) {
    p5.$('icu-configure-image-ok-select-image').raise('onclick');
    return false;
  }
}"
      sys42.icu.get-icu-wrapper
      eval-x:x:/+/*/parent|/+/*/*/*/*/*/*(/void/=icu-configure-image-word/*/value|/literal/=icu-configure-image-description/*/innerValue|/./././*/void/=icu-configure-image-img/*(/src|/alt))
      create-widget:icu-configure-image-result
        class:icu-search-modal-bg
        parent:x:/../*/sys42.icu.get-icu-wrapper?value
        onkeyup:"return p5.configureImageKeyUp(event);"
        widgets
          container
            class:icu-search-modal-window rounded-corners box-shadow air-padding-more
            widgets

              /*
               * Close "search result" modal window button
               */
              literal:icu-hide-configure-image
                element:a
                href:#
                innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
                class:icu-close-dialog
                onclick
                  delete-widget:icu-configure-image-result

                  /*
                   * Closing language builder
                   */
                  sys42.icu.on-close-language-builder
              literal:icu-configure-image-header
                element:h3
                innerValue:Word ...?
              void
                element:hr

              /*
               * Contains all "supply word and description" widgets
               */
              container:icu-configure-image-body
                class:col-lg-6 prepend-bottom
                widgets
                  void:icu-configure-image-word
                    element:input
                    type:text
                    class:form-control
                    placeholder:Word ...
                    value:x:/../*/_word?value
                    onkeypress:"return p5.configureImageKeyPress(event);"
                    tabindex:1
                    oninit
                      send-javascript:@"$('#{0}').focus().select();"
                        :x:/../*/_event?value
                  literal:icu-configure-image-description
                    element:textarea
                    class:form-control prepend-top prepend-bottom
                    placeholder:Description ...
                    innerValue:x:/../*/_description?value
                    tabindex:2
                    rows:13
              void:icu-configure-image-img
                element:img
                class:icu-configure-image-img box-shadow rounded-corners col-lg-6 prepend-bottom
                src:x:/../*/_image?value
                alt:x:/../*/_word?value

              /*
               * Give some "space" for button row at the bottom of modal window
               */
              void
                element:hr
                style:"clear:both;"

              /*
               * Button row at the bottom of preview dialog, to allow user to select image
               */
              container
                class:prepend-top
                style:"width:100%;"
                widgets

                  /*
                   * Cancel configure image button
                   */
                  button:icu-configure-image-cancel-preview
                    class:btn-default btn pull-right
                    innerValue:@"<span class=""glyphicon glyphicon-remove""></span> Cancel"
                    tabindex:4
                    style:"width:100px;"
                    onclick

                      /*
                       * User clicked "cancel", simply closing window
                       */
                      delete-widget:icu-configure-image-result

                      /*
                       * Making sure we hide language builder
                       */
                      sys42.icu.on-close-language-builder

                  /*
                   * OK configure image button (selects image, and creates a new [icu.word] entry in database)
                   */
                  button:icu-configure-image-ok-select-image
                    class:btn-default btn pull-right
                    innerValue:@"<span class=""glyphicon glyphicon-ok""></span> OK"
                    tabindex:3
                    style:"width:100px;margin-right:10px;"
                    onclick

                      /*
                       * Retrieving [word], [description] and [image] properties, for insertion of image into
                       * database
                       */
                      get-widget-property:icu-configure-image-word
                        value
                      get-widget-property:icu-configure-image-description
                        value
                      get-widget-property:icu-configure-image-img
                        src

                      /*
                       * Creates an [icu.word] insertion, to stuff into database
                       */
                      eval-x:x:/+/*/*|/+/*/*/images/*
                      insert-data
                        icu.word
                          word:x:/../*/get-widget-property/[0,1]/*/*?value
                          description:x:/../*/get-widget-property/[1,2]/*/*?value
                          images
                            image:x:/../*/get-widget-property/[2,3]/*/*?value

                      /*
                       * Asks user for "action" to associate with image, passing in [icu.image] ID to file responsible
                       * for configuring action
                       */
                      eval-x:x:/+/*
                      sys42.execute-lisp-file:/system42/apps/icu/board/language-builder/helpers/configure-action.hl
                        _image:x:/../*/insert-data/*?value
                      delete-widget:icu-configure-image-result




    /*
     * Resizes an image to "key size", and puts it into ICU [_destination] folder.
     * Pass in image to resize as [_arg].
     *
     * Will "crop" the image, by making sure it uses the "center parts" of image, if image is not 
     * of equal height/width proportions
     */
    sys42.icu.resize-image-to-keys-folder

      /*
       * Figuring out image's original width and height
       */
      p5.imaging.get-size:x:/../*/_arg?value

      /*
       * Resizing image, such that it becomes square, and has correct size proportions, 
       * and storing result in "keys" folder
       */
      if:x:/../*/p5.imaging.get-size/*/width?value
        >:x:/../*/p5.imaging.get-size/*/height?value

        /*
         * Need to crop left and right parts of image
         * Setting [top], [height] and [width] values to "static" values first
         */
        set:x:/../*/p5.imaging.resize/*/src-rect/*/top?value
          src:int:0
        set:x:/../*/p5.imaging.resize/*/src-rect/*/height?value
          src:x:/../*/p5.imaging.get-size/*/height?value
        set:x:/../*/p5.imaging.resize/*/src-rect/*/width?value
          src:x:/../*/p5.imaging.get-size/*/height?value

        /*
         * Then calculating [left] of source rectangle
         */
        -:x:/../*/p5.imaging.get-size/*/width?value
          _:x:/../*/p5.imaging.get-size/*/height?value
        /:x:/-?value
          _:int:2
        set:x:/../*/p5.imaging.resize/*/src-rect/*/left?value
          src:x:/./-?value

      else-if:x:/../*/p5.imaging.get-size/*/height?value
        >:x:/../*/p5.imaging.get-size/*/width?value

        /*
         * Need to crop top and bottom parts of image
         * Setting [left], [width] and [height] values to "static" values first
         */
        set:x:/../*/p5.imaging.resize/*/src-rect/*/left?value
          src:int:0
        set:x:/../*/p5.imaging.resize/*/src-rect/*/width?value
          src:x:/../*/p5.imaging.get-size/*/width?value
        set:x:/../*/p5.imaging.resize/*/src-rect/*/height?value
          src:x:/../*/p5.imaging.get-size/*/width?value

        /*
         * Then calculating [top] of source rectangle
         */
        -:x:/../*/p5.imaging.get-size/*/height?value
          _:x:/../*/p5.imaging.get-size/*/width?value
        /:x:/-?value
          _:int:2
        set:x:/../*/p5.imaging.resize/*/src-rect/*/top?value
          src:x:/./-?value

      else

        /*
         * No need to crop, since width and height is equal.
         * Simply removing [src-rect], since it will default to original image size
         */
        set:x:/../*/p5.imaging.resize/*/src-rect

      /*
       * Now doing actual resizing of image
       */
      p5.imaging.resize:x:/../*/_arg?value
        width:283
        height:283
        destination-file:x:/../*/_destination?value
        src-rect
          top
          left
          width
          height




    /*
     * Invoked when a file has been uploaded to server
     */
    sys42.icu.file-uploaded

      /*
       * Getting filename and data
       */
      get-http-param:file-data
      get-http-param:file-name

      /*
       * Making sure we get a unique filename, which will be the same for each consecutive upload of the same file
       * over again, to allow overwriting files, while also not overwriting, unless it is the same file uploaded,
       * and not just a different file, with the same name. This is done by adding in 15 characters of "sha256 hash"
       * of file content. While also keeping the filename, in addition to starting the file with the currently logged
       * in user's username
       */
      sha256-hash:x:/../*/get-http-param/*/file-data?value
      to-lower:x:/-?value
      replace:x:/-?value
        not-of:abcdefghijklmnopqrstuvwxyz0123456789-
      split:x:/-?value
        =:int:15
      

      /*
       * Saving file to user's temp directory
       */
      p5.string.decode-base64:x:/../*/get-http-param/*/file-data?value
      whoami
      save-file:/users/{0}/temp/{1}
        :x:/./-/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
        src:x:/../*/p5.string.decode-base64/*?value

      /*
       * Resizing image to keys folder
       */
      eval-x:x:/+/*/_destination
      sys42.icu.resize-image-to-keys-folder:/users/{0}/temp/{1}
        :x:/../*/whoami/*/username?value
        :x:/../*/get-http-param/[1,2]/*?value
        _destination:/system42/apps/icu/media/images/user-space/common-keys/{0}-{1}-{2}
          :x:/../*/whoami/*/username?value
          :x:/../*/split/[0,1]/0?name
          :x:/../*/get-http-param/*/file-name?value

      /*
       * Now we can configure currently active key with resized image, using file name (without extension) as 
       * default "word" for [icu.word]
       */
      date-now
      split:x:/../*/get-http-param/*/file-name?value
        =:.
      eval-x:x:/+/*(/_word|/_description|/_image)
      sys42.icu.configure-image-for-key
        _image:/system42/apps/icu/media/images/user-space/common-keys/{0}-{1}-{2}
          :x:/../*/whoami/*/username?value
          :x:/../*/split/[0,1]/0?name
          :x:/../*/get-http-param/*/file-name?value
        _word:x:/../*/split/[1,2]/0?name
        _description:Uploaded - {0}
          :x:/../*/date-now?value




    /*
     * Closes language builder
     */
    sys42.icu.on-close-language-builder

      /*
       * Retrieving language builder
       */
      sys42.icu.get-language-builder-widget
      sys42.add-css-classes:x:/-?value
        _class:icu-hide-language-builder




  _on-enter

    /*
     * We trap carriage/return here, and raise click on first button
     */
    get-children-widgets:icu-configure-key-search-buttons
    raise-widget-ajax-event:x:/-/*/0?value
      onclick

  widgets

    /*
     * Search textbox
     */
    void:icu-configure-key-search-textbox
      element:input
      type:search
      class:form-control input-lg
      placeholder:Search for image ...
      onkeypress:"return p5.searchIcuTextBox(event);"
      oninit
        send-javascript:@"$('#{0}').focus().select();"
          :x:/../*/_event?value

    /*
     * Button input group, to make sure buttons "melts" together with textbox (Bootstrap CSS feature)
     * This input group will be automatically created, with one button for each "search-plugin" in app.
     */
    span:icu-configure-key-search-buttons
      class:input-group-btn
      widgets

        /*
         * Close search button. This will be the last button in button group
         */
        button:icu-configure-key-close-builder
          innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
          class:btn-default btn btn-lg
          onclick

            /*
             * Closing language builder
             */
            sys42.icu.on-close-language-builder




/*
 * Including JavaScript necessary to trap carriage return in search textbox
 */
include-javascript:@"
p5.searchIcuTextBox = function(e) {
  if(e.keyCode == 13) {
    p5.$('icu-configure-key').raise('_on-enter');
    return false;
  }
}
p5.searchIcuTextBoxEsc = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-configure-key-close-builder').raise('onclick');
    return false;
  }
}"




/*
 * Creating "drag and drop" file uploader
 * But only if this is the first evaluation of our file
 */
get-widget-ajax-event:x:/../*/sys42.icu.get-language-builder-widget?value
  _onupload

/*
 * Checking if ajax event exists from before
 */
if:x:/../*/get-widget-ajax-event/*/*?count
  =:int:0

  /*
   * Nope, running initialization
   */
  set-widget-ajax-event:x:/../*/sys42.icu.get-language-builder-widget?value
    _onupload

      /*
       * Just forwards the request to our uploader method on language builder widget
       */
      sys42.icu.file-uploaded


  eval-x:x:/+/*
  sys42.execute-lisp-file:/system42/misc/drag-and-drop-upload.hl
    _widget:x:/../*/sys42.icu.get-language-builder-widget?value
    _hover-css-class:icu-file-dragged-to-builder
    _dropped-css-class:icu-file-dropped-to-builder




/*
 * Checking if we've got an initial query to use
 */
if:x:/../*/_icu-query?value

  /*
   * We have an explicitly given query, either from session, or from caller.
   * Setting search textbox value to that value
   */
  set-widget-property:icu-configure-key-search-textbox
    value:x:/../*/_icu-query?value


