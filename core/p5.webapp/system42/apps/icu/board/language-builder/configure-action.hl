
/*
 * Configures currently "active" key in keyboard, and allows user to
 * choose "action" to trigger when key is clicked. Expects an [_image] parameter, being
 * the database ID of a pre-existing [icu.word] object
 */




/*
 * Passing in [_image] to whomever needs it
 */
set:x:/../**/={_image}?value
  src:x:/../*/_image?value




/*
 * Iterating all actions in plugin folder, and allows user to use action
 */
list-files:/system42/apps/icu/board/language-builder/action-plugins/
for-each:x:/-/*?name

  /*
   * Used as buffer for "choose action" button
   */
  _buffer
    button
      class:icu-configure-action-button box-shadow rounded-corners
      _file
      onclick
        sys42.toggle-css-classes:x:/../*/_event?value
          _class:icu-selected-action
      widgets
        span
          class:glyphicon glyphicon-flash
          innerValue:&nbsp;
        span
          innerValue

  /*
   * Extracting "action name" from currently iterated action plugin file, and making sure
   * button is associated with correct image file, and "action name"
   */
  load-file:x:/./*/_dp?value
  set:x:/..for-each/*/_buffer/*/*/*/span/[1,2]/*/innerValue?value
    src:x:/./-/*/*/action-name?value
  set:x:/..for-each/*/_buffer/*/*/_file?value
    src:x:/..for-each/*/_dp?value
  add:x:/../*/create-widget/*/widgets/*/*/*/container/=icu-configure-action-body/*
    src:x:/..for-each/*/_buffer/*




/*
 * Creating modal window, using main wrapper as parent
 */
sys42.icu.get-icu-wrapper
eval-x:x:/+/*/parent
create-widget:icu-configure-action
  class:icu-search-modal-bg
  parent:x:/../*/sys42.icu.get-icu-wrapper?value

  /*
   * Helper events for configuration of actions for keys
   */
  events




    /*
     * Configures the first selected event from session [sys42.icu.action-files-to-evaluate], and
     * removes it from the list, updates the page value, and recursively calls itself
     */
    sys42.icu.configure-iterated-event

      /*
       * Retrieving actions from list of session values
       */
      get-session-value:sys42.icu.action-files-to-evaluate

      /*
       * Checking if there are anymore "action files" to evaluate
       */
      if:x:/../*/get-session-value/*/*/*?count
        =:int:0

        /*
         * Configuration of all actions finished!
         */

        /*
         * No more action files to evaluate, now we can configure key towards action, and delete modal window widget.
         * Retrieving entire action from session first.
         *
         * Session value [sys42.icu.combined-action-results] should now contain a lambda object, containing combined 
         * results of all configured actions
         */
        get-session-value:sys42.icu.combined-action-results

        /*
         * Appending entire lambda object, containing results of all configured actions, into event invocation
         * responsible for actually configuring action towards key
         */
        add:x:/..if/*/sys42.icu.configure-key-with-image-action/*/_action
          src:x:/./-/*/*/*

        /*
         * Passing in database ID to [icu.image]
         */
        eval-x:x:/..if/*/sys42.icu.configure-key-with-image-action/*/_image

        /*
         * Invoking event responsible for actually configuring keyboard "key"
         */
        sys42.icu.configure-key-with-image-action
          _action
          _image:x:/../*/_image?value

        /*
         * Deleting modal window widget
         */
        delete-widget:icu-configure-action

        /*
         * Deleting language builder widget
         */
        sys42.icu.get-language-builder-widget
        set-widget-property:x:/-?value
          visible:false

      else

        /*
         * Retrieves the first "action file" from session, evaluates it, with a callback for results,
         * such that when "action configure file" is finsished evaluating, it can simply invoke the callback
         * lambda object, which will append the results of the configured action into a session value
         */

        /*
         * Retrieving any previously iterated and configured actions from session
         */
        get-session-value:sys42.icu.combined-action-results

        /*
         * Loading first file from "list of action files to evaluate"
         */
        load-file:x:/../*/get-session-value/*/*/0?value

        /*
         * Forward evaluates a coupld of our expressions inside of [eval], before evaluating the
         * [configure-action] lambda object from 1st "action configuration file" in our list of files
         */
        eval-x:x:/+/*/_image|/+/*/*/sys42.icu.configure-iterated-event/*/_image
        eval:x:/..else/*/load-file/*/*/configure-action

          /*
           * Passing in [_image] as argument
           */
          _image:x:/../*/_image?value

          /*
           * Passing in "on finished" lambda object, configuration of action should invoke when
           * configuration is done
           */
          _on-finished

            /*
             * First updating our "action combined result" session value, with [_action] lambda object, 
             * that should be passed into this event.
             *
             * Retrieving any previously configured actions first
             */
            get-session-value:sys42.icu.combined-action-results

            /*
             * Appending previously configured actions into session, before appending the results
             * from currently iterated "action configuration file", and updating session value holding
             * combined results of all "action configuration files"
             */
            add:x:/+2/*/*
              src:x:/./-/*/*/*
            add:x:/+/*/*
              src:x:/../*/_action/*
            set-session-value:sys42.icu.combined-action-results
              src
                actions

            /*
             * Then removing the first file from "list of action files to configure"
             */
            get-session-value:sys42.icu.action-files-to-evaluate

            /*
             * Removing first item from "action configuration files", and recursively invoking "self"
             */
            set:x:/../*/get-session-value/[1,2]/*/*/0

            /*
             * Appending remmaining "action file configurations" into [set-session-value]
             */
            add:x:/../*/set-session-value/*
              src:x:/../*/get-session-value/[1,2]/*/*

            /*
             * Updating session, now with one less value in "action configuration files"
             */
            set-session-value:sys42.icu.action-files-to-evaluate
              src

            /*
             * Recursively invoking "self" with [_image] argument, to configure any remaining actions, or finish
             * up configuration
             */
            sys42.icu.configure-iterated-event
              _image:x:/../*/_image?value

  widgets
    container
      class:icu-search-modal-window rounded-corners box-shadow air-padding-more
      widgets




        /*
         * Close "configure action" modal window button
         */
        literal:icu-hide-configure-action
          element:a
          href:#
          innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
          class:icu-close-dialog
          onclick
            delete-widget:icu-configure-action
        literal:icu-configure-action-header
          element:h3
          innerValue:Action ...?
        void
          element:hr




        /*
         * Contains all "action" widgets
         */
        container:icu-configure-action-body
          widgets




        /*
         * Give some "space" for button row at the bottom of modal window
         */
        void
          style:"clear:both;"
          element:hr




        /*
         * Button row at the bottom of preview dialog, to allow user to finish configuring actions
         */
        container
          class:prepend-top
          style:"width:100%;"
          widgets




            /*
             * Cancel configure action button
             */
            button:icu-configure-action-cancel
              class:btn-default btn pull-right
              innerValue:@"<span class=""glyphicon glyphicon-remove""></span> Cancel"
              style:"width:100px;"
              onclick

                /*
                 * User clicked "cancel", simply closing window
                 */
                delete-widget:icu-configure-action




            /*
             * OK configure image button (selects image, and creates a new [icu.word] entry in database)
             */
            button:icu-configure-action-ok
              class:btn-default btn pull-right
              innerValue:@"<span class=""glyphicon glyphicon-ok""></span> OK"
              style:"width:100px;margin-right:10px;"
              onclick

                /*
                 * Finding all of our "selected actions"
                 */
                find-widget-like:icu-configure-action
                  class:icu-selected-action
                if:x:/-/*?count
                  =:int:0

                  /*
                   * No actions were selected
                   */
                  sys42.info-window:You need to select at least one action for key
                    _error:true
                    _time:more
                  return

                /*
                 * Retrieving all filename for currently selected actions
                 */
                get-widget-property:x:/../*/find-widget-like/*?value
                  _file

                /*
                 * Putting all files into session value, and starting iteration of configuration of actions,
                 * making sure we remove any previous "combined action result" from session first
                 */
                set-session-value:sys42.icu.combined-action-results
                add:x:/+/*/*
                  src:x:/../*/get-widget-property/*/*
                set-session-value:sys42.icu.action-files-to-evaluate
                  src
                    files

                /*
                 * Configuring events selected
                 */
                sys42.icu.configure-iterated-event
                  _image:{_image}


