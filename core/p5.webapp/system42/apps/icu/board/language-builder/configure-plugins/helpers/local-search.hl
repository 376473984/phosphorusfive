
/*
 * Local search plugin implementation.
 *
 * Triggered when "local search" button is clicked.
 */




/*
 * Checking if widget already exists, and returns early if so
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-local-result

  /*
   * In case user clicks twice on search button
   */
  return




/*
 * Building modal window, showing first 20 images
 */
sys42.icu.get-icu-wrapper
eval-x:x:/+/*/parent
create-widget:icu-local-result
  class:icu-search-modal-bg
  parent:x:/../*/sys42.icu.get-icu-wrapper?value
  onkeyup:"return p5.localSearchKeyUp(event);"
  events

    /*
     * Fetches 20 images from local database with given search term, starting from [_page]
     */
    sys42.icu.local-search-get-next-items

      /*
       * Clearing out previously created widgets
       */
      clear-widget:icu-local-result-items

      /*
       * Retrieving "search query", and using it as search text for searching local database
       * for 20 first images, matching specified query
       */
      get-widget-property:icu-configure-key-search-textbox
        value
      *:x:/../*/_page?value.int
        _:int:20
      +:x:/-?value.int
        _:int:20
      whoami
      select-data:x:@"/*/*/icu.word/*/username/={0}/./*(/word/""=:regex:/{1}/i""|/description/""=:regex:/{1}/i"")/./[{2}, {3}]"
        :x:/./-/*/username?value
        :x:/../*/get-widget-property/*/*?value
        :x:/../*/\*?value
        :x:/../*/\+?value

      /*
       * Databinding result widget to [select-data] return value
       */
      _widgets
      databind:x:/../*/_widgets
        src:x:/../*/select-data/*
        template
          create-container-widget
            element:button
            parent:icu-local-result-items
            class:icu-search-result-item rounded-corners box-shadow
            onclick

              /*
               * Showing preview of image.
               * First hiding list of images, and then showing preview image widget
               */
              set-widget-property:icu-local-result-items
                visible:false
              set-widget-property:icu-local-preview
                visible:true

              /*
               * Storing ID of button in page value, such that we can set focus back to it, if user closes dialog
               */
              set-page-value:sys42.icu.local-current-button
                src:x:/../*/_event?value

              /*
               * HTML encoding description, in case it contans HTML
               */
              {html-encode}:x:/*/description?value

              /*
               * Then setting src and alt properties of preview image widget to properties from selected thumb
               */
              set-widget-property:icu-local-preview-image
                {src}:x:/*/images/*/original?value
                alt:x:/../*/html-encode?value
                {_id}:x:?value

              /*
               * Settings header of preview dialog to [word] from [icu.word]
               */
              set-widget-property:icu-local-result-header
                {innerValue}:x:/*/word?value

              /*
               * Making sure OK button gets initial focus
               */
              send-javascript:@"$('#icu-local-ok-select-image').focus();"

              /*
               * Making sure "esc" key closes preview
               */
              include-javascript:@"p5.localPreviewKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-local-cancel-preview').raise('onclick');
    e.stopPropagation();
    return false;
  }
}"

            widgets
              img
                {src}:x:/*/images/*/image?value
                {alt}:x:/*/word?value
                style:"max-width:100%;max-height:100%;"

      /*
       * Creating widgets, by evaluating [_widget], which should contain a bunch of [create-literal-widget] invocations now ...
       */
      eval:x:/../*/_widgets

      /*
       * Adding "next" button, but only if there are anymore items
       */
      if:x:/../*/select-data/*?count
        >=:int:20

        /*
         * Possibly more items in database, enabling "next" button
         */
        set:x:/+/**/_page?value
          +:x:/../*/_page?value.int
            _:1
        create-literal-widget
          parent:icu-local-result-items
          element:button
          class:icu-search-result-item rounded-corners box-shadow
          innerValue:@"<span class=""glyphicon glyphicon-play""></span>"
          style:"background-color:White;font-size:24px;"
          title:Get next batch of result
          onclick

            /*
             * Fetching next items from database
             */
            sys42.icu.local-search-get-next-items
              _page

      /*
       * Making sure first search result from database gets focus
       */
      get-children-widgets:icu-local-result-items
      send-javascript:@"$('#{0}').focus();"
        :x:/./-/*/0?value

  widgets
    container
      class:icu-search-modal-window rounded-corners box-shadow air-padding-more
      widgets

        /*
         * Close "search result" modal window button
         */
        literal:icu-local-hide-preview
          element:a
          href:#
          innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
          class:icu-close-dialog
          onclick
            delete-widget:icu-local-result
            send-javascript:@"$('#icu-configure-key-search-textbox').focus().select();"
        literal:icu-local-result-header
          element:h3
          innerValue:Local database
        void
          element:hr

        /*
         * Contains all images in our search result
         */
        container:icu-local-result-items
          oninit

            /*
             * Initial databinding operation
             */
            sys42.icu.local-search-get-next-items
              _page:int:0

            /*
             * Making sure search dialog can be closed with esc key
             */
            include-javascript:@"p5.localSearchKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-local-hide-preview').raise('onclick');
    e.stopPropagation();
    return false;
  }
}"

        /*
         * Used to preview a single image
         */
        container:icu-local-preview
          visible:false
          class:icu-search-preview
          onkeyup:"return p5.localPreviewKeyUp(event);"
          widgets

            /*
             * Actual preview image widget
             */
            literal:icu-local-preview-image
              class:icu-search-preview-image rounded-corners box-shadow
              element:img

            /*
             * Making sure there's some "space" between image preview and button row
             */
            void
              element:hr

            /*
             * Button row at the bottom of preview dialog, to allow user to select image
             */
            container
              class:prepend-top
              style:"width:100%;"
              widgets

                /*
                 * Cancel preview button
                 */
                button:icu-local-cancel-preview
                  class:btn-default btn pull-right
                  innerValue:@"<span class=""glyphicon glyphicon-remove""></span> Cancel"
                  tabindex:2
                  style:"width:100px;"
                  onclick

                    /*
                     * Toggling visibility of preview image widget, and list images widget
                     * This simply allows user to select another image in list of images
                     */
                    set-widget-property:icu-local-result-items
                      visible:true
                    set-widget-property:icu-local-preview
                      visible:false

                    /*
                     * Setting header of preview dialog back to "Database result"
                     */
                    set-widget-property:icu-local-result-header
                      innerValue:Database result

                    /*
                     * Setting focus back to button that was clicked to show preview
                     */
                    get-page-value:sys42.icu.local-current-button
                    send-javascript:@"$('#{0}').focus();"
                      :x:/./-/*?value

                /*
                 * OK preview image button (selects image)
                 */
                button:icu-local-ok-select-image
                  class:btn-default btn pull-right
                  innerValue:@"<span class=""glyphicon glyphicon-ok""></span> OK"
                  tabindex:1
                  style:"width:100px;margin-right:10px;"
                  onclick

                    /*
                     * Getting properties of image previewed from page value
                     */
                    get-widget-property:icu-local-preview-image
                      _id
                    eval-x:x:/+/*
                    sys42.execute-lisp-file:/system42/apps/icu/board/language-builder/helpers/configure-action.hl
                      _image:x:/../*/get-widget-property/*/*?value

                    /*
                     * Deleting local result widget
                     */
                    delete-widget:icu-local-result
