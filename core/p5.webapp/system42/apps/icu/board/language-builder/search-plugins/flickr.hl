
/*
 * Flickr search plugin. Allows user to search for images on flickr to use in ICU app
 */

button
  innerValue:@"<span class=""glyphicon glyphicon-globe""></span>"
  class:btn-default btn btn-lg
  title:Searches for images on flickr
  onclick

    /*
     * Retrieving "search query" and using it as search text for searching flickr.com for images matching criteria
     */
    get-widget-property:icu-configure-key-search-textbox
      value
    p5.flickrnet.search
      text:x:/../*/get-widget-property/*/*?value
      page:0
      per-page:42

    /*
     * Databinding result widget to flickr return value
     */
    databind:x:/../*/create-widget/*/widgets/*/container/*/widgets/*/container/=icu-flickr-result-items/*/widgets
      src:x:/../*/p5.flickrnet.search/*
      template
        button
          class:icu-flickr-result-item rounded-corners box-shadow
          onclick

            /*
             * Showing preview of image.
             * First hiding list of images, and then showing preview image widget
             */
            set-widget-property:icu-flickr-result-items
              visible:false
            set-widget-property:icu-flickr-preview
              visible:true

            /*
             * HTML encoding description, in case it contans HTML
             */
            {_description}:x:/*/description?value
            html-encode:x:/-?value

            /*
             * Then setting src and alt properties of preview image widget to properties from selected thumb
             */
            set-widget-property:icu-flickr-preview-image
              {src}:x:/*/medium?value
              alt:x:/../*/html-encode?value

            /*
             * Settings description of image
             */
            set-widget-property:icu-flickr-preview-description
              {innerValue}:x:/*/description?value

            /*
             * Settings header of preview dialog to title from flickr
             */
            set-widget-property:icu-flickr-result-header
              {innerValue}:x:/*/title?value

            /*
             * Making sure OK button gets initial focus
             */
            send-javascript:@"$('#icu-flickr-ok-select-image').focus();"

            /*
             * Making sure "esc" key closes preview
             */
            include-javascript:@"p5.flickrPreviewKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-flickr-cancel-preview').raise('onclick');
  }
}"

            /*
             * Storing URL, title and description to image we're previewing as page value, 
             * such that we can retreieve these values if user clicks OK
             */
            set-page-value:sys42.icu.current-preview-image
              src
                image
                  {medium}:x:/*/medium?value
                    {width}:x:/*/medium/*/width?value
                    {height}:x:/*/medium/*/height?value
                  {title}:x:/*/title?value
                  {description}:x:/*/description?value

          widgets
            img
              {src}:x:/*/thumb?value
              {alt}:x:/*/title?value

    /*
     * Building modal window, showing first 42 images
     */
    sys42.icu.get-icu-wrapper
    eval-x:x:/+/*/parent
    create-widget:icu-flickr-result
      class:icu-search-modal-bg
      parent:x:/../*/sys42.icu.get-icu-wrapper?value
      widgets
        container
          class:icu-search-modal-window rounded-corners box-shadow air-padding-more
          widgets

            /*
             * Close "search result" modal window button
             */
            literal:icu-flickr-hide-preview
              element:a
              href:#
              innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
              class:icu-close-dialog
              onclick
                delete-widget:icu-flickr-result
                send-javascript:@"$('#icu-configure-key-search-textbox').focus().select();"
            literal:icu-flickr-result-header
              element:h3
              innerValue:Flickr result
            void
              element:hr

            /*
             * Contains all images in our search result
             */
            container:icu-flickr-result-items
              widgets

            /*
             * Used to preview a single image
             */
            container:icu-flickr-preview
              visible:false
              class:icu-flickr-preview
              onkeyup:"return p5.flickrPreviewKeyUp(event);"
              widgets

                /*
                 * Description of image
                 */
                literal:icu-flickr-preview-description
                  class:icu-flickr-preview-description

                /*
                 * Actual preview image widget
                 */
                literal:icu-flickr-preview-image
                  class:icu-flickr-preview-image rounded-corners box-shadow
                  element:img

                /*
                 * Making sure there's some "space" between image preview and button row
                 */
                void
                  element:hr

                /*
                 * Button row at the bottom of preview dialog, to allow user to select image
                 */
                container
                  class:prepend-top
                  style:"width:100%;"
                  widgets

                    /*
                     * Cancel preview button
                     */
                    button:icu-flickr-cancel-preview
                      class:btn-default btn pull-right
                      innerValue:@"<span class=""glyphicon glyphicon-remove""></span> Cancel"
                      style:"width:100px;"
                      onclick

                        /*
                         * Toggling visibility of preview image widget, and list images widget
                         * This simply allows user to select another image in list of images
                         */
                        set-widget-property:icu-flickr-result-items
                          visible:true
                        set-widget-property:icu-flickr-preview
                          visible:false

                        /*
                         * Setting header of preview dialog back to "Flickr result"
                         */
                        set-widget-property:icu-flickr-result-header
                          innerValue:Flickr result

                    /*
                     * OK preview image button (selects image)
                     */
                    button:icu-flickr-ok-select-image
                      class:btn-default btn pull-right
                      innerValue:@"<span class=""glyphicon glyphicon-ok""></span> OK"
                      style:"width:100px;margin-right:10px;"
                      onclick

                        /*
                         * Getting properties of image previewed from page value
                         */
                        get-page-value:sys42.icu.current-preview-image

                        /*
                         * Downloading medium image, and storing to user's "temp" folder
                         */
                        split:x:/../*/get-page-value/*/*/*/medium?value
                          =:/
                        p5.net.http-get:x:/../*/get-page-value/*/*/*/medium?value
                        whoami
                        save-file:/users/{0}/temp/{1}
                          :x:/../*/whoami/*/username?value
                          :x:/../*/split/0/-?name
                          src:x:/../*/p5.net.http-get/*/*/content?value

                        /*
                         * Figuring out a unique filename, with some parts of it being the "title" from Flickr,
                         * and the rest of it being date-time, prepended with "flickr-" to understand where the
                         * image came from later. This allows searching for files with context sensitive filenames
                         * later, to build keys, searching locally
                         */
                        date-now
                        replace:x:/-?value
                          not-of:"0123456789"
                          with:"-"
                        to-lower:x:/../*/get-page-value/*/*/*/title?value
                        replace:x:/-?value
                          not-of:"abcdefghijklmnopqrstuvwxyz0123456789"
                          with:"-"

                        /*
                         * Resizing image to ICU "common-keys" folder
                         */
                        eval-x:x:/+/*/_destination
                        sys42.icu.resize-image-to-keys-folder:/users/{0}/temp/{1}
                          :x:/../*/whoami/*/username?value
                          :x:/../*/split/0/-?name
                          _destination:/system42/apps/icu/media/images/user-space/common-keys/flickr-{0}-{1}.jpg
                            :x:/../*/replace/[0,1]?value
                            :x:/../*/replace/[1,2]?value

                        /*
                         * Starts configuring image to use for key, but first deleting modal flickr window
                         */
                        delete-widget:icu-flickr-result
                        eval-x:x:/+/*
                        sys42.icu.configure-image-for-key
                          _image:/system42/apps/icu/media/images/user-space/common-keys/flickr-{0}-{1}.jpg
                            :x:/../*/replace/[0,1]?value
                            :x:/../*/replace/[1,2]?value
                          _word:x:/../*/to-lower?value
                          _description::x:/../*/get-page-value/*/*/*/description?value

