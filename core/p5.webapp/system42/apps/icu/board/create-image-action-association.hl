
/*
 * Responsible for creating a "key"/action association for the specified [_board], at the given
 * [_x]/[_y] position, with the given [_image] as key's image
 */


/*
 * Making sure we pass in [_image], [_board], [_x] and [_y], to whomever needs it
 */
set:x:/../**/={_board}?value
  src:x:/../*/_board?value.guid

set:x:/../**/={_x}?value
  src:x:/../*/_x?value

set:x:/../**/={_y}?value
  src:x:/../*/_y?value

set:x:/../**/={_image}?value
  src:x:/../*/_image?value


/*
 * Lists all files from within "actions" folder, and allows user to select specific action defined in file,
 * by creating clickable widget for each possible action from folder
 */
list-files:/system42/apps/icu/board/actions/
  filter:hl
for-each:x:/-/*?name
  load-file:x:/./*/_dp?value
  _widget
    container
      element:li
      widgets
        literal
          element:a
          href:#
          innerValue
          onclick

            /*
             * will contain the specified [_x], [_y], [_image] and [_board] value supplied by caller when file 
             * is invoked
             */
            _x:{_x}
            _y:{_y}
            _image:{_image}
            _board:{_board}

            /*
             * Deleting widget that allows user to search for or upload image, for then to delete
             * the widget that allows user to specify action for image/key association
             */
            delete-widget:icu-create-board-image-key-ass
            delete-widget:icu-action-association

            /*
             * Loads "action file", and extracts [_action-creator] as lambda object, and associates
             * this lambda object with [onclick] of "board key" widget, by invoking [sys42.icu.set-board-picture-for-square]
             */
            load-file:{file-name}
            insert-before:x:/../*/load-file/*/*(/_action|/_action-configure)/0
              src:x:/../*(/~_!/_event)
            set:x:/../**/sys42.icu.set-board-picture-for-square/*(!/_action)?value
              rel-src:x:/../*/{0}?value
                :x:?name
            add:x:/../**/sys42.icu.set-board-picture-for-square/*/_action
              src:x:/../*/load-file/*/*/_action/*
            if:x:/../*/load-file/*/*/_action-configure

              /*
               * Action contains an [_action-configure] part. 
               * Invoking [_action-configure] letting it do the work, instead of invoking
               * [sys42.icu.set-board-picture-for-square] immediately with [_action]
               */
              eval:x:/../*/load-file/*/*/_action-configure
            else

              /*
               * Action does not contain any [_action-configure] parts, creating action
               * immediately
               */
              sys42.icu.set-board-picture-for-square
                _x
                _y
                _image
                _board
                _action


  /*
   * Setting [innerValue] of link to [_action-name] from "action plugin file"
   */
  set:x:/..for-each/*/_widget/*/container/*/widgets/*/literal/*/innerValue?value
    src:x:/..for-each/*/load-file/*/*/_action-name?value

  /*
   * Making sure correct "action file" is loaded and evaluated upon clicking "action hyperlink selector"
   */
  set:x:/..for-each/*/_widget/**/={file-name}?value
    src:x:/..for-each/*/_dp?value

  /*
   * Adding widget to [create-widget] event below
   */
  add:x:/../*/create-widget/*/widgets/*/container/*/widgets/*/container/[0,1]/*/widgets
    src:x:/..for-each/*/_widget/*


/*
 * Creating widget that allows user to select "action" to associate with image, now with one widget for
 * each possible action, dynamically created in above [for-each]. Making sure we delete any previously created
 * widgets of same type first
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-action-association
  delete-widget:icu-action-association
create-widget:icu-action-association
  class:action-lightbox
  widgets
    container:action-modal-window
      class:action-modal-window
      widgets
        literal:icu-close-create-action
          class:icu-close-create-action
          element:a
          href:#
          innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
          onkeyup:"p5.actionWindowKeyUp(event);"
          onclick
            sys42.icu.icu-create-board-image-key-ass.closed
            send-javascript:@"
$('#action-modal-window').toggleClass('action-modal-window-fadeout').one('animationend',
  function(e) {
    $('#' + e.target.id).hide();
    $('#icu-action-association').fadeOut({duration:200});
  });"
        literal
          element:h3
          innerValue:Action
        void
          element:hr
        container
          element:ul
          class:action-item-list
          widgets


/*
 * Making sure close buttons gets focus
 */
send-javascript:@"$('#icu-close-create-action').focus();"


/*
 * Including JavaScript necessary to trap escape in modal window
 */
include-javascript:@"
p5.actionWindowKeyUp = function(e) {
  if(e.keyCode == 27) {
    p5.$('icu-close-create-action').raise('onclick');
    return false;
  }
};"
