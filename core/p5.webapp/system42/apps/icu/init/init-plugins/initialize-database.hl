

/*
 * Initializes ICU by making sure there exists at least one "root/home board" for user,
 * and verifies default settings for user is created
 */




/*
 * Necessary to filter database according to user
 */
whoami




/*
 * Checking if there exists default settings for user, and if not, creating default settings
 */
select-data:x:/*/*/icu.settings/*/username/={0}/.?count
  :x:/../*/whoami/*/username?value
if:x:/-?value
  =:int:0

  /*
   * Creating default settings, width/height equals 14x3 for default boards created
   */
  eval-x:x:/+/*/*/username
  insert-data
    icu.settings
      username:x:/../*/whoami/*/username?value
      show-text:bool:true
      animations:bool:true
      default-board-width:int:14
      default-board-height:int:3




/*
 * Checking if there exists a "home" board for user, and if not, creating a default "home" board
 */
select-data:x:/*/*/icu.board/*/username/={0}/./*/name/=home/.?count
  :x:/../*/whoami/*/username?value
if:x:/-?value
  =:int:0

  /*
   * User has no "home board" associated with profile, creating empty "home" board.
   * But first retrieving settings, to figure out how big our board should be.
   */
  select-data:x:/*/*/icu.settings/*/username/={0}/.
    :x:/../*/whoami/*/username?value
  eval-x:x:/+/*/**(/username|/width|/height)
  insert-data
    icu.board
      username:x:/../*/whoami/*/username?value
      name:home
      size
        width:x:/..if/*/select-data/*/*/default-board-width?value
        height:x:/..if/*/select-data/*/*/default-board-height?value
      items

