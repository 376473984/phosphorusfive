
/*
 * Responsible for creating a picture for the specified [_board] at the given
 * [_x]/[_y] position
 */


/*
 * Making sure any previously created widgets are detroyed
 */
if
  fetch:x:/0/0?value
    widget-exist:icu-create-board-picture
  delete-widget:icu-create-board-picture


/*
 * Making sure we pass in parent widget, and other arguments, to whomever needs it
 */
set:x:/../**/={_parent-widget}?value
  src:x:/../*/_parent-widget?value
set:x:/../**/={_board}?value
  src:x:/../*/_board?value
set:x:/../**/={_x}?value
  src:x:/../*/_x?value
set:x:/../**/={_y}?value
  src:x:/../*/_y?value


/*
 * Creating widget which allows for associating new picture with board box
 */
_sys42.show-code-window:x:/..
eval-x:x:/+/*/parent
create-widget:icu-create-board-picture
  parent:x:/../*/_language-builder?value
  class:prepend-bottom
  widgets
    container
      class:icu-create-board
      widgets
        literal:icu-close-create-board
          element:a
          href:#
          innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
          onclick
            delete-widget:icu-create-board-picture
            find-widget-like:{_parent-widget}
              class:icu-active
            if:x:/-/*?value
              set-widget-property:x:/./-/*?value
                class:icu-empty-cell
        container
          class:form col-xs-12 col-md-5
          widgets
            container
              class:input-group
              widgets
                void:icu-create-board-search-box
                  element:input
                  type:search
                  class:form-control input-lg
                  placeholder:...
                  onkeypress:"return p5.searchIcuTextBox(event);"
                  oninit
                    send-javascript:@"$('#{0}').focus();"
                      :x:/../*/_event?value
                span
                  class:input-group-btn
                  widgets
                    button:icu-create-board-search
                      class:btn-default btn btn-lg
                      type:button
                      innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
                      onclick
                        clear-widget:icu-flickr-result
                        get-widget-property:icu-create-board-search-box
                          value
                        send-javascript:@"$('#icu-create-board-search-box').focus().select();"
                        p5.flickrnet.search
                          tags:x:/../*/get-widget-property/*/*?value
                          count:50
                        for-each:x:/../*/p5.flickrnet.search/*/result
                          eval-x:x:/+/**(/img/*/src|/_large|/_thumb)
                          create-widget
                            parent:icu-flickr-result
                            class:icu-flickr-result-item
                            widgets
                              a
                                href:#
                                onclick
                                  create-widget:icu-lightbox-picture
                                    class:icu-lightbox-picture
                                    onclick
                                      delete-widget:x:/../*/_event?value
                                    widgets
                                      a
                                        href:#
                                        widgets
                                          img
                                            src:x:/..for-each/*/_dp/#/*/normal?value
                                            onclick
                                              _board:{_board}
                                              _x:{_x}
                                              _y:{_y}
                                              delete-widget:icu-lightbox-picture
                                              _large:x:/..for-each/*/_dp/#/*/normal?value
                                              _thumb:x:/..for-each/*/_dp/#/*/thumb?value
                                              add:x:/+
                                                src:x:/../*(/_board|/_x|/_y|/_thumb|/_large)
                                              sys42.icu.set-board-picture-for-square
                                widgets
                                  img
                                    src:x:/..for-each/*/_dp/#/*/thumb?value
        literal:icu-upload-icon
          element:span
          innerValue:@"<span class=""glyphicon glyphicon-share-alt""></span>"
    container:icu-flickr-result


/*
 * Including JavaScript necessary to trap carriage return in search textbox
 */
include-javascript:@"
p5.searchIcuTextBox = function(e) {
  if(e.keyCode == 13) {
    p5.$('icu-create-board-search').raise('onclick');
    return false;
  }
}"
