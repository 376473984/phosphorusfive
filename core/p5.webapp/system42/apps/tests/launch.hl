/*
 * Allows the user to evaluate unit tests in system
 */

/*
 * Verifies user is root first, since only root
 * can run unit tests
 */
whoami
if:x:/-/*/role?value
  !=:root
  throw:Only root user can run Unit Tests

/*
 * List all files in directory, but removing "index.hl",
 * assuming all other files are unit test files
 */
list-files:/system42/apps/tests/
set:x:@"/-/*/""/system42/apps/tests/launch.hl"""

/*
 * Callback invoked while databinding innerValue of unit test name
 */
_eval-name
  split:x:/../*/_dn/#?name
    =:/
    =:.
  eval-x:x:/+/*/*
  insert-before:x:/../0
    src
      innerValue:x:/../*/split/0/-2?name

/*
 * Databinding unit tests files to table widget
 */
apply:x:/../*/create-widget/=unit-test-files/*/widgets
  src:x:/../*/list-files/*
  template
    tr

      /*
       * Used for retrieving unit test file row, to signal success or failure,
       * through red or green color on row as a whole
       */
      {_file-row}:x:?name
      widgets

        /*
         * Unit test filename
         */
        td
          widgets
            a
              {@eval}:x:/../*/_eval-name
              href:#
              title:Click to edit or view unit test file
              onclick
                sys42.launch-unit-test-editor
                  {_file}:x:?name

        /*
         * Result text from evaluation of file, contains
         * either one or more bugs, or the names of all
         * unit tests that ran. Value depends upon whether
         * or not evaluation of unit tests was a success
         * or not. Before test is ran, "Undetermined" is
         * its value
         */
        td
          style:"width:60%;"
          innerValue:Undetermined
          {_file-result}:x:?name

        /*
         * Button that allows evaluation of a single unit test file.
         * Notice that you cannot run a single unit test, you can however
         * run a single unit test file
         */
        td
          widgets
            button
              class:btn btn-default form-control
              innerValue:Evaluate
              onclick

                /*
                 * Evaluates the databound unit test file, which may contain
                 * multiple unit tests. [_file] is filename of unit test file
                 */
                {_file}:x:?name
                _html
                sys42.evaluate-unit-test-file:x:/../*/_file?value
                if:x:/-/*/_error

                  /*
                   * Error while evaluating unit test file, setting result
                   * to ell error "messages" + "stack-trace" of exception,
                   * and making sure we signal in context value that error 
                   * occurred
                   */
                  for-each:x:/../*/sys42.evaluate-unit-test-file/*/_error?value
                    html-encode:x:/./*/_dp?value
                    set:x:/../*/_html?value
                      src:@"{0}<pre class=""error"">{1}</pre>"
                        :x:/../*/_html?value
                        :x:/..for-each/*/html-encode?value
                  set-context-value:sys42.unit-test-errors
                    src:bool:true

                  /*
                   * Setting unit tests table row to class "error", and button
                   * CSS class to warning
                   */
                  find-widget
                    _file-row:x:/../*/_file?value
                  set-widget-property:x:/-/*?value
                    class:error
                  set-widget-property:x:/../*/_event?value
                    class:btn btn-warning form-control
                    disabled
                else

                  /*
                   * Success while evaluating unit test file, no errors.
                   * Setting unit tests table row to class "success" and
                   * button CSS class to success
                   */
                  find-widget
                    _file-row:x:/../*/_file?value
                  set-widget-property:x:/-/*?value
                    class:success
                  set-widget-property:x:/../*/_event?value
                    class:btn btn-success form-control
                    disabled

                /*
                 * Updating unit test count
                 */
                get-session-value:sys42.unit-test-count
                if:x:/-/*?value
                  +:x:/./-/*?value
                    _:x:/../*/sys42.evaluate-unit-test-file/*(/_success|/_error)?count
                  set-session-value:sys42.unit-test-count
                    src:x:/./-?value

                /*
                 * Providing textual feedback to user about state of unit tests
                 */
                find-widget
                  _file-result:x:/../*/_file?value
                join:x:/../*/sys42.evaluate-unit-test-file/*/_success?value
                  sep:"]\r\n</li><li>["
                set-widget-property:x:/-2/*?value
                  innerValue:@"{0}<pre><ol><li>[{1}]</li></ol></pre>"
                    :x:/../*/_html?value
                    :x:/././-?value

/*
 * Creating widget showing all unit test files in a table
 */
create-widget:unit-test-files
  parent:content
  element:table
  class:table
  events

    /*
     * Invoked when a unit test file is being edited
     */
    sys42.launch-unit-test-editor

      // Launching editor
      eval-x:x:/+/0/*
      if
        sys42.execute-lisp-file:/system42/editors/launch.hl
          _file:x:/../*/_file?value
          _parent:content
          _widget-id:unit-test-file-editor
        =:bool:true

        // Changing header of page to reflect we're in edit mode
        set-widget-property:header
          innerValue:Editing test file <small>{0}</small>
            :x:/../*/_file?value

        // Hiding unit test table and "add" button
        set-widget-property:unit-test-files
          visible:false
        set-widget-property:add-unit-test
          visible:false

    /*
     * Invoked when file editor is destroyed
     */
    sys42.destroy-file-editor

      // Changing header of page back to original state
      set-widget-property:header
        innerValue:Tests

      // Deleting editor widget
      delete-widget:unit-test-file-editor

      // Making unit test table and "add button" visible again
      set-widget-property:unit-test-files
        visible:true
      set-widget-property:add-unit-test
        visible:true

    /*
     * Evaluates one Unit Test file
     */
    sys42.evaluate-unit-test-file
      load-file:x:/../*/_arg?value
      for-each:x:/-/*/*
        _test
        set:x:/-?value
          src:x:/..for-each/*/_dp/#
        try
          eval:x:/..for-each/*/_test?value
          add:x:/..
            src:"_success:{0}"
              :x:/..for-each/*/_dp/#?name
        catch
          eval-x:x:/+/*/*
          add:x:/..
            src
              _error:@"{0}; ""{1}""

{2}"
                :x:/..for-each/*/_dp/#?name
                :x:/..catch/*/message?value
                :x:/..catch/*/stack-trace?value


  /*
   * Widgets that are being databound above to show table containing all unit test
   * files in system
   */
  widgets
    thead
      widgets
        tr
          widgets
            th
              innerValue:Unit test file
            th
              innerValue:Result
            th
              class:text-right
              widgets
                button
                  class:btn btn-primary form-control
                  innerValue:Evaluate all
                  onclick
                    set-context-value:sys42.unit-test-errors
                      src:bool:false
                    date-now
                    set-session-value:sys42.unit-test-count
                      src:int:0
                    find-widget
                      innerValue:Evaluate
                    raise-widget-ajax-event:x:/../*/find-widget/*?value
                      onclick
                    get-session-value:sys42.unit-test-count
                    date-now
                    -:x:/-?value
                      _:x:/../*/date-now/[0,1]?value
                    eval-x:x:/+
                    sys42.info-window:@"{0} unit tests evaluated in {1:s\.fff} seconds"
                      :x:/./-4/*?value
                      :x:/./-2?value
                      _time:more
                    get-context-value:sys42.unit-test-errors
                    if:x:/-/*?value
                      set-widget-property:x:/../*/_event?value
                        class:btn btn-warning form-control
                        disabled
                    else
                      set-widget-property:x:/../*/_event?value
                        class:btn btn-success form-control
                        disabled


create-widget:add-unit-test
  parent:content
  class:col-xs-6 col-xs-push-6 col-md-4 col-md-push-8 col-lg-3 col-lg-push-9 text-right prepend-top
  widgets

    /*
     * Creates new unit test file
     */
    button
      class:btn btn-default col-xs-6
      innerValue:+
      title:Create new unit test file
      onclick
        sys42.wizard-window
          _header:Name of unit test file
          _body:Please provide a filename for your new test file
          _widgets
            text:filename
              label:Filename
              placeholder:Name of file ...
              mandatory:true
          _onok
            split:x:/../*/filename?value
              =:.
            if:x:/-/*?count
              =:int:1
              set:x:/../*/filename?value
                src:{0}.hl
                  :x:/../*/filename?value
            split:x:/../*/filename?value
              =:/
            if:x:/-/*?count
              >:int:1
              set:x:/..?value
                src:Please only provide filename, no paths!
              send-javascript:@"$('#filename').focus().select();"
            else
              save-file:{0}{1}
                :/system42/apps/tests/
                :x:/../*/filename?value
                src:@"/*
 * Contains unit tests for testing [whatever] in system
 */

/*
 * Tests that checks that ""foo1"" behaves correctly
 */
foo1
  _some-test-code-goes-here
  if:bool:false
    throw:@""Assert error, expected [something], got [something-else]!""

/*
 * Tests that checks that ""foo2"" behaves correctly
 */
foo2
  _some-other-piece-of-test-code-goes-here
  if:bool:false
    throw:@""Assert error, expected [something], got [something-else]!""
"
              set:x:/..?value
                src:bool:true
              delete-widget:add-unit-test
              delete-widget:unit-test-files
              sys42.execute-lisp-file:/system42/apps/tests/launch.hl

    /*
     * Clear unit tests
     */
    button
      class:btn btn-default col-xs-6
      innerValue:Clear
      title:Clear unit tests
      onclick
        delete-widget:add-unit-test
        delete-widget:unit-test-files
        sys42.execute-lisp-file:/system42/apps/tests/launch.hl
