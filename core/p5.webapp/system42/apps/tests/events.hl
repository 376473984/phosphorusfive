/*
 * Contains unit tests for testing dynamically created events in system
 */

/*
 * Tests that events can be stateful
 */
stateful-event
  create-event:test.stateful
    _static:node:"count:int:0"
    set:x:/../*/_static/#?value
      +:x:/../*/_static/#?value
        _:int:1
    eval-x:x:/+
    return:Count {0}
      :x:/../*/_static/#?value
  test.stateful
  test.stateful
  test.stateful
  if:x:/../*/test.stateful/[0,1]?value
    !=:Count 1
    or:x:/../*/test.stateful/[1,2]?value
      !=:Count 2
    or:x:/../*/test.stateful/[2,3]?value
      !=:Count 3
    throw:@"Assert error, stateful events was not stateful"
  delete-event:test.stateful

/*
 * Tests that events can be deleted
 */
delete-event
  create-event:test.to-delete
    return:foo
  test.to-delete
  delete-event:test.to-delete
  test.to-delete
  if:x:/../*/test.to-delete/[0,1]?value
    !=:foo
    or:x:/../*/test.to-delete/[1,2]?value
    throw:@"Assert error, event could not be deleted"


/*
 * Tests that events cannot be created with a "." as the start of their name
 */
create-dot-event-throw
  _success:bool:false
  try
    create-event:.foo
      foo
  catch
    set:x:/../*/_success?value
      src:boool:true
  if:x:/../*/_success?value
    not
    throw:Could create protected event


/*
 * Tests that events cannot be created with a "." as the start of their name
 */
create-underscore-event-throw
  _success:bool:false
  try
    create-event:_foo
      foo
  catch
    set:x:/../*/_success?value
      src:boool:true
  if:x:/../*/_success?value
    not
    throw:Could create protected event
  
  