/*
 * Creates the web widget that allows for
 * editing users and roles in the system
 */


include-stylesheet-file:/system42/apps/users/media/users.css

create-widget:edit-users
  parent:content
  class:form
  widgets


    /*
     * Jesus Google Chrome devs, fucking OBEY THE OPEN WEB STANDARDS!!
     * Not obeying autocomplete="off" is simply not acceptable!!
     * I don't care if you quote King Kong, Lawrence Lessig or Winnie the Pooh!
     * THIS IS SIMPLY NOT ACCEPTABLE!!
     */
    literal
      style:"display:none;"
      element:div
      innerValue:@"<input><input type=""password"">"


    /*
     * The "Edit Existing user ..." drop down select list
     */
    container
      class:col-xs-10 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              class:input-group-addon
              innerValue:Edit user
            container:edit-existing-user-select
              element:select
              class:form-control
              accesskey:U


              /*
               * Populates the "Edit Existing user" select drop down list
               */
              oninit
                sys42.populate-edit-existing-user

                /*
                 * Checking if we're currently editing a user
                 */
                get-session-value:sys42.currently-edited-user
                if:x:/-/*?value
                  set-widget-property:edit-existing-user-select
                    value:x:/././-/*?value
                  sys42.edit-existing-user


              /*
               * Here we create a new page according to which option item
               * was selected
               */
              onchange
                sys42.edit-existing-user


              /*
               * Lambda events for "Edit existing user" widget
               */
              events


                /*
                 * Here we loop through all users in system, and create an option
                 * select list item for each of them
                 */
                sys42.populate-edit-existing-user

                  // Making sure we empty widget, if it has been populated before
                  clear-widget:edit-existing-user-select

                  // Creating "_default" option element, for "un-selecting" edited user
                  create-literal-widget
                    element:option
                    parent:edit-existing-user-select
                    value:_default
                    innerValue:Select user ...
                  list-users
                  for-each:x:/-/*?name
                    if:x:/./*/_dp?value
                      =:x:/../*/_arg?value
                      add:x:/..for-each/*/create-literal-widget
                        src:selected
                    add:x:/..for-each/*/create-literal-widget
                      src:"value:{0}"
                        :x:/..for-each/*/_dp?value
                    add:x:/..for-each/*/create-literal-widget
                      src:"innerValue:{0}"
                        :x:/..for-each/*/_dp?value
                    create-literal-widget
                      element:option
                      parent:edit-existing-user-select


                /*
                 * Edits an existing user
                 */
                sys42.edit-existing-user
                  sys42.execute-lisp-file:/system42/apps/users/edit-specific-user.hl


    /*
     * The "Create new user" button
     */
    container
      class:col-xs-2 form-group
      widgets
        literal
          element:button
          class:form-control btn btn-default
          innerValue:+
          accesskey:P
          title:Creates a new user
          onclick

            /*
             * Displaying modal dialogue to retrieve new user's username and password
             */
            sys42.wizard-window
              _header:Type in username/password of new user
              _body:@"Please supply a username and a password for your new user. <br>
<strong>Notice that the username cannot be changed after the user has been created!</strong>"
              _widgets
                text:username
                  label:Username
                  mandatory:true
                  placeholder:Please supply a username for user ...
                  autocomplete:off
                password:password
                  label:Password
                  mandatory:true
                  placeholder:Please supply a password for user ...
                  autocomplete:off
                password:password-repeat
                  label:Repeat
                  mandatory:true
                  placeholder:Please repeat the password for user ...
                  autocomplete:off
              _onok
                if:x:/../*/password?value
                  =:x:/../*/password-repeat?value

                  /*
                   * Success! Passwords matched! Creating new user.
                   * While also signaling to moda-wizard that modal dialogue can be closed
                   */
                  set:x:/..?value
                    src:bool:true
                  sys42.get-default-user-settings
                  add:x:/+
                    src:x:/./-/*
                  create-user
                    username:x:/../*/username?value
                    password:x:/../*/password?value
                    role:user
                  sys42.populate-edit-existing-user:x:/../*/username?value
                  sys42.edit-existing-user
                else

                  /*
                   * Failure" Passwords did not match! Signaling that fact to caller
                   */
                  set:x:/..?value
                    src:Passwords don't match
                  send-javascript:@"$('#password').focus().select();"

