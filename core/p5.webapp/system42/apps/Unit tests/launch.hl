
/*
 * Allows the user to inspect, edit, and evaluate unit tests in the system.
 * All unit tests can be found in the "/tests/" folder.
 */


/*
 * Verifies user is root first, since only root can run unit tests
 */
whoami
if:x:/-/*/role?value
  !=:root
  throw:Only root user can run unit tests


/*
 * List all files in "/tests/" directory.
 * Each file is considered a "single test collection for specific concept".
 */
list-files:/system42/apps/Unit tests/tests/
  filter:hl


/*
 * Callback invoked while [apply]'ing innerValue of unit test name,
 * such that we can remove the directory path and ".hl" extension.
 */
_eval-name
  split:x:/../*/_dn/#?name
    =:/
    =:.
  eval-x:x:/+/*/*
  insert-before:x:/../0
    src
      innerValue:x:/../*/split/0/-2?name


/*
 * Applying unit tests files to table widget
 */
apply:x:/../*/create-widget/=unit-test-datagrid/*/widgets/*/tbody/*/widgets
  src:x:/../*/list-files/*
  template
    tr

      /*
       * Used for retrieving unit test file row, to signal success or failure,
       * through red or green color on row as a whole
       */
      {_file-row}:x:?name
      widgets

        /*
         * Unit test filename.
         * If the user clicks the filename, then the Hyperlisp/CodeMirror editor is launched, allowing
         * the user to edit the specified test.
         */
        td
          widgets
            a
              {@eval}:x:/../*/_eval-name
              href:#
              title:Click to edit or view unit test file
              {_file-button}:x:?name
              onclick

                /*
                 * Checking  if editor is already loaded, and if so, deleting existing editing widget (entire "tr" element).
                 * Returns early if editor exists.
                 */
                if
                  fetch:x:/0/0?value
                    widget-exist:{0}_editor
                      :x:/../*/_event?value

                  /*
                   * Deleting existing editor, since file is already edited, before returning early.
                   */
                  delete-widget:{0}_editor
                    :x:/../*/_event?value
                  return

                /*
                 * Invoking the Hyperlisp file responsible for creating our CodeMirror 
                 * editor (in a "tr" row beneath test row)
                 */
                add:x:/+
                  src:"_widget:{0}"
                    :x:/../*/_event?value
                sys42.execute-hyper-file:/system42/apps/Unit tests/edit-unit-test.hl
                  {_file}:x:?name

        /*
         * Result text from evaluation of file. Contains either one or more bugs, or the names of all
         * unit tests that was evaluated successfully. Value depends upon whether or not evaluation of 
         * unit tests was a success or not. Before test is evaluated, "Undetermined" is its value.
         */
        td
          style:"width:60%;"
          innerValue:Undetermined
          {_file-result}:x:?name

        /*
         * Button that allows evaluation of a single unit test file.
         * Notice that you cannot run a single unit test, you can run a single test file though.
         */
        td
          widgets
            button
              class:btn btn-default form-control
              innerValue:Evaluate
              onclick

                /*
                 * Evaluates the unit test file, which may contain multiple unit tests. 
                 * [_file] is filename of unit test file, created during [apply].
                 */
                {_file}:x:?name
                add:x:/+
                  src:x:@"/../*/"":regex:/^_/"""
                sys42.execute-hyper-file:/system42/apps/Unit tests/single-test-button-clicked.hl


/*
 * Now, after the above [apply], and other sanity checks, 
 * we can finally create the widget that displays all unit tests in the system.
 * This creates an HTML table, with one row for each test file.
 */
create-widget:unit-test-datagrid
  parent:content
  element:table
  class:table
  events


  /*
   * Widgets that are being databound above to show table containing all unit test
   * files in system
   */
  widgets
    thead
      widgets
        tr
          widgets
            th
              innerValue:Unit test file
            th
              innerValue:Result
            th
              class:text-right
              widgets
                button
                  class:btn btn-primary form-control
                  innerValue:Evaluate all
                  onclick

                    /*
                     * Evaluating all unit tests in one go
                     */
                    add:x:/+
                      src:x:/../*/_event
                    sys42.execute-hyper-file:/system42/apps/Unit tests/evaluate-all.hl
    tbody
      widgets


/*
 * Creates widget at the bottom, containing button row with "+", "Clear" and "Coverage" buttons.
 */
create-widget:unit-test-bottom-toolbar
  parent:content
  class:col-xs-6 col-xs-push-6 col-md-4 col-md-push-8 col-lg-3 col-lg-push-9 text-right prepend-top
  widgets


    /*
     * Creates new unit test file button
     */
    button
      class:btn btn-default col-xs-4
      innerValue:+
      title:Create new unit test file
      onclick
        sys42.execute-hyper-file:/system42/apps/Unit tests/create-new-test-file.hl


    /*
     * Clear unit tests button
     */
    button
      class:btn btn-default col-xs-4
      innerValue:Clear
      title:Clear unit tests
      onclick

        /*
         * Simply deletes all widgets, and invokes "self" again.
         * Which should reset the page.
         */
        delete-widget:unit-test-bottom-toolbar
        delete-widget:unit-test-datagrid
        sys42.execute-hyper-file:/system42/apps/Unit tests/launch.hl


    /*
     * Shows the coverage for the test suite button
     */
    button
      class:btn btn-default col-xs-4
      innerValue:Coverage
      title:Shows the coverage of your unit tests
      onclick

        /*
         * Simply evaluates the "show-coverage.hl" file
         */
        sys42.execute-hyper-file:/system42/apps/Unit tests/show-coverage.hl
