
/*
 * Contains unit tests for testing creation, deletion and so on
 * of files in your system
 */


/*
 * Tests that creating a file behaves
 */
save-and-load-file
  save-file:~/temp/foo.txt
    src:Foo bar
  load-file:~/temp/foo.txt
  if:x:/-/*?value
    !=:Foo bar
    throw:@"Assert error, expected 'Foo bar' in file!"

/*
 * Tests that creating multiple file behaves
 */
save-and-load-multiple-files
  _files
    no1:~/temp/foo1.txt
      content:Foo1
    no1:~/temp/foo2.txt
      content:Foo2
  save-file:x:/../*/_files/*?value
    eval
      return:x:/../*/_dn/#/*/content?value
  load-file:x:/../*/_files/*?value
  if:x:/-/0?value
    !=:Foo1
    or:x:/./-/1?value
      !=:Foo2
    throw:@"Assert error, expected 'Foo1' and 'Foo2' in files!"

/*
 * Tests that deleting a single file behaves
 */
delete-single-file
  save-file:~/temp/bar.txt
    src:Bar was here
  delete-file:~/temp/bar.txt
  if
    fetch:x:/0/0?value
      file-exist:~/temp/bar.txt
    throw:@"Assert error, expected file to have been deleted"


/*
 * Tests that deleting multiple files behaves
 */
delete-multiple-files
  save-file:~/temp/bar1.txt
    src:Bar1 was here
  save-file:~/temp/bar2.txt
    src:Bar2 was here
  delete-file:x:/../*/save-file?value
  if
    fetch:x:/0/0?value
      file-exist:~/temp/bar1.txt
    or
      fetch:x:/0/0?value
        file-exist:~/temp/bar2.txt
    throw:@"Assert error, expected files to have been deleted"


/*
 * Tests that copying a file behaves
 */
copy-file
  save-file:~/temp/foo.txt
    src:Foo bar
  if
    fetch:x:/0/0?value
      file-exist:~/temp/foo-copy.txt
    delete-file:~/temp/foo-copy.txt
  copy-file:~/temp/foo.txt
    dest:~/temp/foo-copy.txt
  load-file:~/temp/foo-copy.txt
  if:x:/-/*?value
    !=:Foo bar
    throw:@"Assert error, expected 'Foo bar' in file!"


/*
 * Tests that moving a file behaves
 */
move-file
  save-file:~/temp/foo-orig.txt
    src:Foo bar
  if
    fetch:x:/0/0?value
      file-exist:~/temp/foo-dest.txt
    delete-file:~/temp/foo-dest.txt
  move-file:~/temp/foo-orig.txt
    dest:~/temp/foo-dest.txt
  load-file:~/temp/foo-dest.txt
  if:x:/-/*?value
    !=:Foo bar
    or
      fetch:x:/0/0?value
        file-exist:~/temp/foo-orig.txt
    throw:@"Assert error, file wasnt't correctly moved!"


/*
 * Tests that renaming a file behaves
 */
rename-file
  save-file:~/temp/foo-orig.txt
    src:Foo bar
  if
    fetch:x:/0/0?value
      file-exist:~/temp/foo-dest.txt
    delete-file:~/temp/foo-dest.txt
  rename-file:~/temp/foo-orig.txt
    dest:~/temp/foo-dest.txt
  load-file:~/temp/foo-dest.txt
  if:x:/-/*?value
    !=:Foo bar
    or
      fetch:x:/0/0?value
        file-exist:~/temp/foo-orig.txt
    throw:@"Assert error, file wasnt't correctly renamed!"


/*
 * Tests that listing files behaves
 */
list-files
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/list-files/
    delete-folder:~/temp/list-files/
  create-folder:~/temp/list-files/
  save-file:~/temp/list-files/foo-orig.txt
    src:Foo bar
  list-files:~/temp/list-files/
  if:x:/-/*?count
    !=:int:1
    or:x:/./-/*/~foo-orig.txt
      not
    throw:@"Assert error, file wasnt't correctly renamed!"

