
/*
 * Unit tests for everything related to folders.
 */


Create a single folder
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo/
    delete-folder:~/temp/foo/
  create-folder:~/temp/foo/
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo/
    not
    throw:Assert error, p5.io misbehaved


Creates multiple folders with expression
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo1/
    delete-folder:~/temp/foo1/
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo2/
    delete-folder:~/temp/foo2/
  _folders
    ~/temp/foo1/
    ~/temp/foo2/
  create-folder:x:/@_folders/*?name
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo1/
    not
    or
      fetch:x:/0/0?value
        folder-exist:~/temp/foo2/
      not
    throw:Assert error, p5.io misbehaved


Copy folder with sub-folders
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo-bar/
    delete-folder:~/temp/foo-bar/
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo-bar-2/
    delete-folder:~/temp/foo-bar-2/
  create-folder:~/temp/foo-bar/
  create-folder:~/temp/foo-bar/inner/
  save-file:~/temp/foo-bar/inner/foo.txt
    src:Foo was here
  copy-folder:~/temp/foo-bar/
    dest:~/temp/foo-bar-2/
  load-file:~/temp/foo-bar-2/inner/foo.txt
  if:x:/@load-file/*?value
    !=:Foo was here
    throw:Assert error, p5.io misbehaved


List folders
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo/
    delete-folder:~/temp/foo/
  _folders
    ~/temp/foo/
    ~/temp/foo/bar1/
    ~/temp/foo/bar2/
  create-folder:x:/@_folders/*?name
  list-folders:~/temp/foo/
  if:x:/@list-folders/*/~bar1
    not
    or:x:/@list-folders/*/~bar2
      not
    or:x:/@list-folders/*?count
      !=:int:2
    throw:Assert error, p5.io misbehaved


List folders with expression
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/foo/
    delete-folder:~/temp/foo/
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/bar/
    delete-folder:~/temp/bar/
  _folders
    ~/temp/foo/
    ~/temp/foo/bar1/
    ~/temp/foo/bar2/
    ~/temp/bar/
    ~/temp/bar/bar3/
    ~/temp/bar/bar4/
  create-folder:x:/@_folders/*?name
  _folders
    no1:~/temp/foo/
    no1:~/temp/bar/
  list-folders:x:/@_folders/*?value
  if:x:/@list-folders/*/~bar1
    not
    or:x:/@list-folders/*/~bar2
      not
    or:x:/@list-folders/*/~bar3
      not
    or:x:/@list-folders/*/~bar4
      not
    or:x:/@list-folders/*?count
      !=:int:4
    throw:Assert error, p5.io misbehaved


List folders throws on bad path
  _success:bool:false
  try
    list-folders:~/temp
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved


List files
  if
    fetch:x:/0/0?value
      folder-exist:~/temp/list-files/
    delete-folder:~/temp/list-files/
  create-folder:~/temp/list-files/
  save-file:~/temp/list-files/foo-orig.txt
    src:Foo bar
  list-files:~/temp/list-files/
  if:x:/-/*?count
    !=:int:1
    or:x:/./-/*/~foo-orig.txt
      not
    throw:@"Assert error, file wasnt't correctly renamed!"


List files throws on bad path
  _success:bool:false
  try
    list-folders:~/temp
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved
