
/*
 * Unit tests for widgets.
 */


Create and delete empty widget
  p5.web.widgets.create:sys42.tests.widget-1
  p5.web.widgets.exists:sys42.tests.widget-1
  p5.web.widgets.delete:sys42.tests.widget-1
  p5.web.widgets.exists:sys42.tests.widget-1
  if:x:/../*/p5.web.widgets.exists/[0,1]/0?value
    not
    or:x:/../*/p5.web.widgets.exists/[1,2]/0?value
    throw:Assert error, widgets misbehaved


Create widget with same ID twice throws
  _success:bool:false
  p5.web.widgets.create:sys42.tests.widget-2
  try
    p5.web.widgets.create:sys42.tests.widget-2
  catch
    set:x:/@_success?value
      src:bool:true
  p5.web.widgets.delete:sys42.tests.widget-2
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create widget with bad parent throws
  _success:bool:false
  try
    p5.web.widgets.create:sys42.tests.widget-3
      parent:non-existing-widget
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Widget properties yields correct
  p5.web.widgets.create:sys42.tests.widget-4
    parent:content
    element:span
    class:foo
    visible:false
    render-type:immediate
  p5.web.widgets.property.get:sys42.tests.widget-4
    parent
    element
    class
    visible
    render-type
  p5.web.widgets.delete:sys42.tests.widget-4
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-4
    parent:content
    element:span
    class:foo
    visible:bool:false
    render-type:immediate"
    throw:Assert error, widgets misbehaved


Widget single property changed with static value
  p5.web.widgets.create:sys42.tests.widget-5
    visible:false
    class:foo
  p5.web.widgets.property.set:sys42.tests.widget-5
    visible:true
    class:bar
  p5.web.widgets.property.get:sys42.tests.widget-5
    visible
    class
  p5.web.widgets.delete:sys42.tests.widget-5
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-5
    visible:bool:true
    class:bar"
    throw:Assert error, widgets misbehaved


Widget find for single constant property
  p5.web.widgets.create:sys42.tests.widget-6
    class:foo
  p5.web.widgets.find
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-6"
    throw:Assert error, widgets misbehaved


Widget find for multiple constant properties
  p5.web.widgets.create:sys42.tests.widget-7
    class:foo
    bar:xyz
  p5.web.widgets.find
    class:foo
    bar:xyz
  p5.web.widgets.delete:sys42.tests.widget-7
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-7"
    throw:Assert error, widgets misbehaved


Widget find for single expression property
  p5.web.widgets.create:sys42.tests.widget-8
    class:foo
  _class:foo
  p5.web.widgets.find
    class:x:/@_class?value
  p5.web.widgets.delete:sys42.tests.widget-8
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-8"
    throw:Assert error, widgets misbehaved


Create widget with innerValue throws
  _success:bool:false
  try
    p5.web.widgets.create:sys42.tests.widget-9
      innerValue:foo
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create container widget with innerValue throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-10
      innerValue:foo
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create literal widget with widgets throws
  _success:bool:false
  try
    p5.web.widgets.create-literal:sys42.tests.widget-11
      widgets
        literal
          innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create void widget with widgets throws
  _success:bool:false
  try
    p5.web.widgets.create-void:sys42.tests.widget-12
      widgets
        literal
          innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create void widget with innerValue throws
  _success:bool:false
  try
    p5.web.widgets.create-void:sys42.tests.widget-13
      innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Default container widget element is div
  p5.web.widgets.create:sys42.tests.widget-14
  p5.web.widgets.property.get:sys42.tests.widget-14
    element
  p5.web.widgets.delete:sys42.tests.widget-14
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-14
    element:div"
    throw:Assert error, widgets misbehaved


Default literal widget element is p
  p5.web.widgets.create-literal:sys42.tests.widget-15
  p5.web.widgets.property.get:sys42.tests.widget-15
    element
  p5.web.widgets.delete:sys42.tests.widget-15
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-15
    element:p"
    throw:Assert error, widgets misbehaved


Default void widget element is input
  p5.web.widgets.create-void:sys42.tests.widget-16
  p5.web.widgets.property.get:sys42.tests.widget-16
    element
  p5.web.widgets.delete:sys42.tests.widget-16
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-16
    element:input"
    throw:Assert error, widgets misbehaved


Default parent widget is cnt
  p5.web.widgets.create-void:sys42.tests.widget-17
  p5.web.widgets.property.get:sys42.tests.widget-17
    parent
  p5.web.widgets.delete:sys42.tests.widget-17
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-17
    parent:cnt"
    throw:Assert error, widgets misbehaved


Create widget with two children widgets with same ID throws
  _success:bool:false
  try
    p5.web.widgets.create:sys42.tests.widget-18
      widgets
        literal:foo
        literal:foo
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    or
      fetch:x:/0/0?value
        p5.web.widgets.exists:sys42.tests.widget-18
    throw:Assert error, widgets misbehaved


Create widget before another widget
  p5.web.widgets.create:sys42.tests.widget-19
  p5.web.widgets.create:sys42.tests.widget-20
    parent:sys42.tests.widget-19
  p5.web.widgets.create:sys42.tests.widget-21
    before:sys42.tests.widget-20
  p5.web.widgets.get-children:sys42.tests.widget-19
  p5.web.widgets.delete:sys42.tests.widget-19
  if:x:/@p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-19
    container:container:sys42.tests.widget-21
    container:container:sys42.tests.widget-20"
    throw:Assert error, widgets misbehaved


Create widget after another widget
  p5.web.widgets.create:sys42.tests.widget-22
  p5.web.widgets.create:sys42.tests.widget-23
    parent:sys42.tests.widget-22
  p5.web.widgets.create:sys42.tests.widget-24
    after:sys42.tests.widget-23
  p5.web.widgets.get-children:sys42.tests.widget-22
  p5.web.widgets.delete:sys42.tests.widget-22
  if:x:/@p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-22
    container:container:sys42.tests.widget-23
    container:container:sys42.tests.widget-24"
    throw:Assert error, widgets misbehaved
