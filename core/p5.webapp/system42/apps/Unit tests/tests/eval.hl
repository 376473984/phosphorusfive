/*
 * Contains unit tests for testing [eval] in system
 */

/*
 * Tests that [eval] behaves correctly when given an expression to evaluate,
 * and eval returns value
 */
expression-value-result
  _x
    set:x:/..?value
      src:success
  eval:x:/-
  if:x:/-
    !=:node:@"eval:success"
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] behaves correctly when given an expression to evaluate,
 * and eval returns node set
 */
expression-node-result
  _x
    insert-before:x:
      src:"result:success"
  eval:x:/-
  if:x:/-
    !=:node:@"eval
  result:success"
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] behaves correctly when given children to evaluate,
 * and returning value
 */
evaluate-children-value-result
  eval
    set:x:/..?value
      src:success
  if:x:/-
    !=:node:@"eval:success"
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] behaves correctly when given children to evaluate,
 * and returning nodes
 */
evaluate-children-node-result
  eval
    insert-before:x:
      src
        result:success
  if:x:/-
    !=:node:@"eval
  result:success"
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] behaves correctly when given arguments to 
 * an expression evaluation
 */
argument
  _x
    set:x:/..?value
      src:"{0}-success"
        :x:/../*/_data?value
  eval:x:/-
    _data:very-much
  if:x:/-?value
    !=:very-much-success
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] behaves correctly when given arguments to 
 * an expression evaluation, and argument is a node passed by
 * reference
 */
reference-argument
  _input:success
  _x
    set:x:/..?value
      src:"{0}-{1}"
        :x:/../*/_data?value
        :x:/../*/_data2/#/../*/_input?value
  set:x:/+/*/_data2?value
    src:x:/..
  eval:x:/-2
    _data:very-much
    _data2
  if:x:/-?value
    !=:very-much-success
    throw:@"Assert error, [eval] misbehaved"

/*
 * Tests that [eval] will not evaluate a node starting with "."
 */
no-eval-dot
  .p5.core.application-folder
  if:x:/-?value
    throw:Eval evaluated an Active Event starting with a '.'

/*
 * Tests that [eval-whitelist] behaves correctly when given a lambda with a whitelist definition it should execute.
 */
eval-whitelist-success
  _input
  set:x:/@_input?value
    eval-whitelist
      _events
        return
      return:success
  if:x:/@_input?value
    !=:success
    throw:@"Assert error, [eval-whitelist] misbehaved"

/*
 * Tests that [eval-whitelist] behaves correctly when given a lambda with a whitelist definition it should execute.
 */
eval-whitelist-failure
  _input
  try
    set:x:/@_input?value
      eval-whitelist
        _events
          add
        set:x:/..?value
  catch
    set:x:/@_input?value
      src:success
  if:x:/@_input?value
    !=:success
    throw:@"Assert error, [eval-whitelist] misbehaved"

/*
 * Tests that [eval-whitelist] behaves correctly when given a lambda with a whitelist definition it should execute.
 */
eval-whitelist-empty-failure
  _input
  try
    set:x:/@_input?value
      eval-whitelist
        set:x:/..?value
  catch
    set:x:/@_input?value
      src:success
  if:x:/@_input?value
    !=:success
    throw:@"Assert error, [eval-whitelist] misbehaved"

/*
 * Tests that [eval-whitelist] behaves correctly when given a lambda with a whitelist definition it should execute.
 */
eval-whitelist-empty-success
  _input:failure
  set:x:/@_input?value
    eval-whitelist
  if:x:/@_input?value
    =:failure
    throw:@"Assert error, [eval-whitelist] misbehaved"

/*
 * Tests that [eval-whitelist] behaves correctly when given an expression with a whitelist definition it should execute.
 */
eval-whitelist-expression-success
  _input
  _eval
    return:success
  set:x:/@_input?value
    eval-whitelist:x:/@_eval
      _events
        return
  if:x:/@_input?value
    !=:success
    throw:@"Assert error, [eval-whitelist] misbehaved"

/*
 * Tests that [eval-whitelist] behaves correctly when given an expression with a whitelist definition it should not execute.
 */
eval-whitelist-expression-failure
  _input
  _eval
    return:failure
  try
    set:x:/@_input?value
      eval-whitelist:x:/@_eval
        _events
          set
  catch
    set:x:/@_input?value
      src:success
  if:x:/@_input?value
    !=:success
    throw:@"Assert error, [eval-whitelist] misbehaved"
