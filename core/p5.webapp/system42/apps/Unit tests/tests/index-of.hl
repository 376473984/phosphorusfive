
/*
 * Unit tests for [index-of].
 */


Index of static string yielding single result
  _data:foobarhowdy
  index-of:x:/@_data?value
    src:bar
  if:x:/@index-of/*?value
    !=:int:3
    or:x:/@index-of/*?count
      !=:int:1
    throw:Assert error, [index-of] misbehaved


Index of static string yielding multiple results
  _data:foobarhowdybar
  index-of:x:/@_data?value
    src:bar
  if:x:/@index-of/0?value
    !=:int:3
    or:x:/@index-of/1?value
      !=:int:11
    or:x:/@index-of/*?count
      !=:int:2
    throw:Assert error, [index-of] misbehaved


Index of expression string yielding single result
  _data:foobarhowdy
  _input:bar
  index-of:x:/@_data?value
    src:x:/@_input?value
  if:x:/@index-of/*?value
    !=:int:3
    or:x:/@index-of/*?count
      !=:int:1
    throw:Assert error, [index-of] misbehaved


Index of static regex yielding multiple result
  _data:Thomas hansen thomas
  index-of:x:/@_data?value
    src:regex:/thomas/i
  if:x:/@index-of/0?value
    !=:int:0
    or:x:/@index-of/1?value
      !=:int:14
    or:x:/@index-of/*?count
      !=:int:2
    throw:Assert error, [index-of] misbehaved


Index of expression regex yielding single result
  _data:Hansen, Thomas
  _input:regex:/thomas/i
  index-of:x:/@_data?value
    src:x:/@_input?value
  if:x:/@index-of/0?value
    !=:int:8
    or:x:/@index-of/*?count
      !=:int:1
    throw:Assert error, [index-of] misbehaved


Index of throws on multiple sources
  _success:bool:false
  _data:foo bar
  try
    index-of:x:/@_data?value
      src:foo
      src:bar
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [index-of] misbehaved
