
/*
 * Creates a new unit test file in system.
 * Simply shows a modal window, asking user to provide name for test file,
 * and if user clicks "OK", creates a new default file for user.
 */
sys42.windows.modal
  _header:Name of unit test file
  _widgets
    container:test-filename-wrp
      class:form-group
      widgets
        label
          for:test-filename
          innerValue:Name of test
        input:test-filename
          type:text
          class:form-control
          placeholder:Name of file ...
  _buttons
    button:test-create-btn
      class:btn btn-default
      innerValue:Create
      onclick

        /*
         * Getting name of new test file, and verifying it's not empty
         */
        get-widget-property:test-filename
          value
        if:x:/../*/get-widget-property/*/*?value
          =:
          set-widget-property:test-filename-wrp
            class:form-group has-error
          send-javascript:@"$('#test-filename').focus().select();"
          return

        /*
         * Checking if user supplied the name without a ".hl" extension, and if so, we add it
         */
        split:x:/../*/get-widget-property/*/*?value
          =:.
        if:x:/-/*?count
          =:int:1
          set:x:/../*/get-widget-property/*/*?value
            src:{0}.hl
              :x:/../*/get-widget-property/*/*?value

        /*
         * Checking if user supplied a slash in the filename, and if so, alert him that this is an error
         */
        split:x:/../*/get-widget-property/*/*?value
          =:/
        if:x:/-/*?count
          >:int:1
          set-widget-property:test-filename-wrp
            class:form-group has-error
          send-javascript:@"$('#test-filename').focus().select();"
        else

          /*
           * Filename of test file is OK, creating a default test file
           */
          save-file:{0}{1}
            :@tests/tests/
            :x:/../*/get-widget-property/*/*?value
            src:@"/*
 * Contains unit tests for testing [whatever] in system
 */

/*
 * Tests that [foo] behaves correctly
 */
foo
  _some-test-code-goes-here
  if:bool:false
    throw:@""Assert error, expected [something], got [something-else]!""
"

          /*
           * Making sure we close our modal window
           */
          sys42.windows.modal.close

          /*
           * Deleting widgets and re-creating datagrid (and other widgets) by invoking "self"
           */
          delete-widget:unit-test-bottom-toolbar
          delete-widget:unit-test-datagrid
          sys42.execute-lambda-file:@tests/launch.hl

          /*
           * Making sure our newly created test is edited
           */
          p5.io.unroll-path.@tests
          find-widget
            _file-button:{0}/tests/{1}
              :x:/././-?value
              :x:/../*/get-widget-property/*/*?value
          raise-widget-ajax-event:x:/-/*?value
            onclick

