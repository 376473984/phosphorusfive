
/*
 * Main entry point for file browser. If there's a value in session
 * called "sys42.download-file", then that file will be directly 
 * downloaded. If not, then file browser will start on page. This is
 * to let the file browser allow for downloading files by simply doing
 * a "window.location.reload(true);" JavaScript inclusion.
 */


get-session:sys42.download-file
if:x:/-/*?value

  /*
   * We have a file to download, figuring out what type of Content-Type
   * we should serve it as
   */
  sys42.get-content-type:x:/../*/get-session/*?value
  set-http-header
    Content-Type:x:/./-?value

  // Figuring out filename
  split:x:/../*/get-session/*?value
    =:/
  set-http-header
    Content-Disposition:"attachment; filename={0}"
      :x:/././-/0/-?name

  // Returning file
  echo-file:x:/../*/get-session/*?value

  // Clearing session value
  set-session:sys42.download-file

else

  // There were no files to download

  // Making sure we start up in "default directory", unless user already
  // has another directory selected. In addition we're making sure that
  // the system won't start inside a directory the user does not have
  // access to
  get-user
  get-session:sys42.current-folder
  if:x:/..else/*/get-session/*?value
    not
    or:x:/..else/*/get-user/*/role?value
      !=:root
      and:x:/..else/*/get-session/*/"=~users/{0}/documents/"
        :x:/..else/*/get-user/*/username?value
        not

    // Either there was no current directory, or the current directory
    // was a directory that the user did not have access to. Therefor
    // we set the "current folder" to the user's "Home" directory
    set-session:sys42.current-folder
      src:users/{0}/documents/
        :x:/..else/*/get-user/*/username?value

  // Setting "root directory" according to user role
  if:x:/..else/*/get-user/*/role?value
    !=:root

    // User is NOT root, therefor we set "root access folder" to
    // "users/username/"
    set-session:sys42.root-directory
      src:users/{0}/
        :x:/..else/*/get-user/*/username?value
  else
    set-session:sys42.root-directory
      src:

  // Starting the actual file-browser implementation
  sys42.execute-lisp-file:system42/file-browser/file-browser.hl


