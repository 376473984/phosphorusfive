/*
 * Allows the user to evaluate unit tests in system
 */

/*
 * Verifies user is root first, since only root
 * can run unit tests
 */
whoami
if:x:/-/*/role?value
  !=:root
  throw:Only root user can run Unit Tests

/*
 * List all files in directory, but removing "index.hl",
 * assuming all other files are unit test files
 */
list-files:/system42/tests/
set:x:@"/-/*/""/system42/tests/index.hl"""

/*
 * Databinding unit tests files to table widget
 */
databind:x:/../*/create-widget/=unit-test-files/*/widgets
  src:x:/../*/list-files/*
  template
    tr

      /*
       * Used for retrieving unit test file row, to signal success or failure,
       * through red or green color on row as a whole
       */
      {_file-row}:x:?name
      widgets

        /*
         * Unit test filename
         */
        td
          widgets
            a
              {innerValue}:x:?name
              href:#
              onclick
                sys42.launch-unit-test-editor
                  {_file}:x:?name

        /*
         * Result text from evaluation of file, contains
         * either one or more bugs, or the names of all
         * unit tests that ran. Value depends upon whether
         * or not evaluation of unit tests was a success
         * or not. Before test is ran, "Undetermined" is
         * its value
         */
        td
          style:"width:60%;"
          innerValue:Undetermined
          {_file-result}:x:?name

        /*
         * Button that allows evaluation of a single unit test file.
         * Notice that you cannot run a single unit test, you can however
         * run a single unit test file
         */
        td
          widgets
            button
              class:btn btn-default btn-xs
              style:"width:100px;"
              innerValue:Evaluate
              onclick

                /*
                 * Evaluates the databound unit test file, which may contain
                 * multiple unit tests. [_file] is filename of unit test file
                 */
                {_file}:x:?name
                if
                  sys42.evaluate-unit-test-file:x:/../*/_file?value
                  =:bool:false

                  /*
                   * Error while evaluating unit test file, setting result
                   * widget to "message" + "stack-trace" of exception
                   */
                  find-widget
                    _file-result:x:/../*/_file?value
                  _error-html
                  for-each:x:/..if/*/sys42.evaluate-unit-test-file/*/_error?value
                    html-encode:x:/./*/_dp?value
                    set:x:/./-?value
                      src:{0}<pre>{1}</pre>
                        :x:/..for-each/-?value
                        :x:/..for-each/*/html-encode?value
                  set-widget-property:x:/./*/find-widget/*/[0,1]?value
                    innerValue:x:/././*/_error-html?value
                  find-widget
                    _file-row:x:/../*/_file?value
                  set-widget-property:x:/-/*?value
                    class:error
                else

                  /*
                   * Success while evaluating unit test file, providing feedback to
                   * user through settings row to green and feedback message to "Success!"
                   */
                  find-widget
                    _file-result:x:/../*/_file?value
                  join:x:/../*/if/*/sys42.evaluate-unit-test-file/*/_success?value
                    sep:"\r\n"
                  set-widget-property:x:/-2/*?value
                    innerValue:<pre>{0}</pre>
                      :x:/././-?value
                  find-widget
                    _file-row:x:/../*/_file?value
                  set-widget-property:x:/-/*?value
                    class:success

/*
 * Creating widget showing all unit test files in a table
 */
create-widget:unit-test-files
  parent:content
  element:table
  class:table table-hover
  events

    /*
     * Invoked when a unit test file is being edited
     */
    sys42.launch-unit-test-editor

      // Changing header of page to reflect we're in edit mode
      set-widget-property:header
        innerValue:Editing test file <small>{0}</small>
          :x:/../*/_file?value

      // Launching editor
      eval-x:x:/+/*
      sys42.execute-lisp-file:/system42/editors/launch.hl
        _file:x:/../*/_file?value
        _parent:content
        _widget-id:unit-test-file-editor

      // Hiding unit test table
      set-widget-property:unit-test-files
        visible:false

    /*
     * Invoked when file editor is destroyed
     */
    sys42.destroy-file-editor

      // Changing header of page back to original state
      set-widget-property:header
        innerValue:Tests

      // Deleting editor widget
      delete-widget:unit-test-file-editor

      // Making unit test table visible again
      set-widget-property:unit-test-files
        visible:true

    /*
     * Evaluates one Unit Test file
     */
    sys42.evaluate-unit-test-file
      load-file:x:/../*/_arg?value
      for-each:x:/-/*/*
        _test
        set:x:/-?value
          src:x:/..for-each/*/_dp/#
        try
          eval:x:/..for-each/*/_test?value
          add:x:/..
            src:"_success:{0}"
              :x:/..for-each/*/_dp/#?name
        catch
          set:x:/..?value
            src:bool:false
          eval-x:x:/+/*/*
          add:x:/..
            src
              _error:@"{0}; ""{1}""

{2}"
                :x:/..for-each/*/_dp/#?name
                :x:/..catch/*/message?value
                :x:/..catch/*/stack-trace?value


  /*
   * Widgets that are being databound above to show table containing all unit test
   * files in system
   */
  widgets
    thead
      widgets
        tr
          widgets
            th
              innerValue:File
            th
              innerValue:Result
            th
              widgets
                button
                  class:btn btn-primary
                  style:"width:150px;"
                  innerValue:Evaluate all
                  onclick
                    find-widget
                      innerValue:Evaluate
                    raise-widget-ajax-event:x:/../*/find-widget/*?value
                      onclick


