/*
 * Contains unit tests for testing comparison operators, 
 * such as =, !=, <=, etc in system
 */

/*
 * Tests that = behaves correctly
 */
equals
  _src1:foo1
  _src2:foo2
  if:x:/../*/_src1?value
    =:x:/../*/_src2?value
    throw:@"Assert error, [equals] operator misbehaved"

/*
 * Tests that != behaves correctly
 */
not-equals
  _src1:foo
  _src2:foo
  if:x:/../*/_src1?value
    !=:x:/../*/_src2?value
    throw:@"Assert error, [not-equals] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
contains-match
  _src1:foobar
  _src2:foo
  if:x:/../*/_src1?value
    ~:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [contains] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
contains-no-match
  _src1:foobar
  _src2:fooX
  if:x:/../*/_src1?value
    ~:x:/../*/_src2?value
    throw:@"Assert error, [contains] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
contains-regex-match
  _src1:foobar
  _src2:regex:/FoO/i
  if:x:/../*/_src1?value
    ~:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [contains] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
contains-regex-no-match
  _src1:foobar
  _src2:regex:/Foo/
  if:x:/../*/_src1?value
    ~:x:/../*/_src2?value
    throw:@"Assert error, [contains] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
not-contains-match
  _src1:foobar
  _src2:foo
  if:x:/../*/_src1?value
    !~:x:/../*/_src2?value
    throw:@"Assert error, [not-contains] operator misbehaved"

/*
 * Tests that ~ behaves correctly
 */
not-contains-no-match
  _src1:foobar
  _src2:fooX
  if:x:/../*/_src1?value
    !~:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [not-contains] operator misbehaved"

/*
 * Tests that !~ behaves correctly
 */
not-contains-regex-match
  _src1:foobar
  _src2:regex:/FoO/i
  if:x:/../*/_src1?value
    !~:x:/../*/_src2?value
    throw:@"Assert error, [not-contains] operator misbehaved"

/*
 * Tests that !~ behaves correctly
 */
not-contains-regex-no-match
  _src1:foobar
  _src2:regex:/Foo/
  if:x:/../*/_src1?value
    !~:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [not-contains] operator misbehaved"

/*
 * Tests that > behaves correctly with string type,
 * and no match should be found
 */
more-than-no-match
  _src1:aaa
  _src2:bbb
  if:x:/../*/_src1?value
    >:x:/../*/_src2?value
    throw:@"Assert error, [more-than] operator misbehaved"

/*
 * Tests that < behaves correctly with string type,
 * and no match should be found
 */
less-than-no-match
  _src1:bbb
  _src2:aaa
  if:x:/../*/_src1?value
    <:x:/../*/_src2?value
    throw:@"Assert error, [less-than] operator misbehaved"

/*
 * Tests that > behaves correctly with string type,
 * and match should be found
 */
more-than-match
  _src1:bbc
  _src2:bbb
  if:x:/../*/_src1?value
    >:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [more-than] operator misbehaved"

/*
 * Tests that < behaves correctly with string type,
 * and match should be found
 */
less-than-match
  _src1:bbb
  _src2:bbc
  if:x:/../*/_src1?value
    <:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [less-than] operator misbehaved"

/*
 * Tests that >= behaves correctly with string type, 
 * and no match should be found
 */
more-than-equals-no-match
  _src1:aaa
  _src2:bbb
  if:x:/../*/_src1?value
    >=:x:/../*/_src2?value
    throw:@"Assert error, [more-than-equals] operator misbehaved"

/*
 * Tests that <= behaves correctly with string type,
 * and no match should be found
 */
less-than-equals-no-match
  _src1:bbb
  _src2:aaa
  if:x:/../*/_src1?value
    <=:x:/../*/_src2?value
    throw:@"Assert error, [less-than-equals] operator misbehaved"

/*
 * Tests that >= behaves correctly with string type, 
 * and match should be found
 */
more-than-equals-match
  _src1:bbb
  _src2:bbb
  if:x:/../*/_src1?value
    >=:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [more-than-equals] operator misbehaved"

/*
 * Tests that <= behaves correctly with string type,
 * and match should be found
 */
less-than-equals-match
  _src1:aaa
  _src2:aaa
  if:x:/../*/_src1?value
    <=:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [less-than-equals] operator misbehaved"

/*
 * Tests that = behaves correctly when types differ
 */
equals-type-difference
  _src1:int:5
  _src2:5
  if:x:/../*/_src1?value
    =:x:/../*/_src2?value
    throw:@"Assert error, [equals] operator misbehaved"

/*
 * Tests that != behaves correctly when types differ
 */
not-equals-type-difference
  _src1:int:5
  _src2:5
  if:x:/../*/_src1?value
    !=:x:/../*/_src2?value
    // Do nothing
  else
    throw:@"Assert error, [not-equals] operator misbehaved"

/*
 * Tests mixed operators joined together
 */
mixed
  _src1:int:5
  _src2:5
  if:x:/../*/_src1?value
    !=:x:/../*/_src2?value.int
    or:x:/../*/_src1?value.string
      !=:x:/../*/_src2?value
    or:thomas
      !=:thomas
    or:abba
      >=:b
    or:x:/../*/_src1?value
      >:int:5
    or:x:/../*/_src1?value
      <:int:4
    or:x:/../*/_src1?value
      !~:regex:/5/
    throw:@"Assert error, mixed operators misbehaved"

/*
 * Tests that Active Event evaluation "operator" behaves as expected,
 * when a match should occur
 */
evaluation-match
  _x
    set:x:/..?value
      src:bool:true
  if
    eval:x:/../*/_x
    // Do nothing
  else
    throw:@"Assert error, if evaluated to true when it should evaluate to false"

/*
 * Tests that Active Event evaluation "operator" behaves as expected,
 * when a match should not occur
 */
evaluation-no-match
  _x
    set:x:/..?value
      src:bool:false
  if
    eval:x:/../*/_x
    throw:@"Assert error, if evaluated to true when it should evaluate to false"

/*
 * Tests that Active Event evaluation "operator" behaves as expected,
 * when a match should occur, and result is "not" with the [not] operator
 */
evaluation-not-no-match
  _x
    set:x:/..?value
      src:bool:true
  if
    eval:x:/../*/_x
    not
    throw:@"Assert error, if evaluated to true when it should evaluate to false"

/*
 * Tests that Active Event evaluation "operator" behaves as expected,
 * when a match should not occur, and result is "not" with the [not] operator
 */
evaluation-not-match
  _x
    set:x:/..?value
      src:bool:false
  if
    eval:x:/../*/_x
    not
    // Do nothing
  else
    throw:@"Assert error, if evaluated to true when it should evaluate to false"
