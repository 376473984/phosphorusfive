/*
 * Contains unit tests for testing [insert] in system
 */

/*
 * Tests that [insert-before] behaves as expected
 */
insert-before
  _foo1
  _foo2
  insert-before:x:/../*/_foo2
    src
      success-name:success-value
  if:x:/../*/_foo2/-
    !=:node:"success-name:success-value"
    throw:@"Assert error, [insert-before] misbehaved!"

/*
 * Tests that [insert-after] behaves as expected
 */
insert-after
  _foo1
  _foo2
  insert-after:x:/../*/_foo1
    src
      success-name:success-value
  if:x:/../*/_foo1/+
    !=:node:"success-name:success-value"
    throw:@"Assert error, [insert-after] misbehaved!"

/*
 * Tests that [insert-after] behaves as expected when given expression source
 */
expression-source
  _data
    success-name:success-value
  _foo1
  _foo2
  insert-after:x:/../*/_foo1
    src:x:/../*/_data/*
  if:x:/../*/_foo1/+
    !=:node:"success-name:success-value"
    throw:@"Assert error, [insert-after] misbehaved!"

/*
 * Tests that [insert-after] behaves as expected when given node source
 */
node-source
  _foo1
  _foo2
  insert-after:x:/../*/_foo1
    src:node:"success-name:success-value"
  if:x:/../*/_foo1/+
    !=:node:"success-name:success-value"
    throw:@"Assert error, [insert-after] misbehaved!"

/*
 * Tests that [insert-before] behaves as expected when given identity expression
 */
insert-before-identity-expression
  insert-before:x:
    src:node:"success-name:success-value"
  if:x:/../*/insert-before/-
    !=:node:"success-name:success-value"
    throw:@"Assert error, [insert-before] misbehaved!"

/*
 * Tests that [insert-after] behaves as expected when given identity expression
 */
insert-after-identity-expression
  _result
  insert-after:x:
    src
      set:x:/../*/_result?value
        src:success
  if:x:/../*/_result?value
    !=:success
    throw:@"Assert error, [insert-after] misbehaved!"
