
/*
 * Launches file editor
 */


// Figuring out file extension, such that we can launch the right editor
split:x:/../*/_file?value
  =:.

// Launching the correct editor
if
  file-exist:/system42/editors/{0}-editor.hl
    :x:/../*/split/0/-?name

  /*
   * Loading specialized editor
   */
  add:x:/..if/*/sys42.execute-lisp-file
    src:"_parent:{0}"
      :x:/../*/_parent?value
  add:x:/..if/*/sys42.execute-lisp-file
    src:"_file:{0}"
      :x:/../*/_file?value
  if:x:/../*/_widget-id?value
    add:x:/./+
      src:"_widget-id:{0}"
        :x:/../*/_widget-id?value
  sys42.execute-lisp-file:/system42/editors/{0}-editor.hl
    :x:/../*/split/0/-?name
  set-session-value:sys42.editor.edited-file
    src:x:/../*/_file?value
  set:x:/..?value
    src:bool:true

else
  switch:x:/../*/split/0/-?name
    case:txt
    case:csv

      /*
       * Loading generic CodeMirror editor in .MD mode
       */
      add:x:/./*/sys42.execute-lisp-file
        src:"_parent:{0}"
          :x:/../*/_parent?value
      add:x:/./*/sys42.execute-lisp-file
        src:"_file:{0}"
          :x:/../*/_file?value
      if:x:/../*/_widget-id?value
        add:x:/./+
          src:"_widget-id:{0}"
            :x:/../*/_widget-id?value
      sys42.execute-lisp-file:/system42/editors/code-mirror-editor.hl
        _mode:md
      set-session-value:sys42.editor.edited-file
        src:x:/../*/_file?value
      set:x:/..?value
        src:bool:true
    case:hl
    case:css
    case:js
    case:md
    case:xml
    case:htm

      /*
       * Loading generic CodeMirror editor
       */
      add:x:/./*/sys42.execute-lisp-file
        src:"_parent:{0}"
          :x:/../*/_parent?value
      add:x:/./*/sys42.execute-lisp-file
        src:"_file:{0}"
          :x:/../*/_file?value
      add:x:/./*/sys42.execute-lisp-file
        src:"_mode:{0}"
          :x:/../*/split/0/-?name
      if:x:/../*/_widget-id?value
        add:x:/./+
          src:"_widget-id:{0}"
            :x:/../*/_widget-id?value
      sys42.execute-lisp-file:/system42/editors/code-mirror-editor.hl
      set-session-value:sys42.editor.edited-file
        src:x:/../*/_file?value
      set:x:/..?value
        src:bool:true
    default

      /*
       * No existing editor
       */
      sys42.info-window:"Sorry, I do no know how to edit '{0}' files"
        :x:/../*/split/0/-?name
      set:x:/..?value
        src:bool:false


