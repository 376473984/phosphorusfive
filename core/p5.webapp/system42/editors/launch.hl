
/*
 * Launches file editor
 */


// Figuring out file extension, such that we can launch the right editor
split:x:/../*/_file?value
  =:.

// Launching the correct editor
if
  file-exist:/system42/editors/{0}-editor.hl
    :x:/../*/split/0/-?name

  /*
   * Loading specialized editor
   */
  add:x:/..if/*/sys42.execute-lisp-file
    src:"_parent:{0}"
      :x:/../*/_parent?value
  add:x:/..if/*/sys42.execute-lisp-file
    src:"_file:{0}"
      :x:/../*/_file?value
  if:x:/../*/_widget-id?value
    add:x:/./+
      src:"_widget-id:{0}"
        :x:/../*/_widget-id?value
  sys42.execute-lisp-file:/system42/editors/{0}-editor.hl
    :x:/../*/split/0/-?name
  set-session-value:sys42.editor.edited-file
    src:x:/../*/_file?value
else-if:x:/../*/split/0/-?name
  =:txt
  /*
   * Loading generic CodeMirror editor in .MD mode
   */
  add:x:/./*/sys42.execute-lisp-file
    src:"_parent:{0}"
      :x:/../*/_parent?value
  add:x:/./*/sys42.execute-lisp-file
    src:"_file:{0}"
      :x:/../*/_file?value
  if:x:/../*/_widget-id?value
    add:x:/./+
      src:"_widget-id:{0}"
        :x:/../*/_widget-id?value
  sys42.execute-lisp-file:/system42/editors/code-mirror-editor.hl
    _theme:the-matrix-reduced
    _mode:md
  set-session-value:sys42.editor.edited-file
    src:x:/../*/_file?value
else-if:x:/../*/split/0/-?name
  =:hl
  or:x:/../*/split/0/-?name
    =:css
  or:x:/../*/split/0/-?name
    =:js
  or:x:/../*/split/0/-?name
    =:md
  or:x:/../*/split/0/-?name
    =:xml
  or:x:/../*/split/0/-?name
    =:config
  or:x:/../*/split/0/-?name
    =:htm

  /*
   * Loading generic CodeMirror editor
   */
  add:x:/./*/sys42.execute-lisp-file
    src:"_parent:{0}"
      :x:/../*/_parent?value
  add:x:/./*/sys42.execute-lisp-file
    src:"_file:{0}"
      :x:/../*/_file?value
  add:x:/./*/sys42.execute-lisp-file
    src:"_mode:{0}"
      :x:/../*/split/0/-?name
  if:x:/../*/_widget-id?value
    add:x:/./+
      src:"_widget-id:{0}"
        :x:/../*/_widget-id?value
  sys42.execute-lisp-file:/system42/editors/code-mirror-editor.hl
    _theme:the-matrix-reduced
  set-session-value:sys42.editor.edited-file
    src:x:/../*/_file?value
else

  /*
   * No existing editor
   */
  sys42.info-window:"Sorry, I do no know how to edit '{0}' files"
    :x:/../*/split/0/-?name


