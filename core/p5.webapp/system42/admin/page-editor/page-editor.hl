
/*
 * Creates the "Page editor" page, which allows for editing pages in the database
 */


/*
 * Creating our "Edit Page" Widget, which actually only contains 
 * a header, back link, and a couple of drop down lists to create 
 * new pages, and edit existing pages
 */
create-widget:page-editor
  class:span-24
  widgets
    literal:page-editor-header
      class:span-11
      style:"margin-top:18px;"
      element:h1
      innerValue:page editor

    // The "Create new page ..." drop down list
    container:create-new-page
      element:select
      class:span-4 prepend-top large

      oninit
        create-literal-widget
          element:option
          parent:create-new-page
          value:_default
          innerValue:Create new page ...
        list-files:system42/admin/page-editor/editors
        for-each:x:/-/*?name
          split:x:/./*/__dp?value
            =:/
          split:x:/-/0/-?name
            =:.
          add:x:/..for-each/*/create-literal-widget
            src:"value:{0}"
              :x:/..for-each/*/split/[1,2]/0?name
          add:x:/..for-each/*/create-literal-widget
            src:"innerValue:{0}"
              :x:/..for-each/*/split/[1,2]/0?name
          create-literal-widget
            element:option
            parent:create-new-page

      onchange
        get-widget-property:create-new-page
          value
        sys42.execute-lisp-file:system42/admin/page-editor/templates/create-new-{0}-page.hl
          :x:/../*/get-widget-property/*/*?value
        add:x:/../*/insert-data/0
          src:"type:{0}"
            :x:/../*/get-widget-property/*/*?value
        add:x:/../*/insert-data/0
          src:x:/./-2/*
        select-data:x:/*/*/p5.page/"=~/change-this-"
        _high:int:1
        for-each:x:/../*/select-data/*?value
          split:x:/./*/__dp?value
            =:-
          if
            can-convert:x:/./-/0/-?name
              type:int
            and:x:/./-/0/-?name.int
              more-than-equals:x:/../*/_high?value
            set:x:/../*/_high?value
              +:x:/..for-each/*/split/0/-?name.int
                _:1
        set:x:/../*/insert-data/0?value
          src:/change-this-{0}
            :x:/../*/_high?value
        insert-data
          p5.page
            name:Change this
        set-widget-property:create-new-page
          value:_default
        sys42.set-currently-edited-page:x:/../*/insert-data/0?value
        sys42.populate-select-page
        sys42.update-page-editor

    literal
      class:span-1 prepend-top large
      innerValue:or

    container:select-page
      element:select
      class:span-6 prepend-top large

      oninit
        sys42.populate-select-page
        sys42.update-page-editor

      onchange
        get-widget-property:select-page
          value
        sys42.set-currently-edited-page:x:/-/*/*(!/=_default)?value
        sys42.update-page-editor

      events

        sys42.set-currently-edited-page
          set-session:sys42.currently-edited-page
            src:x:/../*/_arg?value

        sys42.get-currently-edited-page
          get-session:sys42.currently-edited-page
          set:x:/..?value
            src:x:/./-/*?value

        sys42.populate-select-page
          clear-widget:select-page
          create-literal-widget
            element:option
            parent:select-page
            value:_default
            innerValue:Choose existing page to edit ...
          sys42.get-currently-edited-page
          select-data:x:/*/*/p5.page
          for-each:x:/-/*
            set:x:/..for-each/*/create-literal-widget/*/value?value
              src:x:/..for-each/*/__dp/#?value
            set:x:/..for-each/*/create-literal-widget/*/innerValue?value
              src:{0} - [{1}]
                :x:/..for-each/*/__dp/#/*/name?value
                :x:/..for-each/*/__dp/#/*/type?value
            if:x:/../*/sys42.get-currently-edited-page?value
              equals:x:/..for-each/*/__dp/#?value
              add:x:/..for-each/*/create-literal-widget
                src
                  selected
            create-literal-widget
              element:option
              parent:select-page
              value
              innerValue

    literal:back-link
      class:span-2 last prepend-top large
      element:a
      href:/
      innerValue:back
    container:page-editor-editor-surface
      class:span-24
      events

        sys42.update-page-editor
          sys42.get-currently-edited-page
          if:x:/-?value
            select-data:x:/*/*/p5.page/"={0}"/*/type?value
              :x:/../*/sys42.get-currently-edited-page?value
            add:x:/+
              src:"_id:{0}"
                :x:/../*/sys42.get-currently-edited-page?value
            sys42.create-common-toolbar
            sys42.execute-lisp-file:system42/admin/page-editor/editors/{0}.hl
              :x:/./-3/*?value
              _parent-widget:page-editor-editor-surface
          else
            clear-widget:page-editor-editor-surface

        sys42.create-common-toolbar
          create-widget
            parent:page-editor-editor-surface
            class:span-24
            widgets
              literal
                element:label
                for:page-name
                innerValue:Name
                has-id:false
                class:span-2 large
              void:page-name
                element:input
                type:text
                class:title span-10
                placeholder:Page name ...
                value
              literal
                element:label
                for:page-url
                innerValue:URL
                has-id:false
                class:span-2 prepend-2 large
              void:page-url
                element:input
                type:text
                class:title span-8 last
                placeholder:URL ...
                value
              literal
                element:label
                for:page-role
                innerValue:Role
                has-id:false
                class:span-2 large clear
              void:page-role
                element:input
                type:text
                class:text
                class:title span-10
                placeholder:Access role ...
                value
              literal:save-button
                element:button
                innerValue:Save
                class:span-4 large right
                onclick
              literal:delete-button
                element:button
                innerValue:Delete
                class:span-4 large right
                onclick
