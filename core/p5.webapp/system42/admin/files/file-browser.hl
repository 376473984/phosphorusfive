
/*
 * Allows the user to browse through the files on his system
 */


// including CSS for file browser
include-stylesheet-file:/system42/admin/files/media/files.css


/*
 * Creating main file system browser widget
 */
create-widget:file-browser
  parent:content
  class:col-xs-12 file-browser

  /*
   * Loads up files from current folder, or root folder, 
   * if there is no current folder
   */
  oninit

    // Creating folder bread-crumb buttons
    sys42.create-folder-bread-crumb

    // Creating current folder content
    sys42.create-current-folder-content

    // Making sure selected file gives visual clues, if any files are selected
    get-session:sys42.selected-file-browser-object
    if:x:/-/*/*/object?value
      sys42.create-selected-object-clues:x:/./-/*/*/object?value
      sys42.update-file-browser-toolbar


  widgets
    container:file-browser-bread-crumb
      class:btn-group
    container:file-browser-current-folder-content
    container:file-browser-current-item-toolbar
      class:btn-group
      widgets

        // "Copy" button
        literal:file-browser-copy-button
          element:button
          class:btn btn-default
          innerValue:Copy
          disabled
          onclick

            // Putting currently active object into session, 
            // to store which object has been "Copied"
            get-session:sys42.selected-file-browser-object
            add:x:/../*/set-session/*
              src:x:/./-/*/*
            set-session:sys42.copied-file-browser-object
              src
                operation:copy

            // Updating toolbar
            sys42.update-file-browser-toolbar

        // "Cut" button
        literal:file-browser-cut-button
          element:button
          class:btn btn-default
          innerValue:Cut
          disabled
          onclick

            // Putting currently active object into session, 
            // to store which object has been "Cut"
            get-session:sys42.selected-file-browser-object
            add:x:/../*/set-session/*
              src:x:/./-/*/*
            set-session:sys42.copied-file-browser-object
              src
                operation:move

            // Updating toolbar
            sys42.update-file-browser-toolbar

        // "Paste" button
        literal:file-browser-paste-button
          element:button
          class:btn btn-default
          innerValue:Paste
          disabled

        // "Rename" button
        literal:file-browser-rename-button
          element:button
          class:btn btn-default
          innerValue:Rename
          disabled

        // "Delete" button
        literal:file-browser-delete-button
          element:button
          class:btn btn-default
          innerValue:Delete
          disabled
    container:file-browser-file-editor


  /*
   * Lambda events for the main folder browser widget
   */
  events


    /*
     * Updates the toolbar, showing and hiding items according to
     * whether or not there are any objects selected, etc
     */
    sys42.update-file-browser-toolbar

      // Figuring out if there are any objects currently selected
      get-session:sys42.selected-file-browser-object
      _widgets
        file-browser-copy-button
        file-browser-cut-button
        file-browser-rename-button
        file-browser-delete-button
      if:x:/../*/get-session/*/*

        // There is currently an object active in file browser surface
        delete-widget-property:x:/../*/_widgets/*?name
          disabled
      else

        // There are currently no objects active in file browser surface
        set-widget-property:x:/../*/_widgets/*?name
          disabled

      // Setting "Paste" button to enabled, or vidce versa, depending
      // upon whether or not there's an object in "clipboard" or not
      get-session:sys42.copied-file-browser-object
      if:x:/-/*/*
        delete-widget-property:file-browser-paste-button
          disabled
      else
        set-widget-property:file-browser-paste-button
          disabled


    /*
     * Shows the bread-crumb for the file browser. Takes no arguments
     */
    sys42.create-folder-bread-crumb

      // Deleting previously created bread-crumb
      clear-widget:file-browser-bread-crumb

      // Getting current folder
      get-session:sys42.current-folder

      // Creating bread-crumb home button widget part
      if:x:/../*/get-session/*?value
        not
        add:x:/../*/create-literal-widget
          src:disabled
      create-literal-widget
        element:button
        parent:file-browser-bread-crumb
        class:btn btn-default
        innerValue:/
        onclick
          sys42.change-current-folder

      // Looping through each folder leading to current path, 
      // building "path" as we go along
      _path:
      split:x:/../*/get-session/*?value
        =:/
      for-each:x:/-/*?name

        // Creating a button for each folder leading to current folder
        set:x:/..for-each/*/create-literal-widget/*/innerValue?value
          src:x:/..for-each/*/__dp?value
        set:x:/../*/_path?value
          src:{0}{1}/
            :x:/../*/_path?value
            :x:/..for-each/*/__dp?value
        set:x:/..for-each/*/create-literal-widget/*/onclick/*/sys42.change-current-folder?value
          src:x:/../*/_path?value

        // Checking if this is last bread-crumb button, and if so, disabling it
        if:x:/../*/_path?value
          =:x:/../*/get-session/*?value
          add:x:/..for-each/*/create-literal-widget
            src:disabled

        // Creating bread-crumb button widget part
        create-literal-widget
          element:button
          parent:file-browser-bread-crumb
          class:btn btn-default
          innerValue
          onclick
            sys42.change-current-folder


    /*
     * Creates a list of all folders and files from current folder,
     * for user to browse through
     */
    sys42.create-current-folder-content

      // Clearing previously created folders and files
      clear-widget:file-browser-current-folder-content

      // Listing folders first
      sys42.create-current-folders

      // Listing files afterwards
      sys42.create-current-files


    /*
     * List all folders from current folder
     */
    sys42.create-current-folders

      // Retrieving current folder
      get-session:sys42.current-folder

      // Retrieving selected object
      get-session:sys42.selected-file-browser-object

      // Looping through each folder, creating a widget for each of them
      list-folders:x:/../*/get-session/[0,1]/*?value
      for-each:x:/../*/list-folders/*?name

        // Removing trailing "/" and setting innerValue and title
        split:x:/..for-each/*/__dp?value
          =:/
        set:x:/..for-each/*/create-literal-widget/*(/innerValue|/title)?value
          src:x:/..for-each/*/split/0/-?name

        // Making sure event handler for widget is correctly wired up
        set:x:/..for-each/*/create-literal-widget/*/onclick/0?value
          src:x:/..for-each/*/__dp?value

        // Actually creating folder object widget
        create-literal-widget
          parent:file-browser-current-folder-content
          class:file-object folder
          innerValue
          title
          onclick
            sys42.folder-clicked


    /*
     * List all files from current folder
     */
    sys42.create-current-files

      // Retrieving current folder
      get-session:sys42.current-folder

      // Retrieving selected object
      get-session:sys42.selected-file-browser-object

      // Looping through each folder, creating a widget for each of them
      list-files:x:/../*/get-session/[0,1]/*?value
      for-each:x:/../*/list-files/*?name

        // Removing trailing "/" and setting innerValue
        split:x:/..for-each/*/__dp?value
          =:/
        set:x:/..for-each/*/create-literal-widget/*(/innerValue|/title)?value
          src:x:/..for-each/*/split/0/-?name

        // Making sure event handler for widget is correctly wired up
        set:x:/..for-each/*/create-literal-widget/*/onclick/0?value
          src:x:/..for-each/*/__dp?value

        // Actually creating folder object widget
        create-literal-widget
          parent:file-browser-current-folder-content
          class:file-object file
          innerValue
          title
          onclick
            sys42.file-clicked


    /*
     * Bread-crumb button was clicked. Takes the path to
     * the new folder as an argument
     */
    sys42.change-current-folder

      // Updating current folder
      set-session:sys42.current-folder
        src:x:/../*/_arg?value

      // Removing selected file/folder object
      set-session:sys42.selected-file-browser-object

      // Re-creating bread-crumb
      sys42.create-folder-bread-crumb

      // Listing current folder's folders and files again
      sys42.create-current-folder-content

      // Refreshing toolbar
      sys42.update-file-browser-toolbar


    /*
     * Invoked when a folder widget was clicked. Takes path to
     * folder that is clicked as input argument
     */
    sys42.folder-clicked

      // Destroying any existing file object editors
      sys42.destroy-file-editor

      // Checking if folder is already selected
      get-session:sys42.selected-file-browser-object
      if:x:/../*/get-session/*/*/type?value
        =:folder
        and:x:/../*/get-session/*/*/object?value
          =:x:/../*/_arg?value

        // Clicked folder was already selected, hence opening it, 
        // and destroying any existing object toolbars
        sys42.change-current-folder:x:/../*/_arg?value
      else

        // Clicked folder was not selected, hence we select it
        set:x:/+/*/*/object?value
          src:x:/../*/_arg?value
        set-session:sys42.selected-file-browser-object
          src
            type:folder
            object
        sys42.create-selected-object-clues:x:/../*/_arg?value

        // Refreshing toolbar
        sys42.update-file-browser-toolbar


    /*
     * Invoked when a file widget was clicked. Takes path to
     * file that is clicked as input argument
     */
    sys42.file-clicked

      // Checking if file is already selected
      get-session:sys42.selected-file-browser-object
      if:x:/../*/get-session/*/*/type?value
        =:file
        and:x:/../*/get-session/*/*/object?value
          =:x:/../*/_arg?value

        // File was already selected, opening up file editor
        sys42.create-file-editor:x:/../*/get-session/*/*/object?value
      else

        // Clicked file was not selected, hence we select it, and update view
        set:x:/+/*/*/object?value
          src:x:/../*/_arg?value
        set-session:sys42.selected-file-browser-object
          src
            type:file
            object

        // Destroying any existing editors, since selected object changed
        sys42.destroy-file-editor

        // Showing selected object clues
        sys42.create-selected-object-clues:x:/../*/_arg?value

        // Refreshing toolbar
        sys42.update-file-browser-toolbar


    /*
     * Selects a file/folder object on screen, giving visual clues to user
     * Takes filename as argument
     */
    sys42.create-selected-object-clues
      get-children-widgets:file-browser-current-folder-content
      get-widget-property:x:/-/*/*?value
        class
        innerValue
      split::x:/-/*/*/"=~ active-file"?value
        =:" active-file"
      set-widget-property:x:/-2/*/*/class/"=~ active-file"/.?name
        class:x:/./-/0?name
      split:x:/../*/_arg?value
        =:/
      set-widget-property:x:/-4/*/*/innerValue/"={0}"/.?name
        :x:/./-/0/-?name
        class:{0} active-file
          :x:/././-4/*/*/innerValue/"={0}"/./*/class?value
            :x:/./././-/0/-?name


    /*
     * Creates the "Edit file editor widget" for editing files, if possible
     * Takes filename as argument
     */
    sys42.create-file-editor
      clear-widget:file-browser-file-editor


    /*
     * Destroys any "Edit file editor widgets"
     */
    sys42.destroy-file-editor
      clear-widget:file-browser-file-editor
