
/*
 * Allows the user to browse through the files on his system
 */


// including CSS for file browser
include-stylesheet-file:/system42/admin/files/media/files.css


/*
 * Creating main file system browser widget
 */
create-widget:main-folders
  parent:content
  class:col-xs-12 file-browser

  /*
   * Loads up files from current folder or 
   * root folder if there is no current folder, and
   * if there's a selected file/folder, will display 
   * selected file/folder toolbar
   */
  oninit
    sys42.list-current-folder-content
    get-session:sys42.copied-file
    if:x:/-/*/*?value
      sys42.show-paste-toolbar
    get-session:sys42.selected-file-folder-object
    if:x:/-/*?value
      sys42.show-selected-file-object-toolbar


  widgets
    container:folders-bread-crumb
    container:folder-content
    container:selected-file-object-toolbar
    container:selected-file-object-editor


  /*
   * Lambda events for the main folder browser widget
   */
  events


    /*
     * Shows the toolbar for selected file or folder
     */
    sys42.show-selected-file-object-toolbar

      // Clearing any previous toolbars out
      delete-widget:selected-object-toolbar

      // Creating new toolbar
      create-widget:selected-object-toolbar
        parent:selected-file-object-toolbar
        class:btn-group
        widgets
          void
            element:input
            type:button
            class:btn btn-default
            value:copy
            onclick
              sys42.copy-file-folder
                _cut:false
          void
            element:input
            type:button
            class:btn btn-default
            value:cut
            onclick
              sys42.copy-file-folder
                _cut:true
          void
            element:input
            type:button
            class:btn btn-default
            value:rename
            onclick
              sys42.load-rename-selected-file-folder-widget
          void:delete-selected-file
            element:input
            type:button
            class:btn btn-default
            value:delete
            onclick
              sys42.delete-selected-file-folder


    /*
     * Invoked when user wants to copy/cut a file/folder
     */
    sys42.copy-file-folder
      get-session
        sys42.selected-file-folder-object
        sys42.selected-file-folder-type
        sys42.current-folder
      if:x:/../*/_cut?value.bool
        set:x:/../*/set-session/*/*/operation?value
          src:move
      else
        set:x:/../*/set-session/*/*/operation?value
          src:copy
      if:x:/../*/get-session/[0,1]/*/sys42.current-folder?value
        and:x:/../*/get-session/[0,1]/*/sys42.current-folder?value
          !=:
        set:x:/../*/set-session/*/*/object?value
          src:{0}/{1}
            :x:/../*/get-session/[0,1]/*/sys42.current-folder?value
            :x:/../*/get-session/[0,1]/*/sys42.selected-file-folder-object?value
      else
        set:x:/../*/set-session/*/*/object?value
          src:{0}
            :x:/../*/get-session/[0,1]/*/sys42.selected-file-folder-object?value
      set:x:/../*/set-session/*/*/type?value
        src:x:/../*/get-session/[0,1]/*/sys42.selected-file-folder-type?value
      set-session:sys42.copied-file
        src
          object
          type
          operation
      sys42.show-paste-toolbar


    /*
     * Shows the paste toolbar
     */
    sys42.show-paste-toolbar

      // Creating paste toolbar, but only if it does not exist from before
      if
        widget-exist:selected-object-paste-toolbar
        not
        create-widget:selected-object-paste-toolbar
          parent:selected-file-object-toolbar
          class:btn-group
          position:0
          widgets
            void
              element:input
              type:button
              class:btn btn-default
              value:paste
              onclick
                get-session:sys42.copied-file
                get-session:sys42.current-folder
                set:x:/../*/_eval/0?name
                  src:sys42.{0}-{1}
                    :x:/../*/get-session/[0,1]/*/*/operation?value
                    :x:/../*/get-session/[0,1]/*/*/type?value
                set:x:/../*/_eval/0/*/_src?value
                  src:x:/../*/get-session/*/*/object?value
                split:x:/../*/get-session/[0,1]/*/*/object?value
                  =:/
                if:x:/../*/get-session/[1,2]/*?value
                  and:x:/../*/get-session/[1,2]/*?value
                    !=:
                  set:x:/../*/_eval/0/*/_dest?value
                    src:{0}/{1}
                      :x:/../*/get-session/[1,2]/*?value
                      :x:/../*/split/0/-?name
                else
                  set:x:/../*/_eval/0/*/_dest?value
                    src:x:/../*/split/0/-?name
                _eval
                  foo-bar
                    _src
                    _dest
                eval:x:/-


              /*
               * Lambda events for Paste button
               */
              events
                sys42.move-file
                  sys42.show-code:x:/..
                sys42.copy-file
                  sys42.show-code:x:/..
                sys42.move-folder
                  sys42.show-code:x:/..
                sys42.copy-folder
                  sys42.show-code:x:/..


    /*
     * Deletes the currently selected file/folder
     */
    sys42.delete-selected-file-folder
      get-session
        sys42.selected-file-folder-object
      set:x:/+/*/_message?value
        src::Are you sure you wish to delete <strong>'{0}'</strong>, this action cannot be undone once done
          :x:/././-/*?value
      sys42.confirm-action
        _header:Please confirm deletion
        _message
        _onok
          get-session
            sys42.current-folder
            sys42.selected-file-folder-object
            sys42.selected-file-folder-type
          if:x:/../*/get-session/*/sys42.selected-file-folder-type?value
            =:file

            // User is deleting a file
            delete-file:{0}/{1}
              :x:/../*/get-session/*/sys42.current-folder?value
              :x:/../*/get-session/*/sys42.selected-file-folder-object?value
          else

            // User is deleting a folder
            delete-folder:{0}/{1}
              :x:/../*/get-session/*/sys42.current-folder?value
              :x:/../*/get-session/*/sys42.selected-file-folder-object?value

          // Clearing currently selected object and associated session variables
          set-session
            sys42.selected-file-folder-object
            sys42.selected-file-folder-type

          // Re-listing widgets of current folder
          sys42.list-current-folder-content

          // Deleting edit object toolbar
          delete-widget:selected-object-toolbar


    /*
     * Renames the currently selected file or folder
     */
    sys42.load-rename-selected-file-folder-widget
      get-session
        sys42.selected-file-folder-object
      set:x:/+/*/widgets/0/*/value?value
        src:x:/./-/*/sys42.selected-file-folder-object?value
      create-widget:rename-file
        parent:selected-file-object-editor
        widgets
          void:rename-input-el
            element:input
            type:text
            class:span-8
            value
            onsubmit_
              get-widget-property:rename-input-el
                value
              delete-widget:rename-file
              get-session
                sys42.current-folder
                sys42.selected-file-folder-object
                sys42.selected-file-folder-type
              if:x:/-/*/sys42.selected-file-folder-type?value
                =:file

                // Renaming a file
                move-file:{0}/{1}
                  :x:/../*/get-session/*/sys42.current-folder?value
                  :x:/../*/get-session/*/sys42.selected-file-folder-object?value
                  to:{0}/{1}
                    :x:/../*/get-session/*/sys42.current-folder?value
                    :x:/../*/get-widget-property/*/*/value?value

                // Changing selected file session value
                set-session:sys42.selected-file-folder-object
                  src:x:/../*/get-widget-property/*/*/value?value
                sys42.list-current-folder-content
              else

                // Renaming a folder
                move-folder:{0}/{1}
                  :x:/../*/get-session/*/sys42.current-folder?value
                  :x:/../*/get-session/*/sys42.selected-file-folder-object?value
                  to:{0}/{1}
                    :x:/../*/get-session/*/sys42.current-folder?value
                    :x:/../*/get-widget-property/*/*/value?value

                // Changing selected file session value
                set-session:sys42.selected-file-folder-object
                  src:x:/../*/get-widget-property/*/*/value?value
                sys42.list-current-folder-content
            onclose_
              delete-widget:rename-file
            onkeyup:"window.checkKey(event);"
          void:stupid-fucking-browser-idiotic-behavior-needs-this-fucking-shit-to-not-post-form-on-carriage-return
            element:input
            type:text
            style:"display:none;"


      // Helper JavaScript to handle key events for saving and discarding new file/folder name
      include-javascript:@"(function() {
window.checkKey = function (e) {
  if (e.keyCode == 13) {
    p5.$('rename-input-el').raise('onsubmit_');
    e.preventDefault();
    return false;
  } else if (e.keyCode == 27) {
    p5.$('rename-input-el').raise('onclose_');
  }
};
})();"

      // Helper JavaScript to set focus to rename textbox
      send-javascript:@"(function(){var el = window.p5.$('rename-input-el').el;el.focus();el.select();})();"


    /*
     * Shows the bread-crumb for file browser
     */
    sys42.show-folder-bread-crumb

      // Deleting previously created bread-crumb
      clear-widget:folders-bread-crumb

      // Getting current folder
      get-session:sys42.current-folder
      split:x:/-/*?value
        =:/
      for-each:x:/-/*?name
        _lit
          void
            element:input
            type:button
            class:btn btn-default
            value
            onclick
              sys42.bread-crumb-clicked:x:/../*/_event?value
        set:x:/-/*/*/value?value
          src:x:/././*/__dp?value
        add:x:/../*/create-widget/*/widgets
          src:x:/././*/_lit/*
      add:x:/+/*/widgets/0/-
        src:disabled
      create-widget:bread-crumb
        parent:folders-bread-crumb
        class:btn-group
        widgets
          void
            element:input
            type:button
            class:btn btn-default
            value:/
            onclick
              sys42.bread-crumb-clicked:x:/../*/_event?value


    /*
     * Bread-crumb button was clicked
     */
    sys42.bread-crumb-clicked

      // Clearing selected file toolbar widget
      delete-widget:selected-object-toolbar

      // Clearing selected file session value
      set-session:sys42.selected-file-folder-object
      set-session:sys42.selected-file-folder-type
      get-widget-property:x:/../*/_arg?value
        value
      if:x:/-/*/*?value

        // "home" was clicked
        equals:/
        set-session:sys42.current-folder
      else

        // Anything but home was clicked
        get-session:sys42.current-folder
        split:x:/-/*?value
          =:/
        _new-folder

        // Looping through each [split] results until we're at new current folder
        while:bool:true

          // Making sure each consecutive folder append operation are separated by a "/"
          if:x:/./-?value
            set:x:/././-?value
              src:{0}/
                :x:/././././-?value

          // Appending currently iterated folder into [_new-folder]
          set:x:/./-?value
            src:{0}{1}
              :x:/./././-?value
              :x:/..else/*/split/0?name

          // Checking if we're at "new current folder", and if so, breaking while
          if:x:/../*/get-widget-property/*/*?value
            equals:x:/..else/*/split/0?name
            set:x:/..while?value
              src:bool:false

          // Removing zeroth split item, which is the one we just dealt with
          set:x:/..else/*/split/0

        // Updating current folder to new value
        set:x:/+/0?value
          src:x:/..else/*/_new-folder?value
        set-session:sys42.current-folder
          src

      // Listing current folder's folders and files again
      sys42.list-current-folder-content


    /*
     * Invoked when a folder widget was clicked
     */
    sys42.folder-clicked

      // Getting name of folder that was clicked
      get-widget-property:x:/../*/_arg?value
        innerValue
      get-session:sys42.selected-file-folder-object
      if:x:/-/*?value
        =:x:/./-2/*/*?value

        // Folder is currently selected
        get-session:sys42.current-folder

        // Appending a slash if there's a value in current-folder
        if:x:/-/*?value
          set:x:/./-/*?value
            src:{0}/
              :x:/./././-/*?value

        // Updating current folder
        set-session:sys42.current-folder
          src:{0}{1}
            :x:/././-2/0?value
            :x:/../*/get-widget-property/*/*/innerValue?value
            
        // Deleting the "currently selected object and type" session variable
        set-session:sys42.selected-file-folder-object
        set-session:sys42.selected-file-folder-type

        // Clearing selected file toolbar widget
        delete-widget:selected-object-toolbar
      else

        // Selecting folder
        set-session:sys42.selected-file-folder-object
          src:x:/../*/get-widget-property/*/*/innerValue?value
        set-session:sys42.selected-file-folder-type
          src:folder

        // Shows toolbar for selected folder
        sys42.show-selected-file-object-toolbar

      // Listing all folders and files in current folder
      sys42.list-current-folder-content


    /*
     * Invoked when a file widget was clicked
     */
    sys42.file-clicked

      // Getting name of file that was clicked
      get-widget-property:x:/../*/_arg?value
        innerValue
      get-session:sys42.selected-file-folder-object
      if:x:/-/*?value
        =:x:/./-2/*/*?value

        // Getting file name and path
        _file-path
        get-session:sys42.current-folder
        if:x:/-/*?value
          set:x:/./-2?value
            src:{0}/{1}
              :x:/./././-/*?value
              :x:/../*/get-widget-property/*/*/innerValue?value
        else
          set:x:/./-3?value
            src:x:/../*/get-widget-property/*/*/innerValue?value

        // Returning file over HTTP Response by setting session variable and doing "window.location.reload(true)"
        set-session:sys42.download-file
          src:x:/././*/_file-path?value
        send-javascript:"window.location.reload(true);"
      else

        // Shows toolbar for selected file
        sys42.show-selected-file-object-toolbar

        // Selecting file
        set-session:sys42.selected-file-folder-object
          src:x:/../*/get-widget-property/*/*/innerValue?value
        set-session:sys42.selected-file-folder-type
          src:file

      // Listing all folders and files in current folder
      sys42.list-current-folder-content


    /*
     * Creates a list of all folders and files in current folder 
     * for user to browse through.
     */
    sys42.list-current-folder-content

      // clearing previously created folders and files
      clear-widget:folder-content

      // showing bread crumb
      sys42.show-folder-bread-crumb

      // Common lambda object for both files and folders
      _x
        for-each:x:/../*/_objects/*?name
          split:x:/./*/__dp?value
            =:/
          _lit
            literal
              element:small
              class:file-object
              title
          add:x:/-/*
            src:"innerValue:{0}"
              :x:/./././*/split/+/<?name
          set:x:/./*/_lit/*/*/class?value
            src:{0} {1}
              :x:/./././*/_lit/*/*/class?value
              :x:/././././*/_class?value
          set:x:/./*/_lit/*/*/title?value
            src:x:/..for-each/*/split/0/-?name
          if:x:/../*/_selected-object?value
            =:x:/..for-each/*/split/0/-?name
            set:x:/..for-each/*/_lit/*/*/class?value
              src:{0} {1}
                :x:/..for-each/*/_lit/*/*/class?value
                : active-file
          insert-before:x:/../0
            src:x:/././*/_lit/*
          for-each:x:/../*/_events/*
            add:x:/../0
              src:x:/././*/__dp/#

      // Getting currently selected file/folder object
      get-session:sys42.selected-file-folder-object

      // Looping through all folders
      list-folders
        get-session:sys42.current-folder
      add:x:/+2/*/_objects
        src:x:/./-/*?name
      set:x:/+/*/_selected-object?value
        src:x:/../*/get-session/[0,1]/*?value
      eval:x:/../*/_x
        _objects
        _class:folder
        _selected-object
        _events
          onclick
            sys42.folder-clicked:x:/../*/_event?value
      add:x:/../*/create-widget/*/widgets
        src:x:/./-/*

      // Looping through all files
      list-files
        get-session:sys42.current-folder
      add:x:/+2/*/_objects
        src:x:/./-/*?name
      set:x:/+/*/_selected-object?value
        src:x:/././*/get-session/[0,1]/*?value
      eval:x:/../*/_x
        _objects
        _class:file
        _selected-object
        _events
          onclick
            sys42.file-clicked:x:/../*/_event?value
      add:x:/../*/create-widget/*/widgets
        src:x:/./-/*

      // creating widget containing all folders and files
      create-widget:all-files-and-folders
        parent:folder-content
        class:prepend-top prepend-bottom
        widgets



