
/*
 * Main entry point for file browser. If there's a value in session
 * called "sys42.download-file", then that file will be directly 
 * downloaded. If not, then file browser will start on page. This is
 * to let the file browser allow for downloading files by simply doing
 * a "window.location.reload(true);" JavaScript inclusion.
 */


get-session:sys42.download-file
if:x:/-/*?value

  /*
   * We have a file to download, figuring out what type of Content-Type
   * we should serve it as
   */
  sys42.get-content-type:x:/../*/get-session/*?value
  set-http-header
    Content-Type:x:/./-?value

  // Figuring out filename
  split:x:/../*/get-session/*?value
    =:/
  set-http-header
    Content-Disposition:"attachment; filename={0}"
      :x:/././-/0/-?name

  // Returning file
  echo-file:x:/../*/get-session/*?value

  // Clearing session value
  set-session:sys42.download-file

else

  // There were no files to download

  // Making sure we start up in "default directory"
  sys42.get-user
  get-session:sys42.current-folder
  if:x:/-/*?value
    not
    or:x:/..else/*/sys42.get-user/*/role?value
      !=:admin
      and:x:/..else/*/get-session/*/"=~{0}"
        :/users/{0}/Documents/
          :x:/../*/sys42.get-user/*/username?value
        not
    set-session:sys42.current-folder
      src:users/{0}/Documents/
        :x:/..else/*/sys42.get-user/*/username?value

  // Setting "root directory" according to user role
  if:x:/..else/*/sys42.get-user/*/role?value
    !=:admin
    set-session:sys42.root-directory
      src:users/{0}/Documents/
        :x:/..else/*/sys42.get-user/*/username?value
  else
    set-session:sys42.root-directory
      src:

  // Starting the actual file-browser implementation
  sys42.execute-lisp-file:system42/admin/file-browser/file-browser.hl


