
/*
 * Allows the user to browse through the files on his system
 */


// including CSS for file browser
include-stylesheet-file:/system42/admin/file-browser/media/files.css


/*
 * Creating main file system browser widget
 */
create-widget:file-browser
  parent:content
  class:col-xs-12 file-browser

  /*
   * Loads up files from current folder, or root folder, 
   * if there is no current folder
   */
  oninit

    // Creating folder bread-crumb buttons
    sys42.create-folder-bread-crumb

    // Creating current folder content
    sys42.create-current-folder-content

    // Making sure selected file gives visual clues, if any files are selected
    get-session:sys42.selected-file-browser-object
    if:x:/-/*/*/object?value
      sys42.create-selected-object-clues:x:/./-/*/*/object?value
      sys42.update-file-browser-toolbar

    // Checking if we're currently in "edit file mode", and if
    // so, opening up editor
    get-session:sys42.original-file-editor-classes
    if:x:/-/*/*
      sys42.create-file-editor:x:/../*/get-session/*/*/object?value


  widgets


    /*
     * Toolbar widget for showing "Copy", "Cut", "Paste", "Rename" buttons etc ...
     */
    container:file-browser-current-item-toolbar
      class:btn-group prepend-right-2 prepend-bottom
      oninit
        sys42.update-file-browser-toolbar
      widgets


        /*
         * "Copy" button
         */
        container:file-browser-copy-button
          element:button
          class:btn btn-default
          disabled
          accesskey:C
          title:Copy - C
          onclick
            sys42.copy-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-copy


        /*
         * "Cut" button
         */
        container:file-browser-cut-button
          element:button
          class:btn btn-default
          disabled
          accesskey:X
          title:Cut - X
          onclick
            sys42.cut-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-scissors


        /*
         * "Paste" button
         */
        container:file-browser-paste-button
          element:button
          class:btn btn-default
          disabled
          accesskey:V
          title:Paste - V
          onclick
            sys42.paste-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-paste


        /*
         * "Download" button
         */
        container:file-browser-download-button
          element:button
          class:btn btn-default
          disabled
          accesskey:D
          title:Download - D
          onclick
            sys42.download-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-download


        /*
         * "Rename" button
         */
        container:file-browser-rename-button
          element:button
          class:btn btn-default
          disabled
          accesskey:R
          title:Rename - R
          onclick
            sys42.rename-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-pencil


        /*
         * "Delete" button
         */
        container:file-browser-delete-button
          element:button
          class:btn btn-default
          disabled
          accesskey:T
          title:Trash - T
          onclick
            sys42.delete-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-trash


        /*
         * "Create folder" button
         */
        container:create-folder-button
          element:button
          class:btn btn-default
          accesskey:F
          title:New folder - F
          onclick
            sys42.new-folder-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-asterisk


        /*
         * "Create file" button
         */
        container:create-file-button
          element:button
          class:btn btn-default
          accesskey:N
          title:New file - N
          onclick
            sys42.new-file-button-clicked
          widgets
            literal
              element:span
              class:glyphicon glyphicon-plus


      /*
       * Lambda events for file browser current item toolbar
       */
      events


        /*
         * Raised when "Copy" button is clicked
         */
        sys42.copy-button-clicked

          // Putting currently active object into session, 
          // to store which object has been "Copied"
          get-session:sys42.selected-file-browser-object
          add:x:/../*/set-session/*
            src:x:/./-/*/*
          set-session:sys42.copied-file-browser-object
            src
              operation:copy

          // Updating toolbar
          sys42.update-file-browser-toolbar

          // Showing popup to user, informing him that action was successful
          sys42.info-feedback:@"{0} was stored in clipboard"
            :x:/../*/get-session/*/*/type?value


        /*
         * Raised when "Cut" button is clicked
         */
        sys42.cut-button-clicked

          // Putting currently active object into session, 
          // to store which object has been "Cut"
          get-session:sys42.selected-file-browser-object
          add:x:/../*/set-session/*
            src:x:/./-/*/*
          set-session:sys42.copied-file-browser-object
            src
              operation:move

          // Updating toolbar
          sys42.update-file-browser-toolbar

          // Showing popup to user, informing him that action was successful
          sys42.info-feedback:@"{0} was stored in clipboard"
            :x:/../*/get-session/*/*/type?value


        /*
         * Raised when "Paste" button is clicked
         */
        sys42.paste-button-clicked

          // Getting copied object and current folder, combining
          // filename from copied object and current folder, as destination
          // filepath
          get-session:sys42.copied-file-browser-object
          get-session:sys42.current-folder
          split:x:/../*/get-session/[0,1]/*/*/object?value
            =:/

          // This will do the actual work after modifying "foo-bar" to correct Active Event
          _x-copy-move-file
            foo-bar
              to
            set:x:/..?value
              src:x:/./-?value

          // Making sure we use the right Active Event
          set:x:/../*/_x-copy-move-file/0?name
            src:{0}-{1}
              :x:/../*/get-session/[0,1]/*/*/operation?value
              :x:/../*/get-session/[0,1]/*/*/type?value

          // Setting source file for operation
          set:x:/../*/_x-copy-move-file/0?value
            src:x:/../*/get-session/[0,1]/*/*/object?value

          // Setting destination filepath for operation
          set:x:/../*/_x-copy-move-file/0/0?value
            src:{0}{1}
              :x:/../*/get-session/[1,2]/*?value
              :x:/../*/split/0/-?name

          // Executing actual move/copy/file/folder operation
          eval:x:/../*/_x-copy-move-file

          // Updating currently selected object to be the new file/folder that
          // was moved/copied
          set:x:/../*/set-session/*/*/type?value
            src:x:/../*/get-session/[0,1]/*/*/type?value
          set:x:/../*/set-session/*/*/object?value
            src:x:/../*/eval?value
          set-session:sys42.selected-file-browser-object
            src
              type
              object

          // Re-creating folder content, to reflect our new file/folder here ...
          sys42.create-current-folder-content

          // Updating selected visual clues, since our "new file" is now selected
          sys42.create-selected-object-clues:x:/../*/set-session/*/*/object?value

          // If this was a "move" operation, then we cannot click on "Paste" multiple times
          // since source file is gone, which BTW doesn't logically do much sense anyway, hence
          // we check if this was a "move" operation, and if it was, we simply discard the 
          // "currently cut file browser object"
          if:x:/../*/get-session/[0,1]/*/*/operation?value
            =:move
            set-session:sys42.copied-file-browser-object

          // Since we now have a selected file, where we maybe didn't have one before, in addition
          // to that we might have discarded the "Paste" logic, we update toolbar, since some items
          // might be enabled now, while others might have been disabled ...
          sys42.update-file-browser-toolbar

          // Showing popup to user, informing him that action was successful
          sys42.info-feedback:@"{0} was pasted into current folder"
            :x:/../*/get-session/*/*/type?value


        /*
         * Raised when "Download" button is clicked
         */
        sys42.download-button-clicked

          // Retrieving currently selected file, and passing onto session
          // value tracking which file to download, before we refresh browser window
          get-session:sys42.selected-file-browser-object
          set-session:sys42.download-file
            src:x:/../*/get-session/*/*/object?value
          send-javascript:@"window.location.replace(window.location.pathname);"


        /*
         * Raised when "Rename" button is clicked
         */
        sys42.rename-button-clicked

          // Retrieving selected file-/folder object
          get-session:sys42.selected-file-browser-object

          // Finding only name of selected object, such that we can
          // put it into "Rename" textbox as starting value
          split:x:/../*/get-session/*/*/object?value
            =:/
          set:x:/../*/create-literal-widget/*/value?value
            src:x:/../*/split/0/-?name

          // Setting placeholder and title according to whether 
          // or not this is "file" or "folder"
          set:x:/../*/create-literal-widget/*/placeholder?value
            src:New name of {0} ...
              :x:/../*/get-session/*/*/type?value
          set:x:/../*/create-literal-widget/*/title?value
            src:New name of {0} ...
              :x:/../*/get-session/*/*/type?value

          // Setting focus to "Rename" textbox
          send-javascript:@"$('#rename-file-object').focus().select();"

          // Disabling "Rename" button
          set-widget-property:file-browser-rename-button
            class:btn btn-info

          // Creating "Rename" textbox widget
          create-literal-widget:rename-file-object
            element:input
            type:text
            parent:file-browser-current-item-toolbar
            position:4
            class:form-control
            style:"float:left;width:250px;border-radius:0;border-left:none;border-right:none;"
            value
            placeholder
            title
            onkeypress:"return p5.textBoxKeyPress(event, 'rename-file-object');"
            onkeyup:"p5.textBoxKeyUp(event, 'rename-file-object');"
            onblur
              delete-widget:rename-file-object
              set-widget-property:file-browser-rename-button
                class:btn btn-default
            onenter_
              sys42.rename-file
            onesc_
              delete-widget:rename-file-object
              set-widget-property:file-browser-rename-button
                class:btn btn-default


            /*
             * Lambda events for "Rename file textbox"
             */
            events


              /*
               * Renames file, takes no arguments
               */
              sys42.rename-file

                // Retrieving selected file/folder and finding folder of it
                get-session:sys42.selected-file-browser-object
                split:x:/../*/get-session/*/*/object?value
                  =:/

                // Removing old filename from split results
                set:x:/../*/split/0/-

                // Figuring out new filename
                _new-filepath
                for-each:x:/../*/split/*?name
                  set:x:/../*/_new-filepath?value
                    src:{0}{1}/
                      :x:/../*/_new-filepath?value
                      :x:/..for-each/*/__dp?value

                // Retrieving new file-/folder name
                get-widget-property:rename-file-object
                  value

                // Checking if new filename/foldername exist from before
                file-exist:{0}{1}
                  :x:/../*/_new-filepath?value
                  :x:/../*/get-widget-property/*/*/value?value
                folder-exist:{0}{1}
                  :x:/../*/_new-filepath?value
                  :x:/../*/get-widget-property/*/*/value?value
                if:x:/-2?value
                  or:x:/./-?value

                  // That filename is taken up by another file on system
                  set:x:/+/*/_message?value
                    src:Sorry, I cannot rename your {0} to <strong>'{1}'</strong>, since there's already another file/folder with that name
                      :x:/../*/get-session/*/*/type?value
                      :x:/../*/get-widget-property/*/*/value?value
                  sys42.confirm-action
                    _header:Object exist from before
                    _message
                    _onok
                      send-javascript:@"$('#rename-file-object').focus().select();"
                else

                  // Renaming file
                  set:x:/..else/*/foo-bar2?name
                    src:move-{0}
                      :x:/../*/get-session/*/*/type?value
                  foo-bar2:x:/../*/get-session/*/*/object?value
                    to:{0}{1}
                      :x:/../*/_new-filepath?value
                      :x:/../*/get-widget-property/*/*/value?value

                  // Making sure we update currently selected object, and delete currently copied/cut object
                  set-session:sys42.copied-file-browser-object
                  add:x:/+2/*
                    src:x:/../*/get-session/*/*
                  set:x:/+/*/*/object?value
                    src:x:/..else/*/~move-?value
                  set-session:sys42.selected-file-browser-object
                    src

                  // Making rename button have default colors
                  set-widget-property:file-browser-rename-button
                    class:btn btn-default

                  // Refreshing toolbar
                  sys42.update-file-browser-toolbar

                  // Re-creating current folder's content
                  sys42.create-current-folder-content

                  // Making sure renamed file is still selected
                  sys42.create-selected-object-clues:x:/..else/*/~move-?value

                  // Removing "Rename object" textbox
                  delete-widget:rename-file-object

                  // Showing popup to user, informing him that action was successful
                  sys42.info-feedback:@"File was renamed to '{0}'"
                    :x:/../*/get-widget-property/*/*?value


        /*
         * Raised when "Delete" button is clicked
         */
        sys42.delete-button-clicked

          // Showing a "Confirm Action" modal dialog, to warn user
          // that deleting files cannot be undone
          get-session:sys42.selected-file-browser-object

          // Finding filename, without path
          split:x:/../*/get-session/*/*/object?value
            =:/

          // Setting Header of dialog, including either "file" or "folder"
          set:x:/../*/sys42.confirm-action/*/_header?value
            src:Please confirm deletion of {0}
              :x:/../*/get-session/*/*/type?value

          // Setting body of dialog, including "file/folder" and file-/folder name
          set:x:/../*/sys42.confirm-action/*/_message?value
            src:Are you sure you wish to delete the {0}; <strong>'{1}'</strong>, this action cannot be undone!
              :x:/../*/get-session/*/*/type?value
              :x:/../*/split/0/-?name

          // Making sure we raise the correct Active Event upon "OK" of dialog
          set:x:/../*/sys42.confirm-action/*/_onok/0?name
            src:delete-{0}
              :x:/../*/get-session/*/*/type?value

          // Setting file-/folder name and path to delete upon "OK" of dialog
          set:x:/../*/sys42.confirm-action/*/_onok/0?value
            src:x:/../*/get-session/*/*/object?value

          // Setting filename and object type to info-feedback window popdown
          set:x:/../*/sys42.confirm-action/*/_onok/*/sys42.info-feedback/0?value
            src:x:/../*/get-session/*/*/type?value
          set:x:/../*/sys42.confirm-action/*/_onok/*/sys42.info-feedback/1?value
            src:x:/../*/split/0/-?name

          // Showing "Confirmation Modal Dialog" to have user confirm he wants to delete file or folder
          sys42.confirm-action
            _header
            _message
            _onok

              // This becomes an Active Event invocation for either [delete-file] or [delete-folder] with
              // path to folder/file in value
              foo-bar

              // Making sure we delete both copied/cut object, and currently selected object
              set-session:sys42.copied-file-browser-object
              set-session:sys42.selected-file-browser-object

              // Refreshing toolbar
              sys42.update-file-browser-toolbar

              // Re-creating current folder's content
              sys42.create-current-folder-content

              // Showing popup to user, informing him that action was successful
              sys42.info-feedback:@"{0} '{1}' was succesfully deleted"
                :
                :


        /*
         * "New folder" button clicked
         */
        sys42.new-folder-button-clicked

          // Setting focus to "New folder" textbox
          send-javascript:@"$('#new-folder-name').focus().select();"

          // Creating "New folder" textbox widget
          create-literal-widget:new-folder-name
            element:input
            type:text
            parent:file-browser-current-item-toolbar
            position:7
            class:form-control
            style:"float:left;width:250px;border-radius:0;border-left:none;border-right:none;"
            value
            placeholder:New folder name ...
            title:New folder name
            onkeypress:"return p5.textBoxKeyPress(event, 'new-folder-name');"
            onkeyup:"p5.textBoxKeyUp(event, 'new-folder-name');"
            onblur
              delete-widget:new-folder-name
            onenter_
              sys42.create-folder
            onesc_
              delete-widget:new-folder-name


            /*
             * Lambda events for "Create folder textbox"
             */
            events


              /*
               * Creates new folder, takes no arguments
               */
              sys42.create-folder
                get-session:sys42.current-folder
                get-widget-property:new-folder-name
                  value
                create-folder:{0}{1}/
                  :x:/../*/get-session/*?value
                  :x:/../*/get-widget-property/*/*?value

                // Removing "Create new folder" textbox
                delete-widget:new-folder-name

                // Making sure new folder is selected
                set:x:/+/0/*/object?value
                  src:x:/../*/create-folder/*?name
                set-session:sys42.selected-file-browser-object
                  src
                    type:folder
                    object

                // Listing current folder's folders and files again, and showing visual clues for
                // selected item
                sys42.create-current-folder-content
                sys42.create-selected-object-clues:x:/../*/create-folder/*?name

                // Refreshing toolbar
                sys42.update-file-browser-toolbar

                // Showing feedback to user that folder was created
                sys42.info-feedback:@"Folder '{0}' was succesfully created"
                  :x:/../*/get-widget-property/*/*?value


        /*
         * "New file" button clicked
         */
        sys42.new-file-button-clicked

          // Setting focus to "New folder" textbox
          send-javascript:@"$('#new-file-name').focus().select();"

          // Creating "New file" textbox widget
          create-literal-widget:new-file-name
            element:input
            type:text
            parent:file-browser-current-item-toolbar
            position:8
            class:form-control
            style:"float:left;width:250px;border-radius:0 4px 4px 0;border-left:none;"
            value
            placeholder:New file name ...
            title:New file name
            onkeypress:"return p5.textBoxKeyPress(event, 'new-file-name');"
            onkeyup:"p5.textBoxKeyUp(event, 'new-file-name');"
            onblur
              delete-widget:new-file-name
            onenter_
              sys42.create-file
            onesc_
              delete-widget:new-file-name


            /*
             * Lambda events for "Create file textbox"
             */
            events


              /*
               * Creates new file, takes no arguments
               */
              sys42.create-file
                get-session:sys42.current-folder
                get-widget-property:new-file-name
                  value
                save-file:{0}{1}
                  :x:/../*/get-session/*?value
                  :x:/../*/get-widget-property/*/*?value

                // Removing "Create new file" textbox
                delete-widget:new-file-name

                // Making sure new file is selected
                set:x:/+/0/*/object?value
                  src:x:/../*/save-file?value
                set-session:sys42.selected-file-browser-object
                  src
                    type:file
                    object

                // Listing current folder's folders and files again, and showing visual clues for
                // selected item
                sys42.create-current-folder-content
                sys42.create-selected-object-clues:x:/../*/save-file?value

                // Refreshing toolbar
                sys42.update-file-browser-toolbar

                // Showing feedback to user that file was created
                sys42.info-feedback:@"File '{0}' was succesfully created"
                  :x:/../*/get-widget-property/*/*?value


    /*
     * Bread crumb button group, allowing user to go "back" to where he cam from
     * in the folder hierarchy
     */
    container:file-browser-bread-crumb
      class:btn-group prepend-bottom


    /*
     * Actual folders and files content, showing the "current folder"
     */
    container:file-browser-current-folder-content
      class:prepend-top file-browser-content-surface


    /*
     * Wrapper to load a file-editor, for editing stuff like Hyperlisp files,
     * CSS files, or JavaScript files, etc. No arguments
     */
    container:file-browser-file-editor


  /*
   * Lambda events for the main folder browser widget
   */
  events


    /*
     * Updates the toolbar, showing and hiding items according to
     * whether or not there are any objects selected, etc
     */
    sys42.update-file-browser-toolbar

      // Figuring out if there are any objects currently selected
      get-session:sys42.selected-file-browser-object
      _widgets
        file-browser-copy-button
        file-browser-cut-button
        file-browser-rename-button
        file-browser-delete-button
      if:x:/../*/get-session/*/*

        // There is currently an object active in file browser surface
        delete-widget-property:x:/../*/_widgets/*?name
          disabled

        // Special treatment for download button, since it can't download folders
        if:x:/../*/get-session/*/*/type?value
          =:file
          delete-widget-property:file-browser-download-button
            disabled
        else
          set-widget-property:file-browser-download-button
            disabled
      else

        // There are currently no objects active in file browser surface
        set-widget-property:x:/../*/_widgets/*?name
          disabled

        // Special treatment for download button, since it cannot download folders
        set-widget-property:file-browser-download-button
          disabled

      // Setting "Paste" button to enabled, or disabled, depending
      // upon whether or not there's an object in "clipboard" or not
      get-session:sys42.copied-file-browser-object
      if:x:/-/*/*
        delete-widget-property:file-browser-paste-button
          disabled
      else
        set-widget-property:file-browser-paste-button
          disabled


    /*
     * Shows the bread-crumb for the file browser. Takes no arguments
     */
    sys42.create-folder-bread-crumb

      // Deleting previously created bread-crumb
      clear-widget:file-browser-bread-crumb

      // Getting current folder
      get-session:sys42.current-folder

      // Getting "root folder"
      get-session:sys42.root-directory

      // Splitting path up in folder components
      split:x:/../*/get-session/[0,1]/*?value
        =:/

      // Creating bread-crumb home button widget part
      if:x:/../*/get-session/[0,1]/*?value
        not
        or:x:/../*/get-session/[0,1]/*?value
          =:x:/../*/get-session/[1,2]/*?value
        add:x:/../*/create-widget
          src:disabled

      // Actually creating widget
      create-widget
        element:button
        parent:file-browser-bread-crumb
        class:btn btn-default
        title:Home
        onclick

          // Figuring out where user came from, such that we can make
          // that path selected, therefor we need to retrieve where user came from
          get-session:sys42.current-folder

          // Making sure user can never go beneath "root access folder"
          get-session:sys42.root-directory
          sys42.change-current-folder:x:/../*/get-session/[1,2]/*?value

          // Figuring out where user came from
          // PS, this looks REALLY weird if you inspect it with an admin user, but works fo reasons which makes
          // me freaking LOOOOVE p5.lambda expressions ... ;) (Hint; splitting upon an empty string)
          split:x:/../*/get-session/[0,1]/*?value
            =:x:/../*/get-session/[1,2]/*?value
          split:x:/-/*?name
            =:/
          set:x:/../*/set-session/*/*/object?value
            src:{0}{1}/
              :x:/../*/get-session/[1,2]/*?value
              :x:/../*/split/[1,2]/0?name

          // Setting selected folder to path were we came from
          set-session:sys42.selected-file-browser-object
            src
              type:folder
              object
          sys42.create-selected-object-clues:x:/-/*/*/object?value

          // Refreshing toolbar
          sys42.update-file-browser-toolbar

        widgets
          literal
            class:glyphicon glyphicon-home
            element:span

      // Looping through each folder leading to current path, 
      // building "path" as we go along
      _path:
      for-each:x:/../*/split/*

        // Updating "_path" above
        set:x:/../*/_path?value
          src:{0}{1}/
            :x:/../*/_path?value
            :x:/..for-each/*/__dp/#?name

        // Checking that this folder is beneath "main root access folder", and also NOT actually
        // THE "main root access folder" (since it's handled by "Home" button)
        if:x:/../*/_path/"=~{0}"
          :x:/../*/get-session/[1,2]/*?value
          and:x:/../*/_path/"=~{0}"?value
            :x:/../*/get-session/[1,2]/*?value
            !=:x:/../*/get-session/[1,2]/*?value

          // Creating a button for each folder leading to current folder
          set:x:/..if/*/create-literal-widget/*/innerValue?value
            src:x:/..for-each/*/__dp/#?name
          set:x:/..if/*/create-literal-widget/*/onclick/*/sys42.change-current-folder?value
            src:x:/../*/_path?value

          // Checking if this is last bread-crumb button, and if so, disabling it
          if:x:/../*/_path?value
            =:x:/../*/get-session/[0,1]/*?value
            add:x:/..if/..if/*/create-literal-widget
              src:disabled

          // Making sure that when bread-crumb is clicked, the folder in the path
          // we came from becomes selected
          set:x:/..if/*/create-literal-widget/*/onclick/*/set-session/*/*/object?value
            src:{0}{1}/
              :x:/../*/_path?value
              :x:/..for-each/*/__dp/#/>?name

          // Creating bread-crumb button widget part, which now should be populated 
          // dyanmically according to above logic
          create-literal-widget
            element:button
            parent:file-browser-bread-crumb
            class:btn btn-default
            innerValue
            onclick

              // Changing current folder, folder applied above
              sys42.change-current-folder

              // Making sure path were we came from is selected folder, and updating
              // toolbar and visual clues
              set-session:sys42.selected-file-browser-object
                src
                  type:folder
                  object
              sys42.create-selected-object-clues:x:/-/*/*/object?value
              sys42.update-file-browser-toolbar


    /*
     * Creates a list of all folders and files from current folder,
     * for user to browse through
     */
    sys42.create-current-folder-content

      // Clearing previously created folders and files
      clear-widget:file-browser-current-folder-content

      // Listing folders first
      sys42.create-folders

      // Listing files afterwards
      sys42.create-files


    /*
     * List all folders from current folder
     */
    sys42.create-folders

      // Retrieving current folder
      get-session:sys42.current-folder

      // Retrieving selected object
      get-session:sys42.selected-file-browser-object

      // Looping through each folder, creating a widget for each of them
      list-folders:x:/../*/get-session/[0,1]/*?value
      for-each:x:/../*/list-folders/*?name

        // Removing trailing "/" and setting innerValue and title
        split:x:/..for-each/*/__dp?value
          =:/
        set:x:/..for-each/*/create-literal-widget/*(/innerValue|/title)?value
          src:x:/..for-each/*/split/0/-?name

        // Making sure event handler for widget is correctly wired up
        set:x:/..for-each/*/create-literal-widget/*/onclick/0?value
          src:x:/..for-each/*/__dp?value

        // Actually creating folder object widget
        create-literal-widget
          parent:file-browser-current-folder-content
          class:file-object folder
          innerValue
          title
          onclick
            sys42.folder-clicked


    /*
     * List all files from current folder
     */
    sys42.create-files

      // Retrieving current folder
      get-session:sys42.current-folder

      // Retrieving selected object
      get-session:sys42.selected-file-browser-object

      // Looping through each folder, creating a widget for each of them
      list-files:x:/../*/get-session/[0,1]/*?value
      for-each:x:/../*/list-files/*?name

        // Removing trailing "/" and setting innerValue
        split:x:/..for-each/*/__dp?value
          =:/
        set:x:/..for-each/*/create-literal-widget/*(/innerValue|/title)?value
          src:x:/..for-each/*/split/0/-?name

        // Making sure event handler for widget is correctly wired up
        set:x:/..for-each/*/create-literal-widget/*/onclick/0?value
          src:x:/..for-each/*/__dp?value

        // Actually creating folder object widget
        create-literal-widget
          parent:file-browser-current-folder-content
          class:file-object file
          innerValue
          title
          onclick
            sys42.file-clicked


    /*
     * Bread-crumb button was clicked. Takes the path to
     * the new folder as an argument
     */
    sys42.change-current-folder

      // Updating current folder
      set-session:sys42.current-folder
        src:x:/../*/_arg?value

      // Re-creating bread-crumb
      sys42.create-folder-bread-crumb

      // Listing current folder's folders and files again
      sys42.create-current-folder-content


    /*
     * Invoked when a folder widget was clicked. Takes path to
     * folder that is clicked as input argument
     */
    sys42.folder-clicked

      // Destroying any existing file object editors
      sys42.destroy-file-editor

      // Checking if folder is already selected
      get-session:sys42.selected-file-browser-object
      if:x:/../*/get-session/*/*/type?value
        =:folder
        and:x:/../*/get-session/*/*/object?value
          =:x:/../*/_arg?value

        // Clicked folder was already selected, hence opening it, 
        // and destroying any existing object toolbars
        sys42.change-current-folder:x:/../*/_arg?value

        // Removing selected file/folder object
        set-session:sys42.selected-file-browser-object

        // Refreshing toolbar
        sys42.update-file-browser-toolbar

      else

        // Clicked folder was not selected, hence we select it
        set:x:/+/*/*/object?value
          src:x:/../*/_arg?value
        set-session:sys42.selected-file-browser-object
          src
            type:folder
            object
        sys42.create-selected-object-clues:x:/../*/_arg?value

        // Refreshing toolbar
        sys42.update-file-browser-toolbar


    /*
     * Invoked when a file widget was clicked. Takes path to
     * file that is clicked as input argument
     */
    sys42.file-clicked

      // Checking if file is already selected
      get-session:sys42.selected-file-browser-object
      if:x:/../*/get-session/*/*/type?value
        =:file
        and:x:/../*/get-session/*/*/object?value
          =:x:/../*/_arg?value

        // File was already selected, opening up file editor
        sys42.create-file-editor:x:/../*/get-session/*/*/object?value
      else

        // Clicked file was not selected, hence we select it, and update view
        set:x:/+/*/*/object?value
          src:x:/../*/_arg?value
        set-session:sys42.selected-file-browser-object
          src
            type:file
            object

        // Destroying any existing editors, since selected object changed
        sys42.destroy-file-editor

        // Showing selected object clues
        sys42.create-selected-object-clues:x:/../*/_arg?value

        // Refreshing toolbar
        sys42.update-file-browser-toolbar


    /*
     * Selects a file/folder object on screen, giving visual clues to user
     * Takes filename as argument
     */
    sys42.create-selected-object-clues
      get-children-widgets:file-browser-current-folder-content
      get-widget-property:x:/-/*/*?value
        class
        innerValue
      split::x:/-/*/*/"=~ active-file-object"?value
        =:" active-file-object"
      set-widget-property:x:/-2/*/*/class/"=~ active-file-object"/.?name
        class:x:/./-/0?name
      split:x:/../*/_arg?value
        =:/
      set-widget-property:x:/-4/*/*/innerValue/"={0}"/.?name
        :x:/./-/0/-?name
        class:{0} active-file-object
          :x:/././-4/*/*/innerValue/"={0}"/./*/class?value
            :x:/./././-/0/-?name


    /*
     * Creates the "Edit file editor widget" for editing files, if possible
     * Takes filename as argument
     */
    sys42.create-file-editor

      // Deleting any previous editors
      clear-widget:file-browser-file-editor

      // Retrieving selected file, and opening up editor
      set:x:/+/0?value
        src:x:/../*/_arg?value
      sys42.execute-lisp-file:system42/admin/file-editors/launch.hl
        _file
        _parent:file-browser-file-editor

      // Storing original CSS classes in session for all widgets we're hiding
      _widgets
        file-browser-bread-crumb
        file-browser-current-folder-content
        file-browser-current-item-toolbar
      get-widget-property:x:/-/*?name
        class
      set-widget-property:x:/-2/*?name
        class:hide
      set-session:sys42.original-file-editor-classes
        src:x:/./-2


    /*
     * Destroys any "Edit file editor widgets"
     */
    sys42.destroy-file-editor

      // Destroying editor Widget
      clear-widget:file-browser-file-editor

      // Restoring CSS classes for all widgets that were hidden
      // when editor was opened
      get-session:sys42.original-file-editor-classes
      for-each:x:/-/*/*/*
        set-widget-property:x:/./*/__dp/#?name
          class:x:/././*/__dp/#/*?value

      // Removing CSS classes from widgets hidden out of session
      set-session:sys42.original-file-editor-classes

      // Changing header back to what it was
      set-widget-property:header
        innerValue:Files


/*
 * Including helper JavaScript for keypress DOM events
 */
include-javascript:@"
p5.textBoxKeyPress = function(e, control) {
  if(e.keyCode == 13) {
    p5.$(control).raise('onenter_');
    return false;
  }
};
p5.textBoxKeyUp = function(e, control) {
  if(e.keyCode == 27) {
    p5.$(control).raise('onesc_');
    return false;
  }
};"
