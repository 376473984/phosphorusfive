
/*
 * template for showing [content] types of pages from the database'
 * if you pass in ?showgrid=true as HTTP GET parameter, this template will
 * show the blueprint.css grid to help you align your objects correctly
 */

// setting the [header] and [content] of our page object from our database
set:x:/../*/"create-widget"/*/widgets/**/={header}?value
  src:x:/../*/_args/*/header?value
set:x:/../*/"create-widget"/*/widgets/**/={content}?value
  src:x:/../*/_args/*/content?value

// making sure the "showgrid" property is handled correctly, if we should handle it
if:x:/../*/_args/*/showgrid?value
  equals:true
  set:x:/../*/create-widget/*/class?value
    src:{0} showgrid
      :x:/../*/create-widget/*/class?value


/*
 * checking to see if user is logged in, and if so, we remove the login widget from the page, and replace
 * it with a logout widget
 */
sys42.get-user
if:x:/-/*?count
  more-than:int:0

  // user is logged in with persistent cookie, removing "login widget", and replacing it with "logout widget"
  set:x:/../**/container/=rhs/*/widgets/*
  _logout-controls
    literal:logout
      element:button
      class:span-2 last
      innerValue:logout
      onclick
        sys42.logout-user
  add:x:/../**/container/=rhs/*/widgets
    src:x:/./-/*


/*
 * creating our menu, from all pages in CMS, and adding into the [container:menu] parts of our create-widget Active Event below,
 * but only the pages the user has access to according to his role
 */
select-data:x:/*/*/p5.page
for-each:x:/-/*

  // checking to see if user has access to page
  _go:bool:true
  if:x:/./*/__dp/#/*/role?value
    if:x:/..for-each/*/__dp/#/*/role?value
      not-equals:x:/../*/sys42.get-user/*/role?value
      set:x:/..for-each/*/_go?value
        src:bool:false

  // menu item "template"
  _menu-item
    literal
      element:a
      class:span-4 last
      has-id:false
      innerValue
      href

  // adding menu item, if we're supposed to
  if:x:/./*/_go?value
    equals:bool:true
    set:x:/./-/*/literal/*/innerValue?value
      src:x:/..for-each/*/__dp/#/*/name?value
    set:x:/./-/*/literal/*/href?value
      src:x:/..for-each/*/__dp/#?value
    add:x:/../*/create-widget/*/widgets/**/container/=menu/*/widgets
      src:x:/././-/0


/*
 * creating our main widget, showing the webpage, now modified accordingly to the [_args] given
 */
create-widget:template
  class:main span-24 last
  widgets
    container
      has-id:false
      class:span-20
      widgets
        literal:header
          element:h1
          class:span-16 last header prepend-top prepend-4
          innerValue:{header}
        container:menu
          element:div
          class:span-4 menu
          widgets
        literal:content
          element:div
          class:span-16 last content
          innerValue:{content}
    container:rhs
      element:div
      class:span-4 last prepend-top
      widgets
        literal
          class:span-4 last
          has-id:false
          innerValue:username
          element:label
        void:username
          class:span-4 last
          element:input
          type:text
          placeholder:username ...
          onkeypress-script:"if (event.keyKode == 13) {p5.$('login').raise('onclick2');return false;}"
        literal
          class:span-4 last
          has-id:false
          innerValue:username
          element:label
        void:password
          class:span-4 last
          element:input
          type:password
          placeholder:password ...
          onkeypress-script:"if (event.keyKode == 13) {p5.$('login').raise('onclick2');return false;}"
        literal:error
          class:span-4 last error
          innerValue:wrong credentials
          style:"display:none;"
        container
          class:span-2 prepend-2 last
          widgets
            literal:login
              element:button
              class:span-2 last
              innerValue:login
              onclick-script:"p5.$('login').raise('onclick2');event.preventDefault();"
              onclick2
                get-widget-property:username
                  value
                get-widget-property:password
                  value
                set:x:/+2/*/username?value
                  src:x:/././*/get-widget-property/[0,1]/+/<?value
                set:x:/+1/*/password?value
                  src:x:/././*/get-widget-property/[1,2]/+/<?value
                sys42.login-user
                  username
                  password
                if:x:/-/*/success?value
                  equals:bool:false
                  set-widget-property:error
                    style:
    container:footer
      element:div
      class:span-24 footer
      widgets
        literal
          element:p
          has-id:false
          innerValue:@"System42 is a <a href=""http://phosphorusfive.wordpress.com"">Phosphorus Five</a>
MIT/Free Software production, and copyright Â© Thomas Hansen - phosphorusfive@gmail.com"
          class:prepend-4 span-20 last quiet small


