
/*
 * Template for showing any type of page
 */

/*
 * Checking to see if user is logged in. Needed for parts of logic below
 */
sys42.get-user


/*
 * Creating our menu, from all pages in database, and adding into the 
 * [container:menu] parts of our create-widget Active Event below.
 * But only the pages the user has access to according to his role
 */
select-data:x:/*/*/p5.page
for-each:x:/-/*

  // Menu item "template"
  _menu-item
    container
      element:li
      widgets
        literal
          element:a
          has-id:false
          innerValue
          href

  // Verifying user has access to page object
  if
    sys42.verify-access:x:/..for-each/*/__dp/#/*/role?value
    set:x:/./-/**/literal/*/innerValue?value
      src:x:/..for-each/*/__dp/#/*/name?value
    set:x:/./-/**/literal/*/href?value
      src:x:/..for-each/*/__dp/#?value
    add:x:/../*/create-widget/*/widgets/**/container/=menu/*/widgets
      src:x:/././-/0


/*
 * Adding login/logout button to navbar
 */
add:x:/../*/create-widget/*/widgets/**/container/=menu/*/widgets
  src
    container
      element:li
      widgets
        literal:login-logout-btn
          element:a
          innerValue:Login
          href:#
          onclick:"return p5.showLogin();"


/*
 * Modifying navbar Login button to Logout, if user is logged in
 */
if:x:/../*/sys42.get-user/*

  // User is logged in, changing "Login" to "Logout"
  set:x:/../*/create-widget/**/literal/=login-logout-btn/*/innerValue?value
    src:Logout
  set:x:/../*/create-widget/**/literal/=login-logout-btn/*/onclick?value
  add:x:/../*/create-widget/**/literal/=login-logout-btn/*/onclick
    src
      sys42.logout-user
else
  include-javascript:@"
$('#login-logout-btn').on('click', function(){
    $('.navbar-toggle').click()
});"
    


/*
 * Creating our main template webpage
 */
create-widget:template-root
  class:container
  widgets

    /*
     * Our main bootstrap navbar menu, stuck to the top
     */
    container:menu-wrapper
      element:nav
      class:navbar navbar-default navbar-fixed-top navbar-inverse
      widgets
        container
          has-id:false
          class:container-fluid
          widgets
            container
              has-id:false
              class:navbar-header
              widgets
                literal
                  has-id:false
                  element:button
                  type:button
                  class:navbar-toggle collapsed
                  data-toggle:collapse
                  data-target:#navbar-main
                  aria-expanded:false
                  innerValue:"<span class=\"icon-bar\"></span>"
                literal
                  has-id:false
                  element:a
                  href:"http://phosphorusfive.wordpress.com"
                  class:navbar-brand navbar-icon-p5
                  title:A Phosphorus Five production!
            container:navbar-main
              class:collapse navbar-collapse
              widgets
                container:menu
                  has-id:false
                  element:ul
                  class:nav navbar-nav
                  widgets


    /*
     * Page content starts here, header and login/logout wrapper first
     */    
    container
      has-id:false
      class:row
      widgets
        container
          has-id:false
          class:col-md-7 col-xs-9
          widgets

            /*
             * Header of page
             */
            literal:header
              has-id:false
              element:h1
              innerValue:{header}


        /*
         * Login/logout widgets and helpers
         */
        container:login-wrapper
          class:form-inline col-md-5 hidden col-md-push-1 col-xs-3
          widgets
            container:login-controls
              class:form-group
              widgets
                void:username
                  class:form-control input-sm
                  element:input
                  type:text
                  placeholder:Username ...
                  onkeypress:"return p5.keyPressed(event);"
                  onkeyup:"p5.keyUp(event);"
                void:password
                  class:form-control input-sm
                  element:input
                  type:password
                  placeholder:Password ...
                  onkeypress:"return p5.keyPressed(event);"
                  onkeyup:"p5.keyUp(event);"
                literal:login
                  element:button
                  class:btn btn-default form-control input-sm
                  innerValue:login
                  onclick
                    get-widget-property:username
                      value
                    get-widget-property:password
                      value
                    set:x:/+2/0/*/_username?value
                      src:x:/././*/get-widget-property/[0,1]/+/<?value
                    set:x:/+1/0/*/_password?value
                      src:x:/././*/get-widget-property/[1,2]/+/<?value
                    if
                      sys42.login-user
                        _username
                        _password

                      // Reloading page to make sure everything is wired up together correctly
                      send-javascript:"window.location.replace(window.location.pathname);"
                    else

                      // Oops, wrong credentials, or something
                      set-widget-property:login-controls
                        class:form-group has-error
                      send-javascript:"p5.wrongCredentials();"


    /*
     * Actual dynamic content comes here, after navbar menu at top, 
     * and header + login/logout widgets
     */
    container:content
      class:row content-row
      widgets
        literal
          element:div
          class:col-xs-12
          innerValue:{content}


    /*
     * Copyright notice, and general (muted) information here
     */
    container:footer
      class:row
      widgets
        literal
          element:small
          class:col-md-6 text-muted text-center col-md-push-3 prepend-top col-xs-12
          innerValue:@"Liberated Software, Copyleft Â© <a href=""https://phosphorusfive.wordpress.com/2015/11/22/liberated-software-defined/"">Thomas Hansen</a>"


/*
 * Simple inline JavaScript functions, 
 * referenced in event handlers for widgets above
 */
include-javascript:@"
p5.showLogin = function() {
  $('#login-wrapper').toggleClass('hidden');
  $('#show-login-wrapper').toggleClass('hidden');
  $('#username').focus().select();
  return false;
}
p5.keyPressed = function(e) {
  if(e.keyCode == 13) {
    p5.$('login').raise('onclick');
    return false;
  }
}
p5.keyUp = function(e) {
  if(e.keyCode == 27) {
    $('#login-wrapper').toggleClass('hidden');
    $('#show-login-wrapper').toggleClass('hidden');
  }
}
p5.wrongCredentials = function() {
  $('#login-wrapper').effect('shake');
  $('#username').focus().select();
};"


