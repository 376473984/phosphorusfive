
/*
 * Template for showing any type of page
 */

/*
 * Checking to see if user is logged in, and if so, we remove the login 
 * widget from the page, and replace it with a logout widget.
 */
sys42.get-user
if:x:/-/*?count
  more-than:int:0

  // User is logged in with persistent cookie
  set:x:/../**/container/=login-wrapper
  set:x:/../**/container/=show-login-controls-wrapper/*/widgets/*
  _logout-controls
    literal:logout
      element:button
      class:btn btn-default
      innerValue:logout
      onclick
        sys42.logout-user
  add:x:/../**/container/=show-login-controls-wrapper/*/widgets
    src:x:/./-/*


/*
 * Creating our menu, from all pages in database, and adding into the 
 * [container:menu] parts of our create-widget Active Event below.
 * But only the pages the user has access to according to his role
 */
select-data:x:/*/*/p5.page
for-each:x:/-/*

  // Checking to see if user has access to page
  _go:true
  if:x:/./*/__dp/#/*/role?value
    if:x:/..for-each/*/__dp/#/*/role?value
      not-equals:x:/../*/sys42.get-user/*/role?value
      set:x:/..for-each/*/_go?value
        src:false

  // menu item "template"
  _menu-item
    container
      element:li
      widgets
        literal
          element:a
          class:span-4 last
          has-id:false
          innerValue
          href

  // adding menu item, if we're supposed to
  if:x:/./*/_go?value.bool
    set:x:/./-/**/literal/*/innerValue?value
      src:x:/..for-each/*/__dp/#/*/name?value
    set:x:/./-/**/literal/*/href?value
      src:x:/..for-each/*/__dp/#?value
    add:x:/../*/create-widget/*/widgets/**/container/=menu/*/widgets
      src:x:/././-/0


/*
 * creating our main widget, showing the webpage, now modified accordingly to the [_args] given
 */
create-widget:template
  class:container
  widgets
    container:menu-wrapper
      element:nav
      class:navbar navbar-default navbar-fixed-top
      widgets
        container
          class:container-fluid
          widgets
            container
              class:navbar-header
              widgets
                literal
                  element:button
                  type:button
                  class:navbar-toggle collapsed
                  data-toggle:collapse
                  data-target:#navbar-main
                  aria-expanded:false
                  innerValue:"<span class=\"icon-bar\"></span>"
                literal
                  element:a
                  href:"http://phosphorusfive.wordpress.com"
                  innerValue:P5
                  class:navbar-brand
                  title:A Phosphorus Five production!
            container:navbar-main
              class:collapse navbar-collapse
              widgets
                container:menu
                  element:ul
                  class:nav navbar-nav
                  widgets
    container
      has-id:false
      class:row
      widgets
        container
          has-id:false
          class:col-xs-8
          widgets
            literal:header
              element:h1
              class:col-xs-12
              innerValue:{header}
        container:show-login-controls-wrapper
          class:col-xs-1 col-xs-push-3 prepend-top
          widgets
            literal
              element:button
              class:btn button-default
              innerValue:Login
              onclick
                set-widget-property:show-login-controls-wrapper
                  class:hidden col-xs-2 col-xs-push-2 prepend-top
                set-widget-property:login-wrapper
                  class:form-inline col-xs-4 prepend-top show
                include-javascript:@"var el = p5.$('username').el;el.focus();el.select();"
        container:login-wrapper
          class:form-inline col-xs-4 prepend-top hidden
          widgets
            container
              has-id:false
              class:form-group
              widgets
                void:username
                  class:form-control input-sm
                  element:input
                  type:text
                  placeholder:Username ...
                void:password
                  class:form-control input-sm
                  element:input
                  type:password
                  placeholder:Password ...
                literal:login
                  element:button
                  class:btn btn-default form-control input-sm
                  innerValue:login
                  onclick
                    get-widget-property:username
                      value
                    get-widget-property:password
                      value
                    set:x:/+2/*/username?value
                      src:x:/././*/get-widget-property/[0,1]/+/<?value
                    set:x:/+1/*/password?value
                      src:x:/././*/get-widget-property/[1,2]/+/<?value
                    sys42.login-user
                      username
                      password
                    if:x:/-/*/success?value
                      equals:bool:false
                      set-widget-property:username
                        class:form-control input-sm text-danger
    container:content
      class:row
      widgets
        literal
          element:div
          class:col-xs-12
          innerValue:{content}
    container:footer
      class:row
      widgets
        literal
          element:small
          class:col-xs-8 text-muted col-xs-push-2 prepend-top
          innerValue:@"A <a href=""http://phosphorusfive.wordpress.com"">Phosphorus Five</a>
Free Software product, licensed under the terms of the MIT license, copyright Â© <a href=""mailto:phosphorusfive@gmail.com"">Thomas Hansen</a>"


