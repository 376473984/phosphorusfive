
/*
 * Executed after server has been initially installed. Guides the 
 * user through setting up a secure root password, etc
 */


/*
 * Loading up main template
 */
load-file:/system42/apps/cms/templates/main.hl

// Removing {content} and setting {header} before evaluating template
set:x:/-/*/*/create-widget/*/widgets/**/={header}?value
  src:Installation Guide
set:x:/-2/*/*/create-widget/*/widgets/**/={content}/.
eval:x:/./*/load-file/*

/*
 * Creating actual guide form
 */
create-widget:guide
  parent:content
  class:col-xs-12
  widgets


    /*
     * Marvin image
     */
    literal
      element:img
      style:"float:right;"
      src:/media/images/marvin-headshot.png


/*
 * Checking if user is logged in, and that there are more than 0 users in the system,
 * to determine what to do, and which GUI to show user
 */
whoami
list-users
if:x:/-/*?count
  =:int:0

  if
    select-data:x:/*/*/server-salt?count
    =:int:0

    // Need to create server dynamic salt
    sys42.execute-lisp-file:/system42/installation/create-dynamic-salt.hl
  else

    // Need to set root password, and do other types of installation setup things
    sys42.execute-lisp-file:/system42/installation/set-root-password.hl
else-if:x:/../*/whoami/*/role?value
  =:root

  // Possibly a refresh of page, or previously abandoned setup installation process
  // Continuing from where we left off, which is creating initial non-root user
  sys42.execute-lisp-file:/system42/installation/create-initial-user.hl
