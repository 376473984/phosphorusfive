
/*
 * Executed after server has been initially installed. Guides the 
 * user through setting up a secure root password, etc
 */


/*
 * Creating actual guide form
 */
create-widget:set-server-salt
  parent:guide
  class:flash-from-left
  widgets


    /*
     * First paragraph of informational text
     */
    literal
      element:blockquote
      innerValue:@"<em>""Welcome to System42. I am Marvin, and I will be your guide. The first thing you will have to do, is to create
a <strong>random server salt</strong>, used to make it more difficult to hack your system. Provide a random set of characters in the
form below. You do not have to remember these characters, and you should NOT in fact write them down! Try to create this with at least
100 characters, using all different types of characters, such as $%&123*@()[/]{} and so on!""</em>"


    /*
     * Root password container wrapper
     */
    container
      class:col-xs-9 form-group
      widgets
        literal:server-salt
          element:textarea
          class:form-control
          rows:10
          placeholder:Server salt ...
          oninit
            send-javascript:@"$('#server-salt').focus().select();"
        literal:update-salt
          element:button
          class:col-xs-2 col-xs-push-10 btn btn-primary
          innerValue:Save
          onclick
            get-widget-property:server-salt
              value
            eval-x:x:/+/0/0/0
            _evt
              create-protected-event:p5.security.get-dynamic-password-salt
                set:x:/..?value
                  src:x:/../*/get-widget-property/*/*?value
            save-file:/system42/startup/internals/server-salt.hl
              src:x:/../*/_evt/*
            eval:x:/../*/_evt
            delete-widget:set-server-salt
            sys42.execute-lisp-file:/system42/installation/set-root-password.hl

