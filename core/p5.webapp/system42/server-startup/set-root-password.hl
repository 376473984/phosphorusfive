
/*
 * Executed after server has been initially installed. Guides the 
 * user through setting up a secure root password, etc
 */


/*
 * Creating actual guide form
 */
create-widget:set-root-pwd-tab
  parent:guide
  class:flash-from-right
  widgets


    /*
     * First paragraph of informational text
     */
    literal
      element:blockquote
      innerValue:@"<em>""Welcome to System42. I am Marvin, and I will be your guide. The first thing you will have to do, is to <strong>create a secure
root password</strong>. Your 'root' account is the administrator account for your system, and if this password is compromised, or not
strong enough, then anybody able to guess your password, or getting hold of it somehow, will have complete control over your server.""</em>"


    /*
     * Root password container wrapper
     */
    container
      class:form-inline
      widgets
        container
          class:form-group
          widgets
            container
              class:input-group
              widgets
                literal
                  class:input-group-addon
                  innerValue:Root
                literal:root-password
                  element:input
                  type:password
                  class:form-control
                  placeholder:Root password ...
                  oninit
                    send-javascript:@"$('#root-password').focus().select();"
                  onkeypress:"return p5.keyInstallPressed(event);"
            container
              class:input-group prepend-left
              widgets
                literal
                  class:input-group-addon
                  innerValue:Repeat
                literal:root-password-repeat
                  element:input
                  type:password
                  class:form-control
                  placeholder:Repeat ...
                  onkeypress:"return p5.keyInstallPressed(event);"
            literal:update-root
              element:button
              class:btn btn-primary
              innerValue:Save
              onclick
                _widgets
                  root-password
                  root-password-repeat
                get-widget-property:x:/-/*?name
                  value
                if:x:/-/*/root-password/*?value
                  !=:x:/./-/*/root-password-repeat/*?value
                  sys42.info-feedback:Sorry, passwords don't match
                    _error:true
                  send-javascript:@"$('#root-password').focus().select();"
                else
                  set:x:/+/*?value
                    src:x:/../*/get-widget-property/*/root-password/*?value
                  p5.web.set-root-password
                    password

                  // Logging in root user until we have created our "superuser" account
                  set:x:/+/*/password?value
                    src:x:/../*/get-widget-property/*/root-password/*?value
                  login
                    username:root
                    password
                  delete-widget:set-root-pwd-tab
                  sys42.execute-lisp-file:system42/server-startup/create-initial-user.hl

include-javascript:@"
p5.keyInstallPressed = function(e) {
  if(e.keyCode == 13) {
    p5.$('update-root').raise('onclick');
    return false;
  }
}"

