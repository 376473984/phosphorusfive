
/*
 * Creates the Active Event that allows you to execute Hyperlisp p5.lambda files. the Hyperlisp
 * file you wish to execute is given as value of [sys42.execute-lisp-file], and all nodes benath
 * [sys42.execute-lisp-file] will be passed into your p5.lambda file, underneath [_args], and
 * accessible as parameters from inside your file. File to exeute can be given either as an
 * expression, or a constant. Multiple files can be executed at the same time, by providing an
 * expression that leads to multiple results.
 */
set-event:sys42.execute-lisp-file
  load-file:x:/../*/_arg?value
  add:x:/+
    src:x:/../*/_args
  eval:x:/-2/*


/*
 * Executes all files within given directory recursively, assuming they're Hyperlisp p5.lambda files. 
 * The directory you wish to execute the files within, is given through the value of the 
 * [sys42.execute-lisp-folder] node, which might be either an expression or a constant.
 */
set-event:sys42.execute-lisp-folder
  list-files:x:/../*/_arg?value
  sys42.execute-lisp-file:x:/-/*?value
  list-folders:x:/../*/_arg?value
  if:x:/-/*
    sys42.execute-lisp-folder:x:/./-/*?value


/*
 * After creation of the above Active Events, this file will execute all
 * files in the "system42/startup-files" folder recursively, by using the Active
 * Events above.
 */
sys42.execute-lisp-folder:system42/startup-files
