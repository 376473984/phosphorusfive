/*
 * Editor for [controls] types of pages
 */

/*
 * Loading page from database
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_id?value


/*
 * Setting the content of WYSIWYG surface to the [controls] from database,
 * converting to lambda from Hyperlisp first
 */
lisp2lambda:x:/../*/select-data/*/*/controls?value


/*
 * Changing any existing [onXXX] event handlers to [_onXXX] to
 * make sure they don't mess up design surface with weird events occuring
 * as the user navigates his page
 */
for-each:x:/../*/lisp2lambda/**(/literal/*/~on|/container/*/~on|/void/*/~on)
  if
    fetch:x:/0/0?value
      index-of:x:/..for-each/*/_dp/#?name
        what:on
    =:int:0

    /*
     * This is an event handler, renaming it temporarily for design surface
     */
    set:x:/..for-each/*/_dp/#?name
      src:_{0}
        :x:/..for-each/*/_dp/#?name

    /*
     * Checking if this is a user control, and if so, apply a
     */

/*
 * Then adding [onclick] event handlers for each widget, to
 * allow widget to be selected by clicking it in design surface
 */
add:x:/../*/lisp2lambda/**(/literal|/container|/void)
  src
    onclick
      sys42.wysiwyg-controls.select-widget:x:/../*/_event?value
      if:x:/-?value
        sys42.wysiwyg-controls.databind-select-control:x:/../*/_event?value
      else
        sys42.wysiwyg-controls.databind-select-control:_default

/*
 * Before finally adding widget to [create-widget] invocation below
 */
if:x:/../*/lisp2lambda/*(/container|/literal|/void)
  add:x:/../*/create-widget/*/widgets/*/container/=wysiwyg-editor-surface/*/widgets
    src:x:/../*/lisp2lambda/*


/*
 * Forward evaluate the expressions in [create-widget], before
 * creating WYSIWYG widget
 */
eval-x:x:/+/*
create-widget:wysiwyg-editor
  parent:x:/../*/_parent-widget?value
  widgets

    /*
     * Toolbar for manipulating WYSIWYG widget surface
     */
    container
      class:prepend-bottom col-md-3 col-xs-12 form-group
      widgets
        container
          class:input-group
          widgets

            /*
             * Select widget in page dropdown list
             */
            literal
              class:input-group-addon
              innerValue:Select
            select:wysiwyg-select-control
              class:form-control
              title:Choose any of the widgets on your page from here
              oninit
                sys42.wysiwyg-controls.databind-select-control
              onchange
                get-widget-property:wysiwyg-select-control
                  value
                if:x:/../*/get-widget-property/*/*?value
                  =:_default
                  sys42.wysiwyg-controls.unselect-widget
                else
                  sys42.wysiwyg-controls.select-widget:x:/../*/get-widget-property/*/*?value
              widgets
              events

                /*
                 * Raised whenever the 'select widget' dropdown list 
                 * needs to be re-databound. If [_arg] is given, it
                 * simply selects that as a value, otherwise it re-creates
                 * entire select dropdown list
                 */
                sys42.wysiwyg-controls.databind-select-control
                  if:x:/../*/_arg?value
                    set-widget-property:wysiwyg-select-control
                      value:x:/../*/_arg?value
                    return
                  clear-widget:wysiwyg-select-control
                  create-literal-widget
                    parent:wysiwyg-select-control
                    element:option
                    innerValue:Widget ...
                    value:_default
                  find-widget
                    _selected:true
                  sys42.get-widget:wysiwyg-editor-surface
                    _keep-empty-ids:true
                  for-each:x:/../*/sys42.get-widget/*/**(/container|/literal|/void)
                    _option
                      create-literal-widget
                        element:option
                        parent:wysiwyg-select-control
                        innerValue
                        value
                    if:x:/..for-each/*/_dp/#/*/_oninit
                      /*
                       * Probably a usercontrol
                       */
                      set:x:/..for-each/*/_option/*/*/innerValue?value
                        src:{0} - {1} - {2}
                          :usercontrol
                          :x:/..for-each/*/_dp/#/*/element?value
                          :x:/..for-each/*/_dp/#?value
                    else
                      set:x:/..for-each/*/_option/*/*/innerValue?value
                        src:{0} - {1} - {2}
                          :x:/..for-each/*/_dp/#?name
                          :x:/..for-each/*/_dp/#/*/element?value
                          :x:/..for-each/*/_dp/#?value
                    set:x:/..for-each/*/_option/*/*/value?value
                      src:x:/..for-each/*/_dp/#?value
                    if:x:/../*/find-widget/*?value
                      =:x:/..for-each/*/_dp/#?value
                      add:x:/..for-each/*/_option/*
                        src:selected
                    eval:x:/./*/_option

    container:wysiwyg-add-btn-group
      class:prepend-bottom col-md-6 col-xs-12 btn-group
      widgets

        /*
         * Add [container] widget to surface button
         */
        container:wysiwyg-controls-add-container-wrp
          class:btn-group
          style:"width:20%;"
          widgets
            button
              class:btn btn-default dropdown-toggle
              data-toggle:dropdown
              style:"width:100%;"
              innerValue:@"<span class=""glyphicon glyphicon-plus""></span> container <span class=""caret""></span>"
            ul
              class:dropdown-menu
              widgets
                li
                  widgets
                    a:wysiwyg-controls-add-container-before
                      href:#
                      innerValue:before
                      title:Adds a [container] widget to your page, at the beginning, or before the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:div
                          _before:true
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-container-wrp').toggleClass('open');"
                li
                  widgets
                    a:wysiwyg-controls-add-container-after
                      href:#
                      innerValue:after
                      title:Appends a [container] widget to your page, or after the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:div
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-container-wrp').toggleClass('open');"
                li:wysiwyg-controls-add-container-to-container
                  style:"display:none;"
                  widgets
                    a:wysiwyg-controls-add-container-inside
                      href:#
                      innerValue:inside
                      title:Appends a [container] widget to your currently selected widget
                      onclick
                        find-widget
                          _selected:true
                        eval-x:x:/+/*
                        sys42.wysiwyg-controls.add-control:div
                          _parent:x:/../*/find-widget/*?value
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#wysiwyg-controls-add-container-wrp').toggleClass('open');$('#widget-property-id').focus().select();"
                li
                  widgets
                    a:wysiwyg-controls-add-usercontrol-before
                      href:#
                      innerValue:Usercontrol before
                      title:Adds a [usercontrol] widget to your page, at the beginning, or before the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:_usercontrol
                          _before:true
                        send-javascript:@"$('#wysiwyg-controls-add-container-wrp').toggleClass('open');"
                li
                  widgets
                    a:wysiwyg-controls-add-usercontrol-after
                      href:#
                      innerValue:Usercontrol after
                      title:Appends a [usercontrol] widget to your page, or after the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:_usercontrol
                        send-javascript:@"$('#wysiwyg-controls-add-container-wrp').toggleClass('open');"

        /*
         * Add [literal] widget to surface button
         */
        container:wysiwyg-controls-add-literal-wrp
          class:btn-group
          style:"width:20%;"
          widgets
            button
              class:btn btn-default dropdown-toggle
              data-toggle:dropdown
              style:"width:100%;"
              innerValue:@"<span class=""glyphicon glyphicon-plus""></span> literal <span class=""caret""></span>"
            ul
              class:dropdown-menu
              widgets
                li
                  widgets
                    a:wysiwyg-controls-add-literal-before
                      href:#
                      innerValue:before
                      title:Adds a [literal] widget to your page, at the beginning, or before the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:span
                          _before:true
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-literal-wrp').toggleClass('open');"
                li
                  widgets
                    a:wysiwyg-controls-add-literal-after
                      href:#
                      innerValue:after
                      title:Appends a [literal] widget to your page, or after the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:span
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-literal-wrp').toggleClass('open');"
                li:wysiwyg-controls-add-literal-to-container
                  style:"display:none;"
                  widgets
                    a:wysiwyg-controls-add-literal-inside
                      href:#
                      innerValue:inside
                      title:Appends a [literal] widget to your currently selected widget
                      onclick
                        find-widget
                          _selected:true
                        eval-x:x:/+/*
                        sys42.wysiwyg-controls.add-control:span
                          _parent:x:/../*/find-widget/*?value
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-literal-wrp').toggleClass('open');"

        /*
         * Add [void] widget to surface button
         */
        container:wysiwyg-controls-add-void-wrp
          class:btn-group
          style:"width:20%;"
          widgets
            button
              class:btn btn-default dropdown-toggle
              data-toggle:dropdown
              style:"width:100%;"
              innerValue:@"<span class=""glyphicon glyphicon-plus""></span> void <span class=""caret""></span>"
            ul
              class:dropdown-menu
              widgets
                li
                  widgets
                    a:wysiwyg-controls-add-void-before
                      href:#
                      innerValue:before
                      title:Adds a [void] widget to your page, at the beginning, or before the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:br
                          _before:true
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-void-wrp').toggleClass('open');"
                li
                  widgets
                    a:wysiwyg-controls-add-void-after
                      href:#
                      innerValue:after
                      title:Appends a [void] widget to your page, or after the currently selected widget
                      onclick
                        sys42.wysiwyg-controls.add-control:br
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-void-wrp').toggleClass('open');"
                li:wysiwyg-controls-add-void-to-container
                  style:"display:none;"
                  widgets
                    a:wysiwyg-controls-add-void-inside
                      href:#
                      innerValue:inside
                      title:Appends a [void] widget to your currently selected widget
                      onclick
                        find-widget
                          _selected:true
                        eval-x:x:/+/*
                        sys42.wysiwyg-controls.add-control:br
                          _parent:x:/../*/find-widget/*?value
                        sys42.info-window:@"Please change widget's id and element to what you wish"
                          _time:more
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-add-void-wrp').toggleClass('open');"

        /*
         * Add paste widget to surface button
         */
        container:wysiwyg-controls-paste-wrp
          class:btn-group
          style:"width:20%;"
          events

            /*
             * Pastes a widget to surface. Needs [_position] arguments, being e.g. [before], [after], [parent]
             * and [position] to know where to paste widget
             */
            sys42.wysiwyg-controls.paste-widget

              /*
               * Retrieving widget from "clipboard", and changing the [literal], [void], [container]
               * parts of session value to [create-xxx-widget], in addition to removing the widget's
               * id
               */
              get-session-value:sys42.wysiwyg-controls.clipboard-widget
              set:x:/../*/get-session-value/*/*?name
                src:create-{0}-widget
                  :x:/../*/get-session-value/*/*?name
              set:x:/../*/get-session-value/*/*?value

              /*
               * Then setting the id of all children widgets of widget to empty
               */
              for-each:x:/../*/get-session-value/*/*/**(/container|/literal|/void)
                set:x:/+?value
                  src:x:/..for-each/*/_dp/#
                _node

                /*
                 * Checking if we should remove value, by looping ancestors, 
                 * until we see [create-x-widget] node name, and see if
                 * we only saw [container], [widgets], [void] and [literal] ancestor 
                 * node names. If we did, then value of node should be removed, since
                 * it was an ID for a widget. Otherwise, it was a [container/void/literal]
                 * widget node, inside of an event handler (probably), or some other place
                 * where we should NOT remove the ID of the widget!
                 * [_remove-value] is used to signal whether or not we should remove
                 * value of [container/void/literal] node ...
                 */
                _remove-value:bool:true
                while:x:/..for-each/*/_node/#?name
                  !~:create-
                  if:x:/..for-each/*/_node/#?name
                    !=:container
                    and:x:/..for-each/*/_node/#?name
                      !=:void
                    and:x:/..for-each/*/_node/#?name
                      !=:literal
                    and:x:/..for-each/*/_node/#?name
                      !=:widgets

                    /*
                     * We should NOT remove value of widget, since it was not
                     * a direct descendant widget beneath clipboard widget!
                     */
                    set:x:/..while/-?value
                      src:bool:false

                    /*
                     * Breaking [while], no need to continue, since
                     * we now know we should NOT remove ID of widget node
                     */
                    break

                  /*
                   * Settings while condition's [_node] value to parent node of
                   * currently iterated ([while] iterated) node
                   */
                  set:x:/..for-each/*/_node?value
                    src:x:/..for-each/*/_node/#/.

                /*
                 * Checking if we should remove ID of [literal/container/void] node,
                 * and if so, we remove it
                 */
                if:x:/..for-each/*/_remove-value?value
                  set:x:/..for-each/*/_dp/#?value

              /*
               * Adding "create position" to widget create invocation
               */
              add:x:/../*/get-session-value/*/*
                src:x:/../*/_position/*

              /*
               * Making sure widget becomes "selectable" in wysiwyg surface by clicking
               */
              add:x:/../*/get-session-value/*/*
                src
                  onclick
                    sys42.wysiwyg-controls.select-widget:x:/../*/_event?value
                    sys42.wysiwyg-controls.databind-select-control

              /*
               * Actually creating widget, before selecting widget, and rebinding select dropdown list
               */
              set:x:/+
                src:x:/../*/get-session-value/*/*
              _create-widget
              sys42.wysiwyg-controls.select-widget:x:/-?value
              sys42.wysiwyg-controls.databind-select-control

              /*
               * Toggling button's dropdown state, and showing user some information
               */
              sys42.info-window:@"Please change widget's id and element to what you wish"
                _time:more

          widgets
            button:wysiwyg-controls-paste-btn
              class:btn btn-default dropdown-toggle
              data-toggle:dropdown
              style:"width:100%;"
              oninit
                sys42.wysiwyg-controls.toggle-paste-button
              events
                sys42.wysiwyg-controls.toggle-paste-button
                  get-session-value:sys42.wysiwyg-controls.clipboard-widget
                  if:x:/-/*/*
                    not
                    get-widget-property:wysiwyg-controls-paste-btn
                      class
                    set:x:/-/*/*?value
                      src:{0} disabled
                        :x:/././-/*/*?value
                    set-widget-property:wysiwyg-controls-paste-btn
                      class:x:/./-2/*/*?value
                  else
                    get-widget-property:wysiwyg-controls-paste-btn
                      class
                    replace:x:/-/*/*?value
                      what:" disabled"
                    set-widget-property:wysiwyg-controls-paste-btn
                      class:x:/./-1?value

              innerValue:@"<span class=""glyphicon glyphicon-paste""></span> paste <span class=""caret""></span>"
            ul
              class:dropdown-menu
              widgets
                li
                  widgets
                    a:wysiwyg-controls-paste-before
                      href:#
                      innerValue:before
                      title:Paste the widget from your clipboard, at the beginning, or before the currently selected widget
                      onclick

                        /*
                         * Figuring out where to insert widget
                         */
                        sys42.wysiwyg-controls.get-insertion-position
                          _before:true

                        /*
                         * Invoking Active Event responsible for performing paste operation
                         */
                        add:x:/+/*
                          src:x:/./-/*
                        sys42.wysiwyg-controls.paste-widget
                          _position
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-paste-wrp').toggleClass('open');"
                li
                  widgets
                    a:wysiwyg-controls-paste-after
                      href:#
                      innerValue:after
                      title:Appends the widget from your clipboard to your page, or after the currently selected widget
                      onclick

                        /*
                         * Figuring out where to insert widget
                         */
                        sys42.wysiwyg-controls.get-insertion-position

                        /*
                         * Invoking Active Event responsible for performing paste operation
                         */
                        add:x:/+/*
                          src:x:/./-/*
                        sys42.wysiwyg-controls.paste-widget
                          _position
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-paste-wrp').toggleClass('open');"
                li:wysiwyg-controls-paste-to-container
                  style:"display:none;"
                  widgets
                    a:wysiwyg-controls-paste-inside
                      href:#
                      innerValue:inside
                      title:Appends the widget from your clipboard inside of your currently selected widget
                      onclick

                        /*
                         * Figuring out where to insert widget
                         */
                        sys42.wysiwyg-controls.get-insertion-position

                        /*
                         * Invoking Active Event responsible for performing paste operation
                         */
                        add:x:/+/*
                          src:x:/./-/*
                        sys42.wysiwyg-controls.paste-widget
                          _position
                        send-javascript:@"$('#widget-property-id').focus().select();$('#wysiwyg-controls-paste-wrp').toggleClass('open');"

        /*
         * Help button for 'Adding widgets to form'
         */
        button:wysiwyg-controls-help-add-widget
          class:btn btn-default
          style:"width:20%;"
          innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span> Help"
          title:Explains how adding widgets to your page works!
          onclick
            sys42.confirm-window
              _header:Help for adding widgets
              _body:@"<img src=""/media/images/marvin-headshot.png"" style=""float:right;""/>
<p>A widget is (most often) a visible element on your screen. It can be a paragraph of text, a container for other
widgets, or an input element, such as a textbox or a dropdown list. For those acquinted with HTML, a widget is often represented as a 
single HTML element</p>
<p>There are three basic types of widgets in Phosphorus Five</p>
<ul>
  <li><strong>container</strong> - Contains children widgets through its <em>[widgets]</em> child node</li>
  <li><strong>literal</strong> - Contains an <em>[innerValue]</em> property, which can be static text, or HTML</li>
  <li><strong>void</strong> - Contains neither children, nor inner text, used for &lt;br&gt;, &lt;hr&gt;, &lt;input&gt;, etc</li>
</ul>
<p>Any HTML elements can be rendered with the above three widgets. Each widget declares its HTML element through the <em>[element]</em>
property. In theory, you can render any HTML element with any widget type, but rendering a textarea HTML element, with the void or container widget
for instance, would be meaningless. Textarea HTML elements, should normally use the [literal] widget for instance.</p>
<p>Any widget that contains purely text, should be rendered using the [literal] widget. While any widget that has children
widgets of itself, should be rendered using the [container] widget. Form &lt;input&gt; elements, &lt;br&gt; and &lt;hr&gt; elements, etc,
that does not have any content, should be rendered with the [void] widget.</p>
<p>After you have added a widget to your page, you should add <em>""attributes""</em> to your widget.</p>
<p>Click on either the [container], [literal] or [void] buttons to add a widget to your page!</p>"


    /*
     * Wrapper for "general help" button
     */
    container
      class:prepend-bottom col-md-3 col-xs-12
      widgets

        /*
         * Add [container] widget to surface button
         */
        button
          class:btn btn-default
          style:"width:100%;"
          innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span> General help"
          title:Shows a general help dialog for the [controls] type of page ...
          onclick
            get-session-value:sys42.wysiwyg-controls.help-index
            if:x:/-/*?value
              list-files:/system42/cms/page-editor/editors/controls/help-files/
              load-file:x:/..if/*/list-files/{0}?name
                :x:/../*/get-session-value/*?value
              set:x:/../*/sys42.confirm-window/*/_body?value
                innerValue:x:/..if/*/load-file/*?value
            sys42.confirm-window
              _header:Help for [controls] pages
              _body:@"<img src=""/media/images/marvin-headshot.png"" style=""float:right;""/>
<blockquote>Feel free to tap into my bigger brain, to help ease the starting process of Phosphorus Five.
Start out by accessing the first help file at the bottom, by clicking the <em>""Next""</em> button.
You can close this dialog, and come back to it, where you left off. This particular help file, is about how
to get started using <em>""controls""</em> page types, that allows you to visually create rich web applications,
without any needs for any prior development experience.</blockquote>"
              _extra-buttons
                button
                  class:btn btn-default
                  innerValue:@"Next <span class=""glyphicon glyphicon-menu-right""></span>"
                  onclick
                    get-session-value:sys42.wysiwyg-controls.help-index
                    if:x:/-/*?value
                      set:x:/../*/get-session-value/*?value
                        +:x:/../*/get-session-value/*?value
                          _:1
                    else
                      set:x:/../*/get-session-value/*?value
                        src:int:1
                    list-files:/system42/cms/page-editor/editors/controls/help-files/
                    if:x:/../*/get-session-value/*?value
                      >=:x:/../*/list-files/*?count
                      set:x:/../*/get-session-value/*?value
                        src:int:0
                    load-file:x:/../*/list-files/{0}?name
                      :x:/../*/get-session-value/*?value
                    set-widget-property:confirm-window-body
                      innerValue:x:/../*/load-file/*?value
                    set-session-value:sys42.wysiwyg-controls.help-index
                      src:x:/../*/get-session-value/*?value

    /*
     * Wrapper for manipulating selected widget
     */
    container:wysiwyg-manipulate-selected-widget
      class:prepend-bottom col-xs-12 btn-group
      style:"display:none;"
      widgets

        /*
         * Delete widget button
         */
        button:wysiwyg-button-delete
          class:btn btn-default
          title:Delete currently selected widget
          innerValue:@"<span class=""glyphicon glyphicon-trash""></span> Delete widget"
          onclick
            sys42.confirm-window
              _header:Confirm deletion!
              _body:Are you sure you wish to <strong>delete currently selected widget</strong> from form? This action cannot be undone!
              _onok
                find-widget
                  _selected:true
                delete-widget:x:/../*/find-widget/*?value
                set-widget-property:wysiwyg-manipulate-selected-widget
                  style:"display:none;"
                sys42.wysiwyg-controls.databind-select-control
                set-widget-property:wysiwyg-controls-properties
                  visible:false

                /*
                 * Changing visibility for "create inside of widget" buttons
                 */
                set-widget-property:wysiwyg-controls-add-container-to-container
                  style:"display:none;"
                set-widget-property:wysiwyg-controls-add-literal-to-container
                  style:"display:none;"
                set-widget-property:wysiwyg-controls-add-void-to-container
                  style:"display:none;"
                set-widget-property:wysiwyg-controls-paste-to-container
                  style:"display:none;"

        /*
         * Add attribute to widget button
         */
        button:wysiwyg-button-add-attribute
          class:btn btn-default
          title:Add attribute to widget
          innerValue:@"<span class=""glyphicon glyphicon-plus""></span> Add attribute"
          onclick
            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-attribute.hl

        /*
         * Help with 'Add attribute to widget' button
         */
        button:wysiwyg-button-add-attribute-help
          class:btn btn-default
          style:"border-left:dashed 1px rgba(0,0,0,.2);"
          title:Help me with adding attributes to my widget please ...
          innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span>"
          onclick
            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-attribute-guide.hl

        /*
         * Copy widget
         */
        button:wysiwyg-button-copy-widget
          class:btn btn-default
          title:Copies widget to clipboard, such that you can paste in a copy another place, either on same page, or in another page
          innerValue:@"<span class=""glyphicon glyphicon-copy""></span> Copy widget"
          onclick
            find-widget
              _selected
            sys42.get-widget:x:/../*/find-widget/*?value
            set:x:/-/*/*(/_selected|/onclick|/style)
            add:x:/+/*
              src:x:/../*/sys42.get-widget/*
            set-session-value:sys42.wysiwyg-controls.clipboard-widget
              src
            sys42.info-window:@"A copy of the widget was put into the clipboard. If you wish to 'cut' the widget, 
you can simply delete it now. A complete version of the widget still exists on your 'widget clipboard'."
              _time:more
            sys42.wysiwyg-controls.toggle-paste-button

    /*
     * Properties for chosen widget
     */
    container:wysiwyg-controls-properties
      class:row col-xs-12
      visible:false

    /*
     * Actual WYSIWYG design surface
     */
    container:wysiwyg-editor-surface
      class:col-xs-12
      style:"min-height:400px;border-radius:5px;clear:both;border:dashed 1px rgba(0,0,0,.1);"
      onclick
        sys42.wysiwyg-controls.unselect-widget
        sys42.wysiwyg-controls.databind-select-control:_default
      widgets


  /*
   * Lambda events for Editor Surface
   */
  events


    /*
     * "Sink" event required by main editor to retrieve data
     * from specialized editor during saving process
     */
    sys42.get-page-editor-data

      /*
       * Retrieving 'raw widget' from surface
       */
      sys42.get-widget:wysiwyg-editor-surface

      /*
       * Making sure we remove the [_selected] attribute, 
       * which is used to track the currently selected widget in surface.
       * In addition to removing the selected widget's [style] properties
       */
      set:x:/../*/sys42.get-widget/**/_selected
      set:x:@"/../*/sys42.get-widget/**/style/""=box-shadow: 5px 5px 5px #888888;border:dashed 1px black;"""

      /*
       * Removing all 'select by clicking' event handlers, only relevant
       * while designing widget
       */
      set:x:/../*/sys42.get-widget/**/onclick

      /*
       * Making sure we rename all [_onXXX] event handlers, to their actual name, before
       * returning editor data to caller
       */
      for-each:x:/../*/sys42.get-widget/**(/container/*/~_on|/literal/*/~_on|/void/*/~_on)
        if
          fetch:x:/0/0?value
            index-of:x:/..for-each/*/_dp/#?name
              what:_on
          =:int:0

          /*
           * This is a "hidden" event handler, renaming it back to its actual value
           */
          set:x:/..for-each/*/_dp/#?name
            replace:x:/..for-each/*/_dp/#?name
              what:_on
              with:on

      /*
       * Returning [controls] to caller
       */
      insert-before:x:/../0
        src:controls
      lambda2lisp:x:/../*/sys42.get-widget/*/*/widgets/*
      set:x:/../0?value
        src:x:/../*/lambda2lisp?value

      /*
       * Returning [type] of page to caller
       */
      insert-before:x:/../0
        src
          type:controls

    /*
     * Invoked when a generic control is added to the page
     */
    sys42.wysiwyg-controls.add-control

      /*
       * Retrieving default properties, which differs from widget type
       */
      sys42.wysiwyg-controls.get-default-properties:x:/../*/_arg?value

      /*
       * Adding 'insertion position' to create-widget Active Event invocation.
       * Notice that this might be overridden by explicitly supplying a [_parent]
       * argument to invocation
       */
      if:x:/../*/_parent

        /*
         * Using explicitly given [_parent] as parent for widget
         */
        add:x:/../*/_create-widget
          src:@"parent:{0}"
            :x:/../*/_parent?value
      else

        /*
         * Retrieves insertion position, which might differ according to whether or
         * not a widget is selected on page or not
         */
        add:x:/+
          src:x:/../*/_before
        sys42.wysiwyg-controls.get-insertion-position
        add:x:/../*/_create-widget
          src:x:/..else/*/sys42.wysiwyg-controls.get-insertion-position/*

      /*
       * Adding default properties
       */
      add:x:/../*/_create-widget
        src:x:/../*/sys42.wysiwyg-controls.get-default-properties/*

      /*
       * Checking if an explicit [element] was given, and if not, choose
       * the name of the widget type. Otherwise, use the explicit [element]
       * declaration
       */
      if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/element
        not
        and:x:/../*/_arg?value
          !=:_usercontrol
        add:x:/../*/_create-widget
          src:"element:{0}"
            :x:/../*/_arg?value

      /*
       * Adding [onclick] event handler, such that user can select widget
       * in WYSIWYG screen by clicking it
       */
      if:x:/../*/_arg?value
        !=:_usercontrol
        add:x:/../*/_create-widget
          src
            onclick
              sys42.wysiwyg-controls.select-widget:x:/../*/_event?value
              sys42.wysiwyg-controls.databind-select-control

      /*
       * Checking widget creation Active Event, which might be
       * [create-widget] (container)
       * [create-literal-widget] (innerValue type of widget)
       * [create-void-widget] (widget with neither [widgets] nor [innerValue])
       */
      if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/widgets
        set:x:/../*/_create-widget?name
          src:create-container-widget
      else-if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/innerValue
        set:x:/../*/_create-widget?name
          src:create-literal-widget
      else-if:x:/../*/_arg?value
        !=:_usercontrol
        set:x:/../*/_create-widget?name
          src:create-void-widget
      else

        /*
         * Usercontrol type of widget, invoking creational event, and RETURNING
         */
        add:x:/+2/*
          src:x:/../*/_create-widget
        set:x:/+/*/*?name
          src:create-widget
        sys42.wysiwyg-controls.create-user-control
          _create-widget
        return

      /*
       * At this point, Active Event invocation below, should have been adequately built
       * for correctly inserting our widget into surface
       */
      _create-widget
      sys42.wysiwyg-controls.select-widget:x:/-?value
      sys42.wysiwyg-controls.databind-select-control

    /*
     * Creates a user control type of widget, by asking user which existing predefined
     * [controls] he would like to include as his [usercontrol]
     */
    sys42.wysiwyg-controls.create-user-control

      /*
       * Asking user for which [controls] he wish to include, by creating a modal
       * window, looping through all existing [controls] in database
       */
      add:x:/+
        src:x:/../*/_create-widget
      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-usercontrol.hl

    /*
     * Returns the position where to insert a new control
     */
    sys42.wysiwyg-controls.get-insertion-position
      find-widget
        _selected:true

      /*
       * As a general rule, if any widget is currently selected, we insert
       * the new widget after the currently selected widget, unless [_before]
       * is given. Otherwise, we append the widget into the main design surface
       */
      if:x:/../*/find-widget/*
        if:x:/../*/_before?value.bool
          // [_before] was supplied
          eval-x:x:/+/*
          return
            before:x:/../*/find-widget/*?value
        else
          // No [_before], this is an after operation
          eval-x:x:/+/*
          return
            after:x:/../*/find-widget/*?value
      else
        if:x:/../*/_before?value.bool
          add:x:/./+
            src:"position:0"
        return
          parent:wysiwyg-editor-surface

    /*
     * Returns default properties for specific type of control,
     * takes type of control as [_arg]
     */
    sys42.wysiwyg-controls.get-default-properties
      switch:x:/../*/_arg?value
        case:span
          return
            innerValue:Foo bar
        case:div
          return
            widgets
        case:br
          return

    /*
     * Unselects any selected widgets on page
     */
    sys42.wysiwyg-controls.unselect-widget

      /*
       * Changing visibility for "create inside of widget" buttons
       */
      set-widget-property:wysiwyg-controls-add-container-to-container
        style:"display:none;"
      set-widget-property:wysiwyg-controls-add-literal-to-container
        style:"display:none;"
      set-widget-property:wysiwyg-controls-add-void-to-container
        style:"display:none;"
      set-widget-property:wysiwyg-controls-paste-to-container
        style:"display:none;"

      /*
       * Finding selected widget
       */
      find-widget
        _selected:true

      /*
       * Unselecting previously selected widget, if it exists
       */
      if:x:/../*/find-widget/*
        delete-widget-property:x:/../*/find-widget/*?value
          _selected
          style
      set-widget-property:wysiwyg-controls-properties
        visible:false

      /*
       * Hiding buttons that should only be visible while widget is selected
       */
      set-widget-property:wysiwyg-manipulate-selected-widget
        style:"display:none;"

    /*
     * Selects the given widget on page
     */
    sys42.wysiwyg-controls.select-widget
      find-widget
        _selected:true

      /*
       * Unselecting previously selected widget, if it exists
       */
      if:x:/../*/find-widget/*
        delete-widget-property:x:/../*/find-widget/*?value
          _selected
          style

      /*
       * Making sure selected widget is visible, but only if the clicked
       * widget was NOT the one already selected
       */
      if:x:/../*/_arg?value
        !=:x:/../*/find-widget/*?value

        /*
         * Another widget was selected
         */
        set-widget-property:x:/../*/_arg?value
          _selected:true
          style:"box-shadow: 5px 5px 5px #888888;border:dashed 1px black;"

        /*
         * Changing visibility for "create inside of widget" buttons
         */
        if
          fetch:x:/0/0?name
            list-widgets:x:/../*/_arg?value
          =:container
          set-widget-property:wysiwyg-controls-add-container-to-container
            style
          set-widget-property:wysiwyg-controls-add-literal-to-container
            style
          set-widget-property:wysiwyg-controls-add-void-to-container
            style
          if
            fetch:x:/0/0/0
              get-session-value:sys42.wysiwyg-controls.clipboard-widget
            set-widget-property:wysiwyg-controls-paste-to-container
              style
          else
            set-widget-property:wysiwyg-controls-paste-to-container
              style:"display:none;"
        else

          /*
           * Changing visibility for "create inside of widget" buttons
           */
          set-widget-property:wysiwyg-controls-add-container-to-container
            style:"display:none;"
          set-widget-property:wysiwyg-controls-add-literal-to-container
            style:"display:none;"
          set-widget-property:wysiwyg-controls-add-void-to-container
            style:"display:none;"
          set-widget-property:wysiwyg-controls-paste-to-container
            style:"display:none;"

        /*
         * Showing widget's properties widget
         */
        sys42.wysiwyg-controls.show-selected-widgets-properties:x:/../*/_arg?value

        /*
         * Showing buttons that should only be visible while widget is selected
         */
        delete-widget-property:wysiwyg-manipulate-selected-widget
          style
        return:bool:true
      else

        /*
         * The widget that was already selected was UN-selected.
         * Hiding widget's properties widget
         */
        sys42.wysiwyg-controls.hide-selected-widgets-properties

        /*
         * Changing visibility for "create inside of widget" buttons
         */
        set-widget-property:wysiwyg-controls-add-container-to-container
          style:"display:none;"
        set-widget-property:wysiwyg-controls-add-literal-to-container
          style:"display:none;"
        set-widget-property:wysiwyg-controls-add-void-to-container
          style:"display:none;"
        set-widget-property:wysiwyg-controls-paste-to-container
          style:"display:none;"

        /*
         * Hiding buttons that should only be visible while widget is selected
         */
        set-widget-property:wysiwyg-manipulate-selected-widget
          style:"display:none;"
        return:bool:false

    /*
     * Shows the widget's properties widget, allowing user to
     * edit properties of widget
     */
    sys42.wysiwyg-controls.show-selected-widgets-properties
      set:x:/+/*/_widget?value
        src:x:/../*/_arg?value
      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
        _widget

    /*
     * Hides the widget's properties widget
     */
    sys42.wysiwyg-controls.hide-selected-widgets-properties
      set-widget-property:wysiwyg-controls-properties
        visible:false
