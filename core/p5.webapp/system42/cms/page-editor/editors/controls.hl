/*
 * Editor for [controls] types of pages
 */

/*
 * Loading page from database
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_id?value


/*
 * Setting the content of WYSIWYG surface to the [controls] from database,
 * converting to lambda from Hyperlisp first
 */
lisp2lambda:x:/../*/select-data/*/*/controls?value


/*
 * Then adding [onclick] event handlers for each widget, to
 * allow widget to be selected by clicking it in design surface
 */
add:x:/../*/lisp2lambda/**(/literal|/container|/void)
  src
    onclick
      sys42.wysiwyg-controls.select-widget:x:/../*/_event?value
      if:x:/-?value
        sys42.wysiwyg-controls.databind-select-control:x:/../*/_event?value
      else
        sys42.wysiwyg-controls.databind-select-control:_default

/*
 * Before finally adding widget to [create-widget] invocation below
 */
if:x:/../*/lisp2lambda/*(/container|/literal|/void)
  add:x:/../*/create-widget/*/widgets/*/container/=wysiwyg-editor-surface/*/widgets
    src:x:/../*/lisp2lambda/*


/*
 * Forward evaluate the expressions in [create-widget], before
 * creating WYSIWYG widget
 */
eval-x:x:/+/*
create-widget:wysiwyg-editor
  parent:x:/../*/_parent-widget?value
  widgets

    /*
     * Toolbar for manipulating WYSIWYG widget surface
     */
    container
      class:prepend-bottom col-md-3 col-xs-12 form-group
      widgets
        container
          class:input-group
          widgets

            /*
             * Select widget in page dropdown list
             */
            literal
              class:input-group-addon
              innerValue:Select
            select:wysiwyg-select-control
              class:form-control
              title:Choose any of the widgets on your page from here
              oninit
                sys42.wysiwyg-controls.databind-select-control
              onchange
                get-widget-property:wysiwyg-select-control
                  value
                if:x:/../*/get-widget-property/*/*?value
                  =:_default
                  sys42.wysiwyg-controls.unselect-widget
                else
                  sys42.wysiwyg-controls.select-widget:x:/../*/get-widget-property/*/*?value
              widgets
              events

                /*
                 * Raised whenever the 'select widget' dropdown list 
                 * needs to be re-databound. If [_arg] is given, it
                 * simply selects that as a value, otherwise it re-creates
                 * entire select dropdown list
                 */
                sys42.wysiwyg-controls.databind-select-control
                  if:x:/../*/_arg?value
                    set-widget-property:wysiwyg-select-control
                      value:x:/../*/_arg?value
                    return
                  clear-widget:wysiwyg-select-control
                  create-literal-widget
                    parent:wysiwyg-select-control
                    element:option
                    innerValue:Widget ...
                    value:_default
                  find-widget
                    _selected:true
                  sys42.get-widget:wysiwyg-editor-surface
                    _keep-empty-ids:true
                  for-each:x:/../*/sys42.get-widget/*/**(/container|/literal|/void)
                    _option
                      create-literal-widget
                        element:option
                        parent:wysiwyg-select-control
                        innerValue
                        value
                    set:x:/..for-each/*/_option/*/*/innerValue?value
                      src:{0} - {1} - {2}
                        :x:/..for-each/*/_dp/#?name
                        :x:/..for-each/*/_dp/#/*/element?value
                        :x:/..for-each/*/_dp/#?value
                    set:x:/..for-each/*/_option/*/*/value?value
                      src:x:/..for-each/*/_dp/#?value
                    if:x:/../*/find-widget/*?value
                      =:x:/..for-each/*/_dp/#?value
                      add:x:/..for-each/*/_option/*
                        src:selected
                    eval:x:/./*/_option

    container:wysiwyg-add-btn-group
      class:prepend-bottom col-md-6 col-xs-12 btn-group
      widgets

        /*
         * Add [container] widget to surface button
         */
        button:wysiwyg-controls-add-container
          class:btn btn-default
          style:"width:25%;"
          innerValue:@"<span class=""glyphicon glyphicon-plus""></span> container"
          title:Adds a [container] widget to your page
          onclick
            sys42.wysiwyg-controls.add-control:div
            sys42.info-window:@"Please change widget's id and element to what you wish"
              _time:more
            send-javascript:@"$('#widget-property-id').focus().select();"

        /*
         * Add [literal] widget to surface button
         */
        button:wysiwyg-controls-add-literal
          class:btn btn-default
          style:"width:25%;"
          innerValue:@"<span class=""glyphicon glyphicon-plus""></span> literal"
          title:Adds a [literal] widget to your page
          onclick
            sys42.wysiwyg-controls.add-control:span
            sys42.info-window:@"Please change widget's id and element to what you wish"
              _time:more
            send-javascript:@"$('#widget-property-id').focus().select();"

        /*
         * Add [void] widget to surface button
         */
        button:wysiwyg-controls-add-void
          class:btn btn-default
          style:"width:25%;"
          innerValue:@"<span class=""glyphicon glyphicon-plus""></span> void"
          title:Adds a [void] widget to your page
          onclick
            sys42.wysiwyg-controls.add-control:br
            sys42.info-window:@"Please change widget's id and element to what you wish"
              _time:more
            send-javascript:@"$('#widget-property-id').focus().select();"

        /*
         * Help button for 'Adding widgets to form'
         */
        button:wysiwyg-controls-help-add-widget
          class:btn btn-default
          style:"width:25%;"
          innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span> Help"
          title:Explains to you how adding widgets to your form works!
          onclick
            sys42.confirm-window
              _header:Help for adding widgets
              _body:@"<p>A widget is (most often) a visible element on your screen. It can be a paragraph of text, a container for other
widgets or an input element, such as a textbox or a dropdown list. For those acquinted with HTML, a widget is often represented as a 
single HTML element</p>
<p>There are three basic types of widgets in Phosphorus Five</p>
<ul>
  <li><strong>container</strong> - Contains children widgets through its <em>[widgets]</em> properties</li>
  <li><strong>literal</strong> - Contains an <em>[innerValue]</em> property, which can be static text, or HTML</li>
  <li><strong>void</strong> - Contains neither children nor inner text, used for &lt;br&gt;, &lt;hr&gt;, &lt;input&gt;, etc</li>
</ul>
<p>Any HTML elements can be rendered with the above three widgets. Each widget declares its HTML element through the <em>[element]</em>
property. In theory, you can render any widget with any widget type, but rendering a textarea HTML element, with the void or container widget
for instance, would be meaningless. Textarea HTML elements, should normally use the [literal] widget for instance.</p>
<p>Any widget that contains purely text, should be rendered using the [literal] widget. While any widget that has children
widgets of itself, should be rendered using the [container] widget. Form input elements, &lt;br&gt; and &lt;hr&gt; elements, etc,
that does not have any content, should be rendered with the [void] widget.</p>"

    container:wysiwyg-manipulate-selected-widget
      class:prepend-bottom col-xs-12 btn-group
      style:"display:none;"
      widgets

        /*
         * Delete widget button
         */
        button:wysiwyg-button-delete
          class:btn btn-default
          title:Delete currently selected widget
          innerValue:@"<span class=""glyphicon glyphicon-trash""></span> Delete widget"
          onclick
            sys42.confirm-window
              _header:Confirm deletion!
              _body:Are you sure you wish to <strong>delete currently selected widget</strong> from form? This action cannot be undone!
              _onok
                find-widget
                  _selected:true
                delete-widget:x:/../*/find-widget/*?value
                set-widget-property:wysiwyg-manipulate-selected-widget
                  style:"display:none;"
                sys42.wysiwyg-controls.databind-select-control
                set-widget-property:wysiwyg-controls-properties
                  visible:false

        /*
         * Add attribute to widget button
         */
        button:wysiwyg-button-add-attribute
          class:btn btn-default
          title:Add attribute to widget
          innerValue:@"<span class=""glyphicon glyphicon-plus""></span> Add attribute"
          onclick
            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-attribute.hl

    /*
     * Properties for chosen widget
     */
    container:wysiwyg-controls-properties
      class:row col-xs-12
      visible:false

    /*
     * Actual WYSIWYG design surface
     */
    container:wysiwyg-editor-surface
      class:col-xs-12
      style:"min-height:400px;border-radius:5px;clear:both;border:dashed 1px rgba(0,0,0,.1);"
      onclick
        sys42.wysiwyg-controls.unselect-widget
        sys42.wysiwyg-controls.databind-select-control:_default
      widgets


  /*
   * Lambda events for Editor Surface
   */
  events


    /*
     * "Sink" event required by main editor to retrieve data
     * from specialized editor during saving process
     */
    sys42.get-page-editor-data

      /*
       * Retrieving 'raw widget' from surface
       */
      sys42.get-widget:wysiwyg-editor-surface

      /*
       * Making sure we remove the [_selected] attribute, 
       * which is used to track the currently selected widget in surface.
       * In addition to removing the selected widget's [style] properties
       */
      set:x:/../*/sys42.get-widget/**/_selected
      set:x:@"/../*/sys42.get-widget/**/style/""=box-shadow: 5px 5px 5px #888888;border:dashed 1px black;"""

      /*
       * Removing all 'select by clicking' event handlers, only relevant
       * while designing widget
       */
      set:x:/../*/sys42.get-widget/**/onclick

      /*
       * Returning [controls] to caller
       */
      insert-before:x:/../0
        src:controls
      lambda2lisp:x:/../*/sys42.get-widget/*/*/widgets/*
      set:x:/../0?value
        src:x:/../*/lambda2lisp?value

      /*
       * Returning [type] of page to caller
       */
      insert-before:x:/../0
        src
          type:controls

    /*
     * Invoked when a generic control is added to the page
     */
    sys42.wysiwyg-controls.add-control

      /*
       * Retrieving default properties, which differs from widget type
       */
      sys42.wysiwyg-controls.get-default-properties:x:/../*/_arg?value

      /*
       * Adding 'insertion position' to create-widget Active Event invocation.
       * Notice that this might be overridden by explicitly supplying a [_parent]
       * argument to invocation
       */
      if:x:/../*/_parent

        /*
         * Using explicitly given [_parent] as parent for widget
         */
        add:x:/../*/_create-widget
          src:@"parent:{0}"
            :x:/../*/_parent?value
      else

        /*
         * Retrieves insertion position, which might differ according to whether or
         * not a widget is selected on page or not
         */
        sys42.wysiwyg-controls.get-insertion-position
        add:x:/../*/_create-widget
          src:x:/..else/*/sys42.wysiwyg-controls.get-insertion-position/*

      /*
       * Adding default properties
       */
      add:x:/../*/_create-widget
        src:x:/../*/sys42.wysiwyg-controls.get-default-properties/*

      /*
       * Checking if an explicit [element] was given, and if not, choose
       * the name of the widget type. Otherwise, use the explicit [element]
       * declaration
       */
      if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/element
        not
        add:x:/../*/_create-widget
          src:"element:{0}"
            :x:/../*/_arg?value

      /*
       * Adding [onclick] event handler, such that user can select widget
       * in WYSIWYG screen by clicking it
       */
      add:x:/../*/_create-widget
        src
          onclick
            sys42.wysiwyg-controls.select-widget:x:/../*/_event?value
            sys42.wysiwyg-controls.databind-select-control

      /*
       * Checking widget creation Active Event, which might be
       * [create-widget] (container)
       * [create-literal-widget] (innerValue type of widget)
       * [create-void-widget] (widget with neither [widgets] nor [innerValue])
       */
      if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/widgets
        set:x:/../*/_create-widget?name
          src:create-container-widget
      else-if:x:/../*/sys42.wysiwyg-controls.get-default-properties/*/innerValue
        set:x:/../*/_create-widget?name
          src:create-literal-widget
      else
        set:x:/../*/_create-widget?name
          src:create-void-widget

      /*
       * At this point, Active Event invocation below, should have been adequately built
       * for correctly inserting our widget into surface
       */
      _create-widget
      sys42.wysiwyg-controls.select-widget:x:/-?value
      sys42.wysiwyg-controls.databind-select-control

    /*
     * Returns the position where to insert a new control
     */
    sys42.wysiwyg-controls.get-insertion-position
      find-widget
        _selected:true

      /*
       * As a general rule, if any widget is currently selected, we insert
       * the new widget after the currently selected widget. Otherwise, 
       * we append the widget into the main design surface
       */
      if:x:/../*/find-widget/*
        eval-x:x:/+/*
        return
          after:x:/../*/find-widget/*?value
      else
        return
          parent:wysiwyg-editor-surface

    /*
     * Returns default properties for specific type of control,
     * takes type of control as [_arg]
     */
    sys42.wysiwyg-controls.get-default-properties
      switch:x:/../*/_arg?value
        case:span
          return
            innerValue:Foo bar
        case:div
          return
            widgets
        case:br
          return

    /*
     * Unselects any selected widgets on page
     */
    sys42.wysiwyg-controls.unselect-widget
      find-widget
        _selected:true

      /*
       * Unselecting previously selected widget, if it exists
       */
      if:x:/../*/find-widget/*
        delete-widget-property:x:/../*/find-widget/*?value
          _selected
          style
      set-widget-property:wysiwyg-controls-properties
        visible:false

      /*
       * Hiding buttons that should only be visible while widget is selected
       */
      set-widget-property:wysiwyg-manipulate-selected-widget
        style:"display:none;"

    /*
     * Selects the given widget on page
     */
    sys42.wysiwyg-controls.select-widget
      find-widget
        _selected:true

      /*
       * Unselecting previously selected widget, if it exists
       */
      if:x:/../*/find-widget/*
        delete-widget-property:x:/../*/find-widget/*?value
          _selected
          style

      /*
       * Making sure selected widget is visible, but only if the clicked
       * widget was NOT the one already selected
       */
      if:x:/../*/_arg?value
        !=:x:/../*/find-widget/*?value

        /*
         * Another widget was selected
         */
        set-widget-property:x:/../*/_arg?value
          _selected:true
          style:"box-shadow: 5px 5px 5px #888888;border:dashed 1px black;"

        /*
         * Showing widget's properties widget
         */
        sys42.wysiwyg-controls.show-selected-widgets-properties:x:/../*/_arg?value

        /*
         * Showing buttons that should only be visible while widget is selected
         */
        delete-widget-property:wysiwyg-manipulate-selected-widget
          style
        return:bool:true
      else

        /*
         * The widget that was already selected was UN-selected.
         * Hiding widget's properties widget
         */
        sys42.wysiwyg-controls.hide-selected-widgets-properties

        /*
         * Hiding buttons that should only be visible while widget is selected
         */
        set-widget-property:wysiwyg-manipulate-selected-widget
          style:"display:none;"
        return:bool:false

    /*
     * Shows the widget's properties widget, allowing user to
     * edit properties of widget
     */
    sys42.wysiwyg-controls.show-selected-widgets-properties
      set:x:/+/*/_widget?value
        src:x:/../*/_arg?value
      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
        _widget

    /*
     * Hides the widget's properties widget
     */
    sys42.wysiwyg-controls.hide-selected-widgets-properties
      set-widget-property:wysiwyg-controls-properties
        visible:false
