
/*
 * Editor for [controls] types of pages
 */


/*
 * Including CodeMirror for [lambda] editor
 */
sys42.execute-lisp-file:/system42/editors/codemirror/include-codemirror.hl
  _editor
    editor_surface
      mode:hyperlisp
      autofocus:true
      fullscreen:true
      height:484px
      keys
        Alt-S:@"function(cm) {p5.$('save-button').raise('onclick');}"


/*
 * Loading page from database
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_id?value


/*
 * Setting the content of editor_surface to the [lambda] from database
 */
set:x:/../*/create-widget/*/widgets/*/literal/=editor_surface/*/innerValue?value
  src:x:/../*/select-data/*/*/lambda?value


/*
 * Making sure editor is loaded underneath the correct widget according to 
 * caller's specifications
 */
set:x:/../*/create-widget/*/parent?value
  src:x:/../*/_parent-widget?value


/*
 * Creating our widget to display our editor surface
 */
create-widget
  parent
  class:col-xs-12
  widgets
    literal:editor_surface
      element:textarea
      innerValue


      /*
       * Lambda events for Editor Surface
       */
      events


        /*
         * "Sink" event required by main editor to retrieve data
         * from specialized editor during saving process
         */
        sys42.get-page-editor-data
          insert-before:x:/../0
            src
              type:lambda
          get-widget-property:editor_surface
            innerValue
          insert-before:x:/../0
            src:lambda
          set:x:/../0?value
            src:x:/../*/get-widget-property/*/*?value
