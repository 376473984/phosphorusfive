/*
 * Shows the properties for a single selected widget
 */

/*
 * Fetching widget,without its children widgets
 */
sys42.get-widget:x:/../*/_widget?value
  _no-children:true

/*
 * Sorting widget's properties, first stuffing event handlers last, then alphabetically
 */
sort:x:/../*/sys42.get-widget/*/*(!/widgets!/innerValue!/element!/_selected!/style!/onclick)
  if
    fetch:x:/0/0?value
      index-of:x:/../*/_rhs/#?name
        what:_on
    =:int:0
    and
      fetch:x:/0/0
        index-of:x:/../*/_lhs/#?name
          what:_on
      not

    // [_lhs] is event handler, but not [_rhs]
    return:int:-1
  if
    fetch:x:/0/0?value
      index-of:x:/../*/_lhs/#?name
        what:_on
    =:int:0
    and
      fetch:x:/0/0
        index-of:x:/../*/_rhs/#?name
          what:_on
      not

    // [_rhs] is event handler, but not [_lhs]
    return:int:1

  // Otherwise we sort by name
  if:x:/../*/_lhs/#?name
    >:x:/../*/_rhs/#?name
    return:1
  if:x:/../*/_lhs/#?name
    <:x:/../*/_rhs/#?name
    return:-1
  return:0

/*
 * Setting initial values for widget's properties, first starting
 * out with the 'static properties', such as [element] and [id], which
 * all widgets must have
 */
set:x:/../*/create-widget/*/widgets/*/container/**/void/=widget-property-element/*/value?value
  src:x:/../*/sys42.get-widget/*/*/element?value
set:x:/../*/create-widget/*/widgets/*/container/**/void/=widget-property-id/*/value?value
  src:x:/../*/_widget?value


/*
 * Then iterating all dynamically inserted properties of specific widget,
 * except [innerValue], [element] and [widgets], which requires special handling
 */
for-each:x:/../*/sort/*
  _control
    container
      class:col-md-4 form-group
      widgets
        container
          class:input-group
          widgets
            span
              innerValue:{element-name}
              class:input-group-addon
            void
              element:input
              type:text
              class:form-control
              placeholder:New value ...
              _element-name:{element-name}
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                if
                  fetch:x:/0/0/0?value
                    get-widget-property:x:/../*/_event?value
                      element
                  =:input

                  /*
                   * This is a simple value type of attribute, might be an event,
                   * but if it is, it's a JavaScript client-side event
                   */
                  get-widget-property:x:/../*/_event?value
                    value
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                    _new-value:x:/..if/*/get-widget-property/*/*?value
                else

                  /*
                   * This is a Hyperlisp Ajax event handler attribute. Making sure
                   * we convert the value to lambda in a try/catch block, to trap any
                   * misformatted Hyperlisp syntactic errors
                   */
                  try
                    get-widget-property:x:/../*/_event?value
                      value
                      _element-name
                    lisp2lambda:x:/-/*/0?value
                    set-widget-property:x:/../*/_event?value
                      class:form-control
                    find-widget
                      _selected
                    add:x:/+2
                      src:x:/..try/*/get-widget-property/*/*/_element-name?value
                    add:x:/+/*
                      src:x:/..try/*/lisp2lambda/*
                    set-widget-ajax-event:x:/..try/*/find-widget/*?value
                  catch
                    sys42.info-window:@"Oops, syntax error in Hyperlisp. Probably malformatted child node collections. Please correct! Value was NOT saved!"
                      _error:true
                      _time:more
                    set-widget-property:x:/../*/_event?value
                      class:form-control error
            a
              href:#
              class:input-group-addon input-group-addon-force-float
              innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
              title:Removes the attribute from widget
              onclick
                _element-name:{element-name}
                if
                  fetch:x:/0/0?value
                    index-of:x:/../*/_element-name?value
                      what:_on
                  =:int:0

                  /*
                   * This is an invisible event handler, showing user a special dialog box, 
                   * allowing user to convert between JavaScript event handlers, and Ajax Hyperlisp
                   * event handlers. First checking if this is JavaScript or Ajax Hyperlisp event handler.
                   */
                  find-widget
                    _selected
                  add:x:/+
                    src:{element-name}
                  get-widget-property:x:/-2/*?value
                  if:x:/-/*/*?value

                    /*
                     * This is JavaScript event handler, allowing user to convert to Hyperlisp Ajax event handler
                     */
                    replace:x:/../*/_element-name?value
                      what:_
                    eval-x:x:/+/*
                    sys42.confirm-window
                      _header:Confirm deletion of property
                      _body:@"<p>Please confirm that you wish to <strong>delete the <em>'{0}'</em> event</strong> from widget!</p>
<p>Alternatively, you can choose to convert the existing JavaScript event handler to an Ajax Hyperlisp event handler.</p>
<p>If you do not want to do either, please close the window at the top right corner with the 'X'.</p>"
                        :x:/..if/*/replace?value
                      _extra-buttons
                        button
                          class:btn btn-default
                          innerValue:Convert to Hyperlisp
                          title:Convert existing JavaScript event handler to an Ajax Hyperlisp server-side event handler
                          onclick
                            _element-name:{element-name}
                            find-widget
                              _selected
                            add:x:/+
                              src:x:/../*/_element-name?value
                            delete-widget-property:x:/../*/find-widget/*?value
                            add:x:/+2
                              src:x:/../*/_element-name?value
                            add:x:/+/*
                              src:foo
                            set-widget-ajax-event:x:/../*/find-widget/*?value
                            send-javascript:@"$('#confirm-window').modal('toggle');"
                            set:x:/+/*?value
                              src:x:/../*/find-widget/*?value
                            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
                              _widget
                      _onok

                        /*
                         * Simply delete existing JavaScript event handler
                         */
                        _element-name:{element-name}
                        find-widget
                          _selected
                        add:x:/+
                          src:x:/../*/_element-name?value
                        delete-widget-property:x:/../*/find-widget/*?value
                        set:x:/+/*?value
                          src:x:/../*/find-widget/*?value
                        sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
                          _widget
                  else

                    /*
                     * This is a Hyperlisp Ajax event handler, allowing user to convert to a JavaScript event handler
                     */
                    replace:x:/../*/_element-name?value
                      what:_
                    eval-x:x:/+/*
                    sys42.confirm-window
                      _header:Confirm deletion of property
                      _body:@"<p>Please confirm that you wish to <strong>delete the <em>'{0}'</em> event</strong> from widget!</p>
<p>Alternatively, you can choose to convert the existing Hyperlisp Ajax server-side event handler to a JavaScript event handler.</p>
<p>If you do not want to do either, please close the window at the top right corner with the 'X'.</p>"
                        :x:/..else/*/replace?value
                      _extra-buttons
                        button
                          class:btn btn-default
                          innerValue:Convert to JavaScript
                          title:Convert existing Hyperlisp server-side Ajav event handler to a JavaScript event handler
                          onclick
                            _element-name:{element-name}
                            find-widget
                              _selected
                            add:x:/+
                              src:x:/../*/_element-name?value
                            set-widget-ajax-event:x:/-2/*?value
                            add:x:/+
                              src:"{0}:"
                                :x:/../*/_element-name?value
                            set-widget-property:x:/../*/find-widget/*?value
                            send-javascript:@"$('#confirm-window').modal('toggle');"
                            set:x:/+/*?value
                              src:x:/../*/find-widget/*?value
                            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
                              _widget
                      _onok

                        /*
                         * Simply delete Hyperlisp Ajax event
                         */
                        _element-name:{element-name}
                        find-widget
                          _selected
                        add:x:/+
                          src:x:/../*/_element-name?value
                        set-widget-ajax-event:x:/-2/*?value
                        set:x:/+/*?value
                          src:x:/../*/find-widget/*?value
                        sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
                          _widget
                  return
                eval-x:x:/+/*|/+/**/_eventWidget
                sys42.confirm-window
                  _header:Confirm deletion of property
                  _body:@"<p>Please confirm that you wish to <strong>delete the <em>'{0}'</em> attribute</strong> from widget! 
This might have consequence for the rendering of your widget!</p>
<p>Alternatively, you can choose to 'nullify' its value, meaning the attribute
will still be kept around, but have a 'null value'. This can be useful for [controls] properties of [video]
HTML elements for instance, that don't require a value, but need the property.</p>
<p>If you do not want to do either, please close the window at the top right corner with the 'X'.</p>"
                    :x:/../*/_element-name?value
                  _extra-buttons
                    button
                      class:btn btn-default
                      innerValue:Nullify value
                      title:Only 'nullify' value of property, but do not remove property
                      onclick
                        sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                          _new-value
                        send-javascript:@"$('#confirm-window').modal('toggle');"
                        find-widget
                          _element-name:{element-name}
                        set-widget-property:x:/-/*?value
                          value
                  _onok
                    _eventWidget:x:/../*/_event?value
                    sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                    get-parent-widget:x:/../*/_eventWidget?value
                    get-parent-widget:x:/-/*?value
                    delete-widget:x:/-/*?value

  set:x:/..for-each/*/_control/**/={element-name}?value
    src:x:/..for-each/*/_dp/#?name
  set:x:/..for-each/*/_control/*/*/*/*/*/*/value?value
    src:x:/..for-each/*/_dp/#?value

  /*
   * Checking if this is an "invisible rendered" event handler
   */
  if
    fetch:x:/0/0?value
      index-of:x:/..for-each/*/_dp/#?name
        what:_on
    =:int:0

    /*
     * Checking if this is a JavaScript event handler or a Hyperlisp event handler.
     * JavaScript event handlers have a "value", while Hyperlisp event handlers
     * have children nodes.
     */
    if:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void/*/value?value
      not

      /*
       * This should be handled as a Hyperlisp Ajax event handler
       */
      set:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void/*(/element|/type|/onkeypress)
      set:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void/*/value?name
        src:innerValue
      add:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void
        src:"rows:1"
      add:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void
        src
          onfocus
            get-parent-widget:x:/../*/_event?value
            get-parent-widget:x:/-/*?value
            set-widget-property:x:/-/*?value
              class:col-md-12
            set-widget-property:x:/../*/_event?value
              rows:10
          onblur
            get-parent-widget:x:/../*/_event?value
            get-parent-widget:x:/-/*?value
            set-widget-property:x:/-/*?value
              class:col-md-4
            set-widget-property:x:/../*/_event?value
              rows:1
      lambda2lisp:x:/..for-each/*/_dp/#/*
      if:x:/-?value
        !=:@""""""
        set:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void/*/innerValue?value
          src:x:/././-?value
      set:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/void?name
        src:textarea

    /*
     * This is an invisibly rendered event handler, making sure we render the label
     * with the REAL event handler name, before we add the attribute control to properties
     */
    set:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/span/*/innerValue?value
      replace:x:/..for-each/*/_control/*/container/*/widgets/*/container/*/widgets/*/span/*/innerValue?value
        what:_on
        with:on

  /*
   * Adding properties wrapper to [create-widget]
   */
  add:x:/../*/create-widget/*/widgets/*/container/=wysiwyg-controls-attribute-wrapper/*/widgets
    src:x:/..for-each/*/_control/*

/*
 * Then [innerValue], which requires special handling, as a textarea HTML element
 */
for-each:x:/../*/sys42.get-widget/*/*/innerValue
  _control
    container
      class:col-md-6 form-group
      style:"clear:both;"
      widgets
        a
          href:#
          innerValue:innerValue ...
          onclick
            if
              fetch:x:/0/0/0?value
                get-widget-property:wysiwyg-control-innerValue
                  style
              ~:"display:none;"
              set-widget-property:wysiwyg-control-innerValue
                style:"display:inherit;"
              send-javascript:@"$('#wysiwyg-control-innerValue').focus().select();"
              set-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
                src:bool:true
            else
              set-widget-property:wysiwyg-control-innerValue
                style:"display:none;"
              set-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
        textarea:wysiwyg-control-innerValue
          class:form-control
          placeholder:New value ...
          style:"display:none;"
          rows:5
          innerValue
          oninit
            get-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
            if:x:/-/*?value.bool
              set-widget-property:x:/../*/_event?value
                style:"display:inherit;"
          onchange
            get-widget-property:x:/../*/_event?value
              value
            eval-x:x:/+/*
            sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
              _new-value:x:/../*/get-widget-property/*/*?value
  set:x:/..for-each/**/={element-name}?value
    src:x:/..for-each/*/_dp/#?name
  html-encode:x:/..for-each/*/_dp/#?value
  set:x:/..for-each/*/_control/*/*/*/textarea/*/innerValue?value
    src:x:/..for-each/*/html-encode?value
  add:x:/../*/create-widget/*/widgets
    src:x:/..for-each/*/_control/*


/*
 * Then 'Select parent widget button', unless it is wysiwyg main surface
 */
get-parent-widget:x:/../*/_widget?value
if:x:/-/*?value
  !=:wysiwyg-editor-surface
  eval-x:x:/+/*/*/*/*/**(/_widget|/innerValue)
  add:x:/../*/create-widget/*/widgets
    src
      container
        class:col-xs-12 form-group
        style:"clear:both;"
        widgets
          button
            class:btn btn-default
            style:"clear:both;"
            innerValue:@"<span class=""glyphicon glyphicon-arrow-up""></span> {0}"
              :x:/../*/get-parent-widget/*?value
            onclick
              _widget:x:/../*/_widget?value
              get-parent-widget:x:/../*/_widget?value
              sys42.wysiwyg-controls.select-widget:x:/../*/get-parent-widget/0?value
              sys42.wysiwyg-controls.databind-select-control:x:/../*/get-parent-widget/0?value


/*
 * Then [widgets], which also requires special handling
 */
for-each:x:/../*/sys42.get-widget/*/*/widgets
  _control
    container
      class:col-xs-12 form-group
      style:"clear:both;"
      widgets
        a
          href:#
          innerValue:Children widgets ...
          onclick
            get-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
            if:x:/-/*?value.bool
              not
              set-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
                src:bool:true
              sys42.wysiwyg-controls.create-widgets-for-selected
            else
              set-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
              clear-widget:wysiwyg-control-widgets
        container:wysiwyg-control-widgets
          oninit
            get-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
            if:x:/-/*?value.bool
              sys42.wysiwyg-controls.create-widgets-for-selected
          events

            /*
             * Creates the list of widgets [container] widget has
             */
            sys42.wysiwyg-controls.create-widgets-for-selected

              /*
               * Creating the 'add [container] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> container"
                title:Adds a [container] child widget to your currently selected widget
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:div
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'add [literal] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> literal"
                title:Adds a [literal] child widget to your currently selected widget
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:span
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'add [void] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> void"
                title:Adds a [void] child widget to your currently selected widget
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:br
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'clear selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:a
                class:btn btn-default prepend-left
                href:#
                innerValue:@"<span class=""glyphicon glyphicon-trash""></span> Clear widget"
                onclick
                  sys42.confirm-window
                    _header:Confirm clearing!
                    _body:@"<p>Are you sure you wish to completely <strong>clear widget's children widgets</strong>?</p>
<p>This will delete all widgets that are children of the currently selected widget! This action cannot be undone!</p>"
                    _onok
                      find-widget
                        _selected:true
                      get-children-widgets:x:/-/*?value
                      clear-widget:x:/../*/find-widget/*?value
                      sys42.wysiwyg-controls.databind-select-control
                      sys42.info-window:@"{0} widgets successfully delete from '{1}'"
                        :x:/../*/get-children-widgets/*/*?count
                        :x:/../*/find-widget/*?value
                        _time:more
                      clear-widget:wysiwyg-control-widgets
                      sys42.wysiwyg-controls.create-widgets-for-selected

              /*
               * Finding selected widget, and retrieving its children widgets
               */
              find-widget
                _selected:true
              get-children-widgets:x:/-/*?value

              /*
               * Making sure widget's list of children starts on new line
               */
              if:x:/-/*/*?count
                >:int:0
                create-void-widget
                  parent:wysiwyg-control-widgets
                  element:br

              /*
               * Looping through all children widget, creating a button ([a] type of button)
               * for each widget, allowing user to select any of these widgets by clicking the
               * button
               */
              for-each:x:/../*/get-children-widgets/*/*
                _widget
                  create-literal-widget
                    parent:wysiwyg-control-widgets
                    element:a
                    class:btn btn-default prepend-top
                    href:#
                    innerValue:innerValue:@"<span class=""glyphicon glyphicon-arrow-down""></span> {0} - {1}"
                      :x:/..for-each/*/_dp/#?name
                      :x:/..for-each/*/_dp/#?value
                    onclick
                      sys42.wysiwyg-controls.select-widget:x:/..for-each/*/_dp/#?value
                      sys42.wysiwyg-controls.databind-select-control:x:/..for-each/*/_dp/#?value
                eval-x:x:/-/*/*/innerValue|/-/*/*/onclick/*
                eval:x:/-2
  add:x:/../*/create-widget/*/widgets
    src:x:/..for-each/*/_control/*


/*
 * Creating properties edit widget, but first making sure
 * container to stuff properties into is visible, and deleting
 * any previously properties widgets
 */
clear-widget:wysiwyg-controls-properties
set-widget-property:wysiwyg-controls-properties
  visible:true
create-widget
  parent:wysiwyg-controls-properties
  widgets

    /*
     * Widget's ID
     */
    container
      class:col-md-4 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:id
              class:input-group-addon
            void:widget-property-id
              element:input
              type:text
              class:form-control
              placeholder:id ...
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                get-widget-property:x:/../*/_event?value
                  value
                if:x:/../*/get-widget-property/*/*?value
                  =:
                  sys42.info-window:You cannot have an empty id property of your element! Please supply an id value for your element!
                    _error:true
                    _time:more
                  set-widget-property:x:/../*/_event?value
                    class:error form-control
                  send-javascript:@"$('#widget-property-id').focus().select();"
                  return
                if
                  fetch:x:/0/0?value
                    widget-exist:x:/../*/get-widget-property/*/*?value
                  sys42.info-window:Another widget with the same id exists from before on page, please provide a unique id
                    _error:true
                    _time:more
                  set-widget-property:x:/../*/_event?value
                    class:error form-control
                  send-javascript:@"$('#widget-property-id').focus().select();"
                  return
                set-widget-property:x:/../*/_event?value
                  class:form-control
                eval-x:x:/+/*
                sys42.wysiwyg-controls.change-selected-widgets-property:id
                  _new-value:x:/../*/get-widget-property/*/*?value

            /*
             * Help button for [id] property of widget
             */
            a:wysiwyg-controls-id-help
              href:#
              class:input-group-addon input-group-addon-force-float
              innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span>"
              title:Help me with the [id] property please ...
              onclick
                sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/id-help.hl

    /*
     * Widget's element
     */
    container
      class:col-md-4 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:element
              class:input-group-addon
            void:widget-property-element
              element:input
              type:text
              class:form-control
              placeholder:element ...
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                sys42.wysiwyg-controls.change-selected-widgets-element
              events

                /*
                 * Returns true if [_arg] is a legal HTML element name
                 */
                sys42.wysiwyg-controls.is-legal-html-element
                  switch:x:/../*/_arg?value
                    case:object
                    case:embed
                      sys42.confirm-window
                        _header:Confirm usage!
                        _body:@"<img src=""/media/images/dangerous-shit.jpg"" style=""float:right;"" /><p>Seriously? Are you <strong>really going to use the &lt;object&gt; or &lt;embed&gt; element ...?</strong></p>
<blockquote><em>""The &lt;object&gt; element leads to ActiveX, ActiveX leads to Silverlight, Silverlight leads to the dark side!!""</em></blockquote>
<p>Do <strong>NOT USE THE OBJECT ELEMENT</strong>! It facilitates for virus and other malware being installed on your
user's computers! I will allow it, but only if I can get to do some serious whining and crying first!</p>
<p>I cannot possibly emphasize this strongly enough; &lt;object&gt; is seriously outdatet! Your apps <strong>will not work on iPhones, iPads,
Androids, probably not on Linux and Mac OS X</strong>, or any other modern computing platforms! In addition your users risks having viruses and other crapware installed!</p>
<p>Not even Microsoft suggests using the &lt;object&gt; element anymore ...!!</p>"
                        _onok
                          sys42.info-window:@"Please change your element back to something that's not considered a CRAPWARE HTML ELEMENT!"
                            _time:more
                            _error:true
                      return:bool:true
                    default
                      load-file:/system42/cms/page-editor/editors/controls/legal-elements.hl
                      if:x:@"/-/*/*/""{0}"""
                        :x:/../*/_arg?value
                        and:x:/../*/_arg?value
                          !=:_header
                        return:bool:true
                      return:bool:false

                /*
                 * Changes the HTML element of widget, according to textbox' value
                 */
                sys42.wysiwyg-controls.change-selected-widgets-element
                  get-widget-property:widget-property-element
                    value
                  sys42.wysiwyg-controls.is-legal-html-element:x:/../*/get-widget-property/*/*?value
                  if:x:/-?value
                    !=:bool:true
                    sys42.info-window:Under no circumstances I can allow you to change the HTML tag to render as such!
                      _time:more
                      _error:true
                    set-widget-property:widget-property-element
                      class:form-control error
                    send-javascript:@"$('#widget-property-element').select().focus();"
                    return
                  set-widget-property:widget-property-element
                    class:form-control
                  if:x:/../*/get-widget-property/*/*?value
                    =:
                    sys42.info-window:You cannot have an empty [element] property of your element! Please supply an [element] value for your element!
                      _error:true
                      _time:more
                    set-widget-property:widget-property-element
                      class:error form-control
                    send-javascript:@"$('#widget-property-element').focus().select();"
                    return
                  set-widget-property:widget-property-element
                    class:form-control
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.change-selected-widgets-property:element
                    _new-value:x:/../*/get-widget-property/*/*?value

            /*
             * Help button for [element] property of widget
             */
            a:wysiwyg-controls-element-help
              href:#
              class:input-group-addon input-group-addon-force-float
              innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span>"
              title:Help me with the [element] property please ...
              onclick
                sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/change-element.hl

    /*
     * Wraps all attributes (except [element] and [id]) of widget
     */
    container:wysiwyg-controls-attribute-wrapper
      style:"clear:both;"
      widgets

  events

    /*
     * Changes the given property [_arg] of the currently selected widget
     * to [_new-value]
     */
    sys42.wysiwyg-controls.change-selected-widgets-property
      get-widget-property:wysiwyg-select-control
        value
      if:x:/../*/_new-value?value

        /*
         * User provided a new value for property of widget
         */
        add:x:/+2
          src:x:/../*/_arg?value
        set:x:/+/0?value
          src:x:/../*/_new-value?value
        set-widget-property:x:/../*/get-widget-property/*/*?value
        sys42.wysiwyg-controls.databind-select-control
      else-if:x:/../*/_new-value

        /*
         * User provided a 'null value'
         */
        add:x:/+
          src:{0}
            :x:/../*/_arg?value
        set-widget-property:x:/../*/get-widget-property/*/*?value
      else

        /*
         * User did NOT provide a new value for property of widget.
         * But rather wanted to remove property entirely
         */
        add:x:/+
          src:{0}
            :x:/../*/_arg?value
        delete-widget-property:x:/../*/get-widget-property/*/*?value
        sys42.wysiwyg-controls.databind-select-control

/*
 * If caller requested that initial focus be set, we do so
 */
if:x:/../*/_initial-focus?value
  find-widget
    _element-name:x:/../*/_initial-focus?value
  send-javascript:@"$('#{0}').focus().select();"
    :x:/..if/*/find-widget/*?value

include-javascript:@"p5.keyPressChangeAttributeValue = function(e) {
  if(e.keyCode == 13) {
    $('#wysiwyg-controls-element-help').focus();
    return false;
  }
}"
