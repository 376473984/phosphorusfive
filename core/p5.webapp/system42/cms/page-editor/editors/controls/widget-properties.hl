/*
 * Shows the properties for a single selected widget
 */


/*
 * Fetching widget,without its children widgets
 */
sys42.get-widget:x:/../*/_widget?value
  _no-children:true


/*
 * Removing the parts that makes the widget selected in the 
 * WYSIWYG surface from the widget's definition
 */
set:x:@"/../*/sys42.get-widget/*/*(/_selected|/style/""={0}""|/onclick)"
  :"box-shadow: 5px 5px 5px #888888;border:dashed 1px black;"


/*
 * Setting initial values for widget's properties, first starting
 * out with the 'static properties', such as [element] and [id], which
 * all widgets have
 */
set:x:/../*/create-widget/*/widgets/*/container/**/void/=widget-property-element/*/value?value
  src:x:/../*/sys42.get-widget/*/*/element?value
set:x:/../*/create-widget/*/widgets/*/container/**/void/=widget-property-id/*/value?value
  src:x:/../*/_widget?value


/*
 * Then iterating all dynamically inserted properties of specific widget,
 * except [innerValue], which requires special handling, as a textarea HTML element
 */
for-each:x:/../*/sys42.get-widget/*/*(!/element!/innerValue!/widgets)
  _control
    container
      class:col-md-3 form-group
      widgets
        container
          class:input-group
          widgets
            a
              href:#
              element:div
              innerValue:{element-name}
              class:input-group-addon
              style:"color:#337ab7;"
              onclick
                _element-name:{element-name}
                eval-x:x:/+/*|/+/**/_eventWidget
                sys42.confirm-window
                  _header:Confirm deletion of property
                  _body:@"<p>Please confirm that you wish to <strong>remove the <em>'{0}'</em> attribute</strong> from widget! 
This might have consequence for the rendering of your widget!</p>
<p>Alternatively, you can choose to 'nullify' its value, meaning the attribute
will still be kept around, but have a 'null value'. This can be useful for [controls] properties of [video]
HTML elements for instance, that don't require a value, but need the property.</p>
<p>If you do not want to do either, please close the window at the top right corner with the 'X'.</p>"
                    :x:/../*/_element-name?value
                  _extra-buttons
                    button
                      class:btn btn-default
                      innerValue:Nullify value
                      title:Only 'nullify' value of property, but do not remove property
                      onclick
                        _eventWidget:x:/../*/_event?value
                        sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                          _new-value
                        send-javascript:@"$('#confirm-window').modal('toggle');"
                        find-widget
                          _element-name:{element-name}
                        set-widget-property:x:/-/*?value
                          value
                  _onok
                    _eventWidget:x:/../*/_event?value
                    sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                    get-parent-widget:x:/../*/_eventWidget?value
                    get-parent-widget:x:/-/*?value
                    delete-widget:x:/-/*?value
            void
              element:input
              type:text
              class:form-control
              placeholder:New value ...
              _element-name:{element-name}
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                get-widget-property:x:/../*/_event?value
                  value
                eval-x:x:/+/*
                sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
                  _new-value:x:/../*/get-widget-property/*/*?value
  set:x:/..for-each/**/={element-name}?value
    src:x:/..for-each/*/_dp/#?name
  set:x:/..for-each/*/_control/*/*/*/*/*/*/value?value
    src:x:/..for-each/*/_dp/#?value
  add:x:/../*/create-widget/*/widgets
    src:x:/..for-each/*/_control/*

/*
 * Then [innerValue], which requires special handling, as a textarea HTML element
 */
for-each:x:/../*/sys42.get-widget/*/*/innerValue
  _control
    container
      class:col-md-6 form-group
      style:"clear:both;"
      widgets
        a
          href:#
          innerValue:innerValue ...
          onclick
            if
              fetch:x:/0/0/0?value
                get-widget-property:wysiwyg-control-innerValue
                  style
              ~:"display:none;"
              set-widget-property:wysiwyg-control-innerValue
                style:"display:inherit;"
              send-javascript:@"$('#wysiwyg-control-innerValue').focus().select();"
              set-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
                src:bool:true
            else
              set-widget-property:wysiwyg-control-innerValue
                style:"display:none;"
              set-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
        textarea:wysiwyg-control-innerValue
          class:form-control
          placeholder:New value ...
          style:"display:none;"
          rows:5
          innerValue
          oninit
            get-session-value:sys42.wysiwyg-controls.innerValue-attribute-visible
            if:x:/-/*?value.bool
              set-widget-property:x:/../*/_event?value
                style:"display:inherit;"
          onchange
            get-widget-property:x:/../*/_event?value
              value
            eval-x:x:/+/*
            sys42.wysiwyg-controls.change-selected-widgets-property:{element-name}
              _new-value:x:/../*/get-widget-property/*/*?value
  set:x:/..for-each/**/={element-name}?value
    src:x:/..for-each/*/_dp/#?name
  html-encode:x:/..for-each/*/_dp/#?value
  set:x:/..for-each/*/_control/*/*/*/textarea/*/innerValue?value
    src:x:/..for-each/*/html-encode?value
  add:x:/../*/create-widget/*/widgets
    src:x:/..for-each/*/_control/*

/*
 * Then [widgets], which also requires special handling
 */
for-each:x:/../*/sys42.get-widget/*/*/widgets
  _control
    container
      class:col-xs-12 form-group
      style:"clear:both;"
      widgets
        a
          href:#
          innerValue:Children widgets ...
          onclick
            get-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
            if:x:/-/*?value.bool
              not
              set-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
                src:bool:true
              sys42.wysiwyg-controls.create-widgets-for-selected
            else
              set-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
              clear-widget:wysiwyg-control-widgets
        container:wysiwyg-control-widgets
          oninit
            get-session-value:sys42.wysiwyg-controls.widgets-attribute-visible
            if:x:/-/*?value.bool
              sys42.wysiwyg-controls.create-widgets-for-selected
          events

            /*
             * Creates the list of widgets [container] widget has
             */
            sys42.wysiwyg-controls.create-widgets-for-selected

              /*
               * Creating the 'add [container] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> container"
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:div
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'add [literal] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> literal"
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:span
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'add [void] widget to selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:button
                class:btn btn-default
                innerValue:@"<span class=""glyphicon glyphicon-plus""></span> void"
                onclick
                  find-widget
                    _selected:true
                  eval-x:x:/+/*
                  sys42.wysiwyg-controls.add-control:br
                    _parent:x:/../*/find-widget/*?value
                  sys42.info-window:@"Please change widget's id and element to what you wish"
                    _time:more
                  send-javascript:@"$('#widget-property-id').focus().select();"

              /*
               * Creating the 'clear selected widget' button
               */
              create-literal-widget
                parent:wysiwyg-control-widgets
                element:a
                class:btn btn-default
                href:#
                innerValue:@"<span class=""glyphicon glyphicon-trash""></span> clear widget"
                onclick
                  sys42.confirm-window
                    _header:Confirm clearing!
                    _body:@"<p>Are you sure you wish to completely <strong>clear widget's children widgets</strong>?</p>
<p>This will delete all widgets that are children of the currently selected widget! This action cannot be undone!</p>"
                    _onok
                      find-widget
                        _selected:true
                      get-children-widgets:x:/-/*?value
                      clear-widget:x:/../*/find-widget/*?value
                      sys42.wysiwyg-controls.databind-select-control
                      sys42.info-window:@"{0} widgets successfully delete from '{1}'"
                        :x:/../*/get-children-widgets/*/*?count
                        :x:/../*/find-widget/*?value
                        _time:more
                      clear-widget:wysiwyg-control-widgets
                      sys42.wysiwyg-controls.create-widgets-for-selected

              /*
               * Finding selected widget, and retrieving its children widgets
               */
              find-widget
                _selected:true
              get-children-widgets:x:/-/*?value

              /*
               * Making sure widget's list of children starts on new line
               */
              if:x:/-/*/*?count
                >:int:0
                create-void-widget
                  parent:wysiwyg-control-widgets
                  element:br

              /*
               * Looping through all children widget, creating a button ([a] type of button)
               * for each widget, allowing user to select any of these widgets by clicking the
               * button
               */
              for-each:x:/../*/get-children-widgets/*/*
                _widget
                  create-literal-widget
                    parent:wysiwyg-control-widgets
                    element:a
                    class:btn btn-default prepend-top
                    href:#
                    innerValue:{0} - {1}
                      :x:/..for-each/*/_dp/#?name
                      :x:/..for-each/*/_dp/#?value
                    onclick
                      sys42.wysiwyg-controls.select-widget:x:/..for-each/*/_dp/#?value
                      sys42.wysiwyg-controls.databind-select-control:x:/..for-each/*/_dp/#?value
                eval-x:x:/-/*/*/innerValue|/-/*/*/onclick/*
                eval:x:/-2
  add:x:/../*/create-widget/*/widgets
    src:x:/..for-each/*/_control/*


/*
 * Creating properties edit widget, but first making sure
 * container to stuff properties into is visible, and deleting
 * any previously properties widgets
 */
clear-widget:wysiwyg-controls-properties
set-widget-property:wysiwyg-controls-properties
  visible:true
create-widget
  parent:wysiwyg-controls-properties
  class:clearfix
  widgets

    /*
     * Widget's ID
     */
    container
      class:col-md-3 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:id
              class:input-group-addon
            void:widget-property-id
              element:input
              type:text
              class:form-control
              placeholder:id ...
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                get-widget-property:x:/../*/_event?value
                  value
                if:x:/../*/get-widget-property/*/*?value
                  =:
                  sys42.info-window:You cannot have an empty id property of your element! Please supply an id value for your element!
                    _error:true
                    _time:more
                  set-widget-property:x:/../*/_event?value
                    class:error form-control
                  send-javascript:@"$('#widget-property-id').focus().select();"
                  return
                if
                  fetch:x:/0/0?value
                    widget-exist:x:/../*/get-widget-property/*/*?value
                  sys42.info-window:Another widget with the same id exists from before on page, please provide a unique id
                    _error:true
                    _time:more
                  set-widget-property:x:/../*/_event?value
                    class:error form-control
                  send-javascript:@"$('#widget-property-id').focus().select();"
                  return
                set-widget-property:x:/../*/_event?value
                  class:form-control
                eval-x:x:/+/*
                sys42.wysiwyg-controls.change-selected-widgets-property:id
                  _new-value:x:/../*/get-widget-property/*/*?value

    /*
     * Widget's element
     */
    container
      class:col-md-3 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:element
              class:input-group-addon
            void:widget-property-element
              element:input
              type:text
              class:form-control
              placeholder:element ...
              value
              onkeypress:@"return p5.keyPressChangeAttributeValue(event);"
              onchange
                get-widget-property:x:/../*/_event?value
                  value
                sys42.wysiwyg-controls.is-legal-html-element:x:/../*/get-widget-property/*/*?value
                if:x:/-?value
                  !=:bool:true
                  sys42.info-window:Under no circumstances I can allow you to change the HTML tag to render as such!
                    _time:more
                    _error:true
                  set-widget-property:x:/../*/_event?value
                    class:form-control error
                  send-javascript:@"$('#widget-property-element').select().focus();"
                  return
                set-widget-property:x:/../*/_event?value
                  class:form-control
                if:x:/../*/get-widget-property/*/*?value
                  =:
                  sys42.info-window:You cannot have an empty [element] property of your element! Please supply an [element] value for your element!
                    _error:true
                    _time:more
                  set-widget-property:x:/../*/_event?value
                    class:error form-control
                  send-javascript:@"$('#widget-property-element').focus().select();"
                  return
                set-widget-property:x:/../*/_event?value
                  class:form-control
                eval-x:x:/+/*
                sys42.wysiwyg-controls.change-selected-widgets-property:element
                  _new-value:x:/../*/get-widget-property/*/*?value
              events

                /*
                 * Returns true if [_arg] is a legal HTML element name
                 */
                sys42.wysiwyg-controls.is-legal-html-element
                  switch:x:/../*/_arg?value
                    case:address
                    case:article
                    case:footer
                    case:header
                    case:blockquote
                    case:h1
                    case:h2
                    case:h3
                    case:h4
                    case:h5
                    case:h6
                    case:hgroup
                    case:nav
                    case:section
                    case:dd
                    case:div
                    case:dl
                    case:dt
                    case:figcaption
                    case:figure
                    case:hr
                    case:li
                    case:main
                    case:ol
                    case:p
                    case:pre
                    case:ul
                    case:abbr
                    case:b
                    case:bdi
                    case:bdo
                    case:br
                    case:cite
                    case:code
                    case:data
                    case:dfn
                    case:em
                    case:i
                    case:kbd
                    case:mark
                    case:q
                    case:rp
                    case:rt
                    case:rtc
                    case:ruby
                    case:s
                    case:samp
                    case:small
                    case:span
                    case:strong
                    case:sub
                    case:sup
                    case:time
                    case:u
                    case:var
                    case:wbr
                    case:area
                    case:audio
                    case:map
                    case:track
                    case:video
                    case:embed
                    case:param
                    case:source
                    case:canvas
                    case:noscript
                    case:script
                    case:del
                    case:ins
                    case:caption
                    case:col
                    case:colgroup
                    case:table
                    case:tbody
                    case:td
                    case:tfoot
                    case:th
                    case:thead
                    case:tr
                    case:button
                    case:datalist
                    case:fieldset
                    case:form
                    case:input
                    case:keygen
                    case:label
                    case:legend
                    case:meter
                    case:optgroup
                    case:option
                    case:output
                    case:progress
                    case:select
                    case:details
                    case:dialog
                    case:menu
                    case:menuitem
                    case:summary
                    case:content
                    case:element
                    case:shadow
                    case:template
                      return:bool:true
                    case:object
                      sys42.confirm-window
                        _header:Confirm usage!
                        _body:@"<img src=""/media/images/dangerous-shit.jpg"" style=""float:right;"" /><p>Seriously? Are you <strong>really going to use the &lt;object&gt; element ...?</strong></p>
<blockquote><em>""The &lt;object&gt; element leads to ActiveX, ActiveX leads to Silverlight, Silverlight leads to the dark side!!""</em></blockquote>
<p>Do <strong>NOT USE THE OBJECT ELEMENT</strong>! It facilitates for virus and other malware being installed on your
user's computers! I will allow it, but only if I can get to do some serious whining and crying first!</p>
<p>I cannot possibly emphasize this strongly enough; &lt;object&gt; is seriously outdatet! Your apps <strong>will not work on iPhones, iPads,
Androids, probably not on Linux and Mac OS X</strong>, or any other modern computing platforms! In addition your users risks having viruses and other crapware installed!</p>
<p>Not even Microsoft suggests using the &lt;object&gt; element anymore ...!!</p>"
                        _onok
                          sys42.info-window:@"Please change your element back to something that's not considered a CRAPWARE HTML ELEMENT!"
                            _time:more
                            _error:true
                      return:bool:true
                    default
                      return:bool:false

  events

    /*
     * Changes the given property [_arg] of the currently selected widget
     * to [_new-value]
     */
    sys42.wysiwyg-controls.change-selected-widgets-property
      get-widget-property:wysiwyg-select-control
        value
      if:x:/../*/_new-value?value

        /*
         * User provided a new value for property of widget
         */
        add:x:/+
          src:@"{0}:@""{1}"""
            :x:/../*/_arg?value
            :x:/../*/_new-value?value
        set-widget-property:x:/../*/get-widget-property/*/*?value
        sys42.wysiwyg-controls.databind-select-control
      else-if:x:/../*/_new-value

        /*
         * User provided a 'null value'
         */
        add:x:/+
          src:{0}
            :x:/../*/_arg?value
        set-widget-property:x:/../*/get-widget-property/*/*?value
      else

        /*
         * User did NOT provide a new value for property of widget.
         * But rather wanted to remove property entirely
         */
        add:x:/+
          src:{0}
            :x:/../*/_arg?value
        delete-widget-property:x:/../*/get-widget-property/*/*?value
        sys42.wysiwyg-controls.databind-select-control

/*
 * If caller requested that initial focus be set, we do so
 */
if:x:/../*/_initial-focus?value
  find-widget
    _element-name:x:/../*/_initial-focus?value
  send-javascript:@"$('#{0}').focus().select();"
    :x:/..if/*/find-widget/*?value

include-javascript:@"p5.keyPressChangeAttributeValue = function(e) {
  if(e.keyCode == 13) {
    $('#wysiwyg-button-add-attribute').focus();
    return false;
  }
}"
