/*
 * Shows the "help" window for adding attributes to widget
 */


/*
 * Making sure we delete any previously created (but non-visible)
 * modal HTML add attribute dialogues
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-attributes-window
  delete-widget:wysiwyg-controls-attributes-window


/*
 * Making sure dialogues becomes initially visible, and that we give initial
 * focus to "OK" button
 */
send-javascript:@"$('#wysiwyg-controls-attributes-window').modal('toggle');
$('#wysiwyg-controls-attributes-window').on('shown.bs.modal', function () {
    $('#wysiwyg-controls-attributes-window-ok').focus();
})"


/*
 * Creating actual modal Widget
 */
create-widget:wysiwyg-controls-attributes-window
  class:modal fade
  widgets
    container
      class:modal-dialog
      role:document
      widgets
        container
          class:modal-content
          widgets

            /*
             * Header of dialog
             */
            container
              class:modal-header
              widgets

                // Close button
                literal
                  element:button
                  class:close
                  data-dismiss:modal
                  aria-label:Close
                  innerValue:&times;

                // Header of our modal window
                text:@"<h4>Adding attributes to your widgets</h4>"

            /*
             * Body of dialog
             */
            container
              class:modal-body
              widgets

                // Actual literal widget containing body of dialogue
                container:wysiwyg-controls-attributes-window-body
                  element:div
                  widgets
                    text:@"<p>Attributes changes the state of your widgets (HTML elements), and are additional data
provided to the browser, to for instance, give clues about how to render your widget.</p>
<p>Except from all attributes starting with <em>""data-""</em>, only a specific list of attributes 
are legal to add to your widget. Click the button below to see this list, which might vary according 
to what HTML [element] you choose to render your widget with.</p>"
                    button
                      class:btn btn-default
                      innerValue:@"<span class=""glyphicon glyphicon-info-sign""></span> Suggest attribute"
                      onclick
                        if
                          fetch:x:/0/0/0?value
                            get-widget-property:wysiwyg-window-list-of-attributes
                              visible
                          not
                          set-widget-property:wysiwyg-window-list-of-attributes
                            visible:true
                        else
                          set-widget-property:wysiwyg-window-list-of-attributes
                            visible:false
                    table:wysiwyg-window-list-of-attributes
                      visible:false
                      class:prepend-top table table-hover
                      oninit
                        load-file:/system42/cms/page-editor/editors/controls/legal-attributes.hl
                        for-each:x:/../*/load-file/*/*
                          html-encode:x:/./*/_dp/#?value
                          if:x:/..for-each/*/_dp/#?name
                            =:_header

                            /*
                             * New section of element type
                             */
                            set:x:/+2/**/={element-header}?value
                              src:x:/..for-each/*/_dp/#?value
                            set:x:/+/**/={element-description}?value
                              src:x:/..for-each/*/_dp/#/*?value
                            create-widget
                              parent:wysiwyg-window-list-of-attributes
                              element:tr
                              style:"background-color:transparent !important;"
                              widgets
                                td
                                  colspan:2
                                  widgets
                                    h3
                                      innerValue:{element-header}
                                    p
                                      innerValue:{element-description}
                          else
                            set:x:/..else/*/create-widget/**/={element-name}?value
                              src:x:/..for-each/*/_dp/#?name
                            set:x:/..else/*/create-widget/**/innerValue/={element-description}?value
                              src:x:/..for-each/*/_dp/#?value
                            create-widget
                              parent:wysiwyg-window-list-of-attributes
                              element:tr
                              style:"cursor:pointer;"
                              _element:{element-name}
                              onclick

                                /*
                                 * Figuring out which attribute user clicked
                                 */
                                get-widget-property:x:/../*/_event?value
                                  _element
                                if
                                  fetch:x:/0/0?value
                                    index-of:x:/../*/get-widget-property/*/*?value
                                      what:on
                                  =:int:0
                                  set:x:/../*/get-widget-property/*/*?value
                                    src:_{0}
                                      :x:/../*/get-widget-property/*/*?value

                                /*
                                 * Finding selected widget
                                 */
                                find-widget
                                  data-selected:true

                                /*
                                 * Adding that attribute to currently selected widget
                                 */
                                add:x:/+
                                  src:x:/../*/get-widget-property/*/*?value
                                set-widget-property:x:/../*/find-widget/*?value
                                set:x:/+2/*/_widget?value
                                  src:x:/../*/find-widget/*?value
                                eval-x:x:/+/*
                                sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/widget-properties.hl
                                  _widget
                                  _initial-focus:x:/../*/get-widget-property/[0,1]/*/*?value
                                sys42.info-window:Please provide a value, if any, for your attribute!

                                /*
                                 * Making sure we close modal window
                                 */
                                send-javascript:@"$('#wysiwyg-controls-attributes-window').modal('toggle');"
                              widgets
                                td
                                  widgets
                                    strong
                                      innerValue:{element-name}
                                td
                                  widgets
                                    small
                                      innerValue:{element-description}
                      widgets
                        

            /*
             * Footer of dialog (contains OK and Cancel buttons)
             */
            container
              class:modal-footer
              widgets
                literal:wysiwyg-controls-attributes-window-ok
                  element:button
                  class:btn btn-default
                  innerValue:OK
                  onclick

                    // Hiding dialog
                    send-javascript:@"$('#wysiwyg-controls-attributes-window').modal('toggle');"
