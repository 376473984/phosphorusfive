/*
 * Shows the widgets that allows user to edit an Active Event subscriber
 */


/*
 * Finding selected widget, and retrieving the specified lambda event from it
 */
find-widget
  data-selected
add:x:/+
  src:x:/../*/_event-name?value
get-widget-lambda-event:x:/../*/find-widget/[0,1]/*?value

/*
 * Checking if Active Event is already edited, and if so, simply destroy its editor,
 * and returning immediately, to provide "toggle edit event functionality"
 */
find-widget:wysiwyg-controls-events
  _event-name-editor:x:/../*/_event-name?value
if:x:/-/*
  delete-widget:x:/./-/*?value
  return:bool:false


/*
 * Converting Active Event to lisp, and then making sure the textarea below show its code
 * as its value, in addition to having label showing name of Active Event being edited
 */
lambda2lisp:x:/../*/get-widget-lambda-event/*/*/*
set:x:/../*/create-widget/**/literal/*/element/=textarea/./*/value?value
  src:x:/../*/lambda2lisp?value


/*
 * Making sure we supply the Active Event name to the widget, such that 
 * we know which event is being edited
 */
set:x:/../*/create-widget/*/_event-name-editor?value
  src:x:/../*/_event-name?value


/*
 * Creating widget to show Active Event content, making sure we pass in
 * event name to nodes needing it
 */
set:x:/+2/**/_event-name?value
  src:x:/../*/_event-name?value
eval-x:/+/*/after
create-widget
  after:x:/../*/_after?value
  _event-name-editor
  element:tr
  style:"background-color:Transparent !important;"
  widgets
    td
      widgets
        literal
          element:textarea
          class:form-control
          rows:10
          placeholder:Event content ...
          value
          oninit

            /*
             * Making sure textarea is replaced by a Hyperlisp CodeMirror instance
             */
            set:x:/+/*/*?name
              src:x:/../*/_event?value
            sys42.execute-lisp-file:/system42/editors/codemirror/include-codemirror.hl
              _editor
                execute_input
                  mode:hyperlisp
                  autocomplete:true
                  autofocus:true
                  fullscreen:false
                  raise-update:true
                  height:400px

          _onupdate

            /*
             * Retrieving new Hyperlisp content from CodeMirror textarea
             */
            get-widget-property:x:/../*/_event?value
              value
            find-first-ancestor-widget:x:/../*/_event?value
              _event-name-editor
            get-widget-property:x:/-/*?value
              _event-name-editor

            /*
             * Retrieving selected widget in WYSIWYG surface, Active Event name, and changing lambda
             * event for currently selected widget
             */
            lisp2lambda:x:/../*/get-widget-property/[0,1]/*/*?value
            find-widget
              data-selected
            add:x:/../*/set-widget-lambda-event
              src:x:/../*/get-widget-property/[1,2]/*/*?value
            add:x:/../*/set-widget-lambda-event/*
              src:x:/../*/lisp2lambda/*
            set-widget-lambda-event:x:/../*/find-widget/*?value

        /*
         * Delete Action Subscriber button
         */
        container
          style:"text-align:right;"
          widgets

            /*
             * Opens up a wizard for user to create his Action Subscriber
             */
            button:wysiwyg-controls-create-hyperlisp-wizard
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-info-sign""></span> Wizard"
              onclick
                find-first-ancestor-widget:x:/../*/_event?value
                  element:td
                find-widget:x:/-/*?value
                  element:textarea
                set:x:/../*/sys42.execute-lisp-file/**/_code-mirror?value
                  src:x:/./-/*?value
                sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/create-hyperlisp-wizard.hl
                  _code-mirror
                  _onok
                    _code-mirror
                    get-widget-property:x:/-?value
                      value
                    lisp2lambda:x:/-/*/*?value
                    set:x:/-/*//
                    add:x:/../*/lisp2lambda
                      lisp2lambda:x:/../*/_code?value
                    lambda2lisp:x:/../*/lisp2lambda/*
                    set-widget-property:x:/../*/_code-mirror?value
                      value:x:/../*/lambda2lisp?value
                    raise-widget-ajax-event:x:/../*/_code-mirror?value
                      _onupdate
                    send-javascript:@"window.{0}.getDoc().setValue(p5.$('{0}').el.value);"
                      :x:/../*/_code-mirror?value

            /*
             * Deletes subscriber
             */
            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-trash""></span> Delete subscriber"
              onclick
                set:x:/+/**/_event-button?value
                  src:x:/../*/_event?value
                sys42.confirm-window
                  _header:Confirm action!
                  _body:<p>Are you sure you wish to delete this Action Subscriber? This will <strong>delete all code associated with action</strong>!</p>
                  _onok
                    _event-button
                    find-first-ancestor-widget:x:/../*/_event-button?value
                      _event-name-editor
                    get-widget-property:x:/-/*?value
                      _event-name-editor
                    find-widget
                      data-selected
                    add:x:/+
                      src:x:/../*/get-widget-property/*/*?value
                    set-widget-lambda-event:x:/../*/find-widget/[0,1]/*?value
                    delete-widget:x:/../*/find-first-ancestor-widget/*?value
                    find-widget
                      _event-name:x:/../*/get-widget-property/*/*?value
                    delete-widget:x:/-/*?value

/*
 * Returning success to indicate event was set into "edit mode"
 */
return:bool:true
