/*
 * Shows the modal dialog that allows user to
 * select an existing [controls] page, and use
 * as a "User control"
 */


/*
 * Making sure we delete any previously created (but non-visible)
 * modal HTML element dialogues
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-usercontrol-window
  delete-widget:wysiwyg-controls-usercontrol-window


/*
 * Making sure dialogues becomes initially visible, and that we give initial
 * focus to "Close" button
 */
send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');
$('#wysiwyg-controls-usercontrol-window').on('shown.bs.modal', function () {
    $('#wysiwyg-controls-usercontrol-window-ok').focus();
})"

/*
 * Making sure we pass in our creational statement into "Select" button
 * for page
 */
set:x:/../*/create-widget/**/_create-widget
  src:x:/../*/_create-widget

/*
 * Creating actual modal Widget
 */
create-widget:wysiwyg-controls-usercontrol-window
  class:modal fade
  widgets
    container
      class:modal-dialog
      role:document
      widgets
        container
          class:modal-content
          widgets

            /*
             * Header of dialog
             */
            container
              class:modal-header
              widgets

                /*
                 * Help button
                 */
                button
                  class:close
                  innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span>"
                  onclick
                    set-widget-property:x:/../*/_event?value
                      visible:false
                    set-widget-property:wysiwyg-controls-usercontrol-help
                      visible:true
                    set-widget-property:wysiwyg-controls-usercontrol-main-text
                      visible:false

                // Header of our modal window
                text:@"<h4>Please choose an existing [controls] page</h4>"

            /*
             * Body of dialog
             */
            container
              class:modal-body
              widgets

                // Actual literal widget containing body of dialogue
                container:wysiwyg-controls-usercontrol-window-body
                  element:div
                  widgets
                    literal:wysiwyg-controls-usercontrol-main-text
                      innerValue:@"Select the usercontrol you wish to include on your page ..."
                    literal:wysiwyg-controls-usercontrol-help
                      visible:false
                      innerValue:@"<img src=""/media/images/marvin-headshot.png"" style=""float:right;"" /><p>Usercontrols gives you the possibility of inserting existing [controls] pages,
as reusable objects, into other [controls] pages. This allows you to incrementally build functionality, the same way you would with LEGO. 
Please select which existing page you would like to use below.</p>
<p>Notice that a usercontrol won't render correctly in the design surface. If you wish to view how it will look like, you'll have to 
click <em>""View""</em> to see your page, after saving it.</p>
<p>If you wish, you can modify your usercontrol's <strong>[oninit]</strong> event handler after creating it, to modify its behavior,
and which page to load, etc. But this requires some Hyperlisp knowledge.</p>
<p>When adding a usercontrol, a wrapper [container] control is created. The actual usercontrol [controls] page, will be rendered inside
of this [container] control. Please take care when adding usercontrols, such that you do not create recursive dependencies in any ways. 
Meaning, usercontrols that are referencing the page you are creating, for instance. Neither directly, nor indirectly. You can however
add the same page as a usercontrol, multiple times, without problems, to the same page, as long as none of your controls have <em>""static ids""</em>. 
Meaning, they must all use the automatically assigned ID.</p>
<p>A usercontrol will also render to the client with the attribute <strong>""data-usercontrol='true'""</strong>, which allows you to easily access
usercontrols from client-side JavaScript and CSS. If you remove this attribute from your wrapper widget, you will no longer be able to
see your usercontrol at all in the design surface, but it will still function 100% correctly, and be possible to select from the Widget 
select dropdown list.</p>
<p>You can also add widgets to a usercontrol, but by default, everything you add to a usercontrol, will be rendered at the beginning of it. A
usercontrol in general terms, behaves exactly like any other container widget in those regards. You can also change its CSS class, or add any
other properties to it you wish. If you modify the usercontrol's creation script,
in the [oninit] event handler, you can change this behavior, and have your added controls intermingled with the usercontrol's content, 
any ways you see fit. But this yet again, requires some Hyperlisp knowledge.</p>"
                    table:wysiwyg-window-list-of-usercontrols
                      class:prepend-top table table-hover
                      oninit
                        select-data:x:/*/*/p5.page/*/type/=controls/.
                        get-widget-property:page-url
                          value
                        set:x:@"/../*/select-data/*/""={0}"""
                          :x:/../*/get-widget-property/*/*?value
                        _sys42.show-code-window:x:/..
                        for-each:x:/../*/select-data/*
                          eval-x:x:/+/**(/href|/innerValue|/_page-id)
                          create-widget
                            parent:wysiwyg-window-list-of-usercontrols
                            element:tr
                            widgets
                              td
                                widgets
                                  a
                                    target:_blank
                                    href:x:/..for-each/*/_dp/#?value
                                    innerValue:x:/..for-each/*/_dp/#/*/name?value
                                    title:Click to preview, realise page will be included *WITHOUT* its CMS template though, as a Usercontrol
                              td
                                widgets
                                  button
                                    class:btn btn-default
                                    style:"width:100%;"
                                    innerValue:@"<span class=""glyphicon glyphicon-ok""></span>"
                                    title:Click to select page as Usercontrol
                                    onclick
                                      _create-widget
                                      _page-id:x:/..for-each/*/_dp/#?value
                                      eval-x:x:/+/*/*/*/select-data/*//
                                      add:x:/../*/_create-widget/*
                                        src
                                          oninit
                                            select-data:x:@"/*/*/p5.page/""={0}""/*/controls?value.node"
                                              :x:/../*/_page-id?value
                                            set:x:/-/*?name
                                              rel-src:create-{0}-widget
                                                :x:?name
                                            add:x:/../*/select-data/*
                                              src:"parent:{0}"
                                                :x:/../*/_event?value
                                            _design-time-remover
                                            set:x:/../*/select-data/**(/~container/*/~on|/~literal/*/~on|/~void/*/~on)!(/./*/data-usercontrol/./*/oninit)
                                            eval:x:/../*/select-data
                                      add:x:/../*/_create-widget
                                        src
                                          set:x:/..?value
                                            src:x:/../*/create-widget?value
                                      add:x:/../*/_create-widget/*
                                        src:"data-usercontrol:true"
                                      eval:x:/../*/_create-widget
                                      sys42.wysiwyg-controls.select-widget:x:/-?value
                                      sys42.wysiwyg-controls.databind-select-control
                                      send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');"
                                      sys42.info-window:@"Please change widget's id and element to what you wish"
                                        _time:more
                                      send-javascript:@"$('#widget-property-id').focus().select();"
                      widgets
                        thead
                          innerValue:@"<tr><th>Page name</th><th style=""width:100px;"">Select</th></tr>"
                        

            /*
             * Footer of dialog (contains OK and Cancel buttons)
             */
            container
              class:modal-footer
              widgets
                literal:wysiwyg-controls-usercontrol-window-ok
                  element:button
                  class:btn btn-default
                  innerValue:Close
                  onclick

                    // Hiding dialog
                    send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');"
