/*
 * Shows the modal dialog that allows user to
 * select an existing [controls] page, and use
 * as a "User control"
 */


/*
 * Making sure we delete any previously created (but non-visible)
 * modal HTML element dialogues
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-usercontrol-window
  delete-widget:wysiwyg-controls-usercontrol-window


/*
 * Making sure dialogues becomes initially visible, and that we give initial
 * focus to "Close" button
 */
send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');
$('#wysiwyg-controls-usercontrol-window').on('shown.bs.modal', function () {
    $('#wysiwyg-controls-usercontrol-window-ok').focus();
})"

/*
 * Making sure we pass in our creational statement into "Select" button
 * for page
 */
set:x:/../*/create-widget/**/_create-widget
  src:x:/../*/_create-widget

/*
 * Creating actual modal Widget
 */
create-widget:wysiwyg-controls-usercontrol-window
  class:modal fade
  widgets
    container
      class:modal-dialog
      role:document
      widgets
        container
          class:modal-content
          widgets

            /*
             * Header of dialog
             */
            container
              class:modal-header
              widgets

                /*
                 * Help button
                 */
                button
                  class:close
                  innerValue:@"<span class=""glyphicon glyphicon-question-sign""></span>"
                  onclick
                    set-widget-property:x:/../*/_event?value
                      visible:false
                    set-widget-property:wysiwyg-controls-usercontrol-help
                      visible:true
                    set-widget-property:wysiwyg-controls-usercontrol-main-text
                      visible:false

                // Header of our modal window
                text:@"<h4>Please choose an existing [controls] page</h4>"

            /*
             * Body of dialog
             */
            container
              class:modal-body
              widgets

                // Actual literal widget containing body of dialogue
                container:wysiwyg-controls-usercontrol-window-body
                  element:div
                  widgets
                    literal:wysiwyg-controls-usercontrol-main-text
                      innerValue:@"Select the usercontrol you wish to include on your page ..."
                    literal:wysiwyg-controls-usercontrol-help
                      visible:false
                      innerValue:@"<img src=""/media/images/marvin-headshot.png"" style=""float:right;"" />
<p>Usercontrols gives you the possibility of inserting existing [controls] pages,
as reusable objects, into other [controls] pages. This allows you to incrementally build functionality, the same way you would with LEGO.</p>
<p>In such a regard, a usercontrol is simply another [controls] page. If you wish to have your page recognised as a reusable usercontrol, 
your page must start with the name <strong>""Usercontrol -&nbsp;""</strong>. Otherwise, it won't be recognised as a reusable usercontrol.</p>
<p>When adding a usercontrol, a wrapper [container] control is created. The actual usercontrol [controls] page, will be rendered inside
of this [container] control. Please take care when adding usercontrols, such that you do not create recursive dependencies in any ways. 
Meaning, usercontrols that are referencing the page you are creating. Neither directly, nor indirectly.</p>
<p>You can however add up the same usercontrol, multiple times, without problems, to the same page, <strong>as long as none of your controls 
inside your usercontrol have <em>""static ids""</em></strong>. Meaning, they must all use the automatically assigned id.</p>
<p>A usercontrol will also render to the client with the attribute <strong>""data-usercontrol""</strong>, which allows you to easily access
your usercontrols from client-side JavaScript and CSS.</p>
<p>Please select the usercontrol you wish to include on your page from the list below ...</p>"
                    table:wysiwyg-window-list-of-usercontrols
                      class:prepend-top table table-hover
                      oninit
                        select-data:x:/*/*/p5.page/*/type/=controls/.
                        get-widget-property:page-url
                          value
                        set:x:@"/../*/select-data/*/""={0}"""
                          :x:/../*/get-widget-property/*/*?value
                        for-each:x:/../*/select-data/*
                          if
                            fetch:x:/0/0?value
                              index-of:x:/..for-each/*/_dp/#/*/name?value
                                what:"Usercontrol - "
                            !=:int:0
                            continue
                          eval-x:x:/+/**(/href|/innerValue|/_page-id)
                          create-widget
                            parent:wysiwyg-window-list-of-usercontrols
                            element:tr
                            widgets
                              td
                                widgets
                                  a
                                    target:_blank
                                    href:x:/..for-each/*/_dp/#?value
                                    innerValue:x:/..for-each/*/_dp/#/*/name?value
                                    title:Click to preview, realise page will be included *WITHOUT* its CMS template though, as a Usercontrol
                              td
                                widgets
                                  button
                                    class:btn btn-default
                                    style:"width:100%;"
                                    innerValue:@"<span class=""glyphicon glyphicon-ok""></span>"
                                    title:Click to select page as Usercontrol
                                    onclick
                                      _create-widget
                                      _page-id:x:/..for-each/*/_dp/#?value
                                      eval-x:x:/+/*/*/*/select-data/*//
                                      add:x:/../*/_create-widget/*
                                        src
                                          oninit
                                            select-data:x:@"/*/*/p5.page/""={0}""/*/controls?value.node"
                                              :x:/../*/_page-id?value
                                            set:x:/-/*?name
                                              rel-src:create-{0}-widget
                                                :x:?name
                                            add:x:/../*/select-data/*
                                              src:"parent:{0}"
                                                :x:/../*/_event?value
                                            /*
                                             * This next node is here such that we can "undo" the modifications to the [oninit] event handler
                                             * of usercontrols when page is being saved
                                             */
                                            _design-time-remover

                                            /*
                                             * Deletes all [onXXX] event handlers, except [oninit] event handlers for usercontrols, by looping through
                                             * everything that's a "close match", for then to verify it's an "exact match" for an event
                                             */
                                            for-each:x:/../*/select-data/**(/~container/*/~on(!/./*/data-usercontrol/./*/oninit)|/~literal/*/~on|/~void/*/~on)

                                              /*
                                               * Checking that node's name starts with "on", has at least three more characters, which must be a-z. Meaning
                                               * stuff like [data-foo] and [onX] and [onfoo3] is ignored. Which should result in removing all DOM event handlers,
                                               * leaving the rest alone
                                               */
                                              if
                                                fetch:x:/0/0/0
                                                  match:x:/..for-each/*/_dp/#?name
                                                    what:regex:/^on[a-z]{3}$/
                                                set:x:/..for-each/*/_dp/#

                                            /*
                                             * Making sure we recursively call "self" for each nested usercontrol inside of current usercontrol
                                             */
                                            set:x:/+/*/_node?value
                                              src:x:/../*/select-data
                                            sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/helpers/process-widgets-event-handlers.hl
                                              _node
                                            eval:x:/../*/select-data
                                      add:x:/../*/_create-widget
                                        src
                                          set:x:/..?value
                                            src:x:/../*/create-widget?value
                                      add:x:/../*/_create-widget/*
                                        src:@"data-usercontrol:@""{0}"""
                                          :x:/../*/_create-widget/**/oninit/*/select-data/*/?value

                                      /*
                                       * Finding custom properties of usercontrol, which is done by evaluating
                                       * [select-data] invocation of usercontrol, and retrieving all attributes
                                       * that starts with [_data-], treating these as "settings" for usercontrol,
                                       * by adding them to main usercontrol [container] wrapper, as attributes
                                       */
                                      _eval-select
                                        insert-before:x:
                                          src:x:/../*/select-data/*/**(/literal|/void|/container)/*/~_data-
                                      insert-before:x:/-/0
                                        src:x:/../*/_create-widget/*/create-widget/*/oninit/*/select-data
                                      eval:x:/../*/_eval-select
                                      add:x:/../*/_create-widget/*/create-widget
                                        src:x:/./-/*

                                      /*
                                       * Creating widget, by attaching it to WYSIWYG surface
                                       */
                                      eval:x:/../*/_create-widget

                                      /*
                                       * Selecting widget, and re-populating select widget dropdown list
                                       */
                                      sys42.wysiwyg-controls.select-widget:x:/-?value
                                      sys42.wysiwyg-controls.databind-select-widget-dropdown

                                      /*
                                       * Making modal window invisible, showing some information to user, and making sure id
                                       * property has focus
                                       */
                                      send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');"
                                      sys42.info-window:@"Please change widget's id and element to what you wish"
                                        _time:more
                                      send-javascript:@"$('#widget-property-id').focus().select();"
                      widgets
                        thead
                          innerValue:@"<tr><th>Page name</th><th style=""width:100px;"">Select</th></tr>"
                        

            /*
             * Footer of dialog (contains OK and Cancel buttons)
             */
            container
              class:modal-footer
              widgets
                literal:wysiwyg-controls-usercontrol-window-ok
                  element:button
                  class:btn btn-default
                  innerValue:Close
                  onclick

                    // Hiding dialog
                    send-javascript:@"$('#wysiwyg-controls-usercontrol-window').modal('toggle');"
