/*
 * Shows the modal dialog that allows user to create a new widget, by
 * following a wizard
 */



/*
 * Making sure we delete any previously created (but non-visible)
 * modal HTML element dialogues
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-widget-window
  delete-widget:wysiwyg-controls-widget-window


/*
 * Making sure dialogues becomes initially visible, and that we give initial
 * focus to "Close" button
 */
send-javascript:@"$('#wysiwyg-controls-widget-window').modal('toggle');
$('#wysiwyg-controls-widget-window').on('shown.bs.modal', function () {
    $('#wysiwyg-controls-widget-window-ok').focus();
})"

/*
 * Creating actual modal Widget
 */
create-widget:wysiwyg-controls-widget-window
  class:modal fade
  widgets
    container
      class:modal-dialog
      role:document
      widgets
        container
          class:modal-content
          events

            /*
             * Creates a new widget, according to the given position
             */
            sys42.wysiwyg-controls.create-new-widget

              /*
               * Making sure we close modal window
               */
              send-javascript:@"$('#wysiwyg-controls-widget-window').modal('toggle');"

              /*
               * Retrieving widget type to create
               */
              get-page-value:sys42.wysiwyg-controls.creating-widget-type

              /*
               * Running switch on widget type
               */
              switch:x:/../*/get-page-value/*?value
                case:wizard
                  switch:x:/../*/_arg?value
                    case:before
                      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-wizard-widget.hl
                        _before:true
                    case:after
                      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-wizard-widget.hl
                    case:inside
                      sys42.execute-lisp-file:/system42/cms/page-editor/editors/controls/add-wizard-widget.hl
                        _inside:true
                case:usercontrol
                  switch:x:/../*/_arg?value
                    case:before
                      sys42.wysiwyg-controls.add-control:usercontrol
                        _before:true
                    case:after
                      sys42.wysiwyg-controls.add-control:usercontrol
                    case:inside
                      find-widget:wysiwyg-editor-surface
                        data-selected:true
                      eval-x:x:/+/*
                      sys42.wysiwyg-controls.add-control:usercontrol
                        _parent:x:/././*/find-widget/*?value
                case:literal
                  switch:x:/../*/_arg?value
                    case:before
                      sys42.wysiwyg-controls.add-control:literal
                        _before:true
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:after
                      sys42.wysiwyg-controls.add-control:literal
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:inside
                      find-widget:wysiwyg-editor-surface
                        data-selected:true
                      eval-x:x:/+/*
                      sys42.wysiwyg-controls.add-control:literal
                        _parent:x:/././*/find-widget/*?value
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                case:container
                  switch:x:/../*/_arg?value
                    case:before
                      sys42.wysiwyg-controls.add-control:container
                        _before:true
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:after
                      sys42.wysiwyg-controls.add-control:container
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:inside
                      find-widget:wysiwyg-editor-surface
                        data-selected:true
                      eval-x:x:/+/*
                      sys42.wysiwyg-controls.add-control:container
                        _parent:x:/././*/find-widget/*?value
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                case:void
                  switch:x:/../*/_arg?value
                    case:before
                      sys42.wysiwyg-controls.add-control:void
                        _before:true
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:after
                      sys42.wysiwyg-controls.add-control:void
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more
                    case:inside
                      find-widget:wysiwyg-editor-surface
                        data-selected:true
                      eval-x:x:/+/*
                      sys42.wysiwyg-controls.add-control:void
                        _parent:x:/././*/find-widget/*?value
                      sys42.info-window:@"Please change widget's id and element to what you wish"
                        _time:more

            /*
             * Invoked when user has selected a type of widget to include on page
             */
            sys42.wysiwyg-controls.widget-type-selected

              /*
               * Storing type of widget user wants to create
               */
              set-page-value:sys42.wysiwyg-controls.creating-widget-type
                src:x:/../*/_arg?value

              /*
               * Changing description text of modal window
               */
              set-widget-property:wysiwyg-controls-widget-main-text
                innerValue:@"Choose where you wish to include this widget"

              /*
               * Deleting "type of widget" table
               */
              delete-widget:wysiwyg-controls-widget-table

              /*
               * Finding selected widget, such that we can toggle "inside selection", according
               * to whether or not a container widget is actually selected
               */
              find-widget:wysiwyg-editor-surface
                data-selected:true

              /*
               * Changing visibility for "create inside of widget" buttons
               */
              if
                fetch:x:/0/0?name
                  list-widgets:x:/../*/find-widget/*?value
                !=:container
                or
                  fetch:x:/0/0/0?value
                    get-widget-property:x:/../*/find-widget/*?value
                      data-usercontrol
                or:x:/../*/find-widget/*?count
                  =:int:0
                _sys42.show-code-window:x:/..
                set:x:/../*/create-widget/**/=wysiwyg-controls-insert-new-widget-inside

              /*
               * Creating widget that allows user to select position of type of widget he wants to create
               */
              create-widget:wysiwyg-controls-widget-position
                parent:wysiwyg-controls-widget-window-body
                element:table
                class:table table-hover
                widgets
                  tr
                    onclick
                      sys42.wysiwyg-controls.create-new-widget:before
                    style:"cursor:pointer;"
                    widgets
                      td
                        innerValue:@"<strong>Before</strong>"
                      td
                        innerValue:@"Before selected widget, or at beginning of page"
                        title:@"Inserts a new widget, either before your currently selected widget on surface, or at the beginning of your page"
                  tr
                    onclick
                      sys42.wysiwyg-controls.create-new-widget:after
                    style:"cursor:pointer;"
                    widgets
                      td
                        innerValue:@"<strong>After</strong>"
                      td
                        innerValue:@"After selected widget, or at end of page"
                        title:@"Inserts a new widget, either after your currently selected widget on surface, or at the end of your page"
                  tr:wysiwyg-controls-insert-new-widget-inside
                    onclick
                      sys42.wysiwyg-controls.create-new-widget:inside
                    style:"cursor:pointer;"
                    widgets
                      td
                        innerValue:@"<strong>Inside</strong>"
                      td
                        innerValue:@"As child of your selected container widget"
                        title:@"Inserts a new widget, inside your currently selected container widget, as a child"
                    
          widgets

            /*
             * Header of dialog
             */
            container
              class:modal-header
              widgets
                literal
                  element:h4
                  innerValue:@"Choose type of widget"

            /*
             * Body of dialog
             */
            container
              class:modal-body
              widgets

                /*
                 * Widget containing body of dialogue
                 */
                container:wysiwyg-controls-widget-window-body
                  widgets

                    /*
                     * Simple "body" of modal window
                     */
                    literal:wysiwyg-controls-widget-main-text
                      innerValue:@"Select the type of widget you wish to include on your page ..."
                    container:wysiwyg-controls-widget-table
                      element:table
                      class:table table-hover prepend-top
                      widgets
                        tbody
                          widgets
                            tr
                              onclick
                                sys42.wysiwyg-controls.widget-type-selected:wizard
                              style:"cursor:pointer;"
                              widgets
                                td
                                  innerValue:@"<strong>Wizard</strong>"
                                td
                                  innerValue:@"A pre-fabricated widget (hierarchy)"
                                  title:@"A wizard widget, is a pre-fabricated and reusable type of widget. Contrary to the usercontrol widget, this widget will 'unwrap' into your page, allowing you to modify its content, without modifying the wizard widget itself. In such a regard, it is a 'template widget'."
                            tr
                              onclick
                                sys42.wysiwyg-controls.widget-type-selected:usercontrol
                              style:"cursor:pointer;"
                              widgets
                                td
                                  innerValue:@"<strong>Usercontrol</strong>"
                                td
                                  innerValue:@"Reuse another [controls] page as a widget"
                                  title:@"A usercontrol widget is a reusable widget, created from the contents of another [controls] page. This widget, allows you to inject another page, as an HTML fragment, with server-side events, into your existing page"
                            tr
                              onclick
                                sys42.wysiwyg-controls.widget-type-selected:literal
                              style:"cursor:pointer;"
                              widgets
                                td
                                  innerValue:@"<strong>Literal</strong>"
                                td
                                  innerValue:@"A literal widget can contain text or HTML"
                                  title:@"Useful for widgets that requires text or HTML of some sort"
                            tr
                              onclick
                                sys42.wysiwyg-controls.widget-type-selected:container
                              style:"cursor:pointer;"
                              widgets
                                td
                                  innerValue:@"<strong>Container</strong>"
                                td
                                  innerValue:@"A container widget with children widgets of its own"
                                  title:@"Useful for widgets, where the children HTML element, also needs to be widgets, such as when you need separate event handlers for when user clicks children elements, etc"
                            tr
                              onclick
                                sys42.wysiwyg-controls.widget-type-selected:void
                              style:"cursor:pointer;"
                              widgets
                                td
                                  innerValue:@"<strong>Void</strong>"
                                td
                                  innerValue:@"A void widget can have neither children widgets, nor text"
                                  title:@"Useful for widgets that cannot have content at all, such as <hr>, <br> or <input> HTML elements"

            /*
             * Footer of dialog (contains OK and Cancel buttons)
             */
            container
              class:modal-footer
              widgets
                literal:wysiwyg-controls-widget-window-ok
                  element:button
                  class:btn btn-default
                  innerValue:Close
                  onclick

                    // Hiding dialog
                    send-javascript:@"$('#wysiwyg-controls-widget-window').modal('toggle');"
