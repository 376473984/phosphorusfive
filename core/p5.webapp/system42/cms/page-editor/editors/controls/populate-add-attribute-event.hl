
/*
 * Populates the "Add attribute" dropdown buttons for selected widget
 */



/*
 * Emptying list of attribute buttons possibly created for previously selected widget
 */
clear-widget:wysiwyg-button-add-attribute-list
clear-widget:wysiwyg-button-add-event-list



/*
 * Retrieving selected widget, since we need to figure out its element,
 * since the legal attributes widget can have, is dependent upon which
 * element the widget is rendered with
 */
find-widget:wysiwyg-editor-surface
  data-selected
get-widget-property:x:/-/*?value
  element



/*
 * Loading the file that contains our "Global Attributes", according to HTML5
 */
load-file:/system42/cms/page-editor/editors/controls/helpers/global-attributes.hl


/*
 * Loading file that contains all HTML element declarations, since specific
 * HTML elements might have additional attributes
 */
load-file:/system42/cms/page-editor/editors/controls/helpers/legal-elements.hl



/*
 * Retrieving widget, with all properties, and events, such that we can remove
 * from list created by [load-file], attributes and events that are already handled by
 * widget, before we start creating our "add attribute/event buttons"
 */
sys42.get-widget:x:/../*/find-widget/*?value
  _no-children:bool:true


/*
 * Looping through each existing property, and removing from file-read results
 */
for-each:x:/../*/sys42.get-widget/*/*

  /*
   * Making sure any [_onXXX] event handlers from get-widget is renamed to correct name
   */
  replace:x:/..for-each/*/_dp/#?name
    what:_on
    with:on

  /*
   * Removing currently iterated property from global attribute file (if it exists)
   */
  set:x:/../*/load-file/[0,1]/*/*/{0}
    :x:/..for-each/*/replace?value

  /*
   * Removing currently iterated property from HTML element file's element's declaration (if it exists)
   */
  set:x:/../*/load-file/[1,2]/*/*/{0}/*/attributes/*/{1}
    :x:/../*/get-widget-property/[0,1]/*/*?value
    :x:/..for-each/*/replace?value



/*
 * Looping through each global attribute, adding a button for each 
 * attribute to our "+ Attribute" button
 */
for-each:x:/../*/load-file/[0,1]/*/*(!/_header)

  /*
   * Parametrizing widget creation invocation
   */
  set:x:/..for-each/*/create-widget/*/widgets/*/a/*/innerValue?value
    src:x:/..for-each/*/_dp/#?name
  set:x:/..for-each/*/create-widget/*/widgets/*/a/*/title?value
    src:x:/..for-each/*/_dp/#?value

  /*
   * Checking if this is an event, at which case we add it to "Create event" button dropdown list
   */
  if
    fetch:x:/0/0?value
      index-of:x:/..for-each/*/_dp/#?name
        what:on
    =:int:0

    /*
     * This is an event handler (starts with "onXXX"), hence adding it to "add event button" instead
     * of "add attribute button"
     */
    set:x:/..for-each/*/create-widget/*/parent?value
      src:wysiwyg-button-add-event-list
    set:x:/..for-each/*/create-widget/**/send-javascript/0?value
      src:wysiwyg-button-add-event-wrp

  /*
   * Creating "li" widget that wraps "add attribute" button
   */
  create-widget
    parent:wysiwyg-button-add-attribute-list
    element:li
    widgets
      a
        href:#
        innerValue
        title
        onclick

          /*
           * Retrieving name of attribute to add, and invoking Active Event that adds attribute
           * to currently selected widget
           */
          get-widget-property:x:/../*/_event?value
            innerValue
          sys42.wysiwyg-controls.add-attribute-event-to-widget:x:/../*/get-widget-property/*/*?value

          /*
           * Closing popdown buttonmenu
           */
          send-javascript:@"$('#{0}').toggleClass('open');"
            :wysiwyg-button-add-attribute-wrp

          /*
           * Removing "li" widget wrapping button clicked, since attribute is
           * now added to widget, and we no longer need this option in list
           */
          get-parent-widget:x:/../*/_event?value
          delete-widget:x:/-/*?value


/*
 * The adding from our our legal elements file, which might contain special attributes
 * for the type of HTML element widget is rendered with
 */
for-each:x:/../*/load-file/[1,2]/*/*/{0}/*/attributes/*
  :x:/../*/get-widget-property/[0,1]/*/*?value

  /*
   * Parametrizing widget creation invocation
   */
  html-encode:x:/..for-each/*/_dp/#?value
  set:x:/..for-each/*/create-widget/*/widgets/*/a/*/innerValue?value
    src:x:/..for-each/*/_dp/#?name
  set:x:/..for-each/*/create-widget/*/widgets/*/a/*/title?value
    src:x:/..for-each/*/html-encode?value

  /*
   * Creating "li" widget that wraps "add attribute" button
   */
  create-widget
    parent:wysiwyg-button-add-attribute-list
    element:li
    widgets
      a
        href:#
        innerValue
        title
        onclick

          /*
           * Retrieving name of attribute to add, and invoking Active Event that adds attribute
           * to currently selected widget
           */
          get-widget-property:x:/../*/_event?value
            innerValue
          sys42.wysiwyg-controls.add-attribute-event-to-widget:x:/../*/get-widget-property/*/*?value

          /*
           * Closing popdown buttonmenu
           */
          send-javascript:@"$('#wysiwyg-button-add-attribute-wrp').toggleClass('open');"

          /*
           * Removing "li" widget wrapping button clicked, since attribute is
           * now added to widget, and we no longer need this option in list
           */
          get-parent-widget:x:/../*/_event?value
          delete-widget:x:/-/*?value





