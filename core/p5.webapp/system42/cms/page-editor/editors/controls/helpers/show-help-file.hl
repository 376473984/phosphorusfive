/*
 * Allows the user to read the help file(s) for [controls] types of pages
 */



/*
 * Checking if help widget already exists, and if so, we delete existing one, and
 * do not create a new one, to provide "toggle functionality"
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-help

  /*
   * Help widget is already open, closing it, and returning early.
   * Making sure we delete the session value that indicates that help is open first though
   */
  delete-widget:wysiwyg-controls-help
  set-widget-property:wysiwyg-controls-help-button
    class:btn btn-success
  set-session-value:sys42.wysiwyg-controls.help-file-visible
  return


/*
 * Setting session value indicating that help file is open, such that we can
 * remember it across page loads, etc
 */
set-session-value:sys42.wysiwyg-controls.help-file-visible
  src:true




/*
 * Making help button "toggle"
 */
set-widget-property:wysiwyg-controls-help-button
  class:btn btn-success active


get-parent-widget:wysiwyg-editor-surface

/*
 * Creates the "Help files" widget
 */
eval-x:x:/+/*/parent
create-widget:wysiwyg-controls-help
  parent:x:/../*/get-parent-widget/*?value
  position:0
  class:rounded-corners bg-success prepend-bottom air-padding
  style:"clear:both;min-height:500px;position:relative;"
  widgets
    a:wysiwyg-controls-help-back-to-index
      href:#
      style:"position:absolute;bottom:5px;right:5px;"
      class:btn btn-default
      innerValue:@"<span class=""glyphicon glyphicon-home""></span> Home"
      onclick
        sys42.wysiwyg-controls.show-help-page:/system42/cms/page-editor/editors/controls/help-files/index.hl
    container:wysiwyg-controls-content
      oninit

        /*
         * Retrieving "currently viewed" help file from session
         */
        get-session-value:sys42.wysiwyg-controls.help-index
        if:x:/-/*?value
          not

          /*
           * Initial displaying of help during session, making sure we load "index.hl" help page,
           * and storing currently viewed page in session
           */
          set-session-value:sys42.wysiwyg-controls.help-index
            src:/system42/cms/page-editor/editors/controls/help-files/index.hl
          sys42.wysiwyg-controls.show-help-page:x:/-/*?value
          return

        /*
         * Opening up currently viewed help page
         */
        sys42.wysiwyg-controls.show-help-page:x:/../*/get-session-value/*?value

      events

        /*
         * Opens up a specified help page, specified through [_arg], being
         * either an integer, or the name (filename in string format) of a specific help page
         */
        sys42.wysiwyg-controls.show-help-page

          /*
           * Making sure "back to index" button is enabled
           */
          set-widget-property:wysiwyg-controls-help-back-to-index
            style:"position:absolute;bottom:5px;right:5px;"

          /*
           * Clearing widget for any previously loaded help files
           */
          clear-widget:wysiwyg-controls-content

          /*
           * Loading help file, making sure all [create-xxx-widget] nodes
           * have correct parent, and then evaluating the [controls] part
           * from help file, as a lambda object in itself
           */
          load-file:x:/../*/_arg?value
          add:x:/-/*/*/controls/*/~create-
            src:"parent:wysiwyg-controls-content"
          eval:x:/../*/load-file/*/*/controls

          /*
           * Making sure we save which file user is currently viewing, such that
           * if user closes and re-opens help files, it'll remember where he was.
           */
          set-session-value:sys42.wysiwyg-controls.help-index
            src:x:/../*/_arg?value
    
