/*
 * Shows the "help" window for changing element of
 * currently selected widget
 */


/*
 * Making sure we delete any previously created (but non-visible)
 * modal HTML element dialogues
 */
if
  fetch:x:/0/0?value
    widget-exist:wysiwyg-controls-element-window
  delete-widget:wysiwyg-controls-element-window


/*
 * Making sure dialogues becomes initially visible, and that we give initial
 * focus to "OK" button
 */
send-javascript:@"$('#wysiwyg-controls-element-window').modal('toggle');
$('#wysiwyg-controls-element-window').on('shown.bs.modal', function () {
    $('#wysiwyg-controls-element-window-close').focus();
})"


/*
 * Checking if a [_text] argument was supplied, and if so, making sure we display it at
 * the top of our modal window's body-content
 */
if:x:/../*/_text
  eval-x:x:/+/*/*
  insert-before:x:/../*/create-widget/**/container/=wysiwyg-controls-element-window-body/*/widgets/0
    src
      text:x:/../*/_text?value


/*
 * Creating actual modal Widget
 */
create-widget:wysiwyg-controls-element-window
  class:modal fade
  widgets
    container
      class:modal-dialog
      role:document
      widgets
        container
          class:modal-content
          widgets

            /*
             * Header of dialog
             */
            container
              class:modal-header
              widgets

                /*
                 * Close button
                 */
                literal:wysiwyg-controls-element-window-close
                  element:button
                  class:close
                  data-dismiss:modal
                  aria-label:Close
                  innerValue:&times;

                /*
                 * Header of our modal window
                 */
                text:@"<h4>The [element] property</h4>"

            /*
             * Body of dialog
             */
            container
              class:modal-body
              widgets

                /*
                 * Actual [container] widget, containing body of dialogue
                 */
                container:wysiwyg-controls-element-window-body
                  element:div
                  widgets
                    text:@"<p>Your currently selected widget can legally have these values as its <em>""element""</em> property.</p>"

                    /*
                     * Table showing suggestions for element names for currently selected widget
                     */
                    table:wysiwyg-window-list-of-elements-table
                      class:prepend-top table table-hover
                      oninit

                        /*
                         * Retrieving all legal elements for currently selected widget
                         */
                        sys42.wysiwyg-controls.get-legal-element-names-for-widget

                        /*
                         * Now we know which elements are legal values for currently selected widget, and
                         * can hence create our "suggest table"
                         */
                        for-each:x:/-/*

                          /*
                           * HTML encode value (description of element), since it might contain "<", ">" and such
                           */
                          html-encode:x:/./*/_dp/#?value

                          /*
                           * Creating a short description
                           */
                          split:x:/..for-each/*/html-encode?value
                            =:.

                          /*
                           * Parametrising [create-widget] invocation
                           */
                          set:x:/..for-each/*/create-widget/**/={element-name}?value
                            src:x:/..for-each/*/_dp/#?name
                          set:x:/..for-each/*/create-widget/**/={element-description}?value
                            src:x:/..for-each/*/html-encode?value
                          set:x:/..for-each/*/create-widget/**/={element-short-description}?value
                            src:x:/..for-each/*/split/0?name

                          /*
                           * Creating table row ("tr") widget, which once clicked, selects that element as element
                           * for currently selected widget
                           */
                          create-widget
                            parent:wysiwyg-window-list-of-elements
                            element:tr
                            style:"cursor:pointer;"
                            _element:{element-name}
                            onclick

                              /*
                               * Figuring out which element type user clicked
                               */
                              get-widget-property:x:/../*/_event?value
                                _element

                              /*
                               * Setting that element as the value of widget's
                               * [element] declaration, and initiating change element's
                               * active event
                               */
                              set-widget-property:widget-property-element
                                value:x:/../*/get-widget-property/*/*?value
                              sys42.wysiwyg-controls.change-selected-widgets-element

                              /*
                               * Making sure we close modal window
                               */
                              send-javascript:@"$('#wysiwyg-controls-element-window').modal('toggle');"

                              /*
                               * Refreshing CodeMirror codeview, if it is visible
                               */
                              sys42.wysiwyg-controls.update-code-wiew
                            widgets
                              td
                                widgets
                                  strong
                                    innerValue:{element-name}
                              td
                                title:{element-description}
                                widgets
                                  em
                                    widgets
                                      small
                                        innerValue:{element-short-description}
                      widgets
                        thead
                          widgets
                            tr
                              widgets
                                th
                                  colspan:2
                                  innerValue:@"<h4>Choose an element from list</h4>"
                        tbody:wysiwyg-window-list-of-elements
                          widgets
                        

            /*
             * Footer of dialog (contains OK and Cancel buttons)
             */
            container
              class:modal-footer
              widgets
                literal:wysiwyg-controls-element-window-ok
                  element:button
                  class:btn btn-default
                  innerValue:OK
                  onclick

                    // Hiding dialog
                    send-javascript:@"$('#wysiwyg-controls-element-window').modal('toggle');"
