
/*
 * Creates the toolbar for the Page editor
 */


/*
 * Needed further down
 */
select-data:x:/*/*/p5.page/"={0}"
  :x:/../*/_id?value


/*
 * Setting templates
 */
list-files:/system42/cms/templates/
for-each:x:/-/*?name
  _lit
    literal
      parent:page-template
      element:option
      value
      innerValue
  split:x:/./*/__dp?value
    =:/
  split:x:/-/0/-?name
    =:.
  set:x:/..for-each/*/_lit/*/*/value?value
    src:x:/..for-each/*/__dp?value
  set:x:/..for-each/*/_lit/*/*/innerValue?value
    src:x:/..for-each/*/split/[1,2]/0?name
  if:x:/../*/select-data/[0,1]/*/*/template?value
    equals:x:/..for-each/*/__dp?value
    add:x:/..for-each/*/_lit/*
      src
        selected
  add:x:/../*/create-widget/**/container/=page-template/*/widgets
    src:x:/..for-each/*/_lit/*


/*
 * Setting roles
 */
list-roles
for-each:x:/-/*?name
  _lit
    literal
      parent:page-role
      element:option
      value
      innerValue
  set:x:/..for-each/*/_lit/*/*/value?value
    src:x:/..for-each/*/__dp?value
  set:x:/..for-each/*/_lit/*/*/innerValue?value
    src:x:/..for-each/*/__dp?value
  if:x:/../*/select-data/[0,1]/*/*/role?value
    equals:x:/..for-each/*/__dp?value
    add:x:/..for-each/*/_lit/*
      src:selected
  add:x:/../*/create-widget/**/container/=page-role/*/widgets
    src:x:/..for-each/*/_lit/*


/*
 * Making sure [oninit] event handler knows ID of page edited
 */
set:x:/../*/create-widget/*/oninit/**/={_id}?value
  src:x:/../*/_id?value

/*
 * Creating actual toolbar
 */
create-widget
  parent:page-editor-surface


  /*
   * Ajax events for widget
   */
  oninit


    /*
     * Loading page from database, and setting widget values accordingly
     */
    select-data:x:/*/*/p5.page/"={0}"
      :{_id}
    set-widget-property:page-name
      value:x:/../*/select-data/*/*/name?value
    set-widget-property:page-url
      value:x:/../*/select-data/*?value


  widgets


    /*
     * Page Name
     */
    container
      class:col-md-4 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:Name
              class:input-group-addon
            void:page-name
              element:input
              type:text
              class:form-control
              placeholder:Page name ...


    /*
     * Page URL
     */
    container
      class:col-md-3 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              innerValue:URL
              has-id:false
              class:input-group-addon
            void:page-url
              element:input
              type:text
              class:form-control
              placeholder:URL ...


    /*
     * Page Template
     */
    container
      class:col-md-2 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              class:input-group-addon
              innerValue:Template
              has-id:false
            container:page-template
              element:select
              class:form-control
              widgets


    /*
     * Page Access Role
     */
    container
      class:col-md-3 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              element:div
              class:input-group-addon
              innerValue:Access Role
              has-id:false
            container:page-role
              element:select
              class:form-control
              widgets
                literal
                  element:option
                  value:_default
                  innerValue:No restrictions ...


    /*
     * Custom editor surface
     */
    container:page-editor-editor-surface


    /*
     * Save, Preview and Delete button wrapper
     */
    container
      class:col-md-12 text-right prepend-bottom
      widgets
        container
          class:btn-group
          widgets


            /*
             * Save button
             */
            literal:save-button
              element:button
              type:button
              innerValue:Save
              class:btn btn-default
              onclick
                sys42.save-edited-page
                  :x:/./-?value


              /*
               * Lambda events for "Save" button
               */
              events


                /*
                 * Saves currently edited page
                 */
                sys42.save-edited-page
                  sys42.get-currently-edited-page
                  _widgets
                    page-url
                    page-name
                    page-role
                    page-template
                  get-widget-property:x:/-/*?name
                    value
                  set:x:/../*/update-data/*/*?value
                    src:x:/../*/get-widget-property/*/page-url/*?value
                  set:x:/../*/update-data/*/*/*/name?value
                    src:x:/../*/get-widget-property/*/page-name/*?value
                  set:x:/../*/update-data/*/*/*/template?value
                    src:x:/../*/get-widget-property/*/page-template/*?value
                  if:x:/../*/get-widget-property/*/page-role/*?value
                    not-equals:"_default"
                    add:x:/../*/update-data/*/*
                      src:"role:{0}"
                        :x:/../*/get-widget-property/*/page-role/*?value
                  sys42.get-page-editor-data
                  add:x:/../*/update-data/*/*
                    src:x:/../*/sys42.get-page-editor-data/*
                  update-data:x:/*/*/p5.page/"={0}"
                    :x:/../*/sys42.get-currently-edited-page?value
                    src
                      p5.page
                        name
                        template

                  // Since URL of page (and hence ID) might have changed, we'll need to go through this again
                  sys42.set-currently-edited-page:x:/../*/get-widget-property/*/page-url/*?value
                  sys42.populate-select-page
                  sys42.edit-page
                  sys42.info-window:Page '{0}' was successfully saved
                    :x:/../*/get-widget-property/*/page-name/*?value


            /*
             * Preview button
             */
            literal:preview-button
              element:button
              innerValue:Preview
              class:btn btn-default
              onclick
                sys42.get-currently-edited-page
                send-javascript:@"window.open('{0}', '_blank');"
                  :x:/./-?value


            /*
             * Delete button
             */
            literal:delete-button
              element:button
              innerValue:Delete
              class:btn btn-default
              onclick
                sys42.confirm-window
                  _header:Please confirm deletion
                  _body:Are you sure you want to delete this page? This action cannot be undone!
                  _onok
                    sys42.delete-currently-edited-page
                    sys42.info-window:Page was successfully deleted


              /*
               * Lambda events for delete button
               */
              events


                /*
                 * Deletes the currently edited page
                 */
                sys42.delete-currently-edited-page
                  sys42.get-currently-edited-page
                  delete-data:x:/*/*/p5.page/"={0}"
                    :x:/./-?value
                  sys42.set-currently-edited-page
                  sys42.populate-select-page
                  clear-widget:page-editor-surface








