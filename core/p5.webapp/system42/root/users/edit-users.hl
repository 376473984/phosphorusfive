/*
 * Creates the web widget that allows for
 * editing users and roles in the system
 */


create-widget:edit-users
  parent:content
  class:form
  widgets


    /*
     * The "Edit Existing user ..." drop down select list
     */
    container
      class:col-xs-10 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              class:input-group-addon
              innerValue:Edit user
            container:edit-existing-user-select
              element:select
              class:form-control


              /*
               * Populates the "Edit Existing user" select drop down list
               */
              oninit
                sys42.populate-edit-existing-user


              /*
               * Here we create a new page according to which option item
               * was selected
               */
              onchange
                sys42.edit-existing-user


              /*
               * Lambda events for "Edit existing user" widget
               */
              events


                /*
                 * Here we loop through all users in system, and create an option
                 * select list item for each of them
                 */
                sys42.populate-edit-existing-user

                  // Making sure we empty widget, if it has been populated before
                  clear-widget:edit-existing-user-select

                  // Creating "_default" option element, for "un-selecting" edited user
                  create-literal-widget
                    element:option
                    parent:edit-existing-user-select
                    value:_default
                    innerValue:Select user ...
                  list-users
                  for-each:x:/-/*?name
                    if:x:/./*/__dp?value
                      =:x:/../*/_arg?value
                      add:x:/..for-each/*/create-literal-widget
                        src:selected
                    add:x:/..for-each/*/create-literal-widget
                      src:"value:{0}"
                        :x:/..for-each/*/__dp?value
                    add:x:/..for-each/*/create-literal-widget
                      src:"innerValue:{0}"
                        :x:/..for-each/*/__dp?value
                    create-literal-widget
                      element:option
                      parent:edit-existing-user-select


                /*
                 * Edits an existing user
                 */
                sys42.edit-existing-user

                  // Checking if there exist an "edit existing user" widget from before, 
                  // and if so, deletes it
                  if
                    fetch:x:/0/0?value
                      widget-exist:edit-user-properties
                    delete-widget:edit-user-properties

                  // Getting username of currently selected user from "select" widget
                  get-widget-property:edit-existing-user-select
                    value

                  // Making sure the selected user is not "_default" (which means no user was selected)
                  if:x:/../*/get-widget-property/*/*?value
                    !=:_default

                    /*
                     * User selected an actual user, now create the widget that shows that user's data
                     */
                    create-widget:edit-user-properties
                      parent:content
                      widgets


                        /*
                         * Which role user belongs to
                         */
                        container
                          class:col-xs-2 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Role
                                literal:user-role
                                  element:input
                                  type:text
                                  class:form-control
                                  placeholder:Users role ...


                        /*
                         * Password of user
                         */
                        container
                          class:col-xs-3 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Password
                                literal:user-password
                                  element:input
                                  type:password
                                  class:form-control
                                  placeholder:Users password ...
                                  title:Leave blank to keep existing password
                                  onchange
                                    get-widget-property:user-password
                                      value
                                    if:x:/../*/get-widget-property/*/*?value
                                      !=:
                                      sys42.info-feedback:@"Unless absolutely necessary, you should NOT change user's password! 
Leave password field blank to keep existing password!"
                                        _warning:true
                                        _time:more


                        /*
                         * Repeat password of user
                         */
                        container
                          class:col-xs-3 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Repeat
                                literal:user-password-repeat
                                  element:input
                                  type:password
                                  class:form-control
                                  placeholder:Password repeat ...
                                  title:Leave blank to keep existing password


                        /*
                         * Save user button
                         */
                        container
                          class:col-xs-2 form-group
                          widgets
                            literal
                              element:button
                              class:form-control btn btn-default
                              innerValue:Save
                              onclick
                                sys42.save-edited-user


                              /*
                               * Lambda events for Save button
                               */
                              events


                                /*
                                 * Saves currently edited user
                                 */
                                sys42.save-edited-user
                                  _widgets
                                    edit-existing-user-select
                                    user-role
                                    user-password
                                    user-password-repeat
                                  get-widget-property:x:/../*/_widgets/*?name
                                    value
                                  if:x:/../*/get-widget-property/*/user-password/*?value
                                    !=:x:/../*/get-widget-property/*/user-password-repeat/*?value
                                    sys42.info-feedback:Passwords does not match!
                                      _error:true
                                    send-javascript:"$('#user-password').focus().select();"
                                  else
                                    edit-user:x:/../*/get-widget-property/*/edit-existing-user-select/*?value
                                      role:x:/../*/get-widget-property/*/user-role/*?value
                                      password:x:/../*/get-widget-property/*/user-password/*?value
                                    set-widget-property:x:/../*/_widgets/*/~password?name
                                      value:
                                    sys42.info-feedback:User was succesfully saved
                                    send-javascript:"$('#edit-existing-user-select').focus().select();"


                        /*
                         * The "Delete user" button
                         */
                        container
                          class:col-xs-2 form-group
                          widgets
                            literal
                              element:button
                              class:form-control btn btn-default
                              innerValue:Delete
                              onclick
                                sys42.confirm-action
                                  _header:Confirm deletion of user
                                  _message:@"Please confirm that you really wish to delete this user. 
<strong>All files belonging to user will be deleted</strong>. This action cannot be undone!"
                                  _onok
                                    get-widget-property:edit-existing-user-select
                                      value
                                    delete-user:x:/../*/get-widget-property/*/*?value
                                    sys42.populate-edit-existing-user
                                    delete-widget:edit-user-properties


                    /*
                     * Populating values of widget created above according
                     * to user's data
                     */
                    get-user:x:/../*/get-widget-property/*/edit-existing-user-select/*?value
                    set-widget-property:user-role
                      value:x:/././*/get-user/*/*/role?value
                    send-javascript:"$('#user-role').focus().select();"


    /*
     * The "Create new user" button
     */
    container
      class:col-xs-2 form-group
      widgets
        literal
          element:button
          class:form-control btn btn-default
          innerValue:Create
          onclick
            sys42.get-text-literal
              _header:Supply a username
              _message:@"Please supply a username of new user. Make sure it is correct since usernames 
cannot be changed once created"
              _onok
                eval-x:/+/**(/username|/sys42.populate-edit-existing-user)
                sys42.get-password
                  _header:Supply a password
                  _message:@"Please supply a password for user"
                  _onok
                    create-user
                      username:x:/../*/_text-literal?value
                      password:xxx
                      role:user
                    sys42.populate-edit-existing-user:x:/../*/_text-literal?value
                    sys42.edit-existing-user
                    sys42.info-feedback:"User was successfully created"

