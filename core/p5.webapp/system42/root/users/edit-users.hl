
/*
 * Creates the web widget that allows for
 * editing users and roles in the system
 */


create-widget:edit-users
  parent:content
  class:form
  widgets


    /*
     * The "Edit Existing user ..." drop down select list
     */
    container
      class:col-xs-10 form-group
      widgets
        container
          class:input-group
          widgets
            literal
              class:input-group-addon
              innerValue:Edit user
            container:edit-existing-user-select
              element:select
              class:form-control


              /*
               * Populates the "Edit Existing user" select drop down list
               */
              oninit
                sys42.populate-edit-existing-user


              /*
               * Here we create a new page according to which option item
               * was selected
               */
              onchange
                sys42.edit-existing-user


              /*
               * Lambda events for "Create new page" widget
               */
              events


                /*
                 * Here we loop through all users in system, and create an option
                 * select list item for each of them
                 */
                sys42.populate-edit-existing-user
                  create-literal-widget
                    element:option
                    parent:edit-existing-user-select
                    value:_default
                    innerValue:Select user ...
                  list-users
                  for-each:x:/-/*?name
                    add:x:/..for-each/*/create-literal-widget
                      src:"value:{0}"
                        :x:/..for-each/*/__dp?value
                    add:x:/..for-each/*/create-literal-widget
                      src:"innerValue:{0}"
                        :x:/..for-each/*/__dp?value
                    create-literal-widget
                      element:option
                      parent:edit-existing-user-select


                /*
                 * Edits an existing user
                 */
                sys42.edit-existing-user
                  if
                    fetch:x:/0/0?value
                      widget-exist:edit-user-properties
                    delete-widget:edit-user-properties
                  get-widget-property:edit-existing-user-select
                    value
                  list-users
                  if:x:/../*/get-widget-property/*/*?value
                    !=:_default
                    create-widget:edit-user-properties
                      parent:content
                      widgets
                        container
                          class:col-xs-5 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Role
                                literal:user-role
                                  element:input
                                  type:text
                                  class:form-control
                                  placeholder:Users role ...
                        container
                          class:col-xs-5 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Email
                                literal:user-email
                                  element:input
                                  type:text
                                  class:form-control
                                  placeholder:Users email ...
                        container
                          class:col-xs-2 form-group
                          widgets
                            literal
                              element:button
                              class:form-control btn btn-default
                              innerValue:Save
                              onclick
                                sys42.save-edited-user


                              /*
                               * Lambda events for Save button
                               */
                              events


                                /*
                                 * Saves currently edited user
                                 */
                                sys42.save-edited-user
                                  _widgets
                                    edit-existing-user-select
                                    user-role
                                    user-email
                                    user-password
                                    user-password-repeat
                                  get-widget-property:x:/../*/_widgets/*?name
                                    value
                                  if:x:/../*/get-widget-property/*/user-password/*?value
                                    !=:x:/../*/get-widget-property/*/user-password-repeat/*?value
                                    sys42.info-feedback:Passwords does not match
                                      _error:true
                                    send-javascript:"$('#user-password').focus().select();"
                                  else
                                    edit-user:x:/../*/get-widget-property/*/edit-existing-user-select/*?value
                                      role:x:/../*/get-widget-property/*/user-role/*?value
                                      password:x:/../*/get-widget-property/*/user-password/*?value
                                      email:x:/../*/get-widget-property/*/user-email/*?value
                                    set-widget-property:x:/../*/_widgets/*/~password?name
                                      value:
                                    sys42.info-feedback:User was succesfully saved
                                    send-javascript:"$('#edit-existing-user-select').focus().select();"
                        container
                          class:col-xs-5 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Password
                                literal:user-password
                                  element:input
                                  type:password
                                  class:form-control
                                  placeholder:Users password ...
                                  title:Leave blank to keep existing password
                        container
                          class:col-xs-5 form-group
                          widgets
                            container
                              class:input-group
                              widgets
                                literal
                                  class:input-group-addon
                                  innerValue:Repeat
                                literal:user-password-repeat
                                  element:input
                                  type:password
                                  class:form-control
                                  placeholder:Password repeat ...
                                  title:Leave blank to keep existing password
                    set-widget-property:user-role
                      value:x:/../*/list-users/*/{0}?value
                        :x:/../*/get-widget-property/*/*?value
                    get-user:x:/../*/get-widget-property/*/*?value
                    set-widget-property:user-email
                      value:x:/..if/*/get-user/*/*/email?value
                    send-javascript:"$('#user-role').focus().select();"


    /*
     * The "Create new user" button
     */
    container
      class:col-xs-2 form-group
      widgets
        literal
          element:button
          class:form-control btn btn-default
          innerValue:Create

