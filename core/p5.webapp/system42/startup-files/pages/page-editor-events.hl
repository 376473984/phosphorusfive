
/*
 * creates the Active Events necessary for the page-editor
 */


// event for editing a [p5.page]
set-event:sys42.page-editor.edit-page
  get-widget-property:select-page
    value
  if:x:/./*/get-widget-property/+/<?value
    equals:_default
    clear-widget:editor-main-surface
    clear-widget:editor-toolbar
    set-session-value:page-editor.current-page
  else
    select-data:x:/*/*/p5.page/"={0}"
      :x:/..sys42.page-editor.edit-page/*/get-widget-property/+/<?value
    set:x:/+2/*/_args/*/_page?value
      src:x:/./-/*?value
    set:x:/+?value
      src:system42/admin/page-editor/editor-types/{0}-editor.hl
        :x:/././-2/*/*/type?value
    sys42.execute-lambda-file
      _args
        _page
    set:x:/+/*/src?value
      src:x:/././*/select-data/*?value
    set-session-value:page-editor.current-page
      src

// event for creating a new [p5.page]
set-event:sys42.page-editor.create-page
  get-widget-property:new-page
    value
  if:x:/<?value
    not-equals:_default
    if:x:/././*/get-widget-property/+/<?value
      equals:html
      add:x:/././*/insert-data/0
        src
          type:html
          header:change this
          content:<p>change this too</p>
    else-if:x:/././*/get-widget-property/+/<?value
      equals:controls
      add:x:/././*/insert-data/0
        src
          type:controls
          controls:@"

/*
 * [controls] page sample code
 */

literal
  innerValue:""change this"""
    else-if:x:/././*/get-widget-property/+/<?value
      equals:lambda
      add:x:/././*/insert-data/0
        src
          type:lambda
          lambda:@"

/*
 * [lambda] page sample code
 */

create-widget
  widgets
    literal
      innerValue:""change this"""
    select-data:x:/*/*/p5.page/=~change-this?count
    set:x:/+/0?value
      src:{0}-{1}
        :/change-this
        :x:/././-/0?value
    insert-data
      p5.page
        name:change this
        template:system42/templates/main.hl
    set:x:/+/*/src?value
      src:x:/./-/0?value
    set-session-value:page-editor.current-page
      src
    include-javascript:"location.reload();"

