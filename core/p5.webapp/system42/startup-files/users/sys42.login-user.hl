
/*
 * logs in the user, creating a persistent cookie on disc for the current session. pass in [_username]
 * and [_password] as the credentials you wish to check. pass in [_success] to be used as return value, in
 * case login is not successful
 */

set-event:sys42.login-user

  // checks to see if username/password combination exists in database
  select-data:x:/*/*/p5.user(/*/username/"={0}"/.&/*/password/"={1}"/.)
    :x:/..sys42.login-user/*/username?value
    :x:/..sys42.login-user/*/password?value
  if:x:/-/*?count
    more-than:int:0

    // username/password combination existed in database, storing username in plain, and password hashed
    p5.crypto.hash-string:x:/..sys42.login-user/*/select-data/*/*/password?value
    set:x:/+2/*/src/*/password?value
      src:x:/./-?value
    set:x:/+/*/src/*/username?value
      src:x:/..sys42.login-user/*/select-data/*/*/username?value
    set-cookie:system42-user
      duration:int:365
      src
        username
        password
    include-javascript:"location.reload();"
  else

    // username/password combination did not exist in database
    add:x:/..sys42.login-user
      src
        success:bool:false

