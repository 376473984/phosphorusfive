
/*
 * responsible for creating the Active Event that shows a dialogue containing either the
 * current root node, or the given node's expression as a "debug window"
 */

set-event:sys42.show-lisp

  if:x:/../*/_arg
    not

    // informing used he has to submit a lambda expression or add nodes himself
    set:x:/../**/literal/=code/*/innerValue?value
      src:@"No input given to {0}. Either supply an expression as value of event invocation, or explicitly 
add nodes to invocation as [_arg] children nodes."
        :x:/..?name
  else

    // converting lambda to lisp
    lambda2lisp:x:/../*/_arg/*

    // HTML-encoding HTML
    p5.html.html-encode:x:/-?value

    // setting the literal content to the nodes converted to Hyperlisp (or default code)
    set:x:/../**/literal/=code/*/innerValue?value
      src:x:/./-?value

    // making sure close button has focus
    include-javascript:"window.p5.$('close').el.focus();"

  // creating a widget showing the Hyperlisp code created from the given p5.lambda tree
  create-widget
    class:info
    style:"position:absolute;top:0;left:0;z-index:1000;"
    widgets
      literal:code
        element:pre
        class:span-24
        innerValue
      literal:close
        element:button
        class:span-4 right last
        innerValue:close
        onclick
          get-parent-widget:x:/../*/_event?value
          delete-widget:x:/-/*?value
