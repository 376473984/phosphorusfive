
/*
 * Logs in the user, creating a persistent cookie on disc for the current session if successful.
 * Pass in [_username] and [_password] as the credentials you wish to check. If login is not
 * successful, then return value will be false, otherwise true
 */

set-protected-event:sys42.login-user

  // Checks to see if username/password combination exists in database
  select-data:x:/*/*/p5.user/*(/username/"={0}"/.&/password/"={1}"/.)
    :x:/..sys42.login-user/*/_username?value
    :x:/..sys42.login-user/*/_password?value
  if:x:/-/*

    // Username/password combination existed in database, storing username in plain, 
    // and password hashed in persistent cookie

    // Hashing password
    p5.crypto.hash-string:x:/..sys42.login-user/*/select-data/*/*/password?value
    set:x:/+2/*/src/*/password?value
      src:x:/./-?value
    set:x:/+/*/src/*/username?value
      src:x:/..sys42.login-user/*/select-data/*/*/username?value

    // Returning cookie on browser response
    set-cookie:system42-user
      duration:int:365
      src
        username
        password

    // Returning success to caller
    set:x:/..?value
      src:bool:true
  else

    // Username/password combination did not exist in database, returning failure
    // to caller
    set:x:/..?value
      src:bool:false

