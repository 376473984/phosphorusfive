
/*
 * Responsible for creating the Active Event that zips a folder
 * into user's "temp" folder, and returns the full relative filename
 * of zip file after zipping it
 */

set-protected-event:sys42.zip-with-password

  /*
   * Asks user if he or she wants to associate a password with zip file created,
   * making sure we pass in the foldername of folder to zip and zip file to zip to
   */
  set:x:/../*/sys42.create-new-password/*/_onok/*/_source?value
    src:x:/../*/_arg?value
  set:x:/../*/sys42.create-new-password/*/_onok/*/_destination?value
    src:x:/../*/_to?value
  add:x:/../*/sys42.create-new-password/*/_onok
    src:x:/../*/_onok/*
  sys42.create-new-password
    _header:Supply a password for zip file
    _message:@"If you supply a password for your zip file, then it will be encrypted with 256 bits AES encryption, using your password
as the key. If you do not supply a password, then your zip file will not be encrypted, and easily opened by anyone who have access to it"
    _onok

      _source
      _destination

      // zipping folder
      zip:x:/../*/_source?value
        to:x:/../*/_destination?value
        password:x:/../*/_password?value
    _xtra-buttons
      literal
        element:button
        class:btn btn-default
        innerValue:Create random
        onclick
          create-cs-random-string
            resolution:24
          _widgets
            new-password
            new-password-repeat
          set-widget-property:x:/-/*?name
            value:x:/../*/create-cs-random-string?value
          set-widget-property:modal-error-feedback
            innerValue:Your random password is; '<strong>{0}</strong>', make sure you store it in a safe place, and do not let anyone see it!
              :x:/../*/create-cs-random-string?value
            class:in blink
