
/*
 * Responsible for creating the Active Event that returns the creation script necessary
 * for re-creating the database [p5.page] objects in its current state.
 * Useful for creating your own custom "create-default-pages.hl" file
 */

create-protected-event:sys42.get-pages-database-creation-script

  /*
   * Will contain the lambda object necessary to create the [p5.page] objects in
   * database initially, if the database does not have any [p5.page] objects
   */
  _script
    select-data:x:/*/*/p5.page?count
    if:x:/-?value
      equals:int:0
      insert-data

  /*
   * Selects the [p5.page] objects from database, and appends to [_script]/[if]/[insert-data]
   */
  select-data:x:/*/*/p5.page
  add:x:/../*/_script/*/if/*/insert-data
    src:x:/../*/select-data/*

  /*
   * Returns "creation script" to caller
   */
  _header:@"
/*
 * Automatically created script by [sys42.get-pages-database-creation-script]
 */
"
  _result
  set:x:/-?value
    src:"{0}{1}"
      :x:/../*/_header?value
      :x:/../*/_script/*
  insert-before:x:
    src:x:/../*/_result


