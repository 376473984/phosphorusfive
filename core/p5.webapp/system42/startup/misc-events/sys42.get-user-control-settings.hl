
/*
 * Responsible for creating the Active Event that retrieves the usercontrol
 * settings, from the given [_arg] widget. Will loop upwards starting from given [_arg]
 * widget, retrieving all given arguments as widget properties, for widgets with [user-control]
 * attribute, until it can no longer find ancestors with given attributes, effectively leading
 * to retrieving the "outer most" usercontrol instance attribute values.
 *
 * Will also return [_settings-widget-id], being the widget that actually contained
 * the attribute values returned.
 */

create-protected-event:sys42.get-user-control-settings

  /*
   * Applying all arguments
   */
  add:x:/+/*/*/_args
    src:x:/../*(!/add!/insert-before!/set)
  insert-before:x:
    eval
      _args
        data-usercontrol
      while:bool:true
        add:x:/+
          src:x:/../*/_args/*(!/_arg)
        find-first-ancestor-widget:x:/../*/_args/*/_arg?value
        if:x:/-/*
          not
          break
        set:x:/../*/_args/*/_arg?value
          src:x:/..while/*/find-first-ancestor-widget/*?value
      add:x:/+
        src:x:/../*/_args/*(!/_arg)
      get-widget-property:x:/../*/_args/*/_arg?value
      insert-before:x:
        src:x:/./-/*/*
      insert-before:x:
        src:@"_settings-widget-id:@""{0}"""
          :x:/../*/_args/*/_arg?value