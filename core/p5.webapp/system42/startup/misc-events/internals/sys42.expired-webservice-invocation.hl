
/*
 * Responsible for creating the Active Event that evaluates
 * if a web service invocation has expired or not
 */

create-protected-event:sys42.expired-webservice-invocation

  // Retrieving current time, and calculating delta from "creation of web service invocation to current time"
  date-now
  -:x:/../*/date-now?value
    _:x:/../*/_arg?value

  // Checking that delta between current time and web service creation time does not
  // exceed allowed freshness of web service invocations
  p5.security.get-web-service-invocation-fresh-time
  if:x:/../*/p5.security.get-web-service-invocation-fresh-time?value
    less-than:x:/../*/"\\-"?value

    // This invocation has expired!
    set:x:/..?value
      src:bool:true
  else

    // This invocation is still "fresh"
    set:x:/..?value
      src:bool:false

