
/*
 * Responsible for creating the Active Event that zips a folder
 * into user's "temp" folder, and returns the full relative filename
 * of zip file after zipping it
 */

set-protected-event:sys42.zip-with-password

  /*
   * Asks user if he or she wants to associate a password with zip file created,
   * making sure we pass in the foldername of folder to zip and zip file to zip to
   */
  set:x:/../*/sys42.modal-wizard/*/onok/**/_source?value
    src:x:/../*/_arg?value
  set:x:/../*/sys42.modal-wizard/*/onok/**/_destination?value
    src:x:/../*/_to?value
  add:x:/../*/sys42.modal-wizard/*/onok/*/else
    src:x:/../*/_onok/*


  /*
   * Using "Modal Wizard" dialogue to retrieve zip password from user
   */
  sys42.modal-wizard
    header:Supply a password for zip file
    body:@"If you supply a password for your zip file, then it will be encrypted with 256 bits AES encryption, using your password
as the key. If you do not supply a password, then your zip file will not be encrypted, and easily opened by anyone who have access to it"
    controls
      password:password
        label:Password
        placeholder:Supply password ...
        autocomplete:off
      password:password-repeat
        label:Repeat
        placeholder:Repeat password ...
        autocomplete:off
    onok

      // Checking that passwords matches
      if:x:/../*/password?value
        !=:x:/../*/password-repeat?value

        /*
         * Passwords did not match
         */
        set:x:/..?value
          src:Passwords did not match!!
        send-javascript:@"$('#password').focus().select();"
      else

        /*
         * Passwords did match
         */

        // Source folder and destination file
        _source
        _destination

        // zipping folder
        zip:x:/..else/*/_source?value
          to:x:/..else/*/_destination?value
          password:x:/../*/password?value

        // Making sure we return "success" to caller, such that modal dialogue can be successfully closed
        set:x:/..?value
          src:bool:true
