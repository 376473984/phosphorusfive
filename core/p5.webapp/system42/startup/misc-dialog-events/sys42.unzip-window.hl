
/*
 * Responsible for creating the Active Event that unzips a file,
 * asking the user to supply his AES password first
 *
 * Arguments;
 * [_arg] or value - Path, or expression leading to path, declaring which file you wish to unzip
 * [_to]           - Path to where you wish to have your unzipped files and folders appear
 * [_onok]         - Lambda callback evaluated when files are successfully unzipped
 *
 * There can only be one instance of this window at the time on page
 */

set-protected-event:sys42.unzip-window

  /*
   * Asks the user for his password to unzip the file
   */
  set:x:/../*/sys42.wizard-window/*/_onok/*/_source?value
    src:x:/../*/_arg?value
  set:x:/../*/sys42.wizard-window/*/_onok/*/_destination?value
    src:x:/../*/_to?value
  add:x:/../*/sys42.wizard-window/*/_onok
    src:x:/../*/_onok/*


  /*
   * Using "Modal Wizard Window" to retrieve unzip password from user
   */
  sys42.wizard-window
    _header:Supply password to unzip file
    _body:@"If your zip file was packed with a password, you must use the same password to decrypt it. 
If not, you can leave the password field blank"
    _widgets
      password:password
        label:Password
        placeholder:Supply password ...
        autocomplete:off
    _onok

      _source
      _destination

      // zipping folder
      unzip:x:/../*/_source?value
        to:x:/../*/_destination?value
        password:x:/../*/password?value

      // Making sure we return "success" to "Wizard Window", 
      // such that modal dialogue can be successfully closed
      set:x:/..?value
        src:bool:true


