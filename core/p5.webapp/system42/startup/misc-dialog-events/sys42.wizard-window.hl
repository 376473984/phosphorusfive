
/*
 * Responsible for creating the Active Event that shows the user a modal window,
 * to make him type in arbitrary pieces of data, where the fields user is shown,
 * is configurable by caller
 *
 * [_header]  - Header of modal wizard window
 * [_body]    - Explanatory text of dialogue
 * [_onok]    - Lambda callback to evaluate upon user clicking OK button, and
 *              all fields have been validated
 * [_widgets] - List of widgets (simple syntax) for displaying in modal wizard window
 *              Notice that these can be either "text", "password", "number" or "email",
 *              and the value becomes the ID of the widget, and also the callback value
 *              which you can find beneath [_widgets] of your callback lambda code when
 *              user clicks OK button. If you supply a [mandatory] child node of your
 *              widget, with the value of true, then field will be considered mandatory,
 *              and user cannot close window with OK button, before he or she has supplied
 *              a value for it. If you supply an [autocomplete] child, with the value of [off],
 *              then dialogue will force autocomplete off, even if the browser is Google Chrome.
 *              Besides from the above mentioned arguments, the rest of the children will be added
 *              as properties and attributes for your specified widget. For an example of usage, 
 *              see "sys42.zip-window" Active Event
 */
create-protected-event:sys42.wizard-window

  /*
   * Making sure we delete any previously created dialogs
   * Notice that sice we only hide the dialog when it is closed, we need
   * to check if any previous dialogs exist, and if so, delete them, before 
   * we create any new ones
   */
  if
    fetch:x:/0/0?value
      widget-exist:wizard-window
    delete-widget:wizard-window

  /*
   * Setting [_header] and [_body] property of Modal Dialogue
   */
  set:x:/../*/create-widget/**/literal/=wizard-window-header/*/innerValue?value
    src:x:/../*/_header?value
  set:x:/../*/create-widget/**/literal/=wizard-window-body/*/innerValue?value
    src:x:/../*/_body?value

  /*
   * Appending [_onok] code into OK onclick button's event handler's [_eval] node
   */
  add:x:/../*/create-widget/**/literal/=wizard-window-ok/*/onclick/**/_eval
    src:x:/../*/_onok/*

  /*
   * Appending all dynamically created widgets as sources to retrieve values of 
   * inside of [onclick] of OK button
   */
  add:x:/../*/create-widget/**/literal/=wizard-window-ok/*/onclick/*/_widgets
    src:x:/../*/_widgets/*?value

  /*
   * This is simply NOT FUCKING "OK" GOOGLE CHROME DEVS!
   * When I tell my browser to fucking TURN OFF autocomplete, then 
   * I expect my browser to do as I FUCKING TELL IT TO DO AND 
   * TURN OFF AUTOCOMPLETE!
   * 
   * Fix your phony ass Google Chrome autocomplete features ASAP, or
   * don't be expecting much kudos from web devs in the future!!
   * 
   * Remember IE6 ...!!
   */
  if:x:/../*/_widgets/*/*/autocomplete/=off
    // Turning OFF autocomplete for Google Chrome!
    _literal
      literal:auto-complete-force-off
        element:div
        style:"display:none;"
        innerValue:@"<input><input type=""password"">"
    add:x:/../*/create-widget/*/widgets
      src:x:/././*/_literal/*

  /*
   * Appending dynamically declared [_widgets] into body of dialogue, but first
   * changing [mandatory] to [_mandatory], to make it avoid rendering attribute
   * to client side!
   */
  set:x:/../*/_widgets/*/*/mandatory?name
    src:_mandatory
  for-each:x:/../*/_widgets/*

    // Figuring out which type of control to create
    if:x:/..for-each/*/_dp/#?name
      =:text
      or:x:/..for-each/*/_dp/#?name
        =:password
      or:x:/..for-each/*/_dp/#?name
        =:number
      or:x:/..for-each/*/_dp/#?name
        =:email

      // Creating some sort of "text" control
      _controls
        container
          class:input-group prepend-left prepend-top prepend-bottom prepend-right
          widgets
            literal
              class:input-group-addon
              innerValue
            void
              element:input
              type
              class:form-control
              onkeypress:"return p5.keyModalWizardPress(event);"
      // Label control properties
      set:x:/..if/*/_controls/*/container/*/widgets/0/*/innerValue?value
        src:x:/..for-each/*/_dp/#/*/label?value

      // Input control properties
      set:x:/./*/_controls/*/container/*/widgets/1?value
        src:x:/..for-each/*/_dp/#?value
      set:x:/./*/_controls/*/container/*/widgets/1/*/type?value
        src:x:/..for-each/*/_dp/#?name
      add:x:/./*/_controls/*/container/*/widgets/1
        src:x:/..for-each/*/_dp/#/*(!/label)

      // Checking if this is mandatory field, and if so, adding [onblur] to reset CSS class when changed
      if:x:/..for-each/*/_dp/#/*/_mandatory?value.bool
        =:bool:true
        add:x:/..if/..if/*/_controls/*/container/*/widgets/*/void
          src
            onblur
              get-widget-property:x:/../*/_event?value
                class
                value
              if:x:/../*/get-widget-property/*/*/value?value
                =:
                and:x:/../*/get-widget-property/*/*/class/=~error?value
                  not

                // Adding "error" class
                set-widget-property:x:/../*/_event?value
                  class:"{0} error"
                    :x:/../*/get-widget-property/*/*/class?value
              else-if:x:/../*/get-widget-property/*/*/value?value
                !=:

                // Removing "error" class
                split:x:/../*/get-widget-property/*/*/class?value
                  =:" error"
                set-widget-property:x:/../*/_event?value
                  class:x:/./-/0?name

      // Adding decorated template control to body of modal dialogue, BEFORE error widget
      insert-before:x:/../*/create-widget/**/container/=wizard-window-controls/*/widgets/*/literal/=error-txt
        src:x:/..if/*/_controls/*
    else-if:x:/..for-each/*/_dp/#?name
      =:textarea

      // Creating "textarea" control
      _controls
        container
          class:prepend-left prepend-top prepend-bottom prepend-right
          widgets
            literal
              element:textarea
              rows:7
              class:form-control

      // Input control properties
      set:x:/./*/_controls/*/container/*/widgets/0?value
        src:x:/..for-each/*/_dp/#?value
      add:x:/./*/_controls/*/container/*/widgets/0
        src:x:/..for-each/*/_dp/#/*

      // Checking if this is mandatory field, and if so, adding [onblur] to reset CSS class when changed
      if:x:/..for-each/*/_dp/#/*/_mandatory?value.bool
        =:bool:true
        add:x:/..if/..else-if/*/_controls/*/container/*/widgets/*/literal
          src
            onblur
              get-widget-property:x:/../*/_event?value
                class
                value
              if:x:/../*/get-widget-property/*/*/value?value
                =:
                and:x:/../*/get-widget-property/*/*/class/=~error?value
                  not

                // Adding "error" class
                set-widget-property:x:/../*/_event?value
                  class:"{0} error"
                    :x:/../*/get-widget-property/*/*/class?value
              else-if:x:/../*/get-widget-property/*/*/value?value
                !=:

                // Removing "error" class
                split:x:/../*/get-widget-property/*/*/class?value
                  =:" error"
                set-widget-property:x:/../*/_event?value
                  class:x:/./-/0?name

      // Adding decorated template control to body of modal dialogue, BEFORE error widget
      insert-before:x:/../*/create-widget/**/container/=wizard-window-controls/*/widgets/*/literal/=error-txt
        src:x:/..else-if/*/_controls/*



  // Making sure dialog is visible, and setting focus to first input control in dialogue
  send-javascript:@"
$('#wizard-window').modal('toggle');
setTimeout (function (){{
$('#wizard-window').on('shown.bs.modal', function () {{
    $('#{0}').focus().select();
}})}}, 250);"
    :x:/../*/create-widget/**(/container/=wizard-window-controls/*/widgets/**(/literal/*/element/=textarea/.|/void)|/literal/=wizard-window-ok)/[0,1]?value


  /*
   * Creating actual modal Widget
   */
  create-widget:wizard-window
    class:modal fade
    widgets
      container
        class:modal-dialog
        role:document
        widgets
          container
            class:modal-content
            widgets

              /*
               * Header of dialog
               */
              container
                class:modal-header
                widgets
                  literal
                    element:button
                    class:close
                    data-dismiss:modal
                    aria-label:Close
                    innerValue:&times;
                  literal:wizard-window-header
                    element:h4
                    innerValue

              /*
               * Body of dialog, contains [_body] and dynamically created controls
               */
              container
                class:modal-body form
                widgets
                  literal:wizard-window-body
                    innerValue
                  container:wizard-window-controls
                    class:form-group


                    /*
                     * Dynamic content of modal dialogue ends up here!
                     */
                    widgets

                      /*
                       * Error widget, to show explanation of error to user
                       * Only shown if client evaluation code yields anything but :bool:true
                       */
                      literal:error-txt
                        class:error-text
                        style:"display:none;"


              /*
               * Footer of dialog, contains OK button
               */
              container
                class:modal-footer
                widgets
                  literal:wizard-window-ok
                    element:button
                    class:btn btn-default
                    innerValue:OK
                    onclick

                      // Retrieving data of dynamic controls, and stuffing into params collection of [eval]
                      _widgets
                      get-widget-property:x:/-/*?name
                        value
                        _mandatory
                      _evaluate:bool:true

                      // Looping through each input control of wizard, putting its value into [eval]
                      for-each:x:/../*/get-widget-property/*

                        // Checking if any of the mandatory fields are missing
                        if:x:/..for-each/*/_dp/#/*/_mandatory?value.bool
                          =:bool:true
                          and:x:/..for-each/*/_dp/#/*/value?value
                            =:

                          // Missing [_mandatory] field!! Only setting focus to the FIRST control that's missing
                          if:x:/../*/_evaluate?value
                            =:bool:true
                            send-javascript:@"$('#{0}').focus().select();"
                              :x:/..for-each/*/_dp/#?name
                            set-widget-property:error-txt
                              innerValue:Missing mandatory field!
                              style:

                          // Making sure [_evaluate] becomes false, such that dialogue doesn't close, and evaluates [onok]
                          set:x:/../*/_evaluate?value
                            src:bool:false

                          // Adding error feedback to mandatory controls that are missing values
                          // But only if "error" class is not already set!
                          get-widget-property:x:/..for-each/*/_dp/#?name
                            class
                          if:x:/-/*/*/=~error
                            not
                            set-widget-property:x:/..for-each/*/_dp/#?name
                              class:"{0} error"
                                :x:/./././-/*/*?value

                        // Adding control's value into [eval]
                        add:x:/../**/eval
                          src:"{0}:{1}"
                            :x:/..for-each/*/_dp/#?name
                            :x:/..for-each/*/_dp/#/*/value?value

                      // Contains code to be executed as user clicks OK
                      _eval

                      // Execute callback Hyperlisp
                      if:x:/../*/_evaluate?value

                        // Evaluating callback [onok], makings sure [offset] prevents properties of dynamic controls to be evaluated
                        set:x:/+/*/offset?value
                          src:x:/./+/*?count
                        eval:x:/./-
                          offset
                        if:x:/-?value
                          =:bool:true

                          // Hiding dialog if evaluation of [onok] yields true!
                          send-javascript:@"$('#wizard-window').modal('toggle');"
                        else

                          // Client code did not accept values, there should now be some sort of "error text"
                          // in the return value of evaluation
                          set-widget-property:error-txt
                            innerValue:x:/..if/*/eval?value
                            style:


  /*
   * Including JavaScript to help handle carriage return key in fields of dialogue
   */
  include-javascript:@"
  p5.keyModalWizardPress = function(e) {
    if(e.keyCode == 13) {
      p5.$('wizard-window-ok').raise('onclick');
      return false;
    }
  }"





