
/*
 * Creates the Active Event that sets application specific setting(s).
 *
 * Pass in the name of your app to set setting for as [_app], setting to set as [_arg], and new value of setting as [_src].
 * Will update app setting according to which user is currently logged in. If currently logged in user is
 * a "root account", optionally you can update (or set) settings for a specific user, by supplying user to set setting
 * for as [_username].
 *
 * [_src] can either contain a single value, be a node hierarchy, or both. Regardless, everything in [_src] will be stored as
 * your setting(s) values/nodes.
 */
create-event:sys42.utilities.set-setting

  /*
   * Sanity check
   */
  if:x:/../*/_app?value
    not
    throw:No application specified!

  /*
   * Retrieving currently logged in user, checking if user as "root", and if a specific user was supplied.
   */
  p5.auth.whoami
  if:x:/@p5.auth.whoami/*/role?value
    =:root
    and:x:/../*/_username?value

    /*
     * Changing user to set setting for in [p5.data.select], [p5.data.delete] and [p5.data.insert] below.
     */
    set:x:/../*(/p5.data.delete/0|/p5.data.insert/*/1)?value
      src:x:/../*/_username?value

  else-if:x:/../*/_username?value

    /*
     * Non root account tried to change setting for another user!
     */
    throw:Only root accounts can retrieve settings for other users than the currently logged in identity user

  p5.data.delete:x:@"/*/*/sys42.app-settings/*/username/={0}/./*/app-name/""={1}""/./*/""{2}""/."
    :x:/@p5.auth.whoami/*/username?value
    :x:/../*/_app?value
    :x:/../*/_arg?value
  eval-x:x:/../*/p5.data.insert/*/*

  /*
   * Checking if new setting was provided, or if this was a "delete setting" invocation.
   */
  if:x:/../*/_src
    not
    return
  add:x:/../*/p5.data.insert/*
    src:x:/../*/_src
  set:x:/../*/p5.data.insert/*/*/_src?name
    src:x:/../*/_arg?value
  p5.data.insert
    sys42.app-settings
      app-name:x:/../*/_app?value
      username:x:/@p5.auth.whoami/*/username?value


