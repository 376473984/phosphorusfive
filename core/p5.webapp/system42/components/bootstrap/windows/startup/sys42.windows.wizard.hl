
/*
 * Responsible for creating the Active Event that shows the user a modal dialog,
 * to asl user to update some existing, or supply some new input.
 *
 * Arguments;
 * 
 * [_header]         - Header of modal dialogue
 * [_body]           - Explanatory text of dialogue
 * [_class]          - Override the default CSS class
 * [_inner-class]    - Override the default inner CSS class. Set this to "modal-dialog modal-lg" to create a wider dialogue.
 * [_buttons]        - Makes it possible to exchange the default "OK" button with your own buttons
 *                     If you supply a [_buttons] parameter, then [_ok] has no effect, and you must
 *                     provide your own logic in the onclick handler of these buttons instead.
 *                     To evaluate the [.onok] lambda, you can invoke [sys42.windows.modal.ok],
 *                     To close dialogue, you can invoke [sys42.windows.modal.destroy]. If you wish to
 *                     make sure your [.oncancel] lambda is evaluated, you should use [sys42.windows.modal.cancel]
 *                     instead. [sys42.windows.modal.destroy] will not evaluate your [.oncancel] lambda callback.
 * [_data]           - Will automatically create a widget collection, according to the type of data supplied.
 *                     The default widget type to use, is a normal textbox. However, if you supply a child node with the name of
 *                     [_options], it will create a select HTML element instead, with the option elements supplied as children of [_options].
 * [.onok]           - Code to be evaluated if user clicks OK button
 * [.oncancel]       - Lambda callback to be evaluated if user closes dialogue without pressing "OK".
 *
 * If user closes modal dialogue, without clicking OK, then the given [.onok] is not evaluated.
 * There can be only one instance of this dialogue at the same time in your page.
 */
create-event:sys42.windows.wizard

  /*
   * Simply using the [sys42.windows.modal] to create our confirm window
   */
  add:x:/../*/.lambda/*
    src:x:/../*!/.!/./++
  if:x:/../*/_buttons/*
    not
    add:x:/../*/.lambda/*
      src
        _buttons
          literal
            element:button
            class:btn btn-default
            innerValue:OK
            onclick
              sys42.windows.modal.ok

  /*
   * Looping through each [_data] items, creating an edit widget for each item.
   */
  for-each:x:/../*/_data/*

    /*
     * Checking what type of edit field this is.
     */
    if:x:/..for-each/*/_dp/#/*/_options

      /*
       * Select dropdown choices.
       */
      set:x:/+/*/_selected?value
        src:x:/..for-each/*/_dp/#?value
      _eval-option
        _selected
        eval-x:x:/+/*/*
        add:x:/../*/return
          src
            value:x:/../*/_dn/#?value
            innerValue:x:/../*/_dn/#?name
        if:x:/../*/_selected?value
          =:x:/../*/_dn/#?value
          add:x:/../*/return
            src:selected
        return
      apply:x:/+2/*/*/*/*/*/*/*/widgets
        src:x:/..for-each/*/_dp/#/*/_options/*
        template
          option
            {@eval}:x:/..if/*/_eval-option
      eval-x:x:/+/*/*/*/*/*/*/*
      add:x:/../*/.lambda/*/sys42.windows.modal/*/_widgets
        src
          container
            class:form-group
            widgets
              container
                class:input-group
                widgets
                  label
                    class:input-group-addon
                    innerValue:x:/..for-each/*/_dp/#?name
                  select
                    class:form-control
                    value:x:/..for-each/*/_dp/#?value
                    _data-field-name:x:/..for-each/*/_dp/#?name
                    widgets
    else

      /*
       * Defaulting to "textbox".
       */
      eval-x:x:/+/*/*/*/*/*/*/*
      add:x:/../*/.lambda/*/sys42.windows.modal/*/_widgets
        src
          container
            class:form-group
            widgets
              container
                class:input-group
                widgets
                  label
                    class:input-group-addon
                    innerValue:x:/..for-each/*/_dp/#?name
                  input
                    type:text
                    class:form-control
                    value:x:/..for-each/*/_dp/#?value
                    _data-field-name:x:/..for-each/*/_dp/#?name

    /*
     * Making sure we set initial focus to the first widget in our [_widgets] collection.
     */
    if:x:/../*/.lambda/*/sys42.windows.modal/*/_widgets/*?count
      =:int:1
      add:x:/../*/.lambda/*/sys42.windows.modal/*/_widgets/0/*/widgets/0/*/widgets/1
        src
          oninit
            sys42.windows.modal.initial-focus:x:/../*/_event?value

  /*
   * Now we can evaluate our modal window.
   */
  .lambda
    sys42.windows.modal
      _widgets
  eval:x:/-

  /*
   * Making sure we create our data-retrieval Active Event, after creation of modal window, and associate it with our modal window.
   */
  set-widget-lambda-event:modal-window
    sys42.windows.wizard.get-values

      /*
       * Finding all data-fields in modal window.
       */
      find-widget:modal-window
        _data-field-name

      /*
       * Retrieving their data-field names and values.
       */
      get-widget-property:x:/-/*/*?value
        _data-field-name
        value
      for-each:x:/-/*
        add:x:/../*/return
          src:@"{0}:@""{1}"""
            :x:/..for-each/*/_dp/#/*/_data-field-name?value
            :x:/..for-each/*/_dp/#/*/value?value
      return


