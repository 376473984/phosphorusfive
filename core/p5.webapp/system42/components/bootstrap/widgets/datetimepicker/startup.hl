
/*
 * Creates a DateTimePicker widget, which allows the user to pick a date, and optionally a time.
 *
 * Arguments;
 * 
 * [_label]             - If declared, will create a descriptive input-group-addon label, with the specified content.
 * [_class]             - The CSS class to use for your DateTimePicker. Defaults to "input-group date".
 * [_icon]              - A CSS class, which is used instead of the default 'glyphicon-calendar' class for the 'dropdown button'.
 * [_locale]            - Which locale to use, is basically a reference to the JS files in the 'media/js/locale/' folder, and are helpers
 *                        to format the dates according to whatever is common in whatever language you want your DateTimePicker rendered with.
 * [_format]            - Declares how to format the date in the textbox.
 * [_defaultDate]       - Which date to use by default.
 * [_view-mode]         - Declares how the DateTimePicker should be rendered.
 * [_min-date]          - Declares the minimum valid date. Expected to be a date type, or date formatted string.
 * [_max-date]          - Declares the maximum valid date. Expected to be a date type, or date formatted string.
 * [_show-today-button] - If true, will show the "select today button".
 * [.onchange]          - Lambda callback invoked when the value of the DateTimePicker changes.
 *
 * In addition, the DateTimePicker contains the following Active Events.
 *
 * [sys42.widgets.datetimepicker.get-value]     - Retrieves the value of the widget. Expects the ID of the widget as its [_arg].
 * [sys42.widgets.datetimepicker.set-value]     - Sets the value of the widget. Expects the ID of the widget as its [_arg], in addition
 *                                                to a [_value] being a legal value for the widget.
 *
 * To understand the legal values for the above arguments, please read the documentation for the Bootstrap DateTimePicker here;
 * https://eonasdan.github.io/bootstrap-datetimepicker/
 */
create-event:sys42.widgets.datetimepicker

  /*
   * Applying options for widget, with default values.
   */
  _defaults
    _label
    _class:input-group date
    _icon:glyphicon glyphicon-calendar
    _locale
    _format
    _defaultDate
    _view-mode
    _min-date
    _max-date
    _show-today-button
    .onchange
  _options
  add:x:/../*/_options
    src:x:@"(/../*/"":regex:/^_/""|/../*/"":regex:/^\\./""|/../*/_defaults/*)(!/_defaults!/_options!/_parent)/$"

  /*
   * Checking if [_label] was passed in, and if so, making sure widget gets a descriptive input-group-addon.
   */
  if:x:/../*/_options/*/_label?value
    eval-x:x:/+/*/*/*
    insert-before:x:/../*/return/*/container/*/widgets/*/input
      src
        label
          class:input-group-addon
          innerValue:x:/../*/_options/*/_label?value

  /*
   * Checking if no [_icon] was passed in, and if not, deleting addon
   */
  if:x:/../*/_options/*/_icon?value
    not
    set:x:/../*/return/*/container/*/widgets/*/span

  /*
   * Passing in [.onchange] lambda callback such that it is evaluated upon change, and making sure
   * datetimepickker is initialized knowing it has [.onchange].
   */
  if:x:/../*/_options/*/.onchange/*

    /*
     * We have an [.onchange] lambda callback, making sure we initialize DateTimePicker such that it invokes
     * it during "dp.hide", and adding the callback lambda into [.onchange] of "input" element.
     */
    add:x:/../*/return/*/container/*/widgets/*/input/*/.onchange/*/.onchange
      src:x:/../*/_options/*/.onchange/*
    set:x:/../*/return/*/container/*/widgets/*/input/*/oninit/*/_has-onchange?value
      src:bool:true

  else

    /*
     * We do not have an [.onchange] lambda callback. Making sure datetimepicker is initialized without
     * the "dp.hide" event handled, and removing [.onchange] lambda callback of "input" element.
     */
    set:x:/../*/return/*/container/*/widgets/*/input/*/.onchange
    set:x:/../*/return/*/widgets/*/input/*/oninit/*/_has-onchange?value
      src:bool:false

  /*
   * Passing in arguments to [oninit].
   */
  add:x:/../*/return/**/oninit
    src:x:/../*/_options

  /*
   * Forward evaluating arguments.
   */
  eval-x:x:/../*/return/*/container/*/widgets/*/span/*/innerValue|/../*/return/*/*/class|/../*/return/*/*/widgets/*/input/*/value

  /*
   * Returning datetimepicker to caller.
   */
  return
    container
      class:x:/../*/_options/*/_class?value
      events

        /*
         * Returns the widget's value.
         */
        sys42.widgets.datetimepicker.get-value

          /*
           * First we must make sure this is the widget requested by caller.
           */
          if:x:/../*/_arg?value
            =:x:/../*/_event?value

            /*
             * Finding textbox, and returning value.
             */
            find-widget:x:/../*/_event?value
              element:input
              type:text
            get-widget-property:x:/@find-widget/*/*?value
              value
            return:x:/-/*/*?value

        /*
         * Sets the datetimepicker's value
         */
        sys42.widgets.datetimepicker.set-value

          /*
           * First we must make sure this is the widget requested by caller.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our widget.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Changing value of textbox.
           */
          find-widget:x:/../*/_event?value
            element:input
            type:text
          set-widget-property:x:/-/*/*?value
            value:x:/../*/_value?value

          /*
           * Due to "funny DOM event manipulations" in DateTimePicker, we need to explicitly pass in the
           * new value to our textbox like the following, in addition to using [set-widget-property].
           * Othherwise, value of textbox won't actually change!!
           */
          send-javascript:@"$('#{0}').val('{1}').change();"
            :x:/@find-widget/*/*?value
            :x:/../*/_value?value


      widgets

        /*
         * Textbox containing value of datetimepicker.
         */
        input
          type:text
          class:form-control
          value:x:/../*/_options/*/_value?value

          oninit

            /*
             * Including Bootstrap (and jQuery)
             */
            sys42.bootstrap.include-javascript
            sys42.bootstrap.include-css

            /*
             * The DateTimePicker is dependent upon Moment.js.
             */
            include-javascript-file:@bootstrap/widgets/datetimepicker/media/js/moment.min.js

            /*
             * Including DateTimePicker's JavaScript and CSS
             */
            include-javascript-file:@bootstrap/widgets/datetimepicker/media/js/bootstrap-datetimepicker.min.js
            include-stylesheet-file:@bootstrap/widgets/datetimepicker/media/css/bootstrap-datetimepicker.min.css

            /*
             * Becomes arguments to pass into JavaScript instantiation.
             */
            _arguments

            /*
             * Checking if [_locale] was passed in, and if so, making sure we use specified locale.
             */
            if:x:/../*/_options/*/_locale?value

              /*
               * Including the necessary "locale" JS file, and setting the "locale" option for our JavaScript.
               */
              include-javascript-file:@"@bootstrap/widgets/datetimepicker/media/js/locale/{0}.js"
                :x:/../*/_options/*/_locale?value
              set:x:/@_arguments?value
                src:@"{0},locale:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_locale?value

            /*
             * Checking if [_format] was passed in, and if so, making sure we use specified format.
             */
            if:x:/../*/_options/*/_format?value
              set:x:/@_arguments?value
                src:@"{0},format:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_format?value

            /*
             * Checking if [_default-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_default-date?value
              set:x:/@_arguments?value
                src:@"{0},defaultDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_default-date?value.string

            /*
             * Checking if [_view-mode] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_view-mode?value
              set:x:/@_arguments?value
                src:@"{0},viewMode:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_view-mode?value

            /*
             * Checking if [_format] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_format?value
              set:x:/@_arguments?value
                src:@"{0},format:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_format?value

            /*
             * Checking if [_min-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_min-date?value
              set:x:/@_arguments?value
                src:@"{0},minDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_min-date?value.string

            /*
             * Checking if [_max-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_max-date?value
              set:x:/@_arguments?value
                src:@"{0},maxDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_max-date?value.string

            /*
             * Checking if [_show-today-button] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_show-today-button?value
              set:x:/@_arguments?value
                src:@"{0},showTodayButton:true"
                  :x:/@_arguments?value

            /*
             * Making sure we nicely format arguments.
             */
            if:x:/../*/_arguments?value
              trim:x:/../*/_arguments?value
                chars:,
              set:x:/../*/_arguments?value
                src:"{{{0}}}"
                  :x:/@trim?value

            /*
             * Initializing widget with JavaScript client inclusion.
             * Notice, which widget to use to initialize datetimepicker, depends upon how the picker is displayed.
             * It can be either the main wrapper "div" element, or the "input" element itself, depending upon whether or not
             * an "open icon" was supplied.
             */
            _init-widget
            get-parent-widget:x:/../*/_event?value
            if
              fetch:x:/0/0/0?value
                find-widget-like:x:/../*/get-parent-widget/*/*?value
                  element:span
                  class:add-on

              /*
               * Using parent widget ("div" element) to initialize datetimepicker.
               */
              set:x:/../*/_init-widget?value
                src:x:/../*/get-parent-widget/*/*?value
            else

              /*
               * Using textbox for intializing datetimepicker.
               */
              set:x:/../*/_init-widget?value
                src:x:/../*/_event?value

            /*
             * This one is forward evaluated above, and is true if we have an [.onchange] lambda callback.
             */
            _has-onchange
            if:x:/@_has-onchange?value

              /*
               * We have an [.onchange] lambda callback, making sure we intialize datetimepicker such that it raises [.onchange] when 
               * the widget is hidden.
               */
              send-javascript:@"$('#{0}').datetimepicker({1}).on('dp.hide',function(){{p5.$('{2}').raise('.onchange');}});"
                :x:/../*/_init-widget?value
                :x:/../*/_arguments?value
                :x:/../*/_event?value

            else

              /*
               * We do not have an [.onchange] lambda callback.
               */
              send-javascript:@"$('#{0}').datetimepicker({1});"
                :x:/../*/_init-widget?value
                :x:/../*/_arguments?value

          .onchange

            /*
             * Getting datetimepicker's value, and invoking [.onchange]'s callback.
             */
            get-widget-property:x:/../*/_event?value
              value
            get-parent-widget:x:/../*/_event?value
            .onchange
            eval-x:x:/+/*
            eval:x:/@.onchange
              _value:x:/@get-widget-property/*/*?value
              _event:x:/@get-parent-widget/*/*?value

        /*
         * Icon button for showing DateTimePicker
         */
        span
          class:input-group-addon add-on
          innerValue:@"<span class=""{0}""></span>"
            :x:/../*/_options/*/_icon?value
