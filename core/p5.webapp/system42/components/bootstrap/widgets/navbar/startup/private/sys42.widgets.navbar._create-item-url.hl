
/*
 * Supporting Active Event, to create URL from given [_arg] hyperlink widget.
 * Creates the "unroll URL" for a Navbar, which once opened, will automatically click the Navbar item.
 */
create-event:sys42.widgets.navbar._create-item-url

  /*
   * First we get current location, without GET parameters, before we add the question mark to make sure we 
   * "open" our GET params collection.
   */
  p5.web.get-location-url
  set:x:/@p5.web.get-location-url?value
    src:{0}?
      :x:/@p5.web.get-location-url?value

  /*
   * Then we must fetch all GET parameters, and remove any previously created "sys42_widgets_tree_unroll" params,
   * before we add all other GET parameters, to make sure we keep everything EXCEPT "sys42_widgets_tree_unroll".
   */
  find-first-ancestor-widget:x:/../*/_arg?value
    _root-navbar
  get-widget-property:x:/@find-first-ancestor-widget/*/*?value
    _root-navbar
  p5.web.query.list
  set:x:/@p5.web.query.list/*/{0}
    :x:/@get-widget-property/*/*?value
  if:x:/@p5.web.query.list/*
    p5.web.query.get:x:/@p5.web.query.list/*?name
    for-each:x:/@p5.web.query.get/*
      url-encode:x:/@_dp/#?value
      set:x:/@p5.web.get-location-url?value
        src:{0}{1}={2}&
          :x:/@p5.web.get-location-url?value
          :x:/@_dp/#?name
          :x:/@url-encode?value

  /*
   * Adding key for our "unroll" GET parameter.
   */
  set:x:/@p5.web.get-location-url?value
    src:{0}{1}=
      :x:/@p5.web.get-location-url?value
      :x:/@get-widget-property/*/*?value

  /*
   * Then we can URL encode the parameter, and return URL to caller.
   */
  get-widget-property:x:/../*/_arg?value
    _item-id
  url-encode:x:/@get-widget-property/*/*?value

  return:{0}{1}
    :x:/@p5.web.get-location-url?value
    :x:/@url-encode?value
