
/*
 * Creates an Ajax ColorPicker widget, which allows the user to pick a color, including the opacity for the color.
 *
 * Arguments;
 * 
 * [_value]            - Initial value for widget. Can be either '#xxyyzz', 'rgba(x,y,z,q)' or a 'named color' such as 'yellow'. Defaults to #000000.
 * [_class]            - CSS class to use. Defaults to "input-group colorpicker-component colorpicker-element".
 * [_label]            - If declared, will create a descriptive input-group-addon label, with the specified text.
 * [.onchange]         - Lambda callback invoked when the value of the colorpicker changes.
 *
 * You can use either [sys42.widgets.colorpicker.get-value] or [sys42.widgets.colorpicker.set-value] to get and set the widget's value.
 * Both Active Events requires you to pass in [_arg] being the ID to the colorpicker. The latter also expects a [_value] bwing a legal value
 * for the widget, while the first will return the existing value, if any.
 */
create-event:sys42.widgets.colorpicker

  /*
   * Applying options for widget, with default values.
   */
  _defaults
    _value:#000000
    _class:input-group colorpicker-component colorpicker-element
    .onchange
    _label
  _options
  add:x:/../*/_options
    src:x:@"/../*(/"":regex:/^_/""|/"":regex:/^\\./""|/_defaults/*)(!/_defaults!/_options)/$"


  /*
   * Passing in [.onchange] lambda callback such that it is evaluated upon change, and making sure
   * colorpicker is initialized knowing it has [.onchange].
   */
  if:x:/../*/_options/*/.onchange/*

    /*
     * We have an [.onchange] lambda callback.
     * Making sure we initialize widget, such that it invokes our [.onchange] during "hidePicker".
     */
    add:x:/../*/return/*/container/*/widgets/*/input/*/.onchange/*/.lambda
      src:x:/../*/_options/*/.onchange/*

  else

    /*
     * We do not have an [.onchange] lambda callback.
     * Making sure widget is initialized without the "hidePicker" event handled.
     */
    set:x:/../*/return/*/container/*/widgets/*/input/*/.onchange

  /*
   * Checking if [_label] was passed in,and if so,  making sure colorpicker gets a descriptive input-group-addon.
   */
  if:x:/../*/_options/*/_label?value
    eval-x:x:/+/*/*/*
    insert-before:x:/../*/return/*/container/*/widgets/*/input
      src
        label
          class:input-group-addon
          innerValue:x:/../*/_options/*/_label?value

  /*
   * Forward evaluating arguments.
   */
  eval-x:x:/../*/return/*/container/*/widgets/*/input/*/value|/../*/return/*/*/class

  /*
   * Returning color picker to caller.
   */
  return
    container
      class:x:/../*/_options/*/_class?value
      widgets
        input
          type:text
          class:form-control
          value:x:/../*/_options/*/_value?value
          oninit

            /*
             * Including Bootstrap (and jQuery)
             */
            sys42.bootstrap.include-javascript
            sys42.bootstrap.include-css

            /*
             * Including Colorpicker's JavaScript and CSS.
             */
            include-javascript-file:@BOOTSTRAP/widgets/colorpicker/media/js/bootstrap-colorpicker.min.js
            include-stylesheet-file:@BOOTSTRAP/widgets/colorpicker/media/css/bootstrap-colorpicker.min.css

            /*
             * Retrieving parent widget, since it is needed later during JavaScript intialization of widget.
             */
            get-parent-widget:x:/../*/_event?value

            /*
             * Checking if we have an [.onchange] Ajax event for widget.
             */
            list-widget-ajax-events:x:/../*/_event?value
            if:x:/@list-widget-ajax-events/*/*/.onchange

              /*
               * We have an [.onchange] lambda callback, making sure it is evaluated when colorpicker is hidden.
               */
              include-javascript:@"$('#{0}').colorpicker({{component:'.colorpicker-dropper'}}).on('hidePicker', function() {{p5.$('{1}').raise('.onchange')}});"
                :x:/../*/get-parent-widget/*/*?value
                :x:/../*/_event?value
            else

              /*
               * We do not have an [.onchange] lambda callback, initializing colorpicker without "hidePicker" event declared.
               */
              include-javascript:@"$('#{0}').colorpicker({{component:'.colorpicker-dropper'}});"
                :x:/../*/get-parent-widget/*/*?value

          .onchange

            /*
             * Getting colorpicker's value, and invoking [.onchange]'s callback.
             */
            get-widget-property:x:/../*/_event?value
              value
            get-parent-widget:x:/../*/_event?value
            .lambda
            eval-x:x:/+/*
            eval:x:/@.lambda
              _value:x:/@get-widget-property/*/*?value
              _event:x:/@get-parent-widget/*/*?value

        span
          class:input-group-addon colorpicker-dropper
          widgets
            i
              innerValue:
