
/*
 * Responsible for creating the CodeMirror Hyperlambda editor widget
 *
 * Arguments;
 *
 * [_auto-focus] - If true, CodeMirror editor will gain initial focus upon load. Defaults to false.
 * [_mode] - Which language type to use. Defaults to 'hyperlambda'.
 * [_theme]  - Which theme to use, defaults to 'paraiso'.
 * [_height] - Height, add units, e.g. '250px'. Defaults to '300px'.
 * [_value] - Initial code to display.
 * [_events] - Collection of associated widget lambda events.
 */
create-event:sys42.widgets.codemirror


  /*
   * Check if default Code-Mirror theme setting exists, and if not, create it.
   */
  sys42.utilities.get-setting:sys42.code-mirror-default-theme
    _app:sys42
  if:x:/-/*?value
    not

    /*
     * Making sure default "paraiso" Code-Mirror theme is used by default.
     */
    sys42.utilities.set-setting:sys42.code-mirror-default-theme
      _app:sys42
      _src:paraiso
    add:x:/@sys42.utilities.get-setting
      src:"sys42.code-mirror-default-theme:paraiso"


  /*
   * Applying options for widget, with default values.
   * This lambda uses the boolean algebraic "OR" operator, combined with /$ (unique name), ending
   * up prioritizing specified argument, resorting to [_default] value if not given
   */
  _defaults
    _auto-focus:false
    _mode:hyperlambda
    _theme:paraiso
    _height:300px
    _value:
    _events
  _options
  set:x:/../*/_defaults/*/_theme?value
    src:x:/@sys42.utilities.get-setting/*?value
  add:x:/../*/_options
    src:x:@"/../*(/"":regex:/^_/""|/"":regex:/^\\./""|/_defaults/*)(!/_defaults!/_options)/$"


  /*
   * Making sure necessary JavaScript files are included
   */
  include-javascript-file
    /system42/components/common-widgets/codemirror/media/lib/codemirror.js
    /system42/components/common-widgets/codemirror/media/addon/selection/active-line.js
    /system42/components/common-widgets/codemirror/media/addon/display/placeholder.js
    /system42/components/common-widgets/codemirror/media/addon/display/fullscreen.js
    /system42/components/common-widgets/codemirror/media/addon/hint/show-hint.js
  include-javascript-file:/system42/components/common-widgets/codemirror/media/mode/{0}/{0}.min.js
    :x:/../*/_options/*/_mode?value


  /*
   * Making sure necessary Stylesheet files are included
   */
  include-stylesheet-file
    /system42/components/common-widgets/codemirror/media/lib/codemirror.css
    /system42/components/common-widgets/codemirror/media/addon/display/fullscreen.css
    /system42/components/common-widgets/codemirror/media/addon/hint/show-hint.css
  include-stylesheet-file:/system42/components/common-widgets/codemirror/media/theme/{0}.min.css
    :x:/../*/_options/*/_theme?value


  /*
   * Retrieving vocabulary of server, for auto complete to work, among other things, making sure we accommodate for any whitelist definitions.
   */
  _vocabulary
  sys42.widgets.codemirror._whitelist
  if:x:/@sys42.widgets.codemirror._whitelist/*
    not

    /*
     * No whitelist, adding everything.
     */
    vocabulary
    add:x:/@_vocabulary
      src:x:/@vocabulary/*

  else

    /*
     * Whitelist defined, making sure we only retrieve the legal vocabulary sub-set.
     * First we add the results of "get whitelist invocation" above to our temporary [eval-whitelist].
     */
    add:x:/+/*/events
      src:x:/@sys42.widgets.codemirror._whitelist/*

    /*
     * Then creating a whitelist lambda, from where we invoke [vocabulary], to retrieve the legal sub-set of events.
     */
    eval-whitelist
      events

        // Notice, to make sure our [vocabulary] invocation can even be evaluated, we need to add [vocabulary] and [return] to whitelist, while retrieving vocabulary.
        vocabulary
        return
      vocabulary
      return:x:/@vocabulary/*

    /*
     * Then we check if [vocabulary] and [return] are whitelisted, and if not, removing them.
     */
    if:x:/@sys42.widgets.codemirror._whitelist/*/vocabulary
      not
      set:x:/@eval-whitelist/*/vocabulary
    if:x:/@sys42.widgets.codemirror._whitelist/*/return
      not
      set:x:/@eval-whitelist/*/return

    /*
     * Adding results of above [eval-whitelist] invocation to above [_vocabulary] result node, which later is used to create our JSON.
     */
    add:x:/@_vocabulary
      src:x:/@eval-whitelist/*


  /*
   * Creating our JSON vocabulary object to return to client.
   */
  join:x:/@_vocabulary/*?value
    sep:,
    wrap:@""""


  /*
   * Including JSON object containing full list of keywords to client.
   *
   * Notice, "include-javascript" in case we're having multiple Code-Mirror instances on page!
   */
  include-javascript:@"CodeMirror._hyperlispKeywords = [{0}];"
    :x:/@join?value


  /*
   * Making sure textarea input is replaced with our CodeMirror instance.
   */
  send-javascript:@"p5['{0}'] = CodeMirror.fromTextArea(document.getElementById('{0}'), {{
mode:'hyperlambda',
theme:'{2}',
lineNumbers:true,
styleActiveLine:true,
path:'/system42/components/common-widgets/codemirror/media/',
autofocus:{1},
tabSize:2,
indentAuto:true,
extraKeys: {{
  'Alt-F':function(cm) {{cm.setOption('fullScreen', !cm.getOption('fullScreen'));}},
  'Ctrl-Space':'autocomplete'}}
}});
p5['{0}'].on('change',function (cMirror) {{
  var wdg = p5.$('{0}');
  wdg.el.value = cMirror.getValue();
}});
CodeMirror.keyMap.default['Shift-Tab'] = 'indentLess';
CodeMirror.keyMap.default['Tab'] = 'indentMore';"
    :x:/../*/_arg?value
    :x:/../*/_options/*/_auto-focus?value
    :x:/../*/_options/*/_theme?value


  /*
   * Then setting height.
   */
  send-javascript:@"p5['{0}'].setSize('100%', '{1}');"
    :x:/../*/_arg?value
    :x:/../*/_height?value


  /*
   * Applying any custom widget events to widget.
   */
  if:x:/../*/_options/*/_events/*
    add:x:/+/*/*
      src:x:/../*/_options/*/_events/*
    add:x:/../*/return/*/literal
      src
        events


  /*
   * Then returning our textarea widget to caller, which becomes replaced with a CodeMirror instance on client side, making sure we forward evaluate
   * its [innerValue], which becomes its initial value.
   */
  eval-x:x:/+/*/*/innerValue
  return
    literal
      element:textarea
      innerValue:x:/../*/_options/*/_value?value
