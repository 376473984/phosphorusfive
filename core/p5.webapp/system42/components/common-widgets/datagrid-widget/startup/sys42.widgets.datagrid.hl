
/*
 * Creates the datagrid custom widget Active Event.
 */


/*
 * The actual datagrid widget Active Event.
 * Creates a datagrid Ajax widget, which allows you to display and modify data in a table format.
 */
create-event:sys42.widgets.datagrid

  /*
   * Applying options for widget, with default values.
   */
  _defaults
    _skin:default
    _no-items:int:10
    _on-get-items
      sys42.windows.info-top:You must provide an [_on-get-items] callback lambda!
  _options
  add:x:/../*/_options
    src:x:@"(/../*/"":regex:/^_/""|/../*/_defaults/*)(!/_defaults!/_options)/$"


  /*
   * Making sure we include our datagrid specific skin CSS file.
   */
  include-stylesheet-file:/system42/components/common-widgets/datagrid-widget/media/skins/{0}/{0}.min.css
    :x:/../*/_options/*/_skin?value


  /*
   * Making sure user provided either [_on-edit-item] or [_on-select-items], and not both!
   */
  if:x:/../*/_options/*/_on-select-items
    and:x:/../*/_options/*/_on-edit-item
    throw:Sorry, either provide [_on-edit-item] or [_on-select-items] and not both!


  /*
   * Adding [_on-get-items] callback into [sys42.widgets.datagrid._get-items].
   */
  add:x:/../*/return/*/*/events/*/sys42.widgets.datagrid._get-items
    src:x:/../*/_options/*/_on-get-items/*


  /*
   * Adding [_on-select-items] callback into [sys42.widgets.datagrid.select-items].
   */
  add:x:/../*/return/*/*/events/*/sys42.widgets.datagrid.select-items/*/_eval
    src:x:/../*/_options/*/_on-select-items/*


  /*
   * Checking if caller provided an [_on-edit-item] callback, and if so, making sure we use it, by adding it
   * into [sys42.widgets.datagrid._edit-item-callback].
   */
  if:x:/../*/_options/*/_on-edit-item/*
    add:x:/../*/return/*/*/events/*/sys42.widgets.datagrid._edit-item-callback
      src:x:/../*/_options/*/_on-edit-item/*
  else
    add:x:/../*/return/*
      src:"_no-edit:true"


  /*
   * Forward evaluating class property, [_end] and [_no-items].
   */
  eval-x:x:/../*/return/*/container/*(/class|/_no-items|/oninit/*/sys42.widgets.datagrid._get-items/*/_end)


  /*
   * Returning the datagrid widget back to caller.
   */
  return
    container

      /*
       * Used to track the root datagrid widget, in addition to which page we are currently at in dataset.
       */
      _datagrid-page:0
      _no-items:x:/../*/_options/*/_no-items?value
      class:datagrid-widget-{0}
        :x:/../*/_options/*/_skin?value


      /*
       * Making sure our [oninit] fetches the first items.
       */
      oninit

        /*
         * Fetching the initial items to display.
         */
        sys42.widgets.datagrid._get-items:x:/../*/_event?value
          _start:int:0
          _end:x:/../*/_options/*/_no-items?value.int

        /*
         * Applying the [_items] returned above to invocation that create table header and main body elements.
         */
        add:x:/+/*/_columns
          src:x:/../*/sys42.widgets.datagrid._get-items/*/_items/0/*?name
        sys42.widgets.datagrid._create-datagrid:x:/../*/_event?value
          _columns

        /*
         * Applying the [_items] returned above to databind invocation.
         */
        add:x:/+
          src:x:/../*/sys42.widgets.datagrid._get-items/*/_items
        sys42.widgets.datagrid._append-items:x:/../*/_event?value


      /*
       * Events for datagrid widget.
       */
      events


        /*
         * Expects [_arg] being ID of datagrid and [_query] being text to search for.
         */
        sys42.widgets.datagrid.search

          /*
           * First we must make sure this is our datagrid.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early.
             */
            return

          /*
           * Making sure we update pager to first page.
           */
          set-widget-property:x:/../*/_event?value
            _datagrid-page:0

          /*
           * Storing query in datagrid root widget, if we are given a query.
           */
          if:x:/../*/_query?value
            and:x:/../*/_query?value
              !=:
            set-widget-property:x:/../*/_event?value
              _query:x:/../*/_query?value
          else
            delete-widget-property:x:/../*/_event?value
              _query
            set:x:/../*/sys42.widgets.datagrid._get-items/*/_query

          /*
           * Retrieving page size.
           */
          get-widget-property:x:/../*/_event?value
            _no-items

          /*
           * Retrieving previous page's items.
           */
          eval-x:x:/+/*
          sys42.widgets.datagrid._get-items:x:/../*/_event?value
            _start:int:0
            _end:x:/../*/get-widget-property/*/*?value.int
            _query:x:/../*/_query?value

          /*
           * Clearing out current items from datagrid (tbody widget)
           */
          find-widget:x:/../*/_event?value
            element:tbody
          clear-widget:x:/-/*?value

          /*
           * Disabling previous button.
           */
          find-widget:x:/../*/_event?value
            innerValue:&lt;
            element:a
          sys42.add-css-classes:x:/-/*?value
            _class:disabled

          /*
           * Applying the [_items] returned above to databind invocation.
           */
          add:x:/+
            src:x:/../*/sys42.widgets.datagrid._get-items/*/_items
          sys42.widgets.datagrid._append-items:x:/../*/_event?value

          /*
           * Enabling next button, but only if [sys42.widgets.datagrid._get-items] returned full page.
           */
          if:x:/../*/sys42.widgets.datagrid._get-items/*/_items/*?count
            =:x:/../*/get-widget-property/*/*?value.int
            find-widget:x:/../*/_event?value
              innerValue:&gt;
              element:a
            sys42.delete-css-classes:x:/-/*?value
              _class:disabled


        /*
         * Expects [_arg] being ID of datagrid.
         * Databinds the datagrid again.
         */
        sys42.widgets.datagrid.databind

          /*
           * First we must make sure this is our datagrid.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early.
             */
            return

          /*
           * Retrieving current page, number of items per page, and query (filter).
           */
          get-widget-property:x:/../*/_event?value
            _datagrid-page
            _no-items
            _query

          /*
           * Storing currently selected items, if there are any, to make sure we can make them selected again, 
           * after having databound the grid again.
           */
          _selected
          find-widget-like:x:/../*/_event?value
            element:tr
            class:selected
          if:x:/-/*?value
            get-widget-property:x:/../*/find-widget-like/*?value
              _row
            add:x:/../*/_selected
              src:x:/./-/*/*?value

          /*
           * Checking if we have a zero or negative value in current page, at which we disable previous button.
           */
          if:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
            <=:int:0
            find-widget:x:/../*/_event?value
              innerValue:&lt;
              element:a
            sys42.add-css-classes:x:/-/*?value
              _class:disabled

            /*
             * Checking if value was negative, which might occur if user clicks many times, before Ajax requests
             * are finished, at which we set it to zero, and return early.
             */
            if:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
              <:int:0
              set-widget-property:x:/../*/_event?value
                _datagrid-page:0
              return:bool:false
          else

            /*
             * Enabling previous button.
             */
            find-widget:x:/../*/_event?value
              innerValue:&lt;
              element:a
            sys42.delete-css-classes:x:/-/*?value
              _class:disabled

          /*
           * Multiplying current page number with number of items, and setting [_start] argument
           * of [sys42.widgets.datagrid._get-items] invocation.
           */
          set:x:/../*/sys42.widgets.datagrid._get-items/*/_start?value
            *:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
              _:x:/../*/get-widget-property/*/*/_no-items?value

          /*
           * Adding number of items to [_start] and setting [_end] argument of [sys42.widgets.datagrid._get-items] invocation.
           */
          set:x:/../*/sys42.widgets.datagrid._get-items/*/_end?value
            +:x:/../*/sys42.widgets.datagrid._get-items/*/_start?value.int
              _:x:/../*/get-widget-property/*/*/_no-items?value

          /*
           * Checking if we have a query
           */
          if:x:/../*/get-widget-property/*/2?value
            eval-x:x:/+/*/*
            add:x:/../*/sys42.widgets.datagrid._get-items
              src
                _query:x:/../*/get-widget-property/*/2?value

          /*
           * Retrieving current page's items.
           */
          sys42.widgets.datagrid._get-items:x:/../*/_event?value
            _start
            _end

          /*
           * Checking if we're at the end of our dataset.
           */
          if:x:/../*/sys42.widgets.datagrid._get-items/*/_items/*?count
            !=:x:/../*/get-widget-property/*/1?value.int

            /*
             * No more items, disabling next button.
             */
            find-widget:x:/../*/_event?value
              innerValue:&gt;
              element:a
            sys42.add-css-classes:x:/-/*?value
              _class:disabled

            /*
             * Checking if this was an empty result set, which might occur if user clicks button several times, or 
             * number of items is exact modulo of page size.
             * If it is empty, we do not databind the grid again, since that would create an empty page.
             * Exception is, if datagrid's page is zero, which might mean user deleted last record, or filtered
             * such that no records should shoe, which might occur after an edit operation, where you have a filter, etc.
             */
            if:x:/../*/sys42.widgets.datagrid._get-items/*/_items/*?count
              =:int:0
              and:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
                !=:int:0
              return:bool:false
          else

            /*
             * Enabling next button.
             */
            find-widget:x:/../*/_event?value
              innerValue:&gt;
              element:a
            sys42.delete-css-classes:x:/-/*?value
              _class:disabled

          /*
           * Clearing out current items from datagrid (tbody widget)
           */
          find-widget:x:/../*/_event?value
            element:tbody
          clear-widget:x:/-/*?value

          /*
           * Applying the [_items] returned above to databind invocation.
           */
          add:x:/+
            src:x:/../*/sys42.widgets.datagrid._get-items/*/_items
          sys42.widgets.datagrid._append-items:x:/../*/_event?value

          /*
           * Making sure we set all previously selected items to selected again.
           */
          for-each:x:/../*/_selected/*?name
            find-widget:x:/../*/_event?value
              _row:x:/..for-each/*/_dp?value
            sys42.add-css-classes:x:/-/*?value
              _class:selected

          /*
           * Returning success to caller, since datagrid was actually databound.
           */
          return:bool:true


        /*
         * Expects [_arg] being ID of datagrid.
         * Pages the datagrid to its previous page.
         */
        sys42.widgets.datagrid.go-previous

          /*
           * First we must make sure this is our datagrid.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early.
             */
            return

          /*
           * Retrieving current page and number of items per page.
           */
          get-widget-property:x:/../*/_event?value
            _datagrid-page
            _no-items
            _query

          /*
           * Decrementing page by 1.
           */
          set:x:/../*/get-widget-property/*/*/_datagrid-page?value
            -:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
              _:1

          /*
           * Updating current page to property of datagrid root widget.
           */
          set-widget-property:x:/../*/_event?value
            _datagrid-page:x:/../*/get-widget-property/*/*/_datagrid-page?value

          /*
           * Databinding grid.
           */
          sys42.widgets.datagrid.databind:x:/../*/_event?value


        /*
         * Expects [_arg] being ID of datagrid.
         * Pages the datagrid to its next page.
         */
        sys42.widgets.datagrid.go-next

          /*
           * First we must make sure this is our datagrid.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early.
             */
            return

          /*
           * Retrieving current page and number of items per page.
           */
          get-widget-property:x:/../*/_event?value
            _datagrid-page
            _no-items
            _query

          /*
           * Incrementing page by 1.
           */
          set:x:/../*/get-widget-property/*/*/_datagrid-page?value
            +:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
              _:1

          /*
           * Updating current page to property of datagrid root widget.
           */
          set-widget-property:x:/../*/_event?value
            _datagrid-page:x:/../*/get-widget-property/*/*/_datagrid-page?value

          /*
           * Databinding grid, and if it was unsuccessful, we decrement current page back to what it was again.
           */
          if
            sys42.widgets.datagrid.databind:x:/../*/_event?value
            not
            set:x:/../*/get-widget-property/*/*/_datagrid-page?value
              -:x:/../*/get-widget-property/*/*/_datagrid-page?value.int
                _:1
            set-widget-property:x:/../*/_event?value
              _datagrid-page:x:/../*/get-widget-property/*/*/_datagrid-page?value


        /*
         * Expects [_arg] being ID of datagrid, [_column] being name of column, [_row] being id of record, and [_value] being new value.
         */
        sys42.widgets.datagrid._edit-item-callback

          /*
           * First we must make sure this is the datagrid requested by caller to edit items in.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Notice, the rest of this Active Event is dynamically created according to arguments supplied when
           * creating the datagrid, by invoking [add] to append user supplied lambda callback into body of this lambda.
           * Hence, it might appear to be empty, but it is probably not in fact.
           */


        /*
         * Expects [_arg] being ID of datagrid and [_items] being items that are selected.
         */
        sys42.widgets.datagrid.select-items

          /*
           * First we must make sure this is the datagrid requested by caller.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Finding all previously selected items, and "un-selecting" them.
           */
          find-widget-like:x:/../*/_event?value
            element:tr
            class:selected
          if:x:/-/*?value
            sys42.delete-css-classes:x:/./-/*?value
              _class:selected

          /*
           * Making sure all [_items] becomes "selected".
           */
          for-each:x:/../*/_items/*?name
            find-widget-like:x:/../*/_event?value
              _row:x:/..for-each/*/_dp?value
            sys42.add-css-classes:x:/-/*?value
              _class:selected

          /*
           * Isolating our "user callback lambda" for clarity.
           */
          add:x:/+
            src:x:/../*/_items
          _eval
          eval:x:/-


        /*
         * Returns the row id from a given widget inside of that row.
         */
        sys42.widgets.datagrid.get-selected-items

          /*
           * First we must make sure this is the datagrid requested by caller.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Finding all "tr" widgets with the "selected" class.
           */
          find-widget-like:x:/../*/_arg?value
            element:tr
            class:selected

          /*
           * Looping through these widgets to retrieve their [_row].
           */
          for-each:x:/-/*
            get-widget-property:x:/..for-each/*/_dp/#?value
              _row
            add:x:/../*/return/*/_items
              src:x:/..for-each/*/get-widget-property/*/*?value
          return
            _items


        /*
         * Returns the row id from a given widget inside of that row.
         */
        sys42.widgets.datagrid.get-row-id

          /*
           * Sanity check!
           */
          if:x:/../*/_arg?value
            not
            throw:You have to supply a widget ID to this event!

          /*
           * Verifying that this is our datagrid.
           */
          find-first-ancestor-widget:x:/../*/_arg?value
            _datagrid-page
          if:x:/-/*?value
            !=:x:/../*/_event?value

            /*
             * Not our datagrid, returning early.
             */
            return

          /*
           * Finding "tr" widget from given widget, and returning its [_row].
           */
          find-first-ancestor-widget:x:/../*/_arg?value
            element:tr
            _row
          get-widget-property:x:/-/*?value
            _row
          return:x:/-/*/*?value


        /*
         * Expects [_arg] being ID of datagrid and [_items] being items to append to datagrid.
         */
        sys42.widgets.datagrid._append-items

          /*
           * First we must make sure this is the datagrid requested by caller.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Finds "tbody" widget in datagrid for later references, since we'll need to make sure "tr" widgets are children of "tbody".
           */
          find-widget:x:/../*/_event?value
            element:tbody

          /*
           * Finds root datagrid, to see if it contains [_no-edit] with value of "true", such that we can disable editing of items.
           */
          find-widget:x:/../*/_event?value
            _datagrid-page
          get-widget-property:x:/-/*?value
            _no-edit
          if:x:/-/*/*?value.bool

            /*
             * Removing "a" hyperlink, and injecting a "span" instead, since editing of items is disabled.
             */
            set:x:/../*/for-each/*/apply/*/template/*/*/*
            add:x:/../*/for-each/*/apply/*/template/*/*
              src
                span
                  {innerValue}:x:?value

            /*
             * Since in/place editing is disabled, we turn on "row selection", such that it becomes accessible for screen readers and such.
             */
            add:x:/../*/for-each/*/create-widget
              src
                role:button
                class:clickable
                tabindex:0
                onkeypress:@"return p5.sys42_widgets_datagrid_on_row_keypress(event);"
                onclick

                  /*
                   * Finding main datagrid widget, which is necessary to pass into [sys42.widgets.datagrid.select-row] as [_arg].
                   */
                  find-first-ancestor-widget:x:/../*/_event?value
                    _datagrid-page

                  /*
                   * Figuring out "row id", and passsing it into [select-rows].
                   */
                  get-widget-property:x:/../*/_event?value
                    _row
                  add:x:/+/*
                    src:x:/../*/get-widget-property/*/*?value
                  sys42.widgets.datagrid.select-items:x:/../*/find-first-ancestor-widget/*?value
                    _items

            /*
             * Making sure we include the necessary JavaScript to select row with carriage return.
             */
            include-javascript:@"
p5.sys42_widgets_datagrid_on_row_keypress = function (e) {
  if(e.keyCode == 13) {
    p5.$(e.target.id).raise('onclick');
    return false;
  }
}"

          /*
           * Loops through each item in [_items] collection, creating a "tr" widget for each.
           */
          for-each:x:/../*/_items/*

            /*
             * Lambda object for [apply] determining if this is an editable hyperlink, or a non-editable span.
             */
            _apply-eval
              if:x:/../*/_dn/#/*/_edit
                and:x:/../*/_dn/#/*/_edit?value.bool
                  =:bool:false

                /*
                 * Non-editable item, returning "span" widget with static content.
                 */
                eval-x:x:/+/*/*/innerValue
                return
                  span
                    innerValue:x:/../*/_dn/#?value
              else-if:x:/../*/_dn/#/*/_widgets

                /*
                 * Custom column with widget content.
                 */
                add:x:/+
                  src:x:/../*/_dn/#/*/_widgets/*
                return
              else

                /*
                 * Editable item, returning "a" (hyperlink) widget with [onclick] allowing for editing cell.
                 */
                eval-x:x:/+/*/*(/innerValue|/_column)
                return
                  a
                    href:#
                    innerValue:x:/../*/_dn/#?value
                    _column:x:/../*/_dn/#?name
                    onclick

                      /*
                        * Fetching column name and innerValue.
                        */
                      get-widget-property:x:/../*/_event?value
                        _column
                        innerValue

                      /*
                        * Fetching row ID by retrieving parent's parent widget's [_row] value.
                        */
                      get-parent-widget:x:/../*/_event?value
                      get-parent-widget:x:/-/*/*?value
                      get-widget-property:x:/-/*/*?value
                        _row

                      /*
                        * Need to find main datagrid root widget (div element) to pass into [_edit-item].
                        */
                      find-first-ancestor-widget:x:/../*/_event?value
                        _datagrid-page

                      /*
                        * Forward evaluating all arguments, before we invoke [_edit-item] event, responsible for creating
                        * the edit widget UI.
                        */
                      eval-x:x:/+/*
                      sys42.widgets.datagrid._edit-item:x:/-2/*?value
                        _hyperlink:x:/../*/_event?value
                        _row:x:/../*/get-widget-property/[1,2]/*/*?value
                        _column:x:/../*/get-widget-property/[0,1]/*/0?value
                        _value:x:/../*/get-widget-property/[0,1]/*/1?value

            /*
             * [apply] all "td" widgets inside of currently iterated "tr" widget.
             */
            apply:x:/..for-each/*/create-widget/*/widgets
              src:x:/..for-each/*/_dp/#/*
              template
                td
                  widgets
                    {@eval}:x:/..for-each/*/_apply-eval

            /*
             * Checking if we should use name or value as id. If a value is given, we use value, otherwise we use name.
             */
            _id
            if:x:/..for-each/*/_dp/#?value
              set:x:/./-?value
                src:x:/..for-each/*/_dp/#?value
            else
              set:x:/./-2?value
                src:x:/..for-each/*/_dp/#?name

            /*
             * Forward evaluating [parent] and [_row] before we create "tr" widget.
             */
            eval-x:x:/+/*(/parent|/_row)
            create-widget
              parent:x:/../*/find-widget/[0,1]/*?value
              _row:x:/..for-each/*/_id?value
              element:tr
              widgets


        /*
         * Expects [_arg] being ID of datagrid, [_column], [_row] and [_hyperlink].
         */
        sys42.widgets.datagrid._edit-item

          /*
           * First we must make sure this is the tree requested by caller to toggle items within.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Figuring our parent of hyperlink, which is "td" cell, such that we can create our textbox as a child widget of that widget.
           */
          get-parent-widget:x:/../*/_hyperlink?value

          /*
           * Creating a textbox for editing value of entity.
           */
          eval-x:x:/+/*(/parent|/value|/_column|/_row)
          create-void-widget
            element:input
            type:text
            parent:x:/../*/get-parent-widget/*/*?value
            value:x:/../*/_value?value
            _column:x:/../*/_column?value
            _row:x:/../*/_row?value
            onkeydown:@"return p5.widgets_datagrid_key(event);"
            oninit

              /*
               * Making sure textbox gets initial focus.
               */
              send-javascript:@"p5.$('{0}').el.focus();p5.$('{0}').el.select();"
                :x:/../*/_event?value

            /*
             * Making sure we trap [onblur] such that we can delete textbox again.
             */
            onblur

              /*
               * Deleting textbox.
               * Notice, we do not set focus here, since user probably blurred item by setting focus to something else.
               */
              delete-widget:x:/../*/_event?value

            /*
             * Invisible Ajax event, referenced from JavaScript, to delete textbox.
             */
            _onesc

              /*
               * Deleting textbox, and setting focus to hyperlink.
               */
              get-parent-widget:x:/../*/_event?value
              find-widget:x:/-/*/*?value
                element:a
              send-javascript:@"p5.$('{0}').el.focus();"
                :x:/../*/find-widget/*?value
              delete-widget:x:/../*/_event?value

            /*
             * Invisible Ajax event, referenced from JavaScript, to delete textbox, and update edited item.
             */
            _onenter

              /*
               * Fetching new value, and notifying [_on-edit] that item has been edited.
               */
              get-widget-property:x:/../*/_event?value
                value
                _column
                _row
              find-first-ancestor-widget:x:/../*/_event?value
                _datagrid-page
              eval-x:x:/+/*
              sys42.widgets.datagrid._edit-item-callback:x:/../*/find-first-ancestor-widget/*?value
                _value:x:/../*/get-widget-property/*/0?value
                _column:x:/../*/get-widget-property/*/1?value
                _row:x:/../*/get-widget-property/*/2?value

              /*
               * Checking if callback accepted new value
               */
              if:x:/../*/sys42.widgets.datagrid._edit-item-callback?value

                /*
                 * Deleting textbox, setting focus to hyperlink, and updating value of hyperlink.
                 */
                get-parent-widget:x:/../*/_event?value
                find-widget:x:/-/*/*?value
                  element:a
                send-javascript:@"p5.$('{0}').el.focus();"
                  :x:/./-/*?value
                set-widget-property:x:/-2/*?value
                  innerValue:x:/../*/get-widget-property/*/0?value
                delete-widget:x:/../*/_event?value

          /*
           * Passing in JavaScript to detect ESC and carriage return.
           */
          include-javascript:@"
p5.widgets_datagrid_key = function (e) {
  if(e.keyCode == 27) {
    p5.$(e.target.id).raise('_onesc');
    return false;
  } else if(e.keyCode == 13) {
    p5.$(e.target.id).raise('_onenter');
    return false;
  }
}"


        /*
         * Expects [_arg] being ID of datagrid and [_columns] being column names.
         */
        sys42.widgets.datagrid._create-datagrid

          /*
           * First we must make sure this is the tree requested by caller to toggle items within.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Creating column headers, by [apply]'ing [_columns] to [create-widget] invocation below.
           */
          apply:x:/../*/create-widget/*/widgets/*/tr/*/widgets
            src:x:/../*/_columns/*
            template
              th
                {innerValue}:x:?name

          /*
           * Finding table element inside of datagrid main wrapper.
           */
          find-widget:x:/../*/_event?value
            element:table

          /*
           * Forward evaluating [parent], and creating table "thead" element.
           */
          eval-x:x:/+/*/parent
          create-widget
            parent:x:/../*/find-widget/*?value
            element:thead
            widgets
              tr
                widgets

          /*
           * Forward evaluating [parent] and creating table "tbody" element.
           */
          eval-x:x:/+/*/parent
          create-widget
            parent:x:/../*/find-widget/*?value
            element:tbody
            widgets


        /*
         * Invoked when datagrid needs to retrieve children items for a specific datagrid node.
         * The lambda for this event, is created from the [_on-get-items] callback supplied during creation of datagrid.
         */
        sys42.widgets.datagrid._get-items

          /*
           * First we must make sure this is the datagrid requested by caller to toggle items within.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * This is NOT our datagrid.
             * Returning early, before callback lambda is evaluated.
             */
            return

          /*
           * Notice, the rest of this Active Event is dynamically created according to arguments supplied when
           * creating the datagrid, by invoking [add] to append user supplied lambda callback into body of this lambda.
           * Hence, it might appear to be empty, but it is probably not in fact.
           */


      widgets
        container
          class:datagrid-wrappper
          widgets
            container
              element:table
        container
          class:datagrid-pager
          widgets
            input
              type:search
              placeholder:Search ...
              onkeydown:@"return p5.widgets_datagrid_search_key(event);"
              _onenter

                /*
                 * Searching for items matching search textbox value.
                 */
                get-widget-property:x:/../*/_event?value
                  value
                find-first-ancestor-widget:x:/../*/_event?value
                  _datagrid-page
                eval-x:x:/+/*
                sys42.widgets.datagrid.search:x:/-2/*?value
                  _query:x:/../*/get-widget-property/*/*?value

                /*
                 * Selecting all text in search textbox, and setting focus to it.
                 */
                send-javascript:@"p5.$('{0}').el.focus();p5.$('{0}').el.select();"
                  :x:/../*/_event?value
              oninit

                /*
                 * Passing in JavaScript to detect carriage return in search textbox.
                 */
                include-javascript:@"
p5.widgets_datagrid_search_key = function (e) {
  if(e.keyCode == 13) {
    p5.$(e.target.id).raise('_onenter');
    return false;
  }
}"
            div
              widgets
                a
                  href:#
                  class:disabled
                  innerValue:&lt;
                  onclick

                    /*
                     * Invoking event that moves the grid to its previous items, passing in the id of the root datagrid widget.
                     */
                    find-first-ancestor-widget:x:/../*/_event?value
                      _datagrid-page
                    sys42.widgets.datagrid.go-previous:x:/-/*?value
                a
                  href:#
                  innerValue:&gt;
                  onclick

                    /*
                     * Invoking event that moves the grid to its next items, passing in the id of the root datagrid widget.
                     */
                    find-first-ancestor-widget:x:/../*/_event?value
                      _datagrid-page
                    sys42.widgets.datagrid.go-next:x:/-/*?value
