
/*
 * Invoke this to retrieve currently selected item(s) of widget.
 *
 * [_arg] must be the ID of the main tree widget.
 * 
 * The event will return an [_items] coollection, where the name of the nodes, are the IDs of all
 * items that are currently selected in your tree.
 */
create-event:sys42.widgets.tree.get-selected-items

  /*
   * This is the tree caller requested to retrieve selected items in.
   * Finding all items in the tree that are currently selected.
   */
  find-widget-like:x:/../*/_arg?value
    class:tree-selected
  if:x:/@find-widget-like/*/*?count
    !=:int:0

    /*
     * Tree has selected items.
     * Finding its parent "li" widget, and retrieving its [_item-id], and returning as [_items] collection to caller.
     */
    get-parent-widget:x:/@find-widget-like/*/*?value
    get-widget-property:x:/@get-parent-widget/*/*?value
      _item-id
    add:x:/+/*
      src:x:/@get-widget-property/*/*?value
    return
      _items
