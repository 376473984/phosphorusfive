
/*
 * Invoke this to select items in the widget.
 *
 * [_arg] must be the ID of the main tree widget.
 * [_items] must be a collection of items to select.
 *
 * Notice, all previously selected items are de-selected!
 */
create-event:sys42.widgets.tree.select-items

  /*
   * First finding all previously selected items, and de-selecting them.
   */
  find-widget-like:x:/../*/_arg?value
    class:tree-selected
  if:x:/@find-widget-like/*/*?count
    !=:int:0

    /*
     * Tree has previously selected items, making sure we un-select them.
     */
    sys42.utilities.delete-css-classes:x:/@find-widget-like/*/*?value
      _class:tree-selected

  /*
   * Then adding the selected CSS class to all items requested by caller.
   */
  for-each:x:/../*/_items/*?name
    find-widget:x:/../*/_arg?value
      _item-id:x:/@_dp?value
    find-widget:x:/@find-widget/*/*?value
      element:a
    sys42.utilities.add-css-classes:x:/@find-widget/*/0?value
      _class:tree-selected

  /*
   * Making sure [.on-select] lambda callback is invoked.
   */
  add:x:/+
    src:x:/../*/_items
  sys42.widgets.tree._select:x:/../*/_arg?value
