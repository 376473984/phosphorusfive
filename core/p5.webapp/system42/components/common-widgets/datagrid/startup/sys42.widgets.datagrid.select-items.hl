
/*
 * Selects all specified [_items] in specified [_arg] datagrid.
 */
create-event:sys42.widgets.datagrid.select-items

  /*
   * Finding all previously selected items, and "un-selecting" them, by deleting the "selected" CSS class on them.
   */
  find-widget-like:x:/../*/_arg?value
    element:tr
    class:selected
  if:x:/@find-widget-like/*/*?value
    sys42.utilities.delete-css-classes:x:/@find-widget-like/*/*?value
      _class:selected

  /*
   * Making sure all [_items] passed in ass argument becomes selected, by adding the "selected" CSS class to them.
   */
  for-each:x:/../*/_items/*?name
    find-widget-like:x:/../*/_arg?value
      _row:x:/@_dp?value
    sys42.utilities.add-css-classes:x:/@find-widget-like/*/*?value
      _class:selected

  /*
   * Isolating our "user callback lambda" for clarity.
   */
  add:x:/+
    src:x:/../*/_items
  sys42.widgets.datagrid._select-items:x:/../*/_arg?value
