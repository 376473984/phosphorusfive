
/*
 * Imports the given [_arg] CSV file, and puts into p5.data database.
 */
create-event:sys42.csv.import

  if:x:/../*/_arg?count
    !=:int:1
    throw:We can only import one CSV file at the time!

  /*
   * Loading CSV file.
   */
  load-file:x:/../*/_arg?value

  /*
   * Needed to make sure filename is type of object to [insert-data].
   */
  split:x:/../*/load-file/*?name
    =:/

  /*
   * Looping through each row in file, except the first row, 
   * which are column headers.
   */
  for-each:x:/../*/load-file/*/*/[1,]

    /*
     * Adding database type and column headers to [insert-data].
     */
    add:x:/../*/insert-data
      src:x:/../*/split/0/-?name
    add:x:/../*/insert-data/0/-
      src:x:/../*/load-file/0/0/*?name

    /*
     * Looping through each column in row.
     */
    _no:int:0
    while:x:/-?value
      <:x:/../*/insert-data/0/-/*?count

      /*
       * Settings currently iterated column value for currently iterated row.
       */
      set:x:/../*/insert-data/0/-/{0}?value
        :x:/..for-each/*/_no?value
        src:x:/..for-each/*/_dp/#/{0}?name
          :x:/..for-each/*/_no?value

      /*
       * Incrementing [while] iteration integer.
       */
      set:x:/..for-each/*/_no?value
        +:x:/..for-each/*/_no?value
          _:int:1

  /*
   * Making sure we insert all our data in "one go", to reduce the number of locks,
   * and increase the speed of the operation.
   */
  insert-data
