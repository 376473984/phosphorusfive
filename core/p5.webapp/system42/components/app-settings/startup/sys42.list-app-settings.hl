
/*
 * Returns all application specific settings.
 *
 * Pass in name of app to retrieve setting for as [_app].
 * Will return all app setting according to which user is currently logged in. If currently logged in user is
 * a "root account", optionally you can retrieve settings for a specific user, by supplying user to retrieve setting
 * for as [_username]
 */
create-event:sys42.list-app-settings

  /*
   * Sanity check
   */
  if:x:/../*/_app?value
    not
    throw:No application specified!

  /*
   * Retrieving currently logged in user, checking if user is "root", and if a specific user was supplied.
   */
  whoami
  if:x:/../*/whoami/*/role?value
    =:root
    and:x:/../*/_username?value

    /*
     * Changing user to retrieve setting for in [select-data] below.
     */
    set:x:/../*/select-data/0?value
      src:x:/../*/_username?value

  else-if:x:/../*/_username?value

    /*
     * Non root account tried to change setting for another user!
     */
    throw:Only root accounts can retrieve settings for other users than the currently logged in identity user

  select-data:x:@"/*/*/sys42.app-settings/*/username/={0}/./*/app-name/""={1}""/./*"
    :x:/../*/whoami/*/username?value
    :x:/../*/_app?value

  /*
   * Returning setting value(s)
   */
  insert-before:x:/../0
    src:x:/../*/select-data/*(!/username!/app-name)


