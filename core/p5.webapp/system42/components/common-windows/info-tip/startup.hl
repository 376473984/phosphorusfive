
/*
 * Responsible for creating the Active Event that shows the user a small
 * informational window, providing feedback to user
 *
 * Arguments;
 *
 * [_arg]            - Message, or epxression leading to message, you wish to display to user.
 * [_class]          - Override the default CSS class. Defaults to "info-window".
 * [_parent]         - Override the default parent widget. Defaults to "content".
 *
 * You cannot show more than one instance of this window at the time
 */
create-event:sys42.windows.info-tip

  /*
   * Applying options for widget, with default values.
   * This lambda uses the boolean algebraic "OR" operator, combined with /$ (unique name), ending
   * up prioritizing specified argument, resorting to [_default] value if not given
   */
  _defaults
    _arg:You should really have provided some text here as value of invocation ...
    _class:info-window
    _parent:cnt
  _options
  add:x:/../*/_options
    src:x:@"(/../*/"":regex:/^_/""|/../*/_defaults/*)(!/_defaults!/_options)/$"

  /*
   * Making sure we delete any previously created windows
   * Notice that sice we only hide the window when it is closed, we need
   * to check if any previous windows exist, and if so, delete them, before 
   * we create any new ones
   */
  if
    fetch:x:/0/0?value
      widget-exist:info-window
    delete-widget:info-window

  /*
   * Setting properties of window
   */
  set:x:/../*/create-widget/**/literal/*/innerValue?value
    src:x:/../*/_options/*/_arg?value

  /*
   * Creating actual info window
   */
  eval-x:x:/+/*
  create-widget:info-window
    parent:x:/../*/_options/*/_parent?value
    class:x:/../*/_options/*/_class?value
    widgets
      literal
        innerValue
