
/*
 * Creates the Active Event that allows the user to view a piece of lambda code in a "debug window" during execution.
 * Pass in an expression leading to whatever piece of lambda you'd like to study.
 *
 * Arguments;
 *
 * [_arg]            - Expression leading to lambda you wish to inspect
 * [_class]          - Override the default CSS class
 * [_parent]         - Override the default parent widget
 *
 * This window can have multiple instances at the same time on page
 */

create-event:sys42.windows.show-lambda

  /*
   * Applying options for widget, with default values.
   * This lambda uses the boolean algebraic "OR" operator, combined with /$ (unique name), ending
   * up prioritizing specified argument, resorting to [_default] value if no argument is given.
   */
  _defaults
    _class:col-xs-12 show-code-window
    _parent:content
  _options
  add:x:/../*/_options
    src:x:@"/../*(/"":regex:/^_/""|/"":regex:/^\\./""|/_defaults/*)(!/_defaults!/_options)/$"

  /*
   * Checking if user supplied an expression, and if not, showing error message
   */
  if:x:/../*/_arg
    not

    /*
     * User provided no expression, showing some helper text.
     */
    set:x:/../**/literal/*/element/=pre/./*/innerValue?value
      src:"No input given to [sys42.windows.show-lambda]. Supply an expression as value of event invocation, for instance; 'sys42.windows.show-lambda:x:/../*'"
  else

    /*
     * Converting lambda to Hyperlambda.
     */
    lambda2hyper:x:/../*/_arg/#

    /*
     * HTML-encoding result, in case there are angle brackets, etc in the resulting Hyperlambda.
     */
    html-encode:x:/@lambda2hyper?value

    /*
     * Setting the literal's content to the nodes converted to Hyperlambda
     */
    set:x:/../**/literal/*/element/=pre/./*/innerValue?value
      src:x:/@html-encode?value

  /*
   * Creating a widget showing the Hyperlambda code created from the given p5 lambda tree.
   */
  eval-x:x:/+/*
  p5.web.widgets.create
    parent:x:/../*/_options/*/_parent?value
    class:x:/../*/_options/*/_class?value
    position:0
    widgets
      literal
        element:pre
        innerValue
      literal
        element:a
        href:#
        class:glyphicon glyphicon-remove
        style:"position:absolute;top:10px;right:25px;cursor:pointer;"
        onclick

          /*
           * Removing entire debug window
           */
          p5.web.widgets.get-parent:x:/../*/_event?value
          p5.web.widgets.delete:x:/@p5.web.widgets.get-parent/*/*?value
