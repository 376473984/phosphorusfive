
/*
 * Responsible for creating the Active Event that allows the user to view a piece of
 * lambda code in a "debug window" during execution. Pass in an expression leading to
 * whatever piece of code you'd like to study
 *
 * Arguments;
 *
 * [_arg] or value   - Expression leading to lambda you wish to inspect
 * [_class]          - Override the default CSS class
 * [_parent]         - Override the default parent widget
 *
 * This window can have multiple instances at the same time on page
 */

create-event:sys42.show-code-window

  /*
   * Applying options for widget, with default values.
   * This lambda uses the boolean algebraic "OR" operator, combined with /$ (unique name), ending
   * up prioritizing specified argument, resorting to [_default] value if not given
   */
  _defaults
    _class:col-xs-12 show-code-window
    _parent:content
  _options
  add:x:/../*/_options
    src:x:@"(/../*/"":regex:/^_/""|/../*/_defaults/*)(!/_defaults!/_options)/$"

  /*
   * Checking if user supplied an expression, and if not, showing error message
   */
  if:x:/../*/_arg
    not
    set:x:/../**/literal/*/element/=pre/./*/innerValue?value
      src:"No input given to [sys42.show-code-window]. Supply an expression as value of event invocation, for instance; 'sys42.show-code-window:x:/../*'"
  else

    /*
     * Converting lambda to lisp
     */
    lambda2hyper:x:/../*/_arg/*

    /*
     * HTML-encoding result, in case there are angle brackets, etc in the resulting Hyperlambda
     */
    html-encode:x:/-?value

    /*
     * Setting the literal's content to the nodes converted to Hyperlambda
     */
    set:x:/../**/literal/*/element/=pre/./*/innerValue?value
      src:x:/./-?value

  /*
   * Creating a widget showing the Hyperlambda code created from the given p5 lambda tree
   */
  eval-x:x:/+/*
  create-widget
    parent:x:/../*/_options/*/_parent?value
    class:x:/../*/_options/*/_class?value
    position:0
    widgets
      literal
        element:pre
        innerValue
      literal
        element:a
        href:#
        class:glyphicon glyphicon-remove
        style:"position:absolute;top:10px;right:25px;cursor:pointer;"
        onclick

          /*
           * Removing entire debug window
           */
          get-parent-widget:x:/../*/_event?value
          delete-widget:x:/-/*/*?value


