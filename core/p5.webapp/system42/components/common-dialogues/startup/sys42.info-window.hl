
/*
 * Responsible for creating the Active Event that shows the user a small
 * informational window, providing feedback to user
 *
 * Arguments;
 *
 * [_arg] (or value) - Message, or epxression leading to message, you wish to display to user
 * [_class]          - Additional CS classes that will be added to the widget. Out of the box,
 *                     there exist "info-window-longer", "info-window-error" and "info-window-warning"
 *
 * You cannot show more than one instance of this window at the time
 */
create-event:sys42.info-window

  /*
   * Applying options for widget, with default values.
   * This lambda uses the boolean algebraic "OR" operator, combined with /$ (unique name), ending
   * up prioritizing specified argument, resorting to [_default] value if not given
   */
  _defaults
    _arg:You should really have provided some text here as value of invocation ...
  _options
  add:x:/../*/_options
    src:x:@"(/../*/"":regex:/^_/""|/../*/_defaults/*)(!/_defaults!/_options)/$"

  /*
   * Making sure we delete any previously created windows
   * Notice that sice we only hide the window when it is closed, we need
   * to check if any previous windows exist, and if so, delete them, before 
   * we create any new ones
   */
  if
    fetch:x:/0/0?value
      widget-exist:info-window
    delete-widget:info-window

  /*
   * Setting properties of window
   */
  set:x:/../*/create-widget/**/literal/*/innerValue?value
    src:x:/../*/_options/*/_arg?value

  /*
   * Checking if caller provided "additional CSS classes"
   */
  if:x:/../*/_options/*/_class?value
    set:x:/../*/create-widget/*/class?value
      src:{0} {1}
        :x:/../*/create-widget/*/class?value
        :x:/../*/_options/*/_class?value

  /*
   * Creating actual info window
   */
  create-widget:info-window
    class:info-window
    widgets
      literal
        innerValue
